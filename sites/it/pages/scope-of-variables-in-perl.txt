=title Scope delle variabili in Perl
=timestamp 2013-10-20T09:00:00
=indexes my, scope
=status show
=original scope-of-variables-in-perl
=books beginner
=author szabgab
=translator giatorta
=comments_disqus_enable 0

=abstract start

Ci sono due tipi principali di variabili in Perl. Uno di essi comprende le variabili globali di package, dichiarate con il costrutto
<hl>use vars</hl> (ora obsoleto) oppure con <hl>our</hl>.

L'altro comprende le variabili lessicali dichiarate con <hl>my</hl>.

Vediamo che cosa succede quando dichiarate una variabile usando <hl>my</hl>. In quali parti del codice è visibile?
In altre parole, qual è lo <b>scope</b> della variabile?

=abstract end

<h2>Scope di variabili: blocco</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

{
    my $email = 'pippo@pluto.com';
    print "$email\n";     # pippo@pluto.com
}
# print $email;
# $email does not exists
# Global symbol "$email" requires explicit package name at ...
</code>

All'interno del blocco anonimo (delimitato dalla coppia di parentesi graffe <hl>{}</hl>), troviamo anzitutto la dichiarazione di una nuova variabile 
<hl>$email</hl>. Questa variabile esiste dal punto in cui è dichiarata fino alla fine del blocco. Per questo la linea
dopo la parentesi graffa chiusa <hl>}</hl> ha dovuto essere commentata. Se rimuoveste il <hl>#</hl> dalla linea
<hl># print $email;</hl> e provaste ad eseguire lo script, ricevereste il seguente errore di compilazione:
<a href="/global-symbol-requires-explicit-package-name">Global symbol "$email" requires explicit package name at ...</a>.

In altre parole, lo <b>scope di ogni variabile dichiarata con my è il blocco che la contiene.</b>.

<h2>Scope di variabili: visibilità globale</h2>

La variabile <hl>$lname</hl> è dichiarata all'inizio del codice. Risulta quindi visibile
ovunque fino alla fine del file. Anche all'interno di blocchi. E anche se questi blocchi fanno parte di dichiarazioni di funzioni.
Se modifichiamo la variabile all'interno di un blocco, tale cambiamento si riflette nel resto del codice.
Anche quando si esce dal blocco:

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $lname = "Pippo";
print "$lname\n";        # Pippo

{
    print "$lname\n";    # Pippo
    $lname = "Altro";
    print "$lname\n";    # Altro
}
print "$lname\n";        # Altro
</code>


<h2>Variabili nascoste da altre dichiarazioni</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $fname = "Pippo";
print "$fname\n";        # Pippo

{
    print "$fname\n";    # Pippo

    my $fname  = "Altro";
    print "$fname\n";    # Altro
}
print "$fname\n";        # Pippo
</code>

In questo caso la variabile <hl>$fname</hl> è dichiarata all'inizio del codice. Secondo quanto scritto sopra, essa sarà visibile
ovunque fino alla fine del file, <b>eccetto dove è nascosta dalla dichiarazione di una variabile locale con lo stesso nome</b>.

All'interno del blocco abbiamo usato <hl>my</hl> per dichiarare un'altra variabile con lo stesso nome. In questo modo la variabile <hl>$fname</hl> dichiarata fuori dal blocco viene nascosta finché non si esce dal blocco stesso. Alla fine del blocco (chiuso da <hl>}</hl>), la variabile <hl>$fname</hl> dichiarata nel blocco viene distrutta e la variabile <hl>$fname</hl> originale diventa di nuovo accessibile.
Questo meccanismo è particolarmente importante in quanto rende facile creare delle variabili all'interno di piccoli scope senza doversi preoccupare dei possibili usi degli stessi nomi all'esterno.

<h2>Stesso nome in più blocchi</h2>

Potete liberamente usare lo stesso nome di variabile in più di un blocco. Tali variabili sono indipendenti le une dalle altre.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

{
    my $name  = "Pippo";
    print "$name\n";    # Pippo
}
{
    my $name  = "Altro";
    print "$name\n";    # Altro
}
</code>

<h2>Dichiarazione di package nel file</h2>

Questo esempio è un po' più avanzato, ma è importante menzionarlo qui:

Perl permette di spostarsi da un <b>name-space</b> a un altro usando l'istruzione <hl>package</hl> all'interno
di un file. Una dichiarazione di package <b>NON</b> delimita uno scope. Se dichiarate una variabile <hl>$fname</hl> nel
<b>package main</b> implicito, che è semplicemente il corpo del vostro script, tale
variabile sarà visibile anche in altri name-space nello stesso file.

Se dichiarate una variabile <hl>$lname</hl> nel name-space 'Altro', essa sarà ancora visibile
quando tornate al name-space <hl>main</hl>. Se la dichiarazione del <hl>package Altro</hl>
fosse in un altro file, le variabili avrebbero un diverso scope determinato da tale file.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $fname  = "Pippo";
print "$fname\n";    # Pippo

package Altro;
use strict;
use warnings;

print "$fname\n";    # Pippo
my $lname = 'Pluto';
print "$lname\n";    # Pluto


package main;

print "$fname\n";    # Pippo
print "$lname\n";    # Pluto
</code>


