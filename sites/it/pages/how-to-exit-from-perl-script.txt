=title Come uscire da uno script Perl
=timestamp 2013-10-30T15:00:00
=indexes exit, $?, >>
=status show
=original how-to-exit-from-perl-script
=books beginner
=author szabgab
=translator giatorta
=comments_disqus_enable 0

=abstract start

Se state seguendo il <a href="/perl-tutorial">Tutorial Perl</a>, finora tutti gli script terminavano quando
l'esecuzione giungeva all'ultima riga di codice nel vostro file.

Ci sono però dei casi in cui volete terminare prima l'esecuzione.

Per esempio, chiedete agli utenti la loro età, e se hanno meno di 13 anni terminate lo script.

=abstract end

<code lang="perl">
use strict;
use warnings;
use 5.010;

print "Quanti anni hai? ";
my $age = <STDIN>;
if ($age < 13) {
    print "Sei troppo giovane\n";
    exit;
}

print "Fai qualcosa ...\n";
</code>

Una semplice chiamata a <hl>exit</hl>.

<h2>Il codice di uscita</h2>

Se avete usato la shell Unix/Linux saprete che ogni programma,
quando termina, restituisce un codice di uscita memorizzato nella variabile <hl>$?</hl>.
Potete fornire questo valore di uscita anche da uno script perl, passando un numero alla
funzione <hl>exit()</hl>.


<code lang="perl">
use strict;
use warnings;
use 5.010;

exit 42;
</code>

Per esempio, qui impostiamo il codice di uscita a 42. (Il default è 0.)

<h2>Successo o fallimento?</h2>

In Perl, di solito, 0 e <a href="/undef-and-defined-in-perl">undef</a> rappresentano un fallimento,
mentre altri <a href="/boolean-values-in-perl">valori "vero"</a> rappresentano successo.

Nel mondo della shell Unix/Linux, 0 rappresenta successo e gli altri numeri fallimento.
Tipicamente, ogni applicazione ha il proprio insieme di valori che indicano le diverse condizioni di errore.


<h2>Controllare il codice di uscita in Linux</h2>

In un ambiente Unix/Linux potete eseguire lo script con <hl>perl script.pl</hl> e poi
esaminare il codice di uscita con <hl>echo $?</hl>.


<h2>Controllare il codice di uscita in Perl</h2>

Se vi capita di eseguire uno script perl da un altro script perl, usando per
esempio la funzione <a href="https://perlmaven.com/running-external-programs-from-perl">system</a>, 
Perl ha la stessa variabile <hl>$?</hl> contenente il code di uscita dell'"altro programma".

Se avete il codice sopra salvato in un file script.pl e avete un altro script "executor.pl" come questo:


<code lang="perl">
use strict;
use warnings;
use 5.010;

say system "perl script.pl";
say $?;
say $? >> 8;
</code>

L'output sarà:

<code>
10752
10752
42
</code>

La chiamata a <hl>system</hl> restituisce il codice di uscita, che viene anche memorizzato nella variabile
Perl <hl>$?</hl>. È importante notare che tale valore contiene 2 byte e l'effettivo
codice di uscita è il byte più significativo. Quindi, per ottenere il valore 42 di sopra dovete fare uno shift a destra dei
bit usando l'operatore bit-a-bit <hl>&gt;&gt;</hl> su 8 bit. Tutto ciò è illustrato nell'ultima
riga dell'esempio qui sopra.
