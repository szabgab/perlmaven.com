=title Stringhe in Perl: quotate, interpolate e con escape
=timestamp 2013-07-26T20:00:00
=indexes stringhe, ', ", \, carattere di escape, interpolazione, quotatura, caratteri incorporati, q, qq
=status show
=original quoted-interpolated-and-escaped-strings-in-perl
=books beginner
=author szabgab
=translator giatorta
=comments_disqus_enable 0

=abstract start

Capire come funzionano le stringhe è importante in ogni linguaggio di programmazione, ma in Perl esse sono
parte dell'essenza del linguaggio. Specialmente se tenete presente che uno degli "acronimi a posteriori" di Perl
è <b>Practical Extraction and Reporting Language</b> ("Linguaggio Pratico per Estrazione e Report"), e per fare questo avete bisogno di molte stringhe.

=abstract end

Le stringhe possono essere racchiuse tra apici singoli <hl>'</hl> o doppi <hl>"</hl> e in base a questo il loro comportamento varia leggermente.

<h2>Stringhe a quotatura singola</h2>

Se racchiudete dei caratteri tra apici singoli <hl>'</hl>, quasi tutti,
fatta eccezione per l'apice stesso <hl>'</hl>,
vengono interpretati esattamente come sono scritti nel code.

<code lang="perl">
my $name = 'Pippo';
print 'Ciao $name, come stai?\n';
</code>

L'output sarà:

<code>
Ciao $name, come stai?\n
</code>

<h2>Stringhe a quotatura doppia</h2>

Le stringhe racchiuse tra doppi apici <hl>"</hl> forniscono l'interpolazione
(altre variabili incorporate nella stringa vengono sostituite con il loro contenuto)
e sostituiscono inoltre le sequenze speciali di escape come <hl>\n</hl> e
<hl>\t</hl> rispettivamente con effettivo carattere di a capo e un effettivo tab.

<code lang="perl">
my $name = 'Pippo';
my $time  = "oggi";
print "Ciao $name,\ncome stai $time?\n";
</code>

L'output sarà

<code>
Ciao Pippo,
come stai oggi?

</code>

Notate che c'è un <hl>\n</hl> subito dopo la virgola nella stringa e un altro alla fine della stringa.

Per stringhe semplici come 'Pippo' e "oggi" che non contengono i caratteri <hl>$</hl>, <hl>@</hl> e <hl>\</hl>
non è importante come vengono quotate.

Le due linee seguenti hanno lo stesso effetto:

<code lang="perl">
$name = 'Pippo';
$name = "Pippo";
</code>


<h2>Indirizzi di E-mail</h2>

Dato che <hl>@</hl> viene interpolato nelle stringhe a quotatura doppia, scrivere degli indirizzi di e-mail
richiede un po' di attenzione.

Tra apici singoli <hl>@</hl> non viene interpolato.

Tra doppi apici il codice qui sotto genera un errore:
<a href="/global-symbol-requires-explicit-package-name">Global symbol "@pluto" requires explicit package name at ... line ...</a>
e un warning:
<b>Possible unintended interpolation of @pluto in string at ... line ...</b>

Quest'ultimo ("Possibile interpolazione involontaria di @pluto nella stringa ...") fornisce forse un indizio migliore su quale sia il vero problema.

<code lang="perl">
use strict;
use warnings;
my $broken_email  = "pippo@pluto.com";
</code>

Questo codice, invece, avendo racchiuso l'indirizzo e-mail tra apici singoli, funziona.

<code lang="perl">
use strict;
use warnings;
my $good_email  = 'pippo@pluto.com';
</code>

E se avete bisogno di interpolare qualche variabile scalare ma volete includere dei caratteri chiocciola <hl>@</hl> nella stringa?

<code lang="perl">
use strict;
use warnings;
my $name = 'pippo';
my $good_email  = "$name\@pluto.com";

print $good_email; # pippo@pluto.com
</code>

Potete sempre fare l'<b>escape</b> dei caratteri speciali, in questo caso la chiocciola <hl>@</hl>, usando il cosiddetto <b>carattere di escape</b>
ovvero il carattere back-slash <hl>\</hl>.

<h2>Usare il carattere dollaro $ nelle stringhe a quotatura doppia</h2>

In modo simile, se volete usare un carattere <hl>$</hl> in una stringa che per altri motivi è racchiusa tra doppi apici, potete farne l'escape:

<code lang="perl">
use strict;
use warnings;
my $name = 'pippo';
print "\$name = $name\n";
</code>

Stampa:

<code>
$name = pippo
</code>

<h2>Escape del carattere di escape</h2>

In alcuni casi, anche se poco frequenti, potreste voler includere un carattere back-slash in una stringa.
Se inserite un back-slash <hl>\</hl> in una stringa a doppia quotatura,
Perl penserà che vogliate fare l'escape del carattere seguente e metterà in campo le sue magie.

Niente paura. Potete dire a Perl di starsene buono facendo l'escape del carattere di escape:

Dovete semplicemente metterci davanti un altro back-slash:

<code lang="perl">
use strict;
use warnings;
my $name = 'pippo';
print "\\$name\n";
</code>

<code>
\pippo
</code>

Capisco che questo fare l'escape del carattere di escape possa sembrare un po' strano, ma in linea di massima funziona così anche in tutti gli altri linguaggi.


Se volete capire bene tutta questa faccenda degli escape, provate qualcosa come:

<code lang="perl">
print "\\\\n\n\\n\n";
</code>

guardate che cosa stampa:

<code>
\\n
\n
</code>

e provate a darvi una spiegazione.

<h2>Escape dei doppi apici</h2>

Abbiamo visto come sia possibile includere delle variabili scalari in stringhe con quotatura doppia e come sia invece possibile fare l'escape del sigillo <hl>$</hl>.

Abbiamo visto come usare il carattere di escape <hl>\</hl> e come fare l'escape dell'escape.

E se ora voleste stampare un doppio apice in una stringa a quotatura doppia?


Questo errore contiene un errore di sintassi:

<code lang="perl">
use strict;
use warnings;
my $name = 'pippo';
print "Il "nome" e' "$name"\n";
</code>

quando Perl vede il doppio apice prima della parola "nome" pensa che esso delimiti la fine della stringa
e protesta per la presenza della <a href="/bareword-in-perl">bareword</a> <b>nome</b>.

Come forse avete già indovinato, dobbiamo fare l'escape del carattere <hl>"</hl>:

<code lang="perl">
use strict;
use warnings;
my $name = 'pippo';
print "Il \"nome\" e' \"$name\"\n";
</code>

Questo codice stampa:

<code>
Il "nome" e' "pippo"
</code>

Funziona, ma non è molto leggibile.


<h2>qq, l'operatore double-q</h2>

In questi casi potete usare l'operatore <hl>qq</hl> detto anche operatore double-q:

<code lang="perl">
use strict;
use warnings;
my $name = 'pippo';
print qq(Il "nome" e' "$name"\n);
</code>

Per l'occhio inesperto, qq() può sembrare una chiamata di funzione ma non è così. <hl>qq</hl> è un operatore
e vedremo tra un momento che cos'altro può fare; ma prima cerchiamo di spiegarne l'uso fatto qui sopra.

Abbiamo sostituito i doppi apici <hl>"</hl> che racchiudevano la stringa con le parentesi dell'operatore
<hl>qq</hl>. In questo modo i doppi apici non rappresentano più nulla di speciale all'interno della stringa e possiamo evitare di farne l'escape.
Grazie a ciò otteniamo un codice molto più leggibile.
Lo definirei persino bello, se non temessi gli strali dei programmatori Python.

E se ora voleste includere delle parentesi nella vostra stringa?

<code lang="perl">
use strict;
use warnings;
my $name = 'pippo';
print qq(Il (nome) e' "$name"\n);
</code>

Nessun problema. Almeno fino a quando le parentesi sono bilanciate
(ovvero, c'è un ugual numero di parentesi aperte <hl>(</hl> e chiuse <hl>)</hl>, e inoltre
ogni parentesi aperta viene prima della corrispondente parentesi chiusa) Perl le
accetta.

Lo so. Ora volete creare un problema mettendo una parentesi chiusa prima di una aperta:

<code lang="perl">
use strict;
use warnings;
my $name = 'pippo';
print qq(Il )nome( e' "$name"\n);
</code>

In effetti, perl genererà un errore di sintassi protestando che "nome" è una <a href="/bareword-in-perl">bareword</a>.
Perl non può arrivare a capire tutto da solo, giusto?

Naturalmente potreste fare l'escape delle parentesi nella stringa <hl>\)</hl> e <hl>\(</hl>, ma siamo stati già una volta in quella trappola.
No grazie!

Deve esserci una soluzione migliore!

Vi ricordate che ho detto che <hl>qq</hl> è un operatore e non una funzione? Quindi può permetterci qualche trucco, giusto?

E se sostituissimo le parentesi tonde intorno alla nostra stringa con delle parentesi graffe? <hl>{}</hl>:

<code lang="perl">
use strict;
use warnings;
my $name = 'pippo';
print qq{Il )nome( e' "$name"\n};
</code>

Funziona e stampa ciò che ci aspettiamo:

<code>
Il )nome( e' "pippo"
</code>

(anche se non ho la minima idea del perché dovrei voler stampare una frase come questa...)

Ed ecco che <a href="http://perl.plover.com/yak/presentation/samples/slide027.html">il tipo nella seconda fila</a> alza la mano
e vi chiede che cosa succederebbe se voleste sia le parentesi tonde che quelle graffe nella vostra stringa, <b>e</b> voleste che non fossero bilanciate?

Come in questo codice, giusto?

<code lang="perl">
use strict;
use warnings;
my $name = 'pippo';
print qq[Il )nome} e' "$name"\n];
</code>

che stampa:

<code>
Il )nome} e' "pippo"
</code>


... deve pur esserci un uso anche per le parentesi quadre, o no?


<h2>q, l'operatore single-q</h2>

Analogamente a <hl>qq</hl> esiste anche un operatore <hl>q</hl>.
Anch'esso vi permette di scegliere i delimitatori della vostra stringa, ma si comporta
come gli apici singoli <hl>'</hl>: <b>NON</b> interpola le variabili.

<code lang="perl">
use strict;
use warnings;
print q[Il )nome} e' "$name"\n];
</code>

stampa:

<code>
Il )nome} e' "$name"\n
</code>


