=title Conversione automatica da stringa a numero in Perl
=timestamp 2013-07-06T13:00:00
=indexes is_number, looks_like_number, Scalar::Util, cast, conversione di tipo
=status show
=original automatic-value-conversion-or-casting-in-perl
=books beginner
=author szabgab
=translator giatorta
=comments_disqus_enable 0

=abstract start

Immaginate di scrivere nella lista della spesa

<code>
"2 pagnotte"
</code>

e di darla alla vosta dolce metà la quale in tutta risposta
vi tira contro on errore di conversione non valida di tipo.
Dopo tutto, nella lista "2" è una stringa, non un numero.

Sarebbe piuttosto frustrante, vero?

=abstract end

<h2>Conversioni di tipo in Perl</h2>

In molti linguaggi di programmazione il tipo degli operandi determina il comportamento di un operatore.
Ovvero, la <i>somma</i> di due numeri è la loro addizione numerica, mentre la <i>somma</i> di due stringhe è la loro concatenazione.
Questo meccanismo è detto overloading degli operatori.

Per molti aspetti, Perl ragiona in modo opposto.

In Perl, l'operatore determina come usare gli operandi.

Quindi, se usate un'operazione numerica (ad es. l'addizione) entrambi i valori
vengono automaticamente convertiti in numeri. Se usate un'operazione stringa
(ad es. la concatenazione) entrambi i valori vengono automaticamente convertiti in stringhe.

I programmatori C probabilmente chiamerebbero <b>cast</b> queste conversioni ma nel mondo Perl
questo termine non è utilizzato. Probabilmente perché tutto avviene automaticamente.

A Perl non importa se scrivete qualcosa in forma di numero o di stringa.
Converte automaticamente tra le due possibilità in base al contesto.

La conversione <hl>numero => stringa</hl> è facile.
Basta immaginare di aggiungere "" intorno al valore numerico.

La conversione <hl>stringa => numero</hl> invece può darci un po' da pensare.
Se la stringa presenta l'aspetto di un numero, è di nuovo facile.
Il valore numerico è la stessa cosa senza i doppi apici.

Se c'è un carattere qualunque che impedisce a perl di convertire completamente la stringa in un
numero, perl usa il prefisso della stringa più lungo possibile come
valore numerico e ignora il resto.

Vediamo un paio di esempi:

<code>
Originale   Stringa    Numero

  42         "42"        42
  0.3        "0.3"       0.3
 "42"        "42"        42
 "0.3"       "0.3"       0.3

 "4z"        "4z"        4        (*)
 "4z3"       "4z3"       4        (*)
 "0.3y9"     "0.3y9"     0.3      (*)
 "xyz"       "xyz"       0        (*)
 ""          ""          0        (*)
 "23\n"      "23\n"      23
</code>

In tutti i casi dove la conversione da stringa a numero non è perfetta,
eccetto l'ultimo, perl genera un warning. Almeno se avete
attivato <hl>use warnings</hl> come vi ho raccomandato.

<h2>Esempio</h2>

Ora che abbiamo visto la tabella vediamo un po' di codice:

<code lang="perl">
use strict;
use warnings;

my $x = "4T";
my $y = 3;

</code>

La concatenazione converte entrambi i valori in stringhe:

<code lang="perl">
print $x . $y;    # 4T3
</code>

L'addizione numerica converte entrambi i valori in numeri:

<code lang="perl">
print $x + $y;  # 7
                # Argument "4T" isn't numeric in addition (+) at ...
</code>

<h2>Argument isn't numeric</h2>

Ricevete questo warning quando perl tenta di convertire
una stringa in un numero e la conversione non riesce perfettamente.

Ci sono svariati altri warning ed errori comuni in Perl.
Per esempio <a href="/global-symbol-requires-explicit-package-name">Global symbol requires explicit package name</a>
e <a href="/use-of-uninitialized-value">Use of uninitialized value</a>.

<h2>Come si può evitare il warning?</h2>

È bello che perl vi avverta (quando glielo chiedete) se una conversione di tipo non è perfetta, ma non esiste una funzione
come <b>is_number</b> che controlli se una data stringa è davvero un numero?

Sì e no.

Perl non ha una funzione <b>is_number</b> dato che questo implicherebbe in qualche modo che gli sviluppatori Perl
sappiano che cos'è un numero. Purtroppo non esiste un accordo universale su questo punto. Ci sono sistemi
dove ".2" è accettato come numero e altri in cui non è accettato.
Ancora più spesso, non viene accettato "2.", ma ci sono sistemi dove è considerato un numero perfettamente lecito.

Ci sono perfino casi in cui 0xAB è considerato un numero. Naturalmente, esadecimale.

Quindi non abbiamo a disposizione una funzione <b>is_number</b>, ma ne abbiamo una che ha un po' meno certezze, chiamata <b>looks_like_number</b>.

Fa proprio ciò che pensate. Controlla se, secondo perl, una data stringa sembra un numero.

È fornita dal modulo <a href="http://perldoc.perl.org/Scalar/Util.html">Scalar::Util</a> e
potete usarla così:

<code lang="perl">
use strict;
use warnings;

use Scalar::Util qw(looks_like_number);

print "Quante pagnotte devo comprare? ";
my $loaves = <STDIN>;
chomp $loaves;

if (looks_like_number($loaves)) {
    print "Vado e torno...\n";
} else {
    print "Scusa ma non ho capito\n";
}
</code>


E non dimenticate di prendere anche il latte!


