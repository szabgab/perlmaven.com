=title Abrir y leer archivos de texto
=timestamp 2014-06-09T14:00:56
=indexes open, <$fh>, read, <, encoding, UTF-8, die, open or die
=status show
=original open-and-read-from-files
=books beginner
=author szabgab
=translator lenieto3
=comments_disqus_enable 0

=abstract start

En esta parte de <a href="/perl-tutorial">Perl tutorial</a> vamos a ver <b>cómo leer desde un fichero en Perl</b>.

Esta vez, nos vamos a enfocar en archivos de texto.

=abstract end

Hay dos formas comunes de abrir un fichero dependiendo de cómo le gustaría manejar los casos de error.

<h2>Excepción</h2>

Caso 1: Lanzar una excepción si no puede abrir el fichero:

<code lang="perl">
use strict;
use warnings;

my $filename = 'data.txt';
open(my $fh, '<:encoding(UTF-8)', $filename)
  or die "Could not open file '$filename' $!";

while (my $row = <$fh>) {
  chomp $row;
  print "$row\n";
}
</code>

<h2>Advertir o guardar silencio</h2>

Caso 2: Dar una advertencia si no puede abrir el fichero, pero seguir corriendo:

<code lang="perl">
use strict;
use warnings;

my $filename = 'data.txt';
if (open(my $fh, '<:encoding(UTF-8)', $filename)) {
  while (my $row = <$fh>) {
    chomp $row;
    print "$row\n";
  }
} else {
  warn "Could not open file '$filename' $!";
}
</code>

<h2>Explicación</h2>

Veámoslos explicados:

Primero, usando un editor de texto, crear un fichero llamada 'data.txt' y agregarle unas pocas líneas:

<code>
Primera fila
Segunda fila
Tercera fila
</code>

Abrir un fichero para lectura es muy similar a como <a href="https://perlmaven.com/writing-to-files-with-perl">se abre para escritura</a>, pero en lugar del signo "mayor que" (<hl>></hl>), usamos el signo "menor que" (<hl><</hl>).

Esta vez también configuramos la codificación como UTF-8. En la mayoría del código usted verá sólo el signo "menor que".

<code lang="perl">
use strict;
use warnings;

my $filename = 'data.txt';
open(my $fh, '<:encoding(UTF-8)', $filename)
  or die "Could not open file '$filename' $!";

my $row = <$fh>;
print "$row\n";
print "hecho\n";
</code>

Una vez tenemos el manejador de fichero (fh) podemos leer desde él usando el mismo operador leer línea que fue usado para <a href="/instalacion-y-primeros-pasos-con-perl">leer desde el teclado (STDIN)</a>.
Esto leerá la primer línea del fichero.
Luego imprimimos el contenido de $row e imprimimos "hecho" sólo para aclarar que llegamos al final de nuestro ejemplo.

Si corre el script de arriba verá que imprime

<code>
Primera fila

done
</code>

Podrías preguntarte ¿por qué hay una línea en blanco antes de "hecho"?.

Esto es porque el operador leer línea lee toda la línea, incluyendo el salto de línea.

Así como en el caso de leer desde STDIN, acá tampoco, necesitamos ese salto de línea así que usaremos <hl>chomp()</hl> para removerlo.

<h2>Leer más de una línea</h2>

Una vez que sabemos como leer una línea podemos ir más adelante y poner la llamada a leer línea en la condición de un ciclo <hl>while</hl>.

<code lang="perl">
use strict;
use warnings;

my $filename = $0;
open(my $fh, '<:encoding(UTF-8)', $filename)
  or die "Could not open file '$filename' $!";

while (my $row = <$fh>) {
  chomp $row;
  print "$row\n";
}
print "done\n";
</code>

Cada vez que alcanzamos la condición del ciclo <hl>while</hl>, primero se ejecutará <hl>my $row = <$fh></hl>, la parte que leerá la siguiente línea del fichero. Si esa línea tiene algo, se evaluará como verdadero en el contexto booleano.

Después de que leemos la última línea, en la siguiente iteración el operador leer línea (<hl><$fh></hl>) retornará undef lo cual es falso. El ciclo while terminará.

<h3>Un caso extremo</h3>

Sin embargo hay un caso extremo cuando la última línea tiene sólo un 0 en ella, sin un salto de línea.
El código de arriba evaluará esa línea como falsa y el ciclo no se ejecutará. Afortunadamente, Perl en realidad está haciendo trampa acá. En este caso muy específico (leer una línea de un fichero en un ciclo while), perl actuará como si hubiera escrito <hl>while (defined my $row = <$fh>) {</hl> y entonces incluso tales líneas se ejecutarán adecuadamente.

<h2>abrir sin die</h2>

La forma de manejar ficheros de arriba es usada en los scripts perl cuando necesariamente tiene que tener el fichero abierto o no tiene sentido seguir ejecutando su código.
Por ejemplo cuando todo el trabajo de su script es analizar ese fichero.

¿Y qué si es un fichero de configuración opcional? si puede leerlo cambia algunas configuraciones, si no puede sólo usa las configuraciones por defecto.

En ese caso la segunda solución puede ser una mejor manera de escribir su código.

<code lang="perl">
if (open(my $fh, '<:encoding(UTF-8)', $filename)) {
  while (my $row = <$fh>) {
    chomp $row;
    print "$row\n";
  }
} else {
  warn "Could not open file '$filename' $!";
}
</code>

En este caso revisamos el valor retorno de <hl>open</hl>.
Si es verdadero seguimos y leemos el contenido del fichero.

Si esto falló, damos una advertencia usando la función integrada <hl>warn</hl> pero no lanzamos una excepción. Ni siquiera necesitamos incluir la parte <hl>else</hl>.

<code lang="perl">
if (open(my $fh, '<:encoding(UTF-8)', $filename)) {
  while (my $row = <$fh>) {
    chomp $row;
    print "$row\n";
  }
}
</code>

