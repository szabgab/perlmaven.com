=title undef, el valor por defecto y la función defined en Perl
=timestamp 2014-11-10T20:53:56
=indexes undef, defined
=status show
=original undef-and-defined-in-perl
=books beginner
=author szabgab
=translator davidegx
=comments_disqus_enable 0

=abstract start

En algunos lenguajes hay una forma especial de indicar "esta variable no tiene un valor".
En <b>SQL</b>, <b>PHP</b> y <b>Java</b> este valor es <hl>NULL</hl>. En <b>Python</b>, <hl>None</hl>.
En <b>Ruby</b> se llama <hl>Nil</hl>.

En Perl este valor se llama <hl>undef</hl>.

Veamos algunos detalles.

=abstract end

<h2>¿Como se obtiene undef?</h2>

Cuando declaras una variable escalar sin asignarle ningún valor el contenido será el valor <hl>undef</hl>.

<code lang="perl">
my $x;
</code>

Algunas funciones devuelven <hl>undef</hl> para indicar un error mientras que otras
simplemente retornan undef porque no tienen nada importante que devolver.

<code lang="perl">
my $x = hacer_algo();
</code>

Puedes usar la función <hl>undef()</hl> para restablecer el valor de la variable a <hl>undef</hl>:

<code lang="perl">
# algo de codigo
undef $x;
</code>

También puedes usar el valor devuelto por la función <hl>undef()</hl> para asignar <hl>undef</hl> a una
variable:

<code lang="perl">
$x = undef;
</code>

Los paréntesis después del nombre de la función son opcionales y no los he incluido en el ejemplo.

Como puedes ver hay un diversas maneras de obtener <b>undef</b> en una variable escalar.
La cuestión es, ¿que ocurre si usamos esa variable?

Sin embargo, antes veremos:

<h2>¿Como comprobar si una variable vale undef?</h2>

La función <hl>defined()</hl> devolverá <a href="/valores-booleanos-en-perl">verdadero</a> si
el valor proporcionado <b>no es undef</b>. Devolverá <a href="/valores-booleanos-en-perl">falso</a>
si el valor proporcionado es <b>undef</b>.

Se puede usar así:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $x;

# algun codigo que puede asignar un valor a $x

if (defined $x) {
    say '$x esta definido';
} else {
    say '$x es undef';
}
</code>


<h2>¿Cual es el valor real de undef?</h2>

Aunque <b>undef</b> indica la ausencia de un valor, es todavía usable.
Perl proporciona un par de valores por defecto asociados a undef.

Si usas una variable que contiene undef en una operación numérica, actuará como si fuese 0.

Si la usas en una operación de strings, actuará como la cadena vacía.


Observa el siguiente ejemplo:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $x;
say $x + 4, ;  # 4
say 'Foo' . $x . 'Bar' ;  # FooBar

$x++;
say $x; # 1
</code>

En este ejemplo la variable $x, que vale undef por defecto, se comporta como 0 en la suma (+).
En la concatenación (.) se comporta como la cadena vacía y de nuevo como 0 en la operación de
auto-incremento (++).

No será perfecto. Si has habilitado los warnings con la sentencia <hl>use warnings</hl>
(<a href="/instalacion-y-primeros-pasos-con-perl">lo cual siempre se recomienda</a>)
recibiras dos <a href="https://perlmaven.com/use-of-uninitialized-value">use of uninitialized value (en)</a>
warnings por las dos primeras operaciones, pero no por el auto-incremento:

<code>
Use of uninitialized value $x in addition (+) at ... line 6.
Use of uninitialized value $x in concatenation (.) or string at ... line 7.
</code>

Creo que no recibes la advertencia para la operación de incremento porque perl lo perdona. Después veremos
que es bastante conveniente en sitios donde quieres contar cosas.

Puedes evitar los warnings inicializando la variable con el valor correcto (0 o la cadena vacía,
dependiendo de lo que corresponda), o deshabilitando los warnings selectivamente.
Lo trataremos en una articulo separando.
