=title Como calcular el balance de cuentas bacarias en un fichero CSV usando Perl
=timestamp 2014-11-06T07:01:56
=indexes CSV, split
=status show
=original how-to-calculate-balance-of-bank-accounts-in-csv-file-using-perl
=author szabgab
=translator danimera
=comments_disqus_enable 0

=abstract start



Uno de los lectores de  <a href="/como-leer-un-fichero-csv-en-perl"> ¿Como leer un fichero CSV en perl?</a> artículo me ha enviado un archivo CSV y una pregunta:
<b>Cómo calcular y Mostrar saldo total en cada cuenta usando hash en perl. Sin necesidad de utilizar  función parse?</b>

¿Vamos a ver cómo podemos manejar dicha solicitud?


=abstract end

<code>
TranID,Date,AcNo,Type,Amount,ChequeNo,DDNo,Bank,Branch
13520,01-01-2011,5131342,Dr,5000,,,,
13524,01-01-2011,5131342,Dr,1000,,416123,SB,Ashoknagar
13538,08-01-2011,5131342,Cr,1620,19101,,,
13548,17-01-2011,5131342,Cr,3500,19102,,,
13519,01-01-2011,5522341,Dr,2000,14514,,SBM,Hampankatte
13523,01-01-2011,5522341,Cr,500,19121,,,
13529,02-01-2011,5522341,Dr,5000,13211,,SB,Ashoknagar
13539,09-01-2011,5522341,Cr,500,19122,,,
13541,10-01-2011,5522341,Cr,2000,19123,,,
</code>


Al principio no estaba claro dónde se metió o si ha hecho algún progreso en absoluto.
Con el fin de ayudarlo tenia que tratar de entender  a donde él quería llegar y donde está atascado.

Le pedí el código que había escrito y tengo este script:

<code lang="perl">
#!/usr/bin/perl

print "Content-type:text/html\n\n";

my $sum;
my $sum1;
my $sum2;

open(FILEHANDLE, "<banktran.csv") or die "Could not open 'banktran.csv' $!\n";

while (my $line = <FILEHANDLE>) {
  chomp $line;
  my @fields = split "," , $line;

  if ($fields[2] eq 5131342) {
    if ($fields[3] eq Dr) {
      $sum1 += $fields[4];
    } else {
      $sum2 += $fields[4];
    }
    $sum = $sum1-$sum2;
    print "Total Balance of Account Number is Rs.$sum\n";
  }
}

close(FILEHANDLE);
</code>

Además de algunas cuestiones obvias de principiante que empecé a entender, probablemente quiere crear un informe
por separado para cada cuenta - la tercera columna - <b>AcNo</b> es probablemente el número de cuenta.

La cantidad es en la quinta columna bajo el título de <b>Amount</b>.

Como puedo ver la cuarta columna indica el tipo de la transacción.
Una pequeña búsqueda indica que Dr sería débito y Cr sería crédito, aunque en el código  parece ser lo contrario.

La primera oración en la pregunta parece indicar que ya entendió que necesita utilizar hashes,
en lugar de las variables escalares <hl>$sum</hl>, pero todavía no está claro a él cómo.

La segunda oración, <b>sin necesidad de utilizar la función parse?</b>
parece indicar que me que por alguna razón el lector no puede usar el módulo de Text::CSV que tiene el método parse.
Es una lástima, ya que es la herramienta adecuada en el caso general de análisis y lectura de archivos CSV,
pero en muchos entornos corporativos es difícil instalar un módulo de CPAN. Especialmente a alguien que es nuevo en Perl.

Suponiendo que la CSV file es simple - sin  separadores, sin saltos de línea embebidos - podemos manejarlo con una llamada a la función <hl>split</hl>.


¿El código que me envió es razonable para alguien que empieza a usar Perl,
vamos a ver cómo podemos mejorarlo y cómo podemos tratar de implementar lo que el necesitaba?


<h2>Ejecutando el codigo</h2>

Antes de tratar de mejorar el codigo, vamos a ver si ejecuta, y que es lo que hace? El script es guardado
como <b>banktran.pl</b> y el fichero csv como <b>banktran.csv</b>

<b>perl bantran.pl</b>

<code>
Content-type:text/html

Total Balance of Account Number is Rs.5000
Total Balance of Account Number is Rs.6000
Total Balance of Account Number is Rs.4380
Total Balance of Account Number is Rs.880
</code>

Ahora que vemos que hace algo podemos hacer algunas mejoras:


<h2>use strict and use warnings</h2>

En primer lugar, recomiendo muy fuertemente cada script en Perl  comenzar con las dos declaraciones de la red de seguridad.
Sé que puedo perder mucho tiempo buscando errores que estos dos pueden capturar, así que no quiero estar sin ellos.

<code lang="perl">
use strict;
use warnings;
</code>

Viene justo después del sh-bang. Si agregamos esto al código anterior e intento ejecutarlo otra vez obtenemos lo siguiente:


<code>
Bareword "Dr" not allowed while "strict subs" in use at banktran.pl line 18.
 Execution of banktran.pl aborted due to compilation errors.
</code>

<a href="https://perlmaven.com/barewords-in-perl"> Bareword not allowed while "strict subs" in use (en)</a>
es uno de los avisos comúnes que se describe en el <a href="/perl-tutorial"> tutorial de Perl Maven</a>.
Tenemos que poner las comillas sencillas <hl>'</hl> a la cadena <b>Dr</b>

Al ejecutar el script otra vez obtenemos lo siguiente:


<code>
Content-type:text/html

Use of uninitialized value $sum2 in subtraction (-) at banktran.pl line 23, <FILEHANDLE> line 2.
Total Balance of Account Number is Rs.5000
Use of uninitialized value $sum2 in subtraction (-) at banktran.pl line 23, <FILEHANDLE> line 3.
Total Balance of Account Number is Rs.6000
Total Balance of Account Number is Rs.4380
Total Balance of Account Number is Rs.880
</code>


La advertencia  <a href="https://perlmaven.com/use-of-uninitialized-value"> Use of uninitialized value (en)</a>
es otra advertencia común en Perl. Significa que el <hl>$sum2</hl> era undef en línea 23.



<code lang="perl">
$sum = $sum1-$sum2;
</code>

Probablemente deberíamos inicializar las variables a 0. No siempre es necesario, pero puede conducir
a un código limpio. El código resultante hasta ahora tiene este aspecto:


<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

print "Content-type:text/html\n\n";

my $sum  = 0;
my $sum1 = 0;
my $sum2 = 0;

open(FILEHANDLE, "<banktran.csv") or die "Could not open 'banktran.csv' $!\n";

while (my $line = <FILEHANDLE>) {
  chomp $line;
  my @fields = split "," , $line;

  if ($fields[2] eq 5131342) {
    if ($fields[3] eq 'Dr') {
      $sum1 += $fields[4];
    } else {
      $sum2 += $fields[4];
    }
    $sum = $sum1-$sum2;
    print "Total Balance of Account Number is Rs.$sum\n";
  }
}

close(FILEHANDLE);
</code>

<h2>Usando el modo open "moderno"</h2>

Puse la palabra "moderno" entre comillas porque esto está disponible desde el año 2000 no es realmente
nuevo, pero aún así muchas personas aprenden primero el viejo estilo.

Hay un artículo porqué uno
<a href="https://perlmaven.com/open-files-in-the-old-way"> no debe abrir los archivos de la antigua forma en Perl (en)</a>,
No lo repetiré aquí, sólo lo arreglare el código:


<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

print "Content-type:text/html\n\n";

my $sum  = 0;
my $sum1 = 0;
my $sum2 = 0;

my $filename = 'banktran.csv';
open(my $FILEHANDLE, '<', $filename) or die "Could not open '$filename' $!\n";

while (my $line = <$FILEHANDLE>) {
  chomp $line;
  my @fields = split "," , $line;

  if ($fields[2] eq 5131342) {
    if ($fields[3] eq 'Dr') {
      $sum1 += $fields[4];
    } else {
      $sum2 += $fields[4];
    }
    $sum = $sum1-$sum2;
    print "Total Balance of Account Number is Rs.$sum\n";
  }
}

close($FILEHANDLE);
</code>

Como puedes ver cambié <hl>FILEHANDLE</hl> para ser el léxico escalar <hl>$FILEHANDLE</hl>,
usando 3 parámetros en  la función <hl>open</hl> y también poner el nombre del archivo en una variable.

Este último paso es importante por dos razones:
<ol>
<li>Facilitará pasar el nombre del archivo como un parámetro, si es necesario</hl>
<li>No caemos en la trampa de cambiar el nombre en la llamada <hl>open()</hl> y dejando el antiguo nombre en la
llamada <hl>die()</hl> y confundirse por el mensaje de error.</li>
</ol>


<h2>Mejores nombres de variables</h2>

El hecho de que estamos utilizando una matriz llamada <hl>@fields</hl> e índices en ese arreglo
no esta claro que tipos de valores estan esos campos.

Rápidamente, ¿puedes recordar lo que está en $fields [2]? No puedo. Así que en lugar de utilizar la matriz de @fields
Podríamos utilizar variables con mejores  nombres y escribir:

<h2>Better variable names</h2>

The fact that we are using and array called <hl>@fields</hl> and indexes in that array
makes it unclear what kind of values are in those field.

<code lang="perl">
my ($id, $date, $account, $type, $amount, $cheque, $dd, $bank, $branch)
    = split "," , $line;
</code>

Esto volvió un poco más larga la línea de arriba, pero hará el resto del código más legible.
Esto también hace nos hace crear algunas variables innecesarias


<a href="https://perlmaven.com/advanced-perl-maven-e-book">Advanced Perl developers (en)</a> podría hacerlo mejor mediante el uso de una rebanada de matriz:


<code lang="perl">
my ($account, $type, $amount) = (split "," , $line)[2, 3, 4];
</code>

El bucle tendrá este aspecto:

<code lang="perl">
while (my $line = <$FILEHANDLE>) {
  chomp $line;
  my ($account, $type, $amount) = (split "," , $line)[2, 3, 4];

  if ($account eq 5131342) {
    if ($type eq 'Dr') {
      $sum1 += $amount;
    } else {
      $sum2 += $amount;
    }
    $sum = $sum1-$sum2;
    print "Total Balance of Account Number is Rs.$sum\n";
  }
}
</code>

<h2>Eliminar variables temporales</h2>

Como puedo ver las variables <hl>$sum1</hl> y <hl>$sum2</hl> se utilizan solamente para sostener
los valores que necesitan ser agregadas a la $suma o se deduce de aqui. Realmente no los necesitamos.
Podríamos añadir a <hl>$suma</hl> o deducir de él dentro de la condición:


<code lang="perl">
  if ($account eq 5131342) {
    if ($type eq 'Dr') {
      $sum += $amount;
    } else {
      $sum -= $amount;
    }
</code>

Vamos a ver y probar el código completo de nuevo, antes de la operación.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

print "Content-type:text/html\n\n";

my $sum  = 0;

my $filename = 'banktran.csv';
open(my $FILEHANDLE, '<', $filename) or die "Could not open '$filename' $!\n";

while (my $line = <$FILEHANDLE>) {
  chomp $line;
  my ($account, $type, $amount) = (split "," , $line)[2, 3, 4];

  if ($account eq 5131342) {
    if ($type eq 'Dr') {
      $sum += $amount;
    } else {
      $sum -= $amount;
    }
    print "Total Balance of Account Number is Rs.$sum\n";
  }
}

close($FILEHANDLE);
</code>

<h2>Mostrar el total de todas las cuentas</h2>

Ahora, sólo una cuenta específica (id = 5131342) se resume y se realiza en una variable escalar.
En lugar de esto, nos gustaría resumir todas las cuentas. La forma más sencilla es utilizar un hash.
Los identificadores de cuenta serán las llaves y la suma será el valor.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

print "Content-type:text/html\n\n";

my %sum;

my $filename = 'banktran.csv';
open(my $FILEHANDLE, '<', $filename) or die "Could not open '$filename' $!\n";

while (my $line = <$FILEHANDLE>) {
  chomp $line;
  my ($account, $type, $amount) = (split "," , $line)[2, 3, 4];

  if ($type eq 'Dr') {
    $sum{$account} += $amount;
  } else {
    $sum{$account} -= $amount;
  }
  print "Total Balance of Account Number $account is Rs.$sum{$account}\n";
}

close($FILEHANDLE);
</code>

En este código no necesitamos el <hl>if ($account eq 5131342)</hl> de la condición más.
Podemos acceder a la clave hash directamente, utilizando el número de <hl>$account</hl> como clave.



Después de ejecutar el script el resultado se ve así:

<code>
Content-type:text/html

Argument "Amount" isn't numeric in subtraction (-) at banktran.pl line 19, <$FILEHANDLE> line 1.
Total Balance of Account Number AcNo is Rs.0
Total Balance of Account Number 5131342 is Rs.5000
Total Balance of Account Number 5131342 is Rs.6000
Total Balance of Account Number 5131342 is Rs.4380
Total Balance of Account Number 5131342 is Rs.880
Total Balance of Account Number 5522341 is Rs.2000
Total Balance of Account Number 5522341 is Rs.1500
Total Balance of Account Number 5522341 is Rs.6500
Total Balance of Account Number 5522341 is Rs.6000
Total Balance of Account Number 5522341 is Rs.4000
</code>

La advertencia que obtenemos es debido a la primera línea en el archivo CSV. Antes no teníamos que
preocuparnos por lo que sólo nos ocupabamos de las filas donde el id de la cuenta coinsidiera con el número seleccionado,
pero ahora tenemos que pasar esa línea. Es fácil, solo leer la primera fila antes del bucle
<hl>while</hl> y sacarla.


<code lang="perl">
<$FILEHANDLE>;
while (my $line = <$FILEHANDLE>) {
</code>

<h2>Total sólo al final?</h2>

Esto podría ser la versión final, pero no está claro para mí si realmente necesitamos Mostrar el equilibrio después de cada fila,
o solamente en el final. Así que vamos a hacer otro cambio que mostrará los resultados solamente en el final.

Quitamos la llamada <hl>print</hl> del bucle <hl>while</hl> y añada otro bucle final, revisando todas las
cuentas y mostrar el estado de cuenta:


<code lang="perl">
foreach my $account (sort keys %sum) {
  print "Total Balance of Account Number $account is Rs.$sum{$account}\n";
}
</code>

El código completo

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

print "Content-type:text/html\n\n";

my %sum;

my $filename = 'banktran.csv';
open(my $FILEHANDLE, '<', $filename) or die "Could not open '$filename' $!\n";

<$FILEHANDLE>;
while (my $line = <$FILEHANDLE>) {
  chomp $line;
  my ($account, $type, $amount) = (split "," , $line)[2, 3, 4];

  if ($type eq 'Dr') {
    $sum{$account} += $amount;
  } else {
    $sum{$account} -= $amount;
  }
}

close($FILEHANDLE);

foreach my $account (sort keys %sum) {
  print "Total Balance of Account Number $account is Rs.$sum{$account}\n";
}
</code>

Hay sólo una cosita que todavía me molesta. Por qué imprimimos Content-type
¿al principio del código? ¿Esto va a funcionar como un script CGI?

Si no, entonces podríamos eliminar esa línea.

Si esto es un script CGI entonces nosotros deberíamos probablemente imprimir HTML real. Por lo menos deberíamos
imprimir etiquetas pre alrededor del informe.



