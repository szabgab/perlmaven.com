=title "my" 隐藏同作用域下之前的声明
=timestamp 2013-05-21T15:04:23
=indexes my, scope
=status show
=original my-variable-masks-earlier-declaration-in-same-scope
=books beginner
=author szabgab
=translator terrencehan
=comments_disqus_enable 1

=abstract start

如果错误地尝试在一个作用域两次声明同一个变量，会产生如下编译时警告：

<code>
"my" variable ... masks earlier declaration in same scope at ... line ...
</code>

这是如何产生的？那么，每次循环的迭代重新声明的变量又如何有效呢？

如果不能在一个作用域写两次<hl>my $x</hl>，那该如何清空变量呢？

=abstract end

来看下面几个例子的不同之处：

<h2>普通的脚本</h2>

<code lang="perl">
use strict;
use warnings;

my $x = 'this';
my $z = rand();
my $x = 'that';
print "OK\n";
</code>

此例会产生如下编译时警告：

<code>
"my" variable $x masks earlier declaration in same scope at ... line 7. )
</code>

因为这个脚本也会打印"OK"，所以这仅是一个警告。


<h2>条件语句的代码块</h2>

<code lang="perl">
use strict;
use warnings;

my $z = 1;
if (1) {
    my $x = 'this';
    my $z = rand();
    my $x = 'that';
}
</code>

这次产生如下警告：

<code>
"my" variable $x masks earlier declaration in same scope at ... line 7.
</code>

在这两个例子中，我们都在同一个作用域中声明了两次<hl>$x</hl>，并且都产生了编译时警告。

在第二个例子里，我们也对<hl>$z</hl>进行了两次声明，但是没有产生任何警告。这是因为<hl>$z</hl>所在的代码块是一个独立的<a href="/scope-of-variables-in-perl">作用域</a>。

<h2>函数的作用域</h2>

相同的代码，但这次是在函数里：

<code lang="perl">
use strict;
use warnings;

sub f {
    my $x = 'this';
    my $z = rand();
    my $x = 'that';
}
f(1);
f(2);
</code>

这里也会产生一次针对<hl>$x</hl>的编译时警告。即使变量<hl>$z</hl>重复地在每次调用中出现也是没有问题的。变量<hl>$z</hl>不会触发警告，这是因为Perl可以两次创建同一个变量，只是你不应该这么做。不然，至少不能在同一个作用域里面这样做。

<h2>for循环的作用域</h2>

同样的代码，循环中：

<code lang="perl">
use strict;
use warnings;

for (1 .. 10) {
    my $x = 'this';
    my $z = rand();
    my $x = 'that';
}
</code>

这也会针对<hl>$x</hl>产生一次警告，对于<hl>$z</hl>则不会。

在这个代码中，<b>每次</b>迭代Perl都会为<hl>$z</hl>变量开辟内存。

<h2>"my"有什么意义?</h2>

<hl>my $x</hl>的意思是告诉Perl，尤其是<hl>strict</hl>，你要在<a href="/scope-of-variables-in-perl">当前作用域</a>使用一个私有变量<b>$x</b>。如果没有这个，Perl会在上层的作用域中寻找声明，如果没有找到的话会给出一个编译时错误<a href="/global-symbol-requires-explicit-package-name">Global symbol requires explicit package name</a>。代码块的每一项，每次调用函数，每个循环的迭代都是一个新的作用域。

另一方面，在同一个作用域中写两次<hl>my $x</hl>仅仅表示尝试着告诉Perl两遍相同的事情。这既无必要，也通常意味着出现了某个错误。

换句话说，我们之前得到的警告是与代码的<b>编译</b>相关的，代码并没有运行。这关系到开发者对变量的声明，而不是perl在运行时内存的分配。

<h2>如何清空已存在的变量？</h2>

如果我们不在同一作用域写两次<hl>my $x;</hl>，那如何把变量置“空”？

首先，如果变量在某个作用域内声明（在花括号内），那么出了<a href="/scope-of-variables-in-perl">作用域</a>后它（变量）会自动消失。

如果你仅仅想“清空”当前作用域的标量，把它赋值成<hundef</hl>就好了，如变量是<a href="https://perlmaven.com/undef-on-perl-arrays-and-hashes">数组或hash表</a>，那么通过赋值成空列表就能清空：

<code lang="perl">
$x = undef;
@a = ();
%h = ();
</code>

再说明一下。"my"会告诉perl你要使用一个变量。当执行到"my variable"时Perl会为变量本身以及它的内容分配空间；当执行到<hl>$x = undef;</hl>  或  <hl>@x = ();</hl>  或  <hl>undef @x;</hl>时，Perl会清空已有变量的内容。


