=title How to get notification when specific contact goes online in Skype?
=timestamp 2013-12-05T11:30:01
=indexes Skype::Any
=status show
=books cook_book
=author szabgab
=comments_disqus_enable 1

=abstract start

Skype has plenty of notification options. For example you can separately configure it to give sound or visual notification when one of you contacts comes on-line. Unfortunately it is an all-or-nothing option. Either you get notified for every contact or for none of them.

The constant pop-ups disturb me, but I'd like to get alerted when my mother comes on-line.

Luckily I found <a href="https://metacpan.org/pod/Skype::Any">Skype::Any</a> written by Takumi Akiyama. It's wonderful. It let's you connect to your running Skype client and then it can do all kinds of magic.

=abstract end


<h2>Platforms</h2>

Before looking at the actual code. As I read in the documentation, the module
(version 0.06) it only works on Linux and Mac OSX. Patches for Windows are welcome.

I tried this on Mac OSX.

After installing <a href="https://metacpan.org/pod/Skype::Any">Skype::Any</a>
I also had to install 
<a href="https://metacpan.org/pod/Cocoa::Skype">Cocoa::Skype</a>, and
<a href="https://metacpan.org/pod/Cocoa::EventLoop">Cocoa::EventLoop</a> separately.

The module also comes with a warning. Apparently Skype is  
<a href="https://support.skype.com/en/faq/FA12349/skype-says-my-application-will-stop-working-with-skype-in-december-2013-why-is-that">shutting down part of this API</a> in December 2013. That's now. So this whole solution might go away in a few weeks. We'll see.

<h2>The Selective Skype Notification Agent</h2>

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Skype::Any;
 
my $skype = Skype::Any->new(name => "Selective Notification Agent");

$skype->user(sub {
    my ($user, $status) = @_;
    say $status;
    say $user->handle;
    say $user->fullname;
    say '---';
});

$skype->run;
</code>

That's the whole script.

When you run this script it will try to connect the <b>already running</b> Skype client.
The Skype client will ask for your permission.
(For a screenshot see the <a href="https://metacpan.org/pod/Skype::Any">documentation of Skype::Any</a>.) You can let this script connect to your Skype once or always. Later you can turn this off, from within Skype.

Also, as far as I understand the permission is given to a third-party application with a specific name. By default Skype::Any would use the name <b>Skype::Any</b>, but in the constructor we gave it another name.

Anyway, once the connection is established, the events in the Skype client will generate call-backs in our script. We are interested in the user-related events, so register the event handler <hl>$skype->user</hl> passing an anonymous subroutine to it.

When a user changes their status, this function will be called and two values will be passed. The first one is a <a href="https://metacpan.org/pod/Skype::Any::Object::User">Skype::Any::Object::User</a> object and the second one is the new status of the user. In the short time I ran this script I only saw the strings <hl>NA</hl>, <hl>OFFLINE</hl>, and <hl>ONLINE</hl>.

The Skype::Any::Object::User has all kinds of interesting methods providing details about that specific contact, but the most interesting to us is the <hl>handle</hl> field. This is the unique Skype ID of the person.

In the above example I only printed it along with the </hl>fullname</hl> but I could do other interesting things.

<h2>Filtering Selected Users</h2>

I could create a hash of <hl>"username" => "Text message"</hl> pairs for the important people. (In this case my mother), and print the message only when the user handle matches one of the keys:

<code lang="perl">
my %vip = (
    mothers_handle => 'Your mom is online!',
    handle_of_boss => 'Pretend you are busy working!',
);

$skype->user(sub {
    my ($user, $status) = @_;
    return if $status ne 'ONLINE';
    my $handle = $user->handle;
    if (exists $vip{ $handle }) {
        say $vip{ $user->handle };
        system qq{say "$vip{ $handle }"};
    }
});
</code>

First we disregard notifications where the user has just went off-line by
<hl>next if $status ne 'ONLINE';</hl>.

Then if the <hl>handle</hl> exists as a key in our hash, we do something.
Specifically I just read about the <hl>say</hl> command available on Mac.
It would read the text to me. So instead of a visual notification
I went with a speaker telling me what to do.

You might have a better idea what to do there.










</code>




