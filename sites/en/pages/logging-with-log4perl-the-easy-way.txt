=title Logging with Log4perl the easy way
=timestamp 2015-09-24T20:15:01
=indexes Log::Log4perl, Log::Log4perl::Level
=tags screencast
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

Logging can be very useful for finding problems in a running application. It can be also very useful for people who are not
familiar with an application or a module who want to learn how the code works maybe in order to fix a bug or add a feature.

<a href="https://metacpan.org/pod/Log::Log4perl">Log::Log4perl</a> is a complex module for logging
with lots of knobs to fiddle with, but there is an easy way to get started using it.

=abstract end

<screencast file="logging-with-log4perl-the-easy-way" youtube="BVhSPYZjjHg" />

<h2>Log in a script</h2>

In the most simple case we use it to add logging messages to a script. We need to load the module importing the <hl>:easy</hl> key
that will import 6 functions for the 6 logging levels provided by Log::Log4perl and 6 variables with corresponding names.

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Log::Log4perl qw(:easy);
Log::Log4perl->easy_init($WARN);

FATAL "This is", " fatal";
ERROR "This is error";
WARN  "This is warn";
INFO  "This is info";
DEBUG "This is debug";
TRACE "This is trace";
</code>

Running this script will print out:

<code>
2014/08/24 09:59:37 This is fatal
2014/08/24 09:59:37 This is error
2014/08/24 09:59:37 This is warn
</code>

In the code we can use any of the 6 methods to send logging messages. These logging functions will accept any number of parameters and will concatenate them as can be seen
in the case of the FATAL call.

Once such calls were added to the script we can use the <hl>easy_init</hl> method to set the minimal level to be actually logged. Because in the above example
we set the minimal log-level to be WARN, only the message that have higher priority (FATAL, ERROR and WARN) will be actually printed.

By default Log::Log4perl will print to the STDERR (Standard error) channel which is by default connected to the screen.

If we set the log level to <hl>FATAL</hl> by writing

<code lang="perl">
Log::Log4perl->easy_init($FATAL);
</code>

then the above code will only print only one line.

<h2>Turn off logging</h2>

If we don't call the <hl>easy_init</hl> method at all, nothing will be printed.
Alternatively, a probably better approach is to explicitly turn it off using

<code lang="perl">
Log::Log4perl->easy_init($OFF);
</code>

<h2>Using methods</h2>

Using these functions might be easy, but as the application growth, you might be better of with a more object oriented approach.
In order to use the OOP way, one can fetch the internal logger object using the <hl>get_logger</hl> and then call the appropriate
method names (the lower case version of the levels).

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Log::Log4perl qw(:easy);
Log::Log4perl->easy_init($WARN);

my $logger = Log::Log4perl->get_logger();
$logger->fatal( "This is", " fatal");
$logger->error( "This is error");
$logger->warn(  "This is warn");
$logger->info(  "This is info");
$logger->debug( "This is debug");
$logger->trace( "This is trace");
</code>

This still requires importing the <hl>:easy</hl> tag in order to have the variables <hl>$FATAL</hl>, <hl>$ERROR</hl>, <hl>$WARN</hl> to set the minimal level.
In order to avoid importing even those variables we can use the <hl>to_priority</hl> function of <a href="https://metacpan.org/pod/Log::Log4perl::Level">Log::Log4perl::Level</a>.
Normally the variables such as <hl>$WARN</hl> contain a numerical value, but the <hl>to_priority</hl> function can convert the string representation of the level-name
into that numerical value:

<code lang="perl">
use Log::Log4perl ();
use Log::Log4perl::Level ();

Log::Log4perl->easy_init( Log::Log4perl::Level::to_priority( 'WARN' ) );
</code>

<h2>Full example</h2>

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Log::Log4perl ();
use Log::Log4perl::Level ();

Log::Log4perl->easy_init(Log::Log4perl::Level::to_priority( 'WARN' ));

my $logger = Log::Log4perl->get_logger();
$logger->fatal( "This is", " fatal");
$logger->error( "This is error");
$logger->warn(  "This is warn");
$logger->info(  "This is info");
$logger->debug( "This is debug");
$logger->trace( "This is trace");
</code>

