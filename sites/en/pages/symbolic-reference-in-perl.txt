=title Symbolic references in Perl
=timestamp 2013-07-18T00:30:01
=indexes strict, symbolic references
=status show
=books advanced
=author szabgab
=comments_disqus_enable 0

=abstract start

One of the 3 things <hl>use strict</hl> disables is the use of <b>symbolic references</b>.

Let's see how can that save you time and avoid embarrassment!

Let's see why avoiding symbolic references is a good thing!

At another time we'll see a useful example of symbolic references.

=abstract end

In general, symbolic references are a very powerful tool in Perl, but if used by accident, it can cause a lot of
head scratching. Best to disable them in every script, and enable them only when we know exactly why we need them.

<h2>The danger</h2>

A long time ago, in a Perl training session, we had some discussion that led me mention that you can
use the same name for both scalar variables and arrays and hashes. It is not a recommended practice
but technically it is possible. I got carried away a bit and wanted to show the students that it works.
So I wrote the following code:


<code lang="perl">
my $person = "Foo";
my %person;
$person->{name} = 'Bar';
</code>

...and explained that I declared the <hl>$person</hl> scalar, assigned "Foo" to it.
Then created a hash with the same name, and put a key and a value in it.
To further show it works I printed out the content of the hash:

<code lang="perl">
use Data::Dumper;
print Dumper \%person;
</code>

To my great surprise the following was printed:

<code>
$VAR1 = {};
</code>

I was quite baffled. Where did the new key/value pair go?

I had really no idea what happened.

The situation was quite embarrassing.

Luckily we had lunch break, and just after I finished the soup it occurred to me
that I have not used <b>strict</b> in my code.

<h2>The understanding</h2>

Returning to the class-room I added <hl>use strict</hl> and <b>ran</b> the code again:

<code lang="perl">
use strict;
use Data::Dumper;

my $person = "Foo";
my %person;
$person->{name} = 'Bar';
</code>

I got the following error message:

<code>
Can't use string ("Foo") as a HASH ref while "strict refs" in use at ...
</code>

See another case of <a href="/cant-use-string-as-a-hash-ref-while-strict-refs-in-use">Can't use string (...) as an HASH ref while "strict refs" in use at ...</a>.

Apparently I was using a <b>symbolic HASH reference</b>, by accident.

In fact, I never touched the <hl>%person</hl> hash. In the second assignment
I used the <hl>$person</hl> scalar as a reference to a hash. This would be OK
if the variable <hl>$person</hl> was undef, it would <a href="/autovivification">autovivify</a> (pro page)
to be a reference to a hash. As it already contained a string Perl was trying to use that name, the content of the
variable, as the name of a hash. Effectively I assigned 'Bar' to the 'name' key of the <hl>%Foo</hl> hash.

This is how you can check it too:

<code lang="perl">
use Data::Dumper;

my $person = "Foo";
my %person;
$person->{name} = 'Bar';

print Dumper \%person;
print Dumper \%Foo;
</code>

And the output is:

<code>
$VAR1 = {};
$VAR1 = {
        'name' => 'Bar'
      };
</code>

As you can see, the first print of the <hl>%person</hl> hash is empty,
but the <hl>%Foo</hl> hash sprang to existence and has a 'name' key with 'Bar'
as the value.

Certainly <b>not</b> what I wanted.

Since Perl has real references, you almost never need this capability,
and if such thing happens by mistake then it is way better to get
an error than to silently do the wrong thing.

So <b>always use strict</b>.


<h2>Symbolic references of scalar variables</h2>

<code lang="perl">
print "Hello World\n";
my $name = 'person';
${$name} = 'Foo';
print "$name\n";
print "$person\n";
</code>

Save this code in a file called <hl>programming.pl</hl>
and run it as <hl>perl programming.pl</hl>.

The output will be this:

<code>
Hello World
person
Foo
</code>

The variable <hl>$name</hl> still has the value 'person' in it as expected, but now we also have
a variable called <hl>$person</hl>, that holds the value 'Foo'. Even though we don't have
a clearly visible assignment to <hl>$person</hl>, such as <hl>$person = 'Foo';</hl> would be.

This is in the expression <hl>${$name} = 'Foo';</hl>, perl replaced the variable <hl>$name</hl>
with its value 'person' so effectively perl saw <hl>$person = 'Foo';</hl>. There. Now we can
see what perl saw.

Let's see how can using strict stop us from such mistake?
Add <hl>use strict;</hl> to the beginning of the script and run it again!
You will get:

<code>
Global symbol "$person" requires explicit package name at programming.pl line 10.
Execution of programming.pl aborted due to compilation errors.
</code>

Interesting, this is not the same error as we saw above. This complains about the fact
that we have not declared the <hl>$person</hl> variable. Not only that but this is a
compile-time error. How do I know it, you ask?

Try <hl>perl -c programming.pl</hl>, it will give you the same error message and will
tell you there was a compilation error:

<code>
Global symbol "$person" requires explicit package name at programming.pl line 10.
programming.pl had compilation errors.
</code>

The <hl>-c</hl> flag of perl tells it to compile the script and exit.

In order to further experiment, let's add <hl>my $person;</hl> at the top of the script
and try to compile it again using <hl>perl -c programming.pl</hl>.

The output will be:

<code>
programming.pl syntax OK
</code>

So we can see the code now compiles. What if we try to run it with <hl>perl programming.pl</hl>?

We get:

<code>
Hello World
Can't use string ("person") as a SCALAR ref while "strict refs" in use at programming.pl line 8.
</code>

Note, it runs. I know it as it printed 'Hello World', but then it stops running with an error
similar to what we saw at the beginning of the article.

In general this is a good thing. In most cases we don't want to write code that will
act as symbolic references <b>by mistake</b>.

The only unfortunate part is that perl can only notice this during run-time, so if this
code is a part of the code that rarely executes (eg. within an if statement), then
the problem might only surface when it is running in production.

<h1>Can symbolic references be useful?</h1>

Sometimes it might feel useful to use symbolic references.

<h2>Accessing a variable when its name is in another variable</h2>

For example, sometimes I see code similar to this:

<code lang="perl">
$machineA = '10.3.7.5';
$machineB = '11.3.5.6';

foreach $name ('machineA', 'machineB') {
    print "${$name}\n";
}
</code>

or maybe:

<code lang="perl">
foreach $name ('A', 'B') {
    $key = 'machine' . $name;
    print "${$key}\n";
}
</code>

The programmer has a bunch of variables holding IP addresses of computers, she wants to
go over all the names and do something with each machine. (In this case, just printing
the value.)

This won't work under <hl>use strict;</hl>, and in general this practice should be avoided.

This is a typical example, when one should use a hash instead of several scalar variables.
Like this:

<code lang="perl">
use strict;
use warnings;

my %machine;
$machine{A} = '10.3.7.5';
$machine{B} = '11.3.5.6';

foreach my $name ('A', 'B') {
    print "$machine{$name}\n";
}
</code>

<h2>Several numbered variables</h2>

Another case I saw a couple of times:

<code lang="perl">
$machine1 = '10.3.7.5';
$machine2 = '11.3.5.6';
$machine3 = '12.4.5.6';

foreach $number (1 .. 3) {
    $name = 'machine' . $number;
    print "${$name}\n";
}
</code>

Here we have several machines numbered and then we iterate over them.

This is better written as a hash, similar to the previous example, or
as an array:

<code lang="perl">
use strict;
use warnings;

my @machine = (
    '10.3.7.5',
    '11.3.5.6',
    '12.4.5.6',
);

foreach my $number (0 .. 2) {
    print "$machine[$number]\n";
}
</code>

<h2>Conclusion</h2>

Always use strict!


