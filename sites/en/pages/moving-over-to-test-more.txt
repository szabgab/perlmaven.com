=title Moving over to Test::More
=timestamp 2016-04-16T08:30:01
=indexes Test::Simple, Test::More, ok, is, isnt
=tags screencast
=status show
=books testing
=author szabgab
=comments_disqus_enable 1

=abstract start

What we saw in the earlier articles and screencasts, was an introduction to testing with Perl. We made some nice progress but we kept using
<a href="https://metacpan.org/pod/Test::Simple">Test::Simple</a> which only provides a single <hl>ok()</hl> function with some additional features.

When a test case succeeds we get a nice <hl>ok N</hl> printed, but the problem is that even when a test case fails we only get a simple <hl>not ok</hl>
with the title of the test case, if we set one, and the line number. We could get so much more information. That's where
<a href="https://metacpan.org/pod/Test::More">Test::More</a>, and later a lot of other testing modules come in.

They allow us to be much more explicit in our testing functions and to get much more detailed error message that just <b>not ok</b>.

=abstract end

<screencast file="/media/videos/moving-over-to-test-more" youtube="2HHhostZ0cE" />

<h2>Where to get Test::More ?</h2>

Let's start by the simple fact that <a href="https://metacpan.org/pod/Test::More">Test::More</a> lives in the same distribution that
contains <a href="https://metacpan.org/pod/Test::Simple">Test::Simple</a> and that distribution is itself called
<a href="https://metacpan.org/release/Test-Simple">Test-Simple</a>. Then of course you'll probably be happy to hear that they are also
part of every standard Perl distribution, so by virtue of having perl, you should also have both of these modules.
(Unless your vendor decided to strip them out.)

Of course the version that is on CPAN is probably newer than the version in your perl distribution, so if you'd like to upgrade
it you can do it with the usual tools.

Now let's switch from Test::Simple to Test::More.

<h2>Test::More is a drop-in replacement of Test::Simple</h2>

The good news is that Test::More is a simple superset of Test::Simple, so you can edit your test scripts, replace ::Simple by ::More and everything
will work (or fail) exactly the same way as it did before. Test::More also provides the same <hl>ok()</hl> function as Test::Simple does.

You can try the following test script:

<code lang="perl">
use strict;
use warnings;

use lib 'lib';
use MyTools;

use Test::More tests => 3;
# Test::More instead of Test::Simple

ok( sum(1, 1)    == 2,  '1+1');
ok( sum(2, 2)    == 4,  '2+2');
ok( sum(2, 2, 2) == 6,  '2+2+2');
</code>

Running this script provides the following output:

<code>
1..3
ok 1 - 1+1
ok 2 - 2+2
not ok 3 - 2+2+2
#   Failed test '2+2+2'
#   at t/31.t line 12.
# Looks like you failed 1 test of 3.
</code>

We can see that the test where we add <hl>2+2+2</hl> fails, but cannot see what we were really expecting nor what we actually received.
I know, most of us have a fairly good idea what should we expect from 2+2+2, but what if this was a more complex example and not even related to math?

That's where Test::More shines.

<h2>is</h2>

The <hl>is()</hl> function provided by Test::More accepts 3 parameters:
<ul>
  <li>The actual result</hl>
  <li>The expected result</hl>
  <li>The name of the test case</hl>
</ul>

It compares the actual result with the expected result using <hl>eq</hl>. If they are equal, it prints <hl>ok</hl>,
if they are different it prints <hl>not ok</hl> and then it goes on printing the actual result and the expected result.
Let's see the above test script rewritten with <hl>is()</hl>:


<code lang="perl">
use strict;
use warnings;

use lib 'lib';
use MyTools;

use Test::More tests => 3;

is( sum(1, 1),    2,     '1+1'   );
is( sum(2, 2),    4,     '2+2'   );
is( sum(2, 2, 2), 6,     '2+2+2' );
</code>

And the output from the script:

<code>
1..3
ok 1 - 1+1
ok 2 - 2+2
not ok 3 - 2+2+2
#   Failed test '2+2+2'
#   at t/32.t line 11.
#          got: '4'
#     expected: '6'
# Looks like you failed 1 test of 3.
</code>

This will make it much easier for an observer to understand the actual problem behind this test failure.

One minor thing that you might be put-off with is the fact that <hl>is()</hl> always uses <hl>eq</hl>.
The reason is that in almost every case, even when comparing numbers, the <hl>eq()</hl> will behave the same
as <hl>==</hl>. In the rare cases when you really need the numerical comparison of <hl>==</hl> you can use
the <hl>cmp_ok</hl> function that will be explained later.


<h2>isnt</h2>

There is a negated version of the <hl>is()</hl> function called <hl>isnt()</hl>. It has the same syntax, but
it will compare the two values using <hl>ne</hl>. There isn't a lot of use of this, it is quite rare when 
all the expectations you can have is that something is not equal to some pre-defined value, but actually there is
one that might be useful.

Sometimes all you want is to make sure a variable is not equal to <a href="/undef-and-defined-in-perl">undef</a>.
See this sample test code:

<code lang="perl">
use strict;
use warnings;

use Test::More tests => 2;

my $x;
isnt($x, undef);

$x = 1;
isnt($x, undef);
</code>

Running this script we'll get the following output:

<code>
1..2
not ok 1
#   Failed test at t/isnt_undef.t line 7.
#          got: undef
#     expected: anything else
ok 2
# Looks like you failed 1 test of 2.
</code>

The first test case fails, because we expected a variable not to be <hl>undef</hl>, but it was.


