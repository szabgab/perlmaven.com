=title String operators: concatenation (.), repetition (x)
=timestamp 2014-05-01T01:30:01
=indexes x, ., ++
=status show
=books beginner
=author szabgab
=comments_disqus_enable 1

=abstract start

In addition to the <a href="/numerical-operators">numerical operators</a> Perl has two operators specially used for strings. 
One of them is <hl>.</hl> for concatenation, and the other is <hl>x</hl> (the lower-case X) for repetition.

=abstract end

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $x = 'Hello';
my $y = 'World';

my $z = $x . ' ' . $y;
say $z;
</code>

Running the above script will yield the following output:

<code>
Hello World
</code>

It attached the string from the variables and the literal empty-space thing into one new string.

Actually in the above case we did not have to use the <hl>.</hl> concatenation to achieve this result.
As Perl provides scalar variable interpolation in string, we could just write:

<code lang="perl">
my $z = "$x $y";
</code>

and that would yield the same result.

<h2>When interpolation is not the answer</h2>

There are of course cases when the concatenation cannot be replaced by interpolation. Take for example this code:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $x = 2;
my $y = 3;

my $z = 'Take ' . ($x + $y);

say $z;
</code>

It will print

<code>
Take 5
</code>

On the other hand, if we replace the concatenation with interpolation:

<code lang="perl">
my $z = "Take ($x + $y)";
</code>

We get:

<code>
Take (2 + 3)
</code>

You might have noticed that I also changed the quotes from single-quote to double-quote when I wanted to
use interpolation. We'll discuss this in a separate article.

<h2>x the repetition operator</h2>

The <hl>x</hl> operator expects a string on its left-hand side and a number on its right hand side.
It will return the string on the left hand side repeated as many times as the value on the right hand side.

<include file="examples/string_repetition.pl">

yields:

<code>
Jar Jar 
Jar Jar Binks
</code>


I think this operator is rarely used, but it can be quite useful in those rare cases.
For example when you want to add a line the same length as the title of your report:

<include file="examples/string_repetition_width.pl">

Here the line we print under the title is exactly the same lengths (in number of characters) as the title itself.

<code>
$ perl report.pl 
Please type in the title: hello
hello
-----

$ perl report.pl 
Please type in the title: hello world
hello world
-----------
</code>

<h2>++ on a string</h2>

Although one might expect the auto-increment operator (<hl>++</hl>) to
<a href="/numerical-operators">work only on numbers</a>, Perl has a special use for the <hl>++</hl> operator
on strings.

It takes the last character and increments it by one according to the ASCII table restricted to letters.
Either lower-case letter or upper-case letters. If we increment a string that ends with the letter
'y' it will change it to 'z'. If the string ends with 'z' then an increment will will change it to be
the letter 'a', but the letter to the left of it will be incremented by one as well.

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $x = "ay";
say $x;
$x++;
say $x;


$x++;
say $x;

$x++;
say $x;

$x--;
say $x;
</code>

The result is:

<code>
ay
az
ba
bb
-1
</code>

As you can see <hl>--</hl> does not work on strings.

