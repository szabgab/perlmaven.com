=title Advanced usage of Getopt::Long for accepting command line arguments
=timestamp 2015-01-02T11:20:01
=indexes Getopt::Long
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

We have already seen <a href="/how-to-process-command-line-arguments-in-perl">how to use Getopt::Long to process command line arguments</a>,
but you can do a lot more with that module.

Let's see how else can we use the <a href="https://metacpan.org/pod/Getopt::Long">Getopt::Long</a> module:

=abstract end

<h2>Simple boolean (on/off) argument</h2>

We would like to enable a boolean flag such as 
<hl>--verbose</hl>, <hl>--quiet</hl>, or <hl>--debug</hl> that just by their mere presence make an impact.
Flags that don't need an additional value.


<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);

my $verbose;

GetOption('verbose' => \$verbose);
</code>

If in the definition of the parameter we only give the name (<hl>'verbose'</hl>), Getopt::Long will treat the option as a boolean
flag. By default <hl>$verbose</hl> is <hl>undef</hl> and thus <a href="/boolean-values-in-perl">false</a>. If the user passes <hl>--verbose</hl> on the
command line, the variable <hl>$verbose</hl> will be set to some <a href="/boolean-values-in-perl">true</a> value.

Later in the code we'll see snippets like this:

<code lang="perl">
if ($verbose) {
    say "Some message";
}
</code>

That is, we'll check if <hl>$verbose</hl> is <a href="/boolean-values-in-perl">true</a> and if it is, then we print something to the console.
Of course it does not have to be some extra printing. It can be some other change in the behavior of the script.

For example in one script I have an <hl>--all</hl> flag which means, the script needs to process all the files in the the given directory.


<h2>Negatable boolean arguments</h2>

Basically they are the same as the boolean arguments except the default is
usually true and the user can turn it off from the command line

This is a special case of the boolean flags as in this case <hl>undef</hl> and <hl>0</hl> have
different meaning:

<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);

my $verbose;

GetOptions(
    'verbose!' => \$verbose,
);

if (defined $verbose) {
    say $verbose;
} else {
    say 'undef';
}
</code>

We put an exclamation mark <hl>!</hl> at the end of the flag name where we define the flags and we just printed out
the value of <hl>$verbose</hl>.

If we run the script without providing the <hl>--verbose</hl> flag, the variable will remain <hl>undef</hl>.
If we provide the <hl>--verbose</hl> flag, it will be set to be 1, a <a href="/boolean-values-in-perl">true</a> value.
So far nothing changed. The difference is that now we can supply a <hl>--noverbose</hl> flag that will set the
<hl>$verbose</hl> variable to <hl>0</hl>. In boolean context this is still <a href="/boolean-values-in-perl">false</a>
just as the <hl>undef</hl>, but now, if we want, we can differentiate in the two cases when the user did not ask for
verboseness, and when the user explicitly ask for no verboseness:

<code>
$ perl cli.pl
undef
$ perl cli.pl --verbose
1
$ perl cli.pl --noverbose
0
</code>
 
In certain situation this might be useful, though I have to admit, I have not encountered any such situation yet.

Just for the curiosity, after reading the next part I came back and wanted to see what happens if we supply both
the <hl>--verbose</hl> and the <hl>--noverbose</hl> flags to the script. The answer is that it depends on their order:

<code>
$ perl cli.pl --noverbose --verbose
1
$ perl cli.pl --verbose --noverbose
0
</code>

It would be better to avoid such craziness, but of course, you, as the author of the script
don't have control over what the user supplies on the command line.


<h2>Incremental or counting arguments</h2>

Normally Getopt::Long does not care how many times the user supplies
a boolean argument, it only cares if it was supplied at least once, or not
at all. There are cases when we would like to add meaning to the duplication of
the same boolean argument. For example, we have a debugging mechanism
with several levels of verbosity. (For example in the DBI module we can set the
TraceLevel to any number between 0-15). One way to accomplish this is to
allow the user to supply the --trace option multiple times and count how
many times it was supplied. The <hl>+</hl> sign at the end of the definition
will do this for us.

<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);

my $trace;

GetOptions(
    'trace+' => \$trace,
);

if (defined $trace) {
    say $trace;
} else {
    say 'undef';
}
</code>

The output for various calls:

<code>
$ perl cli.pl
undef
$ perl cli.pl --trace
1
$ perl cli.pl --trace --trace
2
$ perl cli.pl --trace --trace --trace
3
</code>

Actually, in this case it might be better to start with 0 as the default value: <hl>my $trace = 0;</hl>
The only difference in the result is that if we set the default to be 0 then if we don't include any <hl>--trace</hl>
on the command line we'll get 0:

<code>
$ perl cli.pl
0
</code>

which will probably simplify the code checking the value of <hl>$trace</hl> as won't have to create a special
check to make sure <hl>$trace</hl> is defined.

Then again, we could have get the same effect by accepting an option called "trace" with a numerical value:

<h2>Argument with a value</h2>

In the next example we declare two options. Both are optional, but if the user supplies either of those options, the user also has to supply
a value. The <hl>--machine</hl> option is expected to be followed by any string (which of course can be a number as well), 
the <hl>--trace</hl> option is expected to be followed by an integer. This is what the <hl>=s</hl> and the <hl>=i</hl> at the end of the
declarations mean.

If the user supplies either of those parameters without a proper value after it, the GetOptions function will print a warning and
return false. That's when the <hl>or die</hl> added to this example, will be executed.

<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);

my $trace = 0;
my $machine;

GetOptions(
    'machine=s' => \$machine,
    'trace=i' => \$trace,
) or die "Usage: $0 [--trace NUMBER] [--machine NAME]\n";

if (defined $machine) {
    say $machine;
} else {
    say 'undef';
}
say $trace;
</code>


Let's see a couple of examples with various parameters:

<code>
$ perl cli.pl
undef
0

$ perl cli.pl --trace 1
undef
1

$ perl cli.pl --trace 4
undef
4

$ perl cli.pl --trace 4 --machine big
big
4
</code>


And two examples with improper invocation:

<code>
$ perl cli.pl --trace 4 --machine
Option machine requires an argument
Usage: cli.pl [--trace NUMBER] [--machine NAME]

$ perl cli.pl --trace big
Value "big" invalid for option trace (number expected)
Usage: cli.pl [--trace NUMBER] [--machine NAME]
</code>

I think it is much more simple for the user to supply <hl>--trace 4</hl> that to write
<hl>--trace --trace --trace --trace</hl> on the command line.


<h2>Argument that can get a value (but not required to)</h2>

Also known as arguments with an <b>optional value</b>

For example we would like to allow the user to turn on/off logging to
a file and we would like to allow the user to set the name of the logfile.
If no logfilename was given, our script will print its log to STDERR.

We can accomplish this in two ways:  One of them is to have two separate
arguments: one of the arguments is to turn logging on/off, and the other one is to supply
the name of the file:

<h3>Two arguments, one depending the other</h3>

<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);

my $log;
my $logfile;
GetOptions(
	'log'        => \$log,
	'logfile=s'  => \$logfile,
) or die "Usage: $0 [--log  [--logfile FILENAME]]\n";
die "--logfile does not do anything when --log is not supplied\n" if $logfile and not $log;

if ($log) {
	if ($logfile) {
        say "logging to file $logfile\n";
		#open ...
	} else {
        say "logging to STDERR\n";
		#print STDERR ...;
	}
}
</code>

In this case we had to add an extra validation to the code, to notify the user that providing <hl>--logfile FILENAME</hl>
without turning on logging with <hl>--log</hl> does not have any meaning. Other than that we just have two flags,
one with a required <hl>=s</hl> string after it.

Running the above code with different command line parameters look like this:

<code>
$ perl cli.pl 


$ perl cli.pl --log
logging to STDERR

$ perl cli.pl --log --logfile data.log
logging to file data.log
</code>

We have the extra validation in case the user only supplies the <hl>--logfile FILENAME</hl>:

<code>
$ perl cli.pl --logfile data.log
--logfile does not do anything when --log is not supplied
</code>


If the user supplies <hl>--logfile</hl> without an argument the GetOptions will already warn about it and it
will return false that will trigger the first <hl>die</hl> command showing the "Usage" string.

<code>
$ perl cli.pl --logfile
Option logfile requires an argument
Usage: cli.pl [--log  [--logfile FILENAME]]

$ perl cli.pl --log --logfile
Option logfile requires an argument
Usage: cli.pl [--log  [--logfile FILENAME]]
</code>

<h3>Argument that can get a value (but not required to)</h3>

The same can be accomplish using an option with an optional value. We declare that by using <hl>:s</hl> at the end of the
option name as in <hl>logfile:s</hl>.

<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);

my $logfile;
GetOptions(
	'logfile:s'        => \$logfile,
) or die "Usage: $0 [--logfile [FILENAME]]\n";

if (defined $logfile) {
    if ($logfile) {
        say "logging to file $logfile\n";
		#open ...
	} else {
        say "logging to STDERR\n";
		#print STDERR ...;
	}
}
</code>

In this case we don't need the extra parameter checking, though the actual code is quite similar to what we have earlier.
We can run this script in various ways:

<code>
$ perl cli.pl

$ perl cli.pl --logfile
logging to STDERR

$ perl cli.pl --logfile data.log
logging to file data.log
</code>

The extra nice part is that because GetOptions allow the user to shorten the name of the options even this will work:

<code>
$ perl cli.pl

$ perl cli.pl --log
logging to STDERR
</code>

Where we supplied <hl>--log</hl> instead of <hl>--logfile</hl>.


