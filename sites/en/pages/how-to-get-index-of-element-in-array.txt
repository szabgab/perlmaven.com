=title  How to get the index of specific element (value) of an array?
=timestamp 2014-09-09T16:00:01
=indexes List::MoreUtils, first_index, grep
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

Recently I received two very different questions, where the answer is quite similar:

<ul>
  <li>How can we get index of particular value(element) of array quickly?</li>
  <li>Given a list of input values in <hl>@user_array</hl> how can I find their reference in the <hl>@all_values</hl> array?</li>
</ul>

=abstract end

<h2>Index of a single element</h2>

Let's see an example for the first one:

Given an array

<code lang="perl">
my @planets = qw(
   Mercury
   Venus
   Earth
   Mars
   Ceres
   Jupiter
   Saturn
   Uranus
   Neptune
   Pluto
   Charon
);
</code>

Given the value <hl>Mars</hl> we should return the number 3 because <hl>$planets[3]</hl> is <hl>Mars</hl>.

The quick answer is to use the <hl>first_index</hl> method of <a href="https://metacpan.org/pod/List::MoreUtils">List::MoreUtils</a>

<code lang="perl">
use strict;
use warnings;
use 5.010;
use List::MoreUtils qw(first_index);

my @planets = qw(
   Mercury
   Venus
   Earth
   Mars
   Ceres
   Jupiter
   Saturn
   Uranus
   Neptune
   Pluto
   Charon
);

say first_index { $_ eq 'Mars' } @planets;
</code>

<h2>Get list of indexes</h2>

Given the above array and another array listing elements we would like to return the list of the indexes of these elements:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my @planets = qw(
   Mercury
   Venus
   Earth
   Mars
   Ceres
   Jupiter
   Saturn
   Uranus
   Neptune
   Pluto
   Charon
);

use List::MoreUtils qw(first_index);

my @input = qw(Mars Pluto Alderaan Venus);

my @indexes;
foreach my $place (@input) {
    push @indexes, first_index { $_ eq $place } @planets;
}

use Data::Dumper qw(Dumper);
print Dumper \@indexes;
</code>

In this code we loop over all the planet names we would like to locate in the array of planets, and for each one
of them we call the <hl>first_index</hl> function as we did in the first example. We then use the
<a href="/debugging-perl-scripts">Data::Dumper</a> module to print out the results.

<code>
$VAR1 = [
          3,
          9,
          -1,
          1
        ];
</code>

As you can see the 3rd element of the resulting array is -1. That's because we had <a href="http://en.wikipedia.org/wiki/Alderaan">Alderaan</a>,
in the list, a planet that is was not in the original list of planets.

<h2>Implementing manually - index of one element</h2>

Though you are probably better off using the <hl>first_index</hl> function from <a href="https://metacpan.org/pod/List::MoreUtils">List::MoreUtils</a>,
it might be interesting to see how we could solve the above questions if we did not have that module available.

The code with List::MoreUtils:

<code lang="perl">
use List::MoreUtils qw(first_index);
say first_index { $_ eq 'Mars' } @planets;
</code>

The code with core perl functions:

<code lang="perl">
my ($index) = grep { $planets[$_] eq 'Mars' } (0 .. @planets-1);
say defined $index ? $index : -1;
</code>

The built-in <a href="/filtering-values-with-perl-grep">grep</a> function can filter the values of list or array based on some condition.
As we are looking for the <b>index</b> of the specific value we need to filter the potential indexes of all the elements.
The <hl>0 .. @planet-1</hl> expression generates the list of whole numbers between 0 and one less than the number of elements in the @planet array.
As the indexing of an array starts by 0, this will be the largest index available in the array.

As <hl>grep</hl> goes over all the elements of the <hl>0 .. @planets-1</hl> list, in each turn the current element will be placed in the <hl>$_</hl>
variable. The condition then checks if the element of the <hl>@planet</hl> array in that position is equal to the planet we are looking for.
In <a href="/scalar-and-list-context-in-perl">list-context</a> grep will return the list of all the values (all the indexes in our case) that passed the test.
In scalar-context it would return the number of elements passed, which is not very interesting for us. Thus we put the parentheses around the variable on the
left-hand-side <hl>my ($index) = </hl> to create the list-context. By putting only one scalar variable in the parentheses, we will only capture the first
value returned by grep. This distinction is only interesting if there could be more values in the original array matching the searched value and if we were
interested all the indexes and not just the first one.

With this the grep expression will work, but there is another difference. <hl>first_index</hl> will return -1 in case it did not find any matching value,
while <hl>grep</hl> will return an empty list and thus <hl>$index</hl> will be <a href="/undef-and-defined-in-perl">undefined</a>. This can be useful
if we later want to check if there was any value by writing:

<code lang="perl">
if (defined $index) {
    ...
}
</code>

but if we would like to imitate the same behavior as we had with <hl>first_index</hl> we can use
the <a href="/the-ternary-operator-in-perl">ternary operator</a>:

<code lang="perl">
say defined $index ? $index : -1;
</code>

<h2>Implementing manually - list of indexes</h2>

<code lang="perl">
my @idxs;
foreach my $place (@input) {
    my ($index) = grep { $planets[$_] eq $place } (0 .. @planets-1);
    push @idxs, defined $index ? $index : -1;
}
</code>

In this code we used the two lines created for the one-element version together with the ternary operator code to have -1 where
the element was not found in the <hl>@planets</hl> array

<h2>Performance issues?</h2>

One of the big differences between the <hl>first_index</hl> solutions and the manual solutions is that the <hl>first_index</hl>
function will return the index immediately when it was found, while <hl>grep</hl> will go over the list of all the planets before
returning the result. Even if the first element already matched. This is not a problem if the <hl>@planets</hl> array is very short
or if we execute this code rarely, but in other cases this might have a performance penalty.

<hl>grep</hl> is basically <hl>foreach</hl> loop, so if we look at the most recent solution, we can see that we have two loops
(a foreach loop and a grep inside it) which means the complexity of the code is O(n*m) where n is the number of elements in <hl>@planets</hl>
and m is the number of elements in <hl>@input</hl>. The <hl>first_index</hl> solution is better, but in the worst-case situation (when all the
values in the @input array are towards the end of the @planets array) the complexity is similar.

There is another solution we can use. In this solution first we create a look-up table mapping planet names to indexes.
This is the <hl>%planet_index</hl> hash. Then in the second step we create the list of the indexes corresponding to the
values in the <hl>@input</hl> array.

We use the <hl>map</hl> function of Perl to create pairs of "planet-name" => "index". We need to call
<hl>reverse</hl> on the indexes before call <hl>map</hl> in order to ensure that if the same value appears
twice in the @planets array we take the one with the smaller index. (The second assignment to the <hl>%planet_index</hl>
array will overwrite the first one.)

In the second row we call <hl>map</hl> again.  We could use a simple look-up like in this code:
<hl>my @idxs = map { $planet_index{$_} } @input;</hl>, but that would put <hl>undef</hl> for the values where the planet
does not exists. Instead of that we used the defined-or operator <hl>//</hl> introduced in perl 5.010 to put -1 instead of
any undef values.

<code lang="perl">
my %planet_index = map { $planets[$_] => $_ } reverse 0 .. @planets-1;
my @idxs = map { $planet_index{$_} // -1 } @input;
</code>

The complexity of this solution is O(n+m), which, for large m and n values is much smaller than O(m*n).

<h2>Profiling and Benchmarking</h2>

Of course computing the complexity is one thing, but actually comparing the performance of two solutions is
a totally different issue. We won't discuss the details in this article, but before any "optimization" we should
first use a profiler, probably the <a href="https://metacpan.org/pod/Devel::NYTProf">Devel::NYTProf</a> profiler
to see if this part of the application has any measurable impact on the overall performance of the application.
If yes, then we can use the <a href="https://metacpan.org/pod/Benchamrk">Benchmark</a> module to compare the
performance of two or more solutions.



