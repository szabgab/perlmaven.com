=title Writing Command line scripts and accepting command line parameters using Moo
=timestamp 2015-05-01T10:00:01
=indexes Moo, MooX::Options
=books moo
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

The organic way a Perl script grows usually starts with a few statements, conditionals and loops. Sometimes this
can grow to hundreds or even thousands of lines. Then parts of the script might slowly be turned into functions,
but it is usually very painful.

Moo can help us, even when we write a single script, without modules, to write much cleaner code.

Of course these scripts will also need to accept parameters from the command line. Let's see how can this be done.

=abstract end

<h2>A single-file script using Moo</h2>

Let's save the following code as <hl>process.pl</hl>:

<code lang="perl">
use Moo;
use 5.010;

sub run {
    my ($self) = @_;
    say 'Processing ...';
}

main->new->run;
</code>

We don't create a separate file with a separate module, but we <hl>use Moo</hl> right at the beginning of the script.
(Of course we could have added a sh-bang line like this: <hl>#!/usr/bin/env perl</hl> as the first line of the script,
but that is only needed if we would like to turn the script into executable on Linux/Unix.)

Every perl script already has an implicit package declaration, and the package of script is called <hl>main</hl>.
Probably because programs written in C required a function called <hl>main</hl>. By adding a <hl>use Moo;</hl>
statement at the top of our script we effectively turn our script into a class, with
<hl>use strict;</hl> and <hl>use warnings;</hl> implicitly turned on.

We put our main code in a method, in our case called <hl>run</hl>, and then we create an instance of this
<hl>main</hl> class by calling the <hl>new</hl> constructor. It returns an object and then we call the <hl>run</hl>
method of that object.

We could have written it in two separate statements like this:

<code lang="perl">
my $object = main->new;
$object->run;
</code>

but we don't really need the temporary <hl>$object</hl> variable.

When we run this script using <hl>perl process.pl</hl> The output will be <hl>Processing ...</hl>

<h2>Attributes and command line options</h2>

We will need variables in our script, but as we try to use the declarative
style that comes with Moo, we declare an object-attribute in the main class.
This is done exactly the same way we declared attributes in a regular
Moo-based class: <hl>has file => (is => 'ro', required => 1);</hl>

The script will require a file to work on.

The name of the file needs to be passed to the constructor.
It might be hard-coded in the script, in which case we would write:
<hl>main->new(file => 'data.txt')->run;</hl>, but most command line scripts
are supposed to get the parameters from the user passed in on the command line.

So instead of hard-coding the name of the file we expect it to be the parameter.
We can the write:

<code lang="perl">
my $file = shift or die "Usage: $0 FILE\n";

main->new(file => $file)->run;
</code>

This will get the first element from the <hl>@ARGV</hl> created from the command line,
and pass it to the constructor.

The new version of the script looks like this:

<code lang="perl">
use Moo;
use 5.010;

has file => (is => 'ro', required => 1);

sub run {
    my ($self) = @_;
    say 'Processing ' . $self->file;
}

my $file = shift or die "Usage: $0 FILE\n";

main->new(file => $file)->run;
</code>

If we run it without any parameters: <hl>perl process.pl</hl> we will get <hl>Usage: process.pl FILE</hl>
and if we provide a parameter <hl>perl process.pl data.txt</hl> it will print <hl>Processing data.txt</hl>.

So far it looks ok, though there is some duplication in our code. Actually we had to mention <hl>file</hl>
3 times in our code. In the <hl>has</hl> statement, in the <hl>shift</hl>, and when passing it to the constructor.

Our code is not <a href="https://en.wikipedia.org/wiki/Don%27t_Repeat_Yourself">DRY</a>.

We'll have to deal with that later, but let's look at another problem:

<h2>Add an optional parameter</h2>

In most scripts we will want more than one parameter. For example often we might want to be able to
turn on some debugging or verbose mode to see what our script does. So let's add an optional
attribute (it is not <hl>required</hl>) called <hl>verbose</hl>. We need to declare it:
<hl>has verbose => (is => 'ro');</hl>, we need to receive it from the command line along
with the other parameter: <hl>my ($file, $verbose) = @ARGV;</hl>, and finally
we need to pass it to the constructor:
<h>main->new(file => $file, verbose => $verbose)->run;</hl>.

Again, we had to repeat ourself 3 times, but probably worse than that,
the growing number of command line parameters require us to remember their order.
It won't be easy to add another parameter after the optional one, and it is totally
not obvious what the <hl>1</hl> means on the command line.
(It means <a href="/boolean-values-in-perl">TRUE</a>.)

The new version of the script looks like this:

<code lang="perl">
use Moo;
use 5.010;

has verbose => (is => 'ro');
has file    => (is => 'ro', required => 1);

sub run {
    my ($self) = @_;
    if ($self->verbose) {
        say 'Processing ' . $self->file;
    }
}

my ($file, $verbose) = @ARGV;
die "Usage: $0 FILE [1]\n" if not $file;

main->new(file => $file, verbose => $verbose)->run;
</code>

<hl>perl process.pl</hl> will print <hl>Usage: process.pl FILE [1]</hl>

<hl>perl process.pl data.txt</hl> will be silent.

<hl>perl process.pl data.txt 1</hl> will print <hl>Processing data.txt</hl>.

So will <hl>perl process.pl data.txt 42</hl> by the way.


<h2>Two big problems</h2>

We have two major issues:

<ol>
<li>Every new option has to be dealt with in 3 places.</li>
<li>The positional nature of the options does not provide enough flexibility.</li>
</ol>

The latter could be dealt with <a href="http://metacpan.org/module/Getopt::Long">Getopt::Long</a>,
but using Moo we have a better way to handle this.

The generally accepted solution to the problem of the positional arguments is to use named options.
Each option will have a name preceded by a dash. Either a single-character name preceded by a single dash like this:

<hl>perl process.pl -f data.txt</hl>

or a long-name preceded by two dashes like this:

<hl>perl process.pl --file data.txt</hl>

Options that can be either true or false (as in the case of the <hl>$verbose</hl> value, can be set by supplying
a name without any value: <hl>-v</hl> for the single-character version, or <hl>--verbose</hl> for the long version.


<h2>Command line options using MooX::Options</h2>

<a href="https://metacpan.org/pod/MooX::Options">MooX::Options</a> is an extension of <a href="https://metacpan.org/pod/Moo">Moo</a>
that solves problem 2 above. Let's see the new version of our code. Still not perfect, but a step in the right direction:

<code lang="perl">
use Moo;
use MooX::Options;
use 5.010;

option verbose => (is => 'ro');
option file    => (is => 'ro', required => 1);

sub run {
    my ($self) = @_;
    if ($self->verbose) {
        say 'Processing ' . $self->file;
    }
}

main->new_with_options->run;
</code>

After loading the module with <hl>use MooX::Options;</hl>, we replaced the <hl>has</hl> statements by
<hl>option</hl> statements. The <hl>option</hl> statements do everything the <hl>has</hl> statements do,
but they also tell MooX::Options to accept these words as the names of command line options.

In the rest of the code we don't need to deal with <hl>@ARGV</hl> directly, and instead of calling
<hl>new</hl>, we call the <hl>new_with_options</hl> method added by MooX::Options.

The result is, that if we run <hl>perl process.pl</hl> we get the following output:

<code>
file is missing
USAGE: process.pl [-h] [long options...]
    --file       no doc for file
    --verbose    no doc for verbose
    -h --help    show this help message
</code>

That looks interesting. Without doing anything further we already
have a skeleton usage page including a <hl>--help</hl> flag!

If we follow the hint in the usage page we'll run
<hl>perl process.pl --file data.txt</hl> that will print nothing. Which is expected of course.
If we also add the <hl>--verbose</hl> flag and run <hl>perl process.pl --file data.txt --verbose</hl>
we get a strange result: <hl>Processing 1</hl>. After referring to the documentation we realize
that we still have to tell MooX::Options that the <hl>--file</hl> option expects a value after it, while
the <hl>--verbose</hl> does not. Actually, the default is that it expects no value, and thus when we
supplied the <hl>--file</hl> flag it set the <hl>file</hl> attribute to the value <hl>1</hl> (meaning TRUE).

Loading MooX::Options adds a few more keywords to the syntax of Moo. Specifically we can
now add <h2>format => 's'</h2> to the declaration of <hl>file</hl> that means we are expecting
a string value after the name.

The full declaration will then look like this:

<code lang="perl">
option file    => (is => 'ro', required => 1, format => 's');
</code>

Running <hl>perl process.pl --file data.txt --verbose</hl> will now yield:
<hl>Processing data.txt</hl> as expected.

Unfortunately the output of <hl>perl process.pl</hl> does not change,
even though I think it could have indicated that <hl>--file</hl> expects a value.
(Feature request submitted.)

In addition to <hl>s</hl>, the <hl>format</hl> option can also get the value <hl>i</h>
indicating an integer or <hl>f</hl> indicating a floating point number.
(Thought it seems f also accepts values such as 1.2.3.4.5. Bug reported.)

<h2>Add documentation</h2>

As the help output indicates, there is a way to add documentation
to the options. MooX::Options provides the <hl>doc</hl> keyword that allows
us to supply a short description for each option:

<code lang="perl">
option verbose => (is => 'ro', doc => 'Print details');
option file    => (is => 'ro', required => 1, format => 's',
    doc => 'File name to be processed');
</code>

Running <hl>perl process.pl --help</hl> will then print:

<code>
USAGE: process6.pl [-h] [long options...]
    --file       File name to be processed
    --verbose    Print details
    -h --help    show this help message
</code>

Much nicer, and all the information about the options are in a single place.
Our code is <a href="https://en.wikipedia.org/wiki/Don%27t_Repeat_Yourself">DRY</a>.

<h2>Attributes that are not options</h2>

In addition to the two attributes that can provided as command line arguments,
our code might require other attributes that are not filled from the command line.
All those attributes can be still declared using the <hl>has</hl> statement described earlier:

For example: <hl>has counter => (is => 'rw', default => 0);</hl> will add an attribute
to count something but it will not appear in the help output,
and if we supply it on the command
line:  <hl>perl process7.pl --file data.txt --verbose --counter</hl> we will get
an <hl>Unknown option: counter</hl> error and usage explanation.

<h2>Multiple values</h2>

It is also possible to tell MooX::Options that our script will accept the same flag multiple times.
For example we would like to get a list of IP addresses. We do that by adding a <hl>@</hl>
character to the <hl>format</hl> like this:

<code lang="perl">
option ips => (is => 'ro', doc => 'IP addresses', format => 's@');
</code>

Using Data::Dumper we then print out the content of <hl>ips</hl> attribute:

<code lang="perl">
use Data::Dumper qw(Dumper);
sub run {
    my ($self) = @_;
    if ($self->verbose) {
        say 'Processing ' . $self->file;
        say Dumper $self->ips;
    }
}
</code>

Let's see what is the output:

<code>
$ perl process7.pl --file data.txt --verbose
Processing data.txt
$VAR1 = undef;
</code>

<code>
$ perl process7.pl --file data.txt --verbose --ip 1.1.1.1
Processing data.txt
$VAR1 = [
          '1.1.1.1'
        ];
</code>

<code>
$ perl process7.pl --file data.txt --verbose --ip 1.1.1.1 --ip 2.2.2.2
Processing data.txt
$VAR1 = [
          '1.1.1.1',
          '2.2.2.2'
        ];
</code>

The nice part is that we can actually supply a name shorter than the full
name of the attribute: <hl>--ip</hl> instead of <hl>--ips</hl> which looks
much more intuitive in our case.

When we supply <hl>--ip</hl> one or more times, we get an ARRAY ref
with the list of values. On the other hand, when there are no values, the
attribute <hl>ips</hl> will remain <hl>undef</hl>.


This means when we need to write code to processes all the IPs we'll have to write
something like this:

<code lang="perl">
if ($self->ips) {
    foreach my $ip (@{ $self->ips }) {
        say $ip;
    }
}
</code>

We have to protect the loop by an <hl>if-statement</hl> so we don't get a
<hl>Can't use an undefined value as an ARRAY reference at ...</hl> error.

In some cases the above <hl>if-statement</hl> is necessary anyway, but sometimes
we really just want to iterate over all the elements. In that case it is
probably better to set i<a href="/moo-attributes-with-default-values">the default value</a>
of the  <hl>ips</hl> attribute to be an empty array. (Remember, we have to use an anonymous
function to do this.)

<code lang="perl">
option ips => (is => 'ro', doc => 'IP addresses', format => 's@',
    default => sub { [] } );
</code>

Then the code handling the IP addresses will be simplified to the loop:

<code lang="perl">
foreach my $ip (@{ $self->ips }) {
    say $ip;
}
</code>

<h2>The full example</h2>

Let's conclude this article with the full example we have developed:

<code lang="perl">
use Moo;
use MooX::Options;
use 5.010;
use Data::Dumper qw(Dumper);

option verbose => (is => 'ro', doc => 'Print details');
option file    => (is => 'ro', required => 1, format => 's',
    doc => 'File name to be processed');

has counter => (is => 'rw', default => 0);
option ips => (is => 'ro', doc => 'IP addresses', format => 's@', default => sub { [] } );

sub run {
    my ($self) = @_;
    if ($self->verbose) {
        say 'Processing ' . $self->file;
        say Dumper $self->ips;
        foreach my $ip (@{ $self->ips }) {
            say $ip;
        }
    }
}

main->new_with_options->run;
</code>

<h2>Exercise 1</h2>

Allow the user to supply the list of IP addresses as a comma separated list with a single
<hl>--ips</hl>.

<h2>perl process.pl --file data.txt --verbose --ips 1.1.1.1,2.2.2.2</h2>

Please note, it seems due to a bug we need to use the full name of the attribute here <hl>--ips</hl>
as <hl>--ip</hl> won't work. (The bug was reported against v3.83 of MooX::Options.)

<h2>Exercise 2</h2>

Allow the use of the short name for the <hl>verbose</hl> option as <hl>-v</hl>

<hl>perl process8.pl --file data.txt  -v --ips 1.1.1.1,2.2.2.2</hl>

The source code of the most recent version of this script can be found in
<a href="https://github.com/PerlMaven/moo-with-moox-options">GitHub</a>.
You are more than welcome to fork that repository, send your solution as a pull
request and comment on other peoples solution.


