=title Scalar variables
=timestamp 2013-04-20T17:45:56
=indexes strict, my, undef, say, +, x, ., sigil, $, @, %, FATAL warnings
=tags screencast
=status show
=books beginner
=author szabgab
=comments_disqus_enable 1

=abstract start

In this part of the <a href="/perl-tutorial">Perl Tutorial</a>, we are going to take a
look at the data structures available in Perl and how we can use them.

In Perl 5 there are basically 3 data structures. <b>Scalars, arrays and hashes</b>. The latter is also
known as dictionaries, look-up tables or associative arrays in other languages.

=abstract end

<iframe width="640" height="510" src="https://www.youtube.com/embed/4jyGQPGXOK0?cc_load_policy=1" frameborder="0" allowfullscreen></iframe>

Variables in Perl are always preceded with a sign called a <b>sigil</b>. These signs are <hl>$</hl> for scalars,
<hl>@</hl> for arrays, and <hl>%</hl> for hashes.

A scalar can contain a single value such as a number or a string. It can also contain a
reference to another data structure that we'll address later.

The name of the scalar always starts with a <hl>$</hl> (dollar sign) followed by letters, numbers and underscores.
A variable name can be <hl>$name</hl> or <hl>$long_and_descriptive_name</hl>. It can also be
<hl>$LongAndDescriptiveName</hl> which is often referred-to as the "CamelCase",
but the Perl community usually prefers the all-lower case variables with underscores separating the words in the name.

As we are always using <b>strict</b>, we always have to first declare our variables using <b>my</b>.
(Later you will also learn about <b>our</b> and some other ways, but for now let's stick to the <b>my</b> declaration.)
We can either assign a value immediately like in this example:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $name = "Foo";
say $name;
</code>

or we can declare the variable first and assign only later:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $name;

$name = "Foo";
say $name;
</code>

We prefer the former if the logic of the code allows it.

If we declared a variable, but have not assigned a value yet then it has a
value called <a href="/undef-and-defined-in-perl">undef</a> which is similar to <b>NULL</b> in databases,
but which has slightly different behavior.

We can check if a variable is <hl>undef</hl> or not using the <hl>defined</hl> function:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $name;

if (defined $name) {
  say 'defined';
} else {
  say 'NOT defined';
}

$name = "Foo";

if (defined $name) {
  say 'defined';
} else {
  say 'NOT defined';
}

say $name;
</code>

We can set a scalar variable to be <hl>undef</hl> by assigning <hl>undef</hl> to it:

<code lang="perl">
$name = undef;
</code>

The scalar variables can hold either numbers or strings. So I can write:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $x = "hi";
say $x;

$x = 42;
say $x;
</code>

and it will just work.

How does that work together with operators and operator overloading in Perl?

In general Perl works in the opposite way than other languages. Instead of the operands telling
the operator how to behave, the operator tells the operands how they should behave.

So if I have two variables that have numbers in them then the operator decides if they
really behave like numbers or if they behave like strings:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $z = 2;
say $z;             # 2
my $y = 4;
say $y;             # 4

say $z + $y;        # 6
say $z . $y;        # 24
say $z x $y;        # 2222
</code>

<hl>+</hl>, the numerical addition operator, adds two numbers, so both <hl>$y</hl> and <hl>$z</hl> act like numbers.

<hl>.</hl>, concatenates two strings, so both <hl>$y</hl> and <hl>$z</hl> act like strings. (In other languages you might call this the string addition.)

<hl>x</hl>, the repetition operator, repeats the string on the left hand side as many times as the number on the right hand side,
so in this case <hl>$z</hl> acts as a string, and <hl>$y</hl> acts as a number.

The results would be the same if they were both created as strings:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $z = "2";
say $z;             # 2
my $y = "4";
say $y;             # 4

say $z + $y;        # 6
say $z . $y;        # 24
say $z x $y;        # 2222
</code>

Even if one of them was created as a number, and the other one as a string:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $z = 7;
say $z;             # 7
my $y = "4";
say $y;             # 4

say $z + $y;        # 11
say $z . $y;        # 74
say $z x $y;        # 7777
</code>

Perl automatically converts numbers to strings and strings to numbers
as required by the operator.

We call numerical and string <b>contexts</b>.

The above cases were easy. When converting a number to a string it is just as if
you put quotes around it. When converting a string to a number there are simple
cases as we saw, when all the string consists of just digits. The same would happen
if there was a decimal dot in the string, such as in <hl>"3.14"</hl>.
The question is: What if the string contained characters that are not part of any number? e.g <hl>"3.14 is pi"</hl>.
How would that behave in a numerical operation (aka. numerical context)?

Even that is simple, but it might need some explanation.

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $z = 2;
say $z;             # 2
my $y = "3.14 is pi";
say $y;             # 3.14 is pi

say $z + $y;        # 5.14
say $z . $y;        # 23.14 is pi
say $z x $y;        # 222
</code>

When a string is in numerical context Perl looks at the left side of the string,
and tries to convert it to number. As long as it makes sense, that part becomes the
numerical value of the variable. In numerical context (<hl>+</hl>) the string
<hl>"3.14 is pi"</hl> is regarded as the number <hl>3.14</hl>.

In a way it is completely arbitrary, but that's how it behaves so we live with that.

The above code will also generate a warning on the standard error channel (<hl>STDERR</hl>):

<code>
Argument "3.14 is pi" isn't numeric in addition(+) at example.pl line 10.
</code>

assuming you used <b>use warnings</b> which is highly recommended.
Using it will help you notice when something is not exactly as expected.
Hopefully the result of <hl>$x + $y</hl> is now clear.

<h2>Background</h2>

To be sure, Perl did not convert <hl>$y</hl> to 3.14. It just used the numerical
value for the addition.
This probably explains the result of <hl>$z . $y</hl> as well.
In that case Perl is using the original string value.

You might wonder why <hl>$z x $y</hl> shows 222 while we had 3.14 on the right hand side,
but apparently perl can only repeat a string along whole numbers... In the operation
perl silently rounds the number on the right hand side. (If you are really into
deep thinking, you can recognize that the "number" context mentioned earlier has actually
several sub-contexts, one of them is "integer" context. In most cases perl does what
would seem "the right thing" for most people who are not programmers.)

Not only that, but we don't even see the warning of the
"partial string to number" conversion that we saw in the case of <hl>+</hl>.

This is not because of the difference in the operator. If we comment out the addition
we will see the warning on this operation. The reason for the lack of a second warning
is that when perl generated the numerical value of the string <hl>"3.14 is pi"</hl> it
also stored it in a hidden pocket of the <hl>$y</hl> variable. So effectively <hl>$y</hl>
now holds both a string value and a number value, and will use the right one in any
new operation avoiding the conversion.

There are three more things I'd like to address. One is the behavior of a variable with
<hl>undef</hl> in it, the other one is <b>fatal warnings</b> and the third one is avoiding
the automatic "string to number conversion".

<h2>undef</h2>

If in a variable I have <hl>undef</hl> which most people would refer to as "nothing", it can still be used.
In numerical context it will act as 0 in string context it will act as the empty string:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $z = 3;
say $z;        # 3
my $y;

say $z + $y;   # 3
say $z . $y;   # 3

if (defined $y) {
  say "defined";
} else {
  say "NOT";          # NOT
}
</code>

With two warnings:

<code>
Use of uninitialized value $y in addition (+) at example.pl line 9.

Use of uninitialized value $y in concatenation (.) or string at example.pl line 10.
</code>

As you can see the variable is still <hl>undef</hl> at the end and thus the conditional
will print "NOT".


<h2>Fatal warnings</h2>

The other thing is that some people prefer that the application will throw a
hard exception instead of the soft warning. If that's your thing, you could change the beginning of the script and write

<code lang="perl">
use warnings FATAL =&gt; "all";
</code>

Having that in the code, the script will print the number 3, and then throw an exception:

<code>
Use of uninitialized value $y in addition (+) at example.pl line 9.
</code>

This is the same message as was the first warning, but this time the script stops running.
(Unless, of course the exception is caught, but we'll talk about that another time.)

<h2>Avoiding the automatic string to number conversion</h2>

If you would like to avoid the automatic conversion of strings when there is no exact conversion,
you could check if the string looks like a number when you receive it from the outside world.

For this we are going to load the module <a href="https://metacpan.org/pod/Scalar::Util">Scalar::Util</a>,
and use the subroutine <hl>looks_like_number</hl> it supplies.

<include file="examples/fatal_warning.pl">


<h2>operator overloading</h2>

Finally, you could actually have operator overloading in which case
the operands would tell what the operators should do, but let's
leave that as an advanced topic.


