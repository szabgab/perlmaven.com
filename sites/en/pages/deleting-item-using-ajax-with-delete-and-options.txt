=title Deleting item using Ajax request with DELETE and OPTIONS
=timestamp 2016-07-29T10:10:01
=indexes Access-Control-Allow-Methods, OPTIONS, DELETE, remove
=status show
=books dancer2
=author szabgab
=archive 1
=comments_disqus_enable 0

=abstract start

We can now <a href="/add-and-retreive-items-jquery-ajax">add new elements and list the existing elements</a> from our list of items, but how can we delete an element?

For this we need another ajax request that will send in the ID of the item. For that we'll have to create a button on the HTML page that already has this ID. Before doing the front-end though, let's add the API call for deleting an item.

=abstract end

I could send another <hl>GET</hl> request for this, but if I understand <a href="http://en.wikipedia.org/wiki/Representational_state_transfer">REST</a> correctly, this operation should be a <hl>DELETE</hl> request.


<h2>Travis-CI</h2>

Before going on I wanted to turn on <a href="https://travis-ci.org/">Travis-CI</a> for Continuous Integration and testing
in two commits:
<a href="https://github.com/szabgab/D2-Ajax/commit/c89a071c6919369eebdb88c2c297b10974845bd1">commit 1</a>
and
<a href="https://github.com/szabgab/D2-Ajax/commit/e86b05b8c7e4480319311cbb5dc48ba7b41f60c6">commit 2</a>

<code>
branches:
  except:
    - gh-pages
language: perl
perl:
  - "5.20"
  - "5.18"
  - "5.16"
  - "5.14"
  - "5.12"
  - "5.10"
before_install:
  - cpanm --notest MongoDB
services:
  - mongodb
</code>

Check out the post of Neil Bowers for more details on <a href="http://blogs.perl.org/users/neilb/2014/08/try-travis-ci-with-your-cpan-distributions.html">how to set up Travis-CI</a>.

<h2>Refactoring the code</h2>

Then, before I implemented the <hl>DELETE</hl> feature I felt I have to do a little refactoring.
We had two places where we connected to MongoDB and accessed the collection. I wanted to unite
them and hide them in a single function. Hence I created the <hl>_mongodb</hl> function:

<code lang="perl">
sub _mongodb {
    my ($collection) = @_;

    my $client = MongoDB::MongoClient->new(host => 'localhost', port => 27017);
    my $db   = $client->get_database( config->{app}{mongodb} );
    return $db->get_collection($collection);
}
</code>

and in the two places where it was used, I replaced the same code snippet with a call to

<code lang="perl">
my $items = _mongodb('items');
</code>

<h2>The back-end in Dancer</h2>

<a href="/dancer">Dancer</a> supports the implementation of the <hl>DELETE</hl> <a href="http://en.wikipedia.org/wiki/Hypertext_Transfer_Protocol#Request_methods">HTTP method</a>, but instead of using the <a href="/search/delete">delete</a> word, which is an existing built-in function in perl, it used the <hl>del</hl>
keyword.

Here we expect that the client will send in a url that looks like this: <hl>/api/v2/item/913397349743</hl>,
the last part being the OID of the object in the MongoDB. In MongoDB every document has a unique ID in the <hl>_id</hl>
field. If we don't supply one, MongodDB will create one for us. If you recall in the earlier articles when we wrote
the tests, we had to disregard these ids, because they are totally unpredictable and they change from test-run to test-run.

The <hl>param</hl> function of Dancer will return the string that was the last part of that URL which is expected to be the ID.

Then we call <hl>_mongodb('items')</hl> that we have just created with the refactoring and we call the <hl>remove</hl>
method passing the filter that will locate the document with the specific ID.

After that we return a hash reference indicating success.

As far as I understood, the current MongoDB driver for Perl won't be able to tell if there was anything deleted or
if the supplied ID did not match anything, though we could add some more robust error reporting.
For now I think we'll live with this code.

<hl>lib/D2/Ajax.pm</hl>

<code lang="perl">
del '/api/v2/item/:id' => sub {
    my $id = param('id');

    my $items = _mongodb('items');
    $items->remove({ _id => MongoDB::OID->new($id) });

    my $json = JSON::MaybeXS->new;
    return to_json { ok  => 1 };
};
</code>

<h2>Testing the API</h2>

Implementing the API without any test would not be full, so I edited <hl>t/v2.t</hl>
and added the following code to the <hl>v2_items</hl> subtest:

<code lang="perl">
    my @items = ("One 1", "Two 2", "Three 3");
    foreach my $it (@items) {
        my $res = $test->request( POST '/api/v2/item', { text => $it });
        is_deeply decode_json($res->content), { ok => 1, text => $it };
    }
    my $get5  = $test->request( GET '/api/v2/items');
    my $items5 = decode_json($get5->content);
    is scalar @{$items5->{items}}, 5;

    my $del3  = $test->request( DELETE '/api/v2/item/' . $items5->{items}[3]{'_id'}{'$oid'} );
    is $del3->content, '{"ok":1}';

    my $get6  = $test->request( GET '/api/v2/items');
    my $items6 = decode_json($get6->content);
    is scalar @{$items6->{items}}, 4;
    is_deeply $items5->{items}[0], $items6->{items}[0];
    is_deeply $items5->{items}[1], $items6->{items}[1];
    is_deeply $items5->{items}[2], $items6->{items}[2];
    is_deeply $items5->{items}[4], $items6->{items}[3];
    is_deeply $items5->{items}[5], $items6->{items}[4];
</code>

At first this code adds 3 more items checking if they were added successfully.
Then it sends a "DELETE" request using the ID of the 4th element (index 3)
and checks if the returned JSON string represents the
expected <hl>{ ok => 1 }</hl> data structure from Perl.

Then we fetch the list of items again, check if there are only 4 left  and check if the 4 that were
left match the 4 we expected to be there. (Without the element we have deleted.)

This was not enough to run the tests though. Apparently
<a href="https://metacpan.org/pod/HTTP::Request::Common">HTTP::Request::Common</a> exports
<hl>GET</hl> and <hl>POST</hl> automatically, but if we also want to use <hl>DELETE</hl>
we need to ask for it. Hence we also had to change the use-statement at the top of the test script to be:

<code lang="perl">
use HTTP::Request::Common qw(GET POST DELETE);
</code>

After updating the "test plan" as well, the test ran successfully.

<a href="https://github.com/szabgab/D2-Ajax/commit/14112caddf8afda7c6c1f79a506e52148f0428df">commit</a>

<h2>Client side for DELETE-ing</h2>

Then I set out to implement the client-side code for deleting an item.
The first thing I had to do is to add a "delete" button next to each item that will
also hold the ID of that item. Luckily the Ajax request that retrieves the list of items
also sends over the ID of each item. We can modify the HTML we generate to include a button:

This is the new code:

<code lang="javascript">
for (i = 0; i < data["items"].length; i++) {
    html += '<li>' + data["items"][i]["text"] + '<button class="delete" data-id="' +  data["items"][i]["_id"]["$oid"]  + '">x</a></li>';
}
</code>

This will add a <hl>button</hl> element at the end of each item with two attributes. One of them is <hl>class="delete"</hl>
will make it easy to locate all the delete buttons, the other one <hl>data-id="...."></hl> holds the id of the current element.

It will look like this:

<img src="/img/dancer2_ajax_list_with_delete.png">

Once the new HTML is injected in the existing page we also call

<code lang="javascript">
    $(".delete").click(delete_item);
</code>

this will attach the <hl>delete_item</hl> function to each on of the buttons that are in the "delete" class.

The new code generating the HTML looks like this:

<code lang="javascript">
function show_items() {
    jQuery.get('http://127.0.0.1:5000/api/v2/items', function(data) {
        var i, html;
        html  = '<ul>';
        console.log(data);
        for (i = 0; i < data["items"].length; i++) {
            html += '<li>' + data["items"][i]["text"] + '<button class="delete" data-id="' +  data["items"][i]["_id"]["$oid"]  + '">x</a></li>';
        }
        html += '</ul>';
        $("#items").html(html);
        $(".delete").click(delete_item);
    });
}
</code>

Now we only need to implement the <hl>delete_item</hl> function:

<code lang="javascript">
function delete_item() {
    var id = $(this).attr('data-id');
    jQuery.ajax({
        url: 'http://127.0.0.1:5000/api/v2/item/' + id,
        type: 'DELETE',
        success: function(data) {
            show_items();
        }
    });
}
</code>

It is a simple Ajax call just like the <hl>get</hl> and the <hl>post</hl> calls we saw earlier, but jQuery does not have the nice syntactic sugar for sending a delete request and thus we had to write a few more lines. Nevertheless, here too we have a call-back that will be executed once the server replied. The only thing the callback does is calling the <hl>show_items</hl> function.
Actually I could have written <hl>success: show_items</hl> as well.

It was time to try it in the browser. I loaded the page, and clicked on one of the <hl>x</hl> buttons and nothing happened.

Looking at the console I saw this error:

<code>
XMLHttpRequest cannot load http://127.0.0.1:5000/api/v2/item/556db39fa114604bac0757d1. No 'Access-Control-Allow-Origin' header is present on the requested resource. Origin 'null' is therefore not allowed access. The response had HTTP status code 404.
</code>

This baffled me for quite some time, after all I've already <a href="/stand-alone-ajax-client">fixed the Access-Control-Allow-Origin</a> issue.

Looking at the error message more could have given me a clue, but it didn't.
The clue would been the fact the I also had <hl>The response had HTTP status code 404.</hl> which means the server could not find
the route and returned a 404 which does not have the Access-Control-Allow-Origin header.

There was also another clue I missed. In the console, just before this error, there was another error:

<code>
OPTIONS http://127.0.0.1:5000/api/v2/item/556db39fa114604bac0757d1
</code>

which shows that jQuery, instead of sending the <hl>DELETE</hl> request, it sends an <hl>OPTIONS</hl> request.

That was not enough for me. I looked at the console where I run the <hl>plackup -R lib bin/app.psgi</hl> which said,
among many other things:

<code>
looking for options /api/v2/item/556db39fa114604bac0757d1
</code>

That further confused me. Why is Dancer looking for the "options" HTTP method while I requested the "delete" method?

Apparently this is due to some safety feature of Chrome when using cross domain requests. If in a cross-domain request
we ask for anything else than basic GET or POST, Chrome will first send an "OPTIONS" request to the same URL and will look for the
<hl>Access-Control-Allow-Methods</hl> header that needs to list the HTTP methods the server will accept. The content of the response of that
request can be empty, just the header needs to be set. So I added


<code lang="perl">
        header 'Access-Control-Allow-Methods' => 'GET, POST, OPTIONS, DELETE';
</code>

to the <hl>before</hl> hook so it will send it on every request in the v2 API, and I've also added a route:

<code lang="perl">
options '/api/v2/item/:id' => sub {
    return '';
};
</code>

After that the "delete" buttons started to function as I expected.

<a href="https://github.com/szabgab/D2-Ajax/commit/f0c2bf2d3a6a716726afdea7d8b421ca27050984">commit</a>


<h2>Updating the tests</h2>

Now both the client work and our tests are still passing, but I think I'd better update the tests to check
the latest changes to the back-end code.

I've added the test code:

<code lang="perl">
    my $options  = $test->request( OPTIONS '/api/v2/item/anything' );
    ok $options->is_success, '[POST /] successful';
    is $options->header('Access-Control-Allow-Methods'), 'GET, POST, OPTIONS, DELETE';

</code>

and wanted to import the <hl>OPTIONS</a> method from <a href="https://metacpan.org/pod/HTTP::Request::Common">HTTP::Request::Common</a>,
but apparently it does not support it. There is even an <a href="https://rt.cpan.org/Ticket/Display.html?id=68644">open ticket</a> from 2011.

For now I am going to leave the test in, commented out and include the link to the ticket. Later we might revisit the issue.

<a href="https://github.com/szabgab/D2-Ajax/commit/bd94d5c1c0b1ccf228de43b704c2cf5d14e9de76">commit</a>

<h2>Comments</h2>

Hello, sir I try to delete the product id from session, it already deleted the data, but I still have problem that the data is still keep the old data until we click to other pages that it will not show on the view page. How make it smoothly to get new data after we deleted?

         function clearCart()
{
    var msgClear = confirm ("Are you sure you want to clear carts list?");
    if (msgClear) {
        $.ajax({
            type: "POST",
            url: "",
            data: "",
            success: function (response) {
                alert(response);
              
            }
        });
    }
}


