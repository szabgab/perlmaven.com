=title Length of an array in Perl
=timestamp 2014-11-26T08:20:56
=description In Perl the length function is only used for strings. In order to get the length of an array use the scalar function.
=indexes length, scalar
=status show
=books beginner
=author szabgab
=comments_disqus_enable 0

=abstract start

In Perl the <b>length</b> function is only used for strings (scalars). In order to get the length of an array use the <b>scalar</b> function,
or put the array in SCALAR context by some other means.

=abstract end

See this example and note the printed results are shown in the comments:

<code lang="perl">
use 5.010;

my $one_string = "hello world";
say length $one_string;    # 11

my @many_strings = ("abc", "cd", "e", "fg", "hi", "hello world");
say length @many_strings;  # 1
say scalar @many_strings;  # 6

say scalar $one_string;    # hello world
</code>

As you can see the <hl>length</hl> function worked on a scalar variable (one starting with a <hl>$</hl> sign) but it gave incorrect result
when it was used for an <a href="/perl-arrays">array</a> (starting with a <hl>@</hl> sign).

On the other hand, using the <hl>scalar</hl> function on the array worked well and <hl>say scalar @many_strings;</hl> printed the number of elements
in the array.
Probably even more strangely using <hl>scalar</hl> on a scalar variable got us printing the content of the variable.


So what happened there? Why did <hl>say length @many_strings;</hl> print 1 and why did <hl>say scalar $one_string;</hl> print "hello world"?

<h2>How to avoid?</h2>

Before trying to explain with my own words let's see what does Perl say?
What if we add <hl>use strict;</hl> and <hl>use warnings</hl> to the beginning of the script just as
I recommend in the <a href="/installing-perl-and-getting-started">first episode</a> of the <a href="/perl-tutorial">Perl tutorial</a>.

Then this is the output:

<code>
length() used on @many_strings (did you mean "scalar(@many_strings)"?) at ... line 10.
11
1
6
hello world
</code>

If you also turn on <a href="/use-diagnostics-or-splain">diagnostics</a> then this will be the output:

<code>
length() used on @many_strings (did you mean "scalar(@many_strings)"?) at ...  line 11 (#1)
    (W syntax) You used length() on either an array or a hash when you
    probably wanted a count of the items.
    
    Array size can be obtained by doing:
    
        scalar(@array);
    
    The number of items in a hash can be obtained by doing:
    
        scalar(keys %hash);
</code>

Please always <a href="/strict">use warnings and use strict</a>!


<h2>The explanation</h2>

Perl works differently than other languages for the good and the bad. One needs to learn how Perl thinks in order to enjoy it and to take the most out of it.
Specifically Perl has <a href="/scalar-and-list-context-in-perl">scalar and list context</a>.

The <hl>length</hl> function always works on strings and it creates <hl>SCALAR</hl> context for its parameters. Hence if we pass an array as a parameter,
that array will be placed in <hl>SCALAR</hl> context and it will return the <a href="/scalar-and-list-context-in-perl">number of elements</a> in it.
In our example the array had 6 elements thus the expression <hl>say length @many_strings;</hl> was converted to <hl>say length 6;</hl> and the length
of the number 6 is the same as the length of the string "6" which is 1. That's why <hl>say length @many_strings;</hl> printed 1.

Try this:

<code lang="perl">
#use strict;
#use warnings;
#use diagnostics;
use 5.010;

my @many_strings = ("abc", "cd", "e", "fg", "hi", "hello world", "abc", "cd", "e", "fg", "hi", "hello world");

say length @many_strings;  # 2 
say scalar @many_strings;  # 12
</code>

After deliberately turning off <hl>use strict; use warnings; use diagnostics;</hl> we created an array with 12
elements. <hl>say length @many_strings;</hl> printed 2 because that's the length of the number 12.


The other strange issue might be the fact that <hl>say scalar $one_string;</hl> printed the content of the variable
<hl>$one_string</hl>, but if you know that the <hl>scalar</hl> function only creates SCALAR context and does not
do anything else, but it already had a scalar value as a parameter so it just returned the content of that
scalar variable.

So the <hl>scalar</hl> function returns the length of an array because an array in scalar context returns its size.

<h2>Conclusion</h2>

<b>Always use strict and use warnings.</b>

