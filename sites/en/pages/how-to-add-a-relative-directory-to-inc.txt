=title How to add a relative directory to @INC
=timestamp 2015-04-13T09:50:00
=indexes Path::Tiny, path, FindBin, $Bin, File::Spec, catdir, File::Basename, dirname, Dir::Self, __DIR__, @INC, lib, rlib, FindBin::libs
=status show
=books advanced
=author szabgab
=comments_disqus_enable 1

=abstract start

When writing an application, or even "just" a module with a command line script,
it is very useful to make sure every <hl>use</hl> statement
will load the modules from a directory <b>relative</b>
to where the main perl program can be found.

This will make sure that when you are developing the code, it won't,
by accident, load the old installed versions of the modules.

If you check out several copies of the whole project from your version control system.
In each copy the scripts will use the module in the same copy. So you can work on several
changes at the same time without them interfering with each other.

=abstract end


<h2>Directory layout and use-case</h2>

A common directory layout of a project would look like this:

<code>
.../
    project/
        script/
            app.pl
            code.pl
        lib/
            App/
                Module.pm
                Other/
                    Class.pm
</code>

Though there might be some other files and directories, this is what currently interests us.

There are several executable scripts like the <hl>app.pl</hl> and the <hl>code.pl</hl> in our case.
They are all placed in the directory called <hl>script</hl> inside the project directory.
(Though some people would use the name <hl>bin</hl> instead of <hl>script</hl>, this does not matter for us.)

Each one of these scripts start like this:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use App::Module;
use App::Other::Class;

...
</code>

If we run this script it will try to load the file implementing <hl>App::Module</hl>, and later the file
implementing <hl>App::Other::Class</hl>.

In order to find <hl>App::Module</hl> perl will go over the content of
the <hl>@INC</hl> array that has a list of file-system directory paths in it.
It will look for a subdirectory called <hl>App</hl> and within that subdirectory
a file called <hl>Module.pm</hl>.

The content of <hl>@INC</hl> was set when perl was installed and while in another article we can see
<a href="/how-to-change-inc-to-find-perl-modules-in-non-standard-locations">how to change @INC</a>
in several ways, but in all those cases we assumed we know the absolute location of the module within the file-system.

In our case we would like to make sure that the <hl>app.pl</hl>
script will find the module in the <hl>lib</hl> directory next to it,
regardless of where we put the whole <hl>project</hl> directory.

We would even like to be able to have 2 or more copies of the whole tree,
and each copy of the <hl>app.pl</hl> script should find
and load the module from the directory next to it.

This is especially important during development. We might make some changes
to the code in one copy of the tree, and we might be working on a totally
different change in the other copy. Maybe even on a totally different version of the application.

So we would like to make sure each script will find the module relative to the location of the script.

In other words we would like to <b>add a directory relative to the location of the script to @INC</b>.

Let me stress that. We are not trying to add a directory relative to the <b>current working directory</b>,
but relative to the directory where the script can be found.

<h2>Solution 1 - FindBin</h2>

<code lang="perl">
use FindBin;
use File::Spec;
use lib File::Spec->catdir($FindBin::Bin, '..', 'lib');
</code>

The <hl>FindBin</hl> module has a global variable called <hl>$Bin</hl> that we can access as <hl>$FindBin::Bin</hl>.
It contains the path to the directory where the current script can be found. (In our case that would <hl>.../project/script</hl>.)
The <hl>catdir</hl> method of the <hl>File::Spec</hl> module/class can concatenate parts of a file-system path in
a platform specific way. It is almost like <a href="/join">join</a> except that the string it uses to join the parameters depends
on the Operating System. On Unix/Linux that will be a slash (<hl>/</hl>). On Windows that will be back-slash (<hl>\</hl>).
On some other operating systems it might be something else.

So <hl>File::Spec->catdir($FindBin::Bin, '..', 'lib')</hl> means take the path, go one directory up and then descend into the
<hl>lib</hl> directory.

Passing that path as a parameter to <hl>use lib</hl> will add the path
to the beginning of <hl>@INC</hl>. So our scripts will look like this:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use FindBin;
use File::Spec;
use lib File::Spec->catdir($FindBin::Bin, '..', 'lib');

use App::Module;
use App::Other::Class;

...
</code>

One of the advantages of this example is that both modules are standard.

<h2>Solution 2 - $0</h2>

In the second solution we rely on <hl>$0</hl> that always contains the name of the
current executable script.

<code lang="perl">
use File::Basename;
use File::Spec;
use lib File::Spec->catdir(
            File::Basename::dirname(File::Spec->rel2abs($0)),
            '..',
            'lib');
</code>

Because <hl>$0</hl> might contain just the name of the script we first need to
ask the currently running perl to calculate the absolute path. We do that using
the <hl>rel2abs</hl> method of the <hl>File::Spec</hl> module. Once we have the
absolute path, we get rid of the last part of the path which is the name of the file
We do that by calling the <hl>dirname</hl> function of <hl>File::Basename</hl.

Then, as before, we use <hl>catdir</hl> to go one directory up (<hl>..</hl>) and
then descend to the <hl>lib</hl> directory.

This solution too uses only standard modules.

<h2>Solution 3 - Cwd</h2>

In the above example we could replace the call <hl>File::Spec->rel2abs($0)</hl>
by a call to <hl>Cwd::abs_path($0)</hl>.
That would still work the same way.

<h2>Solution 4 - Dir::Self</h2>

Another solution I saw recently uses <a href="https://metacpan.org/pod/Dir::Self">Dir::Self</a>.
When loading, this module will add a symbol <hl>__DIR__</hl> to the name-space of the current code.
This symbol contains the absolute path to the directory where the file is located.
So we only need to compute the relative directory using <hl>catdir</hl>:

<code lang="perl">
use Dir::Self;
use lib File::Spec->catdir(
			__DIR__,
            '..',
            'lib');
</code>

This solution already looks better, but it has the drawback of needing an external module.
There is also this thing with the strange-looking <hl>_DIR__</hl> symbol.

Under the hood, the <hl>__DIR__</hl> function uses the following code-snippet to
to calculate the current directory:

<code lang="perl">
my $file = (caller 0)[1];
File::Spec->rel2abs(join '', (File::Spec->splitpath($file))[0, 1])
</code>

<h2>Solution 5 - Path::Tiny</h2>

The solution that I currently like the best, involves the use of the
<a href="https://metacpan.org/pod/Path::Tiny">Path::Tiny</a> module

<code lang="perl">
use Path::Tiny qw(path);
use lib path($0)->absolute->parent->child('lib')->stringify;
</code>

<hl>Path::Tiny</hl> by default exports the <hl>path</hl> function, but I like to be explicit
- for the sake of the maintenance programmer who will need to figure out where does the <hl>path</hl>
function come from -, so I load the module by writing <hl>use Path::Tiny qw(path);</hl>.

Given an absolute or relative path, the <hl>path</hl> function returns an object representing that
thing in the file-system. We passed the <hl>$0</hl> variable that contains the name of the script
being executed.

The <hl>absolute</hl> method turns it into an absolute path even if it was a relative path originally.

That's important, because if we run the script from the same directory where the script is:
<hl>perl programming.pl</hl>, then <hl>$0</hl> will only contain <b>programming.pl</b> and
it will be hard to find out what is the parent directory. With absolute paths, we don't have
the problem.

The <hl>parent</hl> method will point to the parent directory. While it is longer than writing <hl>'..'</hl>
but our intentions are clearer with <hl>parent</hl>, than if we wrote <hl>'..'</hl> or even if we called
<hl>dirname</hl>.

In addition, though we don't need it for our purposes, you can pass a number to it (e.g. <hl>parent(2)</hl>)
and go several levels up in one call.

Then we are calling the <hl>child</hl> method with the name of the specific child-directory.

This returns a <hl>Path::Tiny</hl> object so we still need to call the <hl>stringify</hl>
method to turn it into a real string.

I think our intentions are much clearer with this code, and besides, the <hl>Path::Tiny</hl> module
is very useful for other tasks as well. We used it in the article
<a href="/checking-the-whois-record-of-many-domains">fetching whois records</a>, for
locating a file and reading the content into memory.

<h2>Solution 6 - Path::Tiny</h2>

As suggested by Yary H in the comments, we can even make it shorter and nicer by using the <hl>sibling</hl> method:

<code lang="perl">
use Path::Tiny qw(path);
use lib path($0)->absolute->sibling('lib')->stringify;
</code>

<h2>Solution 7 - rlib</h2>

I've never seen this before but Tony Edwardson has suggested  <a href="https://metacpan.org/pod/rlib">rlib</a> which looks great.
It has not seen any release since 1998, but it very simple and it seems to "just work". (I've tried on Linux and OSX.)

<code lang="perl">
use rlib '../lib';
</code>


<h2>Solution 8 - FindBin::libs<?h2>

<a href="https://metacpan.org/pod/FindBin::libs">FindBin::libs</a> is a bit magical. By default it looks around the location
of the currently running script, finds the <hl>lib</hl> directory and adds it to <hl>@INC</hl>. You only need to load the module:

<code lang="perl">
use FindBin::libs;
</code>

It will work with seeral directory layouts. I have tried the following two cases:

<code>
/bin/script.pl
/lib/Module.pm
</code>

or if the script is in the root directory of the project:

<code>
script.pl
/lib/Module.pm
</code>

Unlike the other solutions, if there is no <hl>lib</hl> directory, this won't add the path to a not-existing directory to <hl>@INC</hl>.


<h2>Conclusion</h2>

If you can install CPAN modules, and I really hope for your that you can, I'd recommend <hl>Path::Tiny</hl>, <hl>rlib</hl>, or <hl>FindBin::libs</hl>.


