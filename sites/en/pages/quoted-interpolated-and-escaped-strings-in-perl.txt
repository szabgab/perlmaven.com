=title Strings in Perl: quoted, interpolated and escaped
=timestamp 2013-05-15T11:45:03
=indexes strings, ', ", \, escape character, interpolation, quote, embedded characters, q, qq
=status show
=books beginner
=author szabgab
=comments_disqus_enable 1

=abstract start

Understanding how strings work is important in every programming language, but in Perl they are
part of the essence of the language. Especially if you consider that one of the acronyms of Perl
is <b>Practical Extraction and Reporting Language</b> and for that you need to use lots of strings.

=abstract end

Strings can be placed either between single quotes <hl>'</hl> or double quotes <hl>"</hl> and they have slightly different behavior.

<h2>Single quoted strings</h2>

If you put characters between single quotes <hl>'</hl>, then almost all the characters,
except the single-quote itself <hl>'</hl>,
are interpreted as they are written in the code.

<code lang="perl">
my $name = 'Foo';
print 'Hello $name, how are you?\n';
</code>

The output will be:

<code>
Hello $name, how are you?\n
</code>

<h2>Double quoted strings</h2>

Strings placed between double quotes <hl>"</hl> provide interpolation
(other variables embedded in the string will be replaced by their content),
and they also replace the special escape sequences such as <hl>\n</hl> by a real newline
and <hl>\t</hl> by a real tab.

<code lang="perl">
my $name = 'Foo';
my $time  = "today";
print "Hello $name,\nhow are you $time?\n";
</code>

The output will be

<code>
Hello Foo,
how are you today?

</code>

Note, there is a <hl>\n</hl> right after the comma in the string and another one at the end of the string.

For simple strings such as 'Foo' and "today" that have no <hl>$</hl>, <hl>@</hl>, and <hl>\</hl> characters
in them, it does not matter how they are quoted.

The next two lines have the exact same result:

<code lang="perl">
$name = 'Foo';
$name = "Foo";
</code>


<h2>E-mail addresses</h2>

As <hl>@</hl> also interpolates in double quoted strings, writing e-mail addresses
needs a little more attention.

In single quotes <hl>@</hl> does not interpolate.

In double quotes this code will generate an error:
<a href="/global-symbol-requires-explicit-package-name">Global symbol "@bar" requires explicit package name at ... line ...</a>
and a warning:
<b>Possible unintended interpolation of @bar in string at ... line ...</b>

The latter might be the one that provides the better clue what is really the problem.

<code lang="perl">
use strict;
use warnings;
my $broken_email  = "foo@bar.com";
</code>

This code, on the other hand, having the e-mail address in single quotes, will work.

<code lang="perl">
use strict;
use warnings;
my $good_email  = 'foo@bar.com';
</code>

What if you need both the interpolation of scalar variables but you want to include at marks <hl>@</hl> in the string?

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
my $good_email  = "$name\@bar.com";

print $good_email; # foo@bar.com
</code>

You can always <b>escape</b> the special characters, in this case the at-mark <hl>@</hl> by using the so-called <b>escape character</b>
which is the back-slash <hl>\</hl> character.

<h2>Embedding dollar $ sign in double quoted strings</h2>

In a similar way if you'd like to include a <hl>$</hl> sign in an otherwise double-quoted string you can escape that too:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print "\$name = $name\n";
</code>

Will print:

<code>
$name = foo
</code>

<h2>Escaping the escape character</h2>

There are rare cases when you'd like to include a back-slash character in a string.
If you put a back-slash <hl>\</hl> in a double-quoted string,
Perl will think you want to escape the next character and do its magic.

Don't worry though. You can tell Perl to stop that by escaping the escape character:

You just put another back-slash in front of it:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print "\\$name\n";
</code>

<code>
\foo
</code>

I know this escaping the escape character is a bit strange, but this is basically how it works in every other language as well.


If you'd like to understand this whole escaping business, try something like this:

<code lang="perl">
print "\\\\n\n\\n\n";
</code>

see what does that print:

<code>
\\n
\n
</code>

and try to explain it to yourself.

<h2>Escaping double quotes</h2>

We saw that you can put scalar variables in double-quoted strings but you can also escape the <hl>$</hl> sign.

We saw how you can use the escape character <hl>\</hl> and how you can escape that too.

What if you'd like to print a double quote in a double-quoted string?


This code has a syntax error:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print "The "name" is "$name"\n";
</code>

when Perl sees the double-quote just before the word "name" it thinks that was the end of the string
and then it complains about the word <b>name</b> being a <a href="/barewords-in-perl">bareword</a>.

You might have already guessed, we need to escape the embedded <hl>"</hl> character:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print "The \"name\" is \"$name\"\n";
</code>

This will print:

<code>
The "name" is "foo"
</code>

This works, but it is quite hard to read.


<h2>qq, the double-q operator</h2>

That's where you can use <hl>qq</hl> or the double-q operator:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print qq(The "name" is "$name"\n);
</code>

For the untrained eyes, the qq() might look like a function call, but it is not. <hl>qq</hl> is an operator
and you'll see in a second what else it can do, but first let me explain this.

We replace the double-quotes <hl>"</hl> that used to surround the string by the parentheses of the <hl>qq</hl>
operator. This means the double-quotes are not special any more in this string, so we don't need to escape them.
That makes the code a lot more readable.
I'd even call it beautiful, if I did not fear the wrath of the Python programmers.

But what if you would like to include parentheses in your string?

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print qq(The (name) is "$name"\n);
</code>

No problem. As long as they are balanced
(that is, having the same number of opening <hl>(</hl>, and closing <hl>)</hl> parentheses,
and always having the opening parentheses before the corresponding closing parentheses) Perl can
understand it.

I know. You'll want to break it now, by putting a closing before the opening:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print qq(The )name( is "$name"\n);
</code>

Indeed, Perl will give you a syntax error about "name" being a <a href="/barewords-in-perl">bareword</a>.
Perl can't understand everything, can it?

You could, of course, escape the parentheses in the string<hl>\)</hl> and <hl>\(</hl>, but we were down that rabbit hole already.
No thank you!

There must be a better way!

Do you remember, I wrote <hl>qq</hl> is an operator and not a function? So it can do tricks, right?

What if we replaced the parentheses around our string by curly braces? <hl>{}</hl>:

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print qq{The )name( is "$name"\n};
</code>

That works and prints the string as we meant:

<code>
The )name( is "foo"
</code>

(even though I have not idea why would I want to print something like that...)

Then <a href="http://perl.plover.com/yak/presentation/samples/slide027.html">the guy from the second row</a> raises his hand,
and asks what if you want both parentheses and curly braces in your string, <b>and</b> you want them imbalanced?

You mean like this, right?

<code lang="perl">
use strict;
use warnings;
my $name = 'foo';
print qq[The )name} is "$name"\n];
</code>

printing this:

<code>
The )name} is "foo"
</code>


... there must be a use for the square brackets too, right?


<h2>q, the single-q operator</h2>

Similar to <hl>qq</hl> there is also an operator called <hl>q</hl>.
That too allows you select the delimiters of your string, but it works
as a single quote <hl>'</hl> works: It does <b>NOT</b> interpolate variables.

<code lang="perl">
use strict;
use warnings;
print q[The )name} is "$name"\n];
</code>

prints:

<code>
The )name} is "$name"\n
</code>


