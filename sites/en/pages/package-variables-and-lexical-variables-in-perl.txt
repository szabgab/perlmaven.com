=title Package variables and Lexical variables in Perl
=timestamp 2014-02-11T09:55:01
=indexes my, state, our, use vars, package
=status show
=books advanced
=author szabgab
=comments_disqus_enable 0

=abstract start

In Perl there are 3 types of variables differing mostly in the their scoping.

<ul>
  <li>Lexical variables</li>
  <li>Static or state variables</li>
  <li>Package variables</li>
</ul>

=abstract end


<h2>Lexical variables</h2>

These are the variables used in the vast majority of cases.

A variable declared using the <hl>my</hl> keyword is a <b>lexical variable</b>.
It lives from the place where it was declared using the <hl>my</hl> keyword
till the end of the current block. (Pair of curly braces.) This is the <hl>scope</hl>
of the variable.

If it is declared outside of any block, it lives till the end of the file. Its scope
is the whole file starting from the point of declaration.

<code lang="perl">
my $x;
my @y;
my %z;
</code>


<h2>Static or state variables</h2>

Version <a href="/what-is-new-in-perl-5.10--say-defined-or-state">5.10 introduced</a> a new keyword called <hl>state</hl>.
Using this keyword will declare a lexical variable, just as <hl>my</hl> does, but it
the variable will be initialized only once.
It is really only interesting in subroutines that are called more than once.
There they behave like <a href="/static-and-state-variables-in-perl">static variables</a> in some other languages.

<code lang="perl">
sub f {
   state $x = 0;
   state @z;
   state %h;
}
</code>

<hl>state</hl> cannot be used in <a href="/scalar-and-list-context-in-perl">list context</a>
which poses some limitation, but in most case a bit of workaround can solve the problem.
Like in <a href="/static-and-state-variables-in-perl">this example</a>.


<h2>Package variable</h2>

These are the oldest type of variables in Perl. They are still used in some cases, even though in most cases you
should just use lexical variables.

In old times, if we started to use a variable without declaring it with the <hl>my</hl> or <hl>state</hl> keywords,
we automatically got a variable in the current namespace.
Thus we could write:

<code lang="perl">
$x = 42;
print "$x\n";  # 42
</code>

Please note, we don't <hl>use strict;</hl> in these examples. Even though you should <a href="/strict">always use strict</a>.
We'll fix this in a bit.

The default namespace in every perl script is called "main" and you can always access variables using their full name
including the namespace:

<code lang="perl">
$x = 42;
print "$x\n";        # 42
print "$main::x\n";  # 42
</code>

The <hl>package</hl> keyword is used to switch namespaces:

<code lang="perl">
$x = 42;
print "$x\n";        # 42

print "$main::x\n";  # 42

package Foo;
print "Foo: $x\n";   # Foo:
</code>

Please note, once we switched to the "Foo" namespace, the <hl>$x</hl> name refers to the variable in the Foo namespace.
It does not have any value yet.

<code lang="perl">
$x = 42;
print "$x\n";        # 42

print "$main::x\n";  # 42

package Foo;
print "Foo: $x\n";   # Foo:
$x = 23;
print "Foo: $x\n";   # Foo 23;
</code>

Do we really have two $x-es? Can we reach the $x in the main namespace while we are in the Foo namespace?

<code lang="perl">
$x = 42;
print "$x\n";              # 42

print "$main::x\n";        # 42

package Foo;
print "Foo: $x\n";         # Foo:
$x = 23;
print "Foo: $x\n";         # Foo 23
print "main: $main::x\n";  # main: 42
print "Foo: $Foo::x\n";    # Foo: 23

package main;

print "main: $main::x\n";  # main: 42
print "Foo: $Foo::x\n";    # Foo: 23
print "$x\n";              # 42
</code>

We even switched back to the <hl>main</hl> namespace (using <hl>package main;</hl>) and
if you look closely, you can see that while we were already in the main package we could reach 
to the $x of the Foo package using <hl>$Foo::x</hl> but if we accessed <hl>$x</hl> without the full
package name, we reach the one in the main namespace.

Every package (or namespace) can hold variables with the same name.

<h2>use strict - and use explicit package name</h2>

As I mentioned, the above code did not <hl>use strict;</hl>. That's because it would require us 
to declare the variables using <hl>my</h> as <a href="/global-symbol-requires-explicit-package-name">explained here</a>.

Actually, if we put <hl>use strict;</hl> at the beginning of the above code, and  we'll get
<a href="/global-symbol-requires-explicit-package-name">Global symbol "$x" requires explicit package name ...</a>.

Try this:

<code lang="perl">
use strict;

$x = 42;
print "$x\n";
</code>

Now we might already understand what does that error message really mean. We don't <b>have to</b> declare the variables using <hl>my</hl>.
We could use the explicit package name like this:

<code lang="perl">
use strict;

$main::x = 42;
print "$main::x\n";
</code>

This will already work and we are using the same package-variable as we did earlier.

We could even access the variable in the <hl>Foo</hl> namespace:

<code lang="perl">
use strict;

$main::x = 42;
print "$main::x\n";   # 42

$Foo::x = 23;
print "$Foo::x\n";    # 23
print "$main::x\n";   # 42
</code>

Of course this is going to be really  tiring after a while. Especially if we want to access a package variable
in the current package, we don't want to write down the name of the package again and again:

<code lang="perl">
use strict;
package VeryLongName;
$VeryLongName::x = 23;
print "VeryLongName: $VeryLongName::x\n";
</code>

This works, but we don't like to type so much.

<h2>use vars</h2>

The problem is that <hl>use strict</hl> is complaining that there is a variable <hl>$x</hl> which is not declared with <hl>my</hl>
and that it does not know about it. So we need a way to tell <hl>strict</hl> that it is ok. We know about the <hl>$x</hl> variable
and we want to use it, but we want it to be a package variable. We don't want to declare it using <hl>my</hl> and we don't want to
always prefix it with the package name.

With <hl>use vars ('$x')</hl> we can achieve that:

<code lang="perl">
use strict;

package VeryLongName;
use vars ('$x');
$x = 23;
print "VeryLongName: $x\n";
</code>

This works, but the documentation of <hl>vars</hl> tells us that
<b>the functionality provided by this pragma has been superseded by "our" declarations</b>.

So how does <hl>our</hl> work?

<h2>our</h2>

<code lang="perl">
use strict;

package VeryLongName;
our $x = 23;
print "VeryLongName: $x\n";
</code>


<h2>Caveat</h2>

The <hl>our</hl> declaration itself is lexically scoped, meaning it is limited by the file or by enclosing
curly braces. In the next example we don't have curly braces and thus the declaration <hl>our $x = 23;</hl>
will be intact even after switching namespaces. This can lead to very unpleasant situations.
My recommendation is to avoid using <hl>our</hl> (you almost always need to use <hl>my</hl> anyway)
and to put every package in its own file.

<code lang="perl">
use strict;

package VeryLongName;
our $x = 23;
print "VeryLongName: $x\n"; # VeryLongName: 23

package main;
print "$x\n";  # 23
</code>

<h2>Comments</h2>

Gabor - I'm having problems accessing a hash in the beginning of a program within a subroutine.
my %TK={'title'=>'does not work'};
sub dosomething
{
print join(', ', (keys %TK));
}
only works when I change my to our.
Any other solutions?

---
In this example you never call dosomething. Show a full example please.
---

You're assigning anonymous hash to %TK. Change curly brackets to ordinary parenthesis:

my %TK=('title'=>'does not work');

Or consequently try to use references.


