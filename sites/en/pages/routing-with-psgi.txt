=title Routing with PSGI
=timestamp 2015-05-19T00:30:01
=indexes PSGI
=status show
=books psgi
=author szabgab
=comments_disqus_enable 0

=abstract start

For any web application one of the critical questions is how the various URLs are handled? Are they mapped to subroutines in
a single file, like in <a href="/dancer">Dancer</a>, or are they mapped to subroutines in modules as it is done in 
<a href="/catalyst">Catalyst</a>.

As <a href="/psgi">PSGI</a> is the low level building block for any modern web application in Perl, you can write your own
routing. In this article we'll see a simple case of routing.

=abstract end

We create the following script that we call <hl>echo_routing.psgi</hl> and we run it using <hl>plackup echo_routing.psgi</hl>.
If we browse to <hl>http://localhost:5000/</hl> we see an input box and a button. We type in "hello" and click on the button.
The URL changes to <hl>http://localhost:5000/echo?field=hello</hl> and we get back: <hl>You said: hello</hl>.

If we click on the "Back" button of the browser, clear the input box and click on the button again the URL changes to
<hl>http://localhost:5000/echo?field=</hl> and the browser displays: <hl>You did not say anything.</hl>.

If we now change the URL to be <hl>http://localhost:5000/abc</hl>, the browser will display <hl>404 Not Found</hl>.

<h2>Details of the usage</h2>

Please, note after clicking on the button the URL changes. Both the actual path that now includes the word "echo", and it also has some parameters
after it. The parameters are just as they were in the earlier <a href="/how-to-build-a-dynamic-web-application-using-psgi">echo example</a>.
The new thing is the change in the path. This happens because in the <hl>form</hl> in the HTML below (in the get_html function)
we added an <hl>action="/echo"</hl> attribute.
By default a form will submit to the same URL where it came from, but the <hl>action</hl> attribute can control the URL.

<h2>The code</h2>

For each route we create a subroutine. <hl>server_root</hl> will serve the request to <hl>/</hl> and
<hl>serve_echo</hl> will serve the request to <hl>/echo</hl>. If you look at the code, it is
quite straight forward returning the 3-element array ref as required by the PSGI specification.
It is explained in the <a href="/getting-started-with-psgi">getting started with PSGI</a> article.

The interesting part is that we have created a hash called <hl>%ROUTING</hl> which is a dispatch-table.
The keys are the possible routes, the values are references to functions handling the give route.

The main part of the code is the anonymous subroutine assigned to <hl>$app</hl>. We use the
<a href="https://metacpan.org/pod/Plack::Request">Plack::Request</a> module to fetch the <hl>path_info</hl>,
which is essentially the route. We check if it is an existing key in the <hl>%ROUTING</hl> hash and if it has
a value: <hl>my $route = $ROUTING{$request->path_info};</hl>. The variable <hl>$route</hl> will either be
<hl>undef</hl> if the route the user accessed was not defined (as is in the case of <hl>/abc</hl>), or
it will be a reference to a function. If it is a reference to a function we de-reference the variable and
call the function. Using the <hl>$route->($env)</hl> syntax we also pass the <hl>$env</hl> variable as the first parameter.

If the <hl>path_info</hl> did not match any of the keys in the routing table, we return the HTTP Error code
<a href="http://en.wikipedia.org/wiki/HTTP_404">404 Not Found</a>.


<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

use Plack::Request;

my %ROUTING = (
    '/'      => \&serve_root,
    '/echo'  => \&serve_echo,
);


my $app = sub {
    my $env = shift;
 
    my $request = Plack::Request->new($env);
    my $route = $ROUTING{$request->path_info};
    if ($route) {
        return $route->($env);
    }
    return [
        '404',
        [ 'Content-Type' => 'text/html' ],
        [ '404 Not Found' ],
    ];
};

sub serve_root {
    my $html = get_html();
    return [
        '200',
        [ 'Content-Type' => 'text/html' ],
        [ $html ],
    ];
} 

sub serve_echo {
    my $env = shift;
 
    my $request = Plack::Request->new($env);
    my $html;
    if ($request->param('field')) {
        $html = 'You said: ' . $request->param('field');
    } else {
        $html = 'You did not say anything.';
    }
    return [
        '200',
        [ 'Content-Type' => 'text/html' ],
        [ $html ],
    ];
}

sub get_html {
    return q{
      <form action="/echo">

      <input name="field">
      <input type="submit" value="Echo">
      </form>
      <hr>
    }
}
</code>


<h2>Comments</h2>

Would be nice to list (and compare?) here also url dispatching modules from CPAN like Path::Router, Path::Dispatcher, HTTP::Router, Router::Simple, Router::Boom, Router::R3, ....


