=title Switching in Perl 5.10
=timestamp 2013-11-06T23:30:01
=indexes v5.10, switch, case, given, when
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

<h2>The given statement was removed from later versions of Perl. Don't use this!</h2>

TL;DR

There is <b>no case/switch statement on Perl</b>. Use if/elsif/elsif/...

One of the complaints about Perl was always that it lacks a real
case or switch statement. While you could always fake one, people
were not satisfied. Even though Python does not have one either...

In any case, in Perl version 5.10 the <hl>given</hl> statement was
added and then in version 5.18 is was marked as experimental
and started to emit warnings.

The article below was added mostly to replace an older version of
the same article and to include the recommendation:

<b>Please, don't use the <hl>given</hl> statement until the experimental
flag is removed.</b>

=abstract end

So in accordance with the Perl 6 design perl will NOT have a case
or switch. It will have a <b>given</b> keyword. It will also behave
differently than the usual case. It will do The Right Thing (tm).

The syntax is quite simple:

<code lang="perl">
use 5.010;

given($value) {
    when(3) { say "Three"; }
    when(7) { say "Seven"; }
    when(9) { say "Nine"; }
    default { say "None of the expected values"; }
}
</code>

We are taking the value in $value and comparing it to the 
values within the when() statements. When we find one that 
matches, the block after the when() is executed AND the
given statement is terminated. That is, no more when()
is checked.

If non of the when() cases fit then the (optional)
default {} block is executed.

Let's see another example:

<code lang="perl">
use 5.010;

given($value) {
    when(/^\d+$/)            { say "digits only"; }
    when(/^\w+$/)            { say "Word characters"; }
    when(/^[a-zA-Z0-9.-]+$/) { say "Domain namish"; }
    default { say "None of the expected"; }
}
</code>

It is very similar but now we have regular expressions instead
of fixed values in the when() statement. Each regex on its 
turned is tried against the value. When one of them matches
its block is executed and the given() statement is terminated.


Yes, what you are suspecting is right. The when() statements 
are actually applying ~~, the smart match operator.

I have already written about it earlier in
<a href="/smart-matching-in-perl-5.10">Smart Matching in Perl 5.10</a>.
So you can use any kind of value in the when() statement.
A number will check numeric equality using ==, a string will use
eq, a regex will try to match the <b>given value</b> and if you
supply a subroutine reference then Perl will call that subroutine
using the <b>given value</b> as a parameter and check the true-ness
of the return value.

See this example:

<code lang="perl">
use 5.010;

given($value) {
    when(10) {
        say "Number 10";
    }
    when([11, 23, 48]) {
        say "In the list";
    }
    when(/^\d+$/) {
        say "digits only";
    }
    when(\&is_number) {
        say "Is number";
    }
    default {
        say "None of the above";
    }
}

sub is_number {
    return $_[0] =~ /\d/ and $_[0] =~ /^[+-]?\d*\.\d*$/;
}
</code>

There are few more minor issues:

Perl will automatically <b>break</b> out from the <hl>given()</hl>
statement after the execution of block. If you would like to
force checking the additional <hl>when()</hl> statements use the
<hl>continue</hl> keyword.

On the other hand if you would like to break out from a <hl>given()</hl>
statement before reaching the end of the <hl>when()</hl> block, you can use the
<hl>break</hl> statement yourself.

The <hl>given()</hl> actually assigns the <hl>$value</hl> to <hl>$_</hl> so you can use that 
as well to write when clauses such as this:

<code lang="perl">
when($_ < 18) { say "Less than 18"; }
</code>

<hl>when()</hl> can be used outside the <hl>given()</hl> block as well.

For further details see the documentation of the
<a href="http://perldoc.perl.org/perlsyn.html#Switch-statements">Switch statements</a>.

