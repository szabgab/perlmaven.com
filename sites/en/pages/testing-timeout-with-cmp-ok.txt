=title Testing timeout with cmp_ok
=timestamp 2016-06-06T22:30:01
=indexes Test::More, cmp_ok
=tags screencast
=status show
=books testing
=author szabgab
=comments_disqus_enable 1

=abstract start

As mentioned earlier, the <hl>is()</hl> function compares the values using string-<hl>eq</hl> which is correct in 99% of the cases, but
if you'd like to compare the values with numerical-<hl>==</hl> you can do it using the <hl>cmp_ok</hl>.

More than that, with <hl>cmp_ok</hl> you can compare the two values with any operator, so you can check if the actual value is,
let's say, smaller than an expected value.

For example when measuring timeout, we cannot expect the result to be exact on the millisecond.

=abstract end

<screencast file="/media/videos/testing-timeout-with-cmp-ok" />

Let's say we have a function called <hl>wait_for_input_with_timeout</hl> that accepts a number, the number of
seconds to wait for input before timing out. How can we test that this timeout really works?
We cannot really expect the system to wait exactly the given amount of seconds. After all there are all kinds of other things
going on in the computer. What we can expect is that it will time out at the given amount of seconds +- some margin.

Then, based on the actual business-case, the margin will change. Our expectation for time-accuracy is quite different when
we click on the 'like'-button on a web-page, than when we wait for feedback from our airplane navigation system.

In order to show how to test this I created a function called <hl>wait_for_input_with_timeout</hl> which will wait
for a random number of seconds. Somewhere around the number passed to the function.

<code lang="perl">
sub wait_for_input_with_timeout {
    sleep rand shift()+2;
}
</code>


In the test script then we can call the function and measure the elapsed time.

<code lang="perl">

my $start = time;
wait_for_input_with_timeout(3);
my $end = time;

print $end-$start, "\n";
</code>

We can then use the <hl>cmp_ok</hl> function of <a href="http://metacpan.org/pod/Test::More">Test::More</a> twice(!) to compare
the elapsed time to the minimum and maximum elapsed time we accept:


<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

use Test::More tests => 2;

my $start = time;
wait_for_input_with_timeout(3);
my $end = time;

cmp_ok $end - $start, ">=", 2, "process was waiting at least 2 secs";
cmp_ok $end - $start, "<=", 3, "process was waiting at most 3 secs";

sub wait_for_input_with_timeout {
    sleep rand shift()+2;
}
</code>

That is, we expect the elapsed time to be up to 3 seconds (the number we passed to the function), and at least 2 seconds.

Running the script sometimes will print:

<code>
1..2
ok 1 - process was waiting at least 2 secs
ok 2 - process was waiting at most 3 secs
</code>

And sometimes, when the actual timeout is out of the 

<code>
1..2
not ok 1 - process was waiting at least 2 secs
#   Failed test 'process was waiting at least 2 secs'
#   at examples/intro/cmp_ok.t line 11.
#     '0'
#         >=
#     '2'
ok 2 - process was waiting at most 3 secs
# Looks like you failed 1 test of 2.
</code>

Using <hl>cmp_ok</hl> we can compare two values with any operator we like.


