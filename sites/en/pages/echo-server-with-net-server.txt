=title Echo server with logging and timeout
=timestamp 2015-04-29T09:00:01
=indexes Net::Telnet, ALRM, %SIG, alarm, eval, $@
=status show
=books net_server
=author szabgab
=comments_disqus_enable 1

=abstract start

In this article we continue to improve the Echo server we
<a href="/getting-started-with-net-server">started earlier</a>.
Two additions:
<ul>
 <li>Logging to the server</li>
 <li>Timeout for the client</li>
</ul>

=abstract end

<h2>Logging on the server</h2>

This is the simple part. Inside the <hl>process_request</hl> everything we print to the
Standard Output (<hl>STDOUT</hl>) will go to the client, and everything we print to
the Standard Error (<hl>STDERR</hl>) will go to the console.
So all we have to to is to <hl>print STDERR "some text\n";</hl> and it will show up
on the console.

You can see it in the next example as well.

<h2>Timeout handled by the server</h2>

The following code is from the <hl>lib/EchoServer.pm</hl> file,
which is part of the same directory structure we had
<a href="/getting-started-with-net-server">at the beginning.</a>


<code lang="perl">
package EchoServer;
use warnings;
use strict;

use base 'Net::Server';

my $timeout = 5; # give the user 5 seconds to type a line
my $EOL   = "\015\012";

sub process_request {
    print "Welcome to the Echo server, please type in some text and press enter. Say 'bye' if you want to leave$EOL";
    print STDERR "New user connected\n";
    eval {
        local $SIG{ALRM} = sub { die "Timeout\n" };

        alarm($timeout);
        while( my $line = <STDIN> ) {
            alarm($timeout);
            $line =~ s/\r?\n$//;
            print qq(You said "$line"$EOL);
            last if $line eq "bye";
        }
        alarm(0);
    };
    my $err = $@;
    alarm(0);
    if ( $err ) {
        chomp $err;
        if ( $err eq 'Timeout' ) {
            print "Timed Out. Disconnecting...$EOL";
            print STDERR "Client timed Out.\n";
        } else {
            print "Unknown internal error. Disconnecting...$EOL";
            print STDERR "Unknown internal error: $err\n";
        }
    } else {
        print STDERR "User said bye\n";
    }
    return;
}

1;
</code>

The script that runs it located in <hl>bin/echo_server.pl</hl>,
is similar to what we had earlier.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/../lib";
use EchoServer;

EchoServer->run(port => 8000);
</code>

We run the server by typing <hl>perl bin/echo_server.pl</hl> in one console.
Then we connect to it using <hl>telnet localhost 8000</hl> in another console. This is the client.

Firs it will show the banner:

<code>
Connected to localhost.
Escape character is '^]'.
Welcome to the Echo server, please type in some text and press enter. Say 'bye' if you want to leave
</code>

Then we can type in <hl>hello</hl> and the server will reply to us:

<code>
hello
You said "hello"
</code>

Then we wait 5 seconds and suddenly we see:

<code>
Timed Out. Disconnecting...
Connection closed by foreign host.
$
</code>

The first line was sent by the server. The second line was printed by our telnet client
and then we got the prompt back.

If we switch to the console where we launched the server, we will see this output
and the server is still running.

<code>
2013/08/08-08:46:01 EchoServer (type Net::Server) starting! pid(14978)
Resolved [*]:8000 to [0.0.0.0]:8000, IPv4
Binding to TCP port 8000 on host 0.0.0.0 with IPv4
Group Not Defined.  Defaulting to EGID '1000 4 20 24 46 104 115 120 1000'
User Not Defined.  Defaulting to EUID '1000'
New user connected
Client timed Out.
</code>

<h2>How does it work?</h2>

The operating system has an alarm clock, just as most of us have at home or in our mobile phone. Except that
in the operating system we usually don't set the alarm clock to a specific hour, but instead we set how many
seconds from now we want the alarm to go off.

Using the <hl>alarm($timeout)</hl> function, basically we ask the operating system to send us an <hl>ALRM-signal</hl>
in <hl>$timeout</hl> seconds which happens to be 5 in our case.
Normally nothing would happen when an ALRM signal arrives but we also configure a signal-handler:
<hl>local $SIG{ALRM} = sub { die "Timeout\n" };</hl> tells perl that when an <hl>ALRM</hl> signal is received
we want out script to throw an exception.

Of course we don't really want our script to <hl>die</hl>, so we use an <hl>eval</hl> block to capture
the exception that might happen within the block.

(We also use <hl>local</hl> to make sure we get the previous behavior of the
ALRM handle after leaving the enclosing block. This is to avoid the
possibility of receiving the ALRM signal just as we left the <hl>eval</hl> block
after receiving input from the client.)

Within the <hl>while-loop</hl> we also immediately set the <hl>alarm</hl> again to extend
the wait period. When we leave the while loop in a normal way - after receiving the string "bye"
from the client, we immediately set the <hl>alarm(0)</hl>. This will turn off our alarm clock.

<code lang="perl">
    eval {
        local $SIG{ALRM} = sub { die "Timeout\n" };

        alarm($timeout);
        while( my $line = <STDIN> ) {
            alarm($timeout);
            $line =~ s/\r?\n$//;
            print qq(You said "$line"$EOL);
            last if $line eq "bye";
        }
        alarm(0);
    };
</code>

If you are used to try-catch code for capturing exceptions, the <hl>eval</hl> block
was the <hl>try</hl> part. The <hl>catch</hl> part is in the following part.

<h2>Timeout - catch</h2>

The first thing we do here is copy the content of <hl>$@</hl> into our own private variable.
This is useful in general so if we call some code that uses <hl>eval</hl> it won't overwrite
the exception we caught.

Then we turn off the <hl>alarm(0)</hl> again. It does not matter to perl if we call <hl>alarm(0)</hl>
several times, but if we left the <hl>eval</hl> block because of an exception other than the one caused
by the <hl>ALRM</hl> signal then the alarm clock is still on. We don't want any wake-up later on.
So we make sure the alarm clock is turned off.

Then we inspect the<hl>$err</hl> variable. If we left the <hl>eval</hl> block without receiving an exception,
this variable will be <a href="/undef-and-defined-in-perl">undef</a> and we enter the <hl>else</hl>-block.

Inside the <hl>if</hl>-block we handle both the case when we raised the <hl>Timeout</hl> exception and if
some other exception occurred. (Which is very unlikely in our simple example, but that can happen
in more complex code.)

<code lang="perl">
    my $err = $@;
    alarm(0);
    if ( $err ) {
        chomp $err;
        if ( $err eq 'Timeout' ) {
            print "Timed Out. Disconnecting...$EOL";
            print STDERR "Client timed Out.\n";
        } else {
            print "Unknown internal error. Disconnecting...$EOL";
            print STDERR "Unknown internal error: $err\n";
        }
    } else {
        print STDERR "User said bye\n";
    }
</code>

Just one more note: In both cases inside the <hl>if</hl> block we print both to
<hl>STDOUT</hl> that goes back to the client
and to <hl>STDERR</hl> that goes to the console of the server.

