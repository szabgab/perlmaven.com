=title Type checking with Moo
=timestamp 2015-03-02T13:10:01
=indexes Moo, looks_like_number, Scalar::Util, MooX::Types::MooseLike, MooX::Types::CLike, Perl::Critic
=books moo
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

People arriving from other programming languages or from using Moose
might be expecting to find a system for types checking, but Moo
being the <b>Minimal Object Orientation</b> system of Perl, does
<b>not</b> provide any type-checking.

However you can easily add type checking of your own. Let's see how.

=abstract end

<h2>Type restrictions with regex</h2>

<include file="examples/mootype/Person.pm">

In the above code, the <hl>Person</hl> class has two attributes. <hl>name</hl>
has no restrictions, but <hl>age</hl> does have one. It is declared using the 
<hl>isa</hl> keyword of Moo. The value of the <hl>isa</hl> key is a reference
to an anonymous function. This function is called every time when someone tries to assign
a value to this attribute via the constructor or the setter of the attribute.

The value to be assigned to the attribute is passed to the function as its first parameter.
In our example we used the rather unpleasant looking <hl>$_[0]</hl> to access this value.
(We could have <hl>shift</hl>-ed it to an internal variable, but in such a short
subroutine, this seems to be the more readable version.
We can use then any code to check if the value matches the expectations and throw an
exception if it does not. In this case we checked if the value consist of one or more digits:
<hl>/^\d+$/</hl> and we threw a very simple, text-based exception using <hl>die</hl>.

Let's see how does that work when we use the module?

<include file="examples/mootype/student_1.pl">

The output of this code will look like this:

<code>
Foo
22
isa check for "age" failed: 'young' is not an integer! at Person.pm line 8.
</code>

Calling the constructor worked. Calling the <hl>name</hl> and the <hl>age</hl>
methods worked and the <hl>say</hl> function printed the values. Then calling
<hl>$student->age('young');</hl> threw an exception. The exception contains some
text Moo provided and the text we included in the <hl>die</hl> call.
The last call to <hl>say<hl> was never reached.

Let's see what happens when the incorrect value is already provided in the constructor:

<include file="examples/mootype/student_2.pl">

The exception is thrown right during the call of the constructor:

<code>
isa check for "age" failed: 'old' is not an integer! at Person.pm line 8.
</code>

<h2>Where is the problem?</h2>

Unfortunately in both cases, the error location was given in the <hl>Person.pm</hl> file.
Indeed that's the place where the exception was thrown, but that's not very
useful for the developer who just happens to use the Person module. After all,
the real source of the problem is in the script that called the constructor
or the setter with incorrect value and not in the Person.pm module.

Veteran Perl developers might immediately remember that the <hl>croak</hl> function of the <hl>Carp</hl> module
is useful in such situations. It is almost the same as <hl>die</hl> except when it reports the location of the error
it will report the place where the current function was called.

So we include:

<code lang="perl">
use Carp qw(croak);
</code>

and replace the <hl>die</hl> call by <hl>croak</hl>. The result:

<code>
isa check for "age" failed: 'old' is not an integer! at (eval 11) line 37.
</code>

Not exactly what we expected. Apparently Moo wraps the isa-checking code in
a <a href="/string-eval">string-eval</a> of its own.

Let's try the <hl>confess</hl> function of <hl>Carp</hl> then. That too
is similar to <hl>die</hl> but it provides the full stack-trace from point
where it was called up to your main script.

We put this in the code:

<code lang="perl">
use Carp qw(confess);
</code>

And replaced the call to <hl>croak</hl> by a call to <hl>confess</hl>. The result:


<code>
isa check for "age" failed: 'old' is not an integer! at Person.pm line 11.
    Person::__ANON__('old') called at (eval 11) line 37
    Person::new('Person', 'name', 'Foo', 'age', 'old') called at t.pl line 6
</code>

Here we can see that the problem was noticed in line 11 of the Person.pm file, but
it was called in line 6 of the t.pl file (my example script).

This is probably a better way to throw the exception than either of the above 2.

Just for the record let's see the Person.pm file now:

<code lang="perl">
package Person;
use Moo;

use Carp qw(confess);

has name => (is => 'rw');
has age  => (
    is  => 'rw',
    isa => sub {
       confess "'$_[0]' is not an integer!"
          if $_[0] !~ /^\d+$/;
    },
);

1;
</code>

<h2>Other ways to check if value is a number</h2>

In the previous example we used a regular expression to check if the assigned value
contains one or more digits. There are other cases when you'd like to check if the
value "is a number"? Perl does not have an is_number function, but there is a function
called <hl>looks_like_number</hl> in the <hl>Scalar::Util</hl> module. It basically
checks if the given value can be
<a href="/automatic-value-conversion-or-casting-in-perl">automatically converted to a number</a>
without any warning. So we can write:

<code lang="perl">
use Scalar::Util qw(looks_like_number);
</code>

to load the module and import the function, and rewrite the isa-checking:

<code lang="perl">
    isa => sub {
       confess "'$_[0]' is not a number!"
          unless looks_like_number $_[0];
    },
</code>

<h2>Pre-defined types</h2>

While Moo itself does not have types defined, there are several extension that provide
type definitions. For example <a href="https://metacpan.org/pod/MooX::Types::CLike">MooX::Types::CLike</a>
provides types a C programmer might be familiar with and
<a href="https://metacpan.org/pod/MooX::Types::MooseLike">MooX::Types::MooseLike</a>
imitates the types of <a href="https://metacpan.org/pod/MooseX::Types">MooseX::Types</a>.

It can be used to create your own type definitions, but it already comes with several types listed in
<a href="https://metacpan.org/pod/MooX::Types::MooseLike::Base">MooX::Types::MooseLike::Base</a>.

You can use the types it provides without any extra work.

Just load the types using <hl>use MooX::Types::MooseLike::Base qw(:all);</hl> and then you can
use the types as the values of the <hl>isa</hl> field. For example we used <hl>Int</hl> in the following example:

<code lang="perl">
package Person;
use Moo;

use MooX::Types::MooseLike::Base qw(:all);

has name => (is => 'rw');
has age  => (
    is  => 'rw',
    isa => Int,
);

1;
</code>

The resulting error look like this:

<code>
isa check for "age" failed: old is not an integer! at (eval 13) line 37.
   Person::new('Person', 'name', 'Foo', 'age', 'old') called at t.pl line 6
</code>

Apparently this module already provides a stack trace but excludes the line
where the exception was thrown.

That's the basics of adding type checking to

<h2>A little warning</h2>

Beware though. As the actual objects created by Moo are just blessed references
to hashes, any user will have direct access to the attributes.
Moo cannot enforce the type restrictions if someone directly accesses the internals of the object.
Of course no one will do that, right?
See the bad example:


<code lang="perl">
use strict;
use warnings;
use 5.010;

use Person;
my $student = Person->new( name => 'Foo', age => '20' );
say $student->age;
$student->{age} = 'young';     # BAD BAD BAD!
say $student->age;
</code>

The output then:

<code>
20
young
</code>

This is basically a limitation of Perl. There is a solution to this using, so called
inside-out objects, but they have a complexity price and they usually not worth the hassle.

<h2>Perl::Critic</h2>

A better approach is to use the regular objects and use <a href="https://metacpan.org/pod/Perl::Critic">Perl::Critic</a> to
locate code that breaks the encapsulation. Install <a href="https://metacpan.org/pod/Perl::Critic::Nits">Perl::Critic::Nits</a>
and check your code for violation of
<a href="https://metacpan.org/pod/Perl::Critic::Policy::ValuesAndExpressions::ProhibitAccessOfPrivateData">Perl::Critic::Policy::ValuesAndExpressions::ProhibitAccessOfPrivateData</a>

If you have them in your code you will get reports like this:

<code>
Private Member Data shouldn't be accessed directly at line 8, column 1.
Accessing an objects data directly breaks encapsulation and should be avoided.  Example: $object->{ some_key }.  (Severity: 5)
</code>

You can then use the strategy to <a href="/perl-critic-one-policy">improve your Perl code</a> one policy at a time.


