=title Chomp and $/, the Input Record Separator
=timestamp 2014-01-27T22:13:10
=indexes chomp, $/, English, $INPUT_RECORD_SEPARATOR, $RS
=status show
=books beginner
=author szabgab
=comments_disqus_enable 0

=abstract start

The most common use of the <hl>chomp</hl> function is to remove trailing newlines from strings.
Either when reading from the Standard Input (STDIN), or when reading from a file like this:

<code lang="perl">
my $input = <STDIN>;
chomp $input;

while (my $row = <$fh>) {
    chomp $row;
    ...
}
</code>

This is not the whole story though.

=abstract end

When dealing with a file, it can be either <a href="/what-is-a-text-file">binary or text file</a>.
We usually consider files that have "lines" to be text files. When we read such file we usually do it line-by-line.
We know that different operating systems have different meaning of what a new-line is, the most common ones
use either a single <hl>LF - line feed</hl> (hexa 0x0A or decimal 10) character (OSX/Linux/Unix),
or a <hl>CR - carriage return</hl> (hexa 0x0D or decimal 13) followed by a <hl>LF - line feed</hl> (MS Windows).

So when we open a text-file for reading and we call the read-line operator in scalar context: <hl>$line = <$fh></hl>
Perl will know what to do. Perl will adapt itself to the environment and will know what is the new-line symbol
in the current operating system.

In order to do this, Perl maintains a variable called the <b>Input Record Separator</b>.
In native Perl this is the <hl>$/</hl> variable, but if you use the
<a href="https://metacpan.org/pod/English">English</a> module
you can use the name <hl>$INPUT_RECORD_SEPARATOR</hl> or <hl>$RS</hl> as well.
This variable contains <hl>LF</hl> (ASCII character 10).


<h2>chomp</h2>

The <hl>chomp</hl> function uses the same Input record separator <hl>$/</hl> to determine what to remove
from the end of the string. In normal circumstances the default behavior is to remove the trailing,
os-specific new-line from the parameter of <hl>chomp</hl>. That's what we do in most of the cases.


<h2>Changing the Input record separator</h2>

We could actually change the value of <hl>$/</hl>. For example, by assigning the letter <b>Q</b> like this: <hl>$/ = 'Q';</hl>.
Then every call to the read-line operator <hl>$row = <$fh></hl> will read in all the characters up-to and including the first <hl>Q</hl>.

In that case, calling <hl>chomp</hl> would remove the <b>Q</b> character from the end of the string.

We could also assign longer strings to <hl>$/</hl> and then that would be the input record separator.

Let's try this perl program:

<code lang="perl">
use strict;
use warnings;
use 5.010;

$/ = 'perl';
open my $fh, '<', 'data.txt' or die;
while (my $row = <$fh>) {
    say $row;
    say '---';
    chomp $row;
    say $row;
    say '==========';
}
</code>

On this data.txt file:

<code>
What do you think about perl, and what about
some other language called perl.
Or maybe Java?
</code>

The output looks like this:

<code>
What do you think about perl
---
What do you think about
==========
, and what about
some other language called perl
---
, and what about
some other language called
==========
.
Or maybe Java?

---
.
Or maybe Java?

==========
</code>

We can observe how each call reads up-to and including the word <b>perl</b> and then how <hl>chomp</hl>
removes the string <b>perl</b>.

<h2>chomp removes only one copy of $/</h2>

In the following example we have multiple copies of the word <b>perl</b> at the end of the string and we set
the Input Record Separator to be <hl>$/ = 'perl';</hl>.
The first call to <hl>chomp</hl> removed one occurrence of <b>perl</b>. The second call to <hl>chomp</hl> removed
the other occurrence. Calling <hl>chomp</hl> again, when there were no more copies of <b>perl</b> at the end of the string,
did not do any harm.

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $str = "helloperlperl";

$/ = 'perl';
say $str;        # helloperlperl
chomp $str;
say $str;        # helloperl
chomp $str;
say $str;        # hello
chomp $str;
say $str;        # hello
</code>


This would be the same behavior if we left the default value (the os-aware new-line) in <hl>$/</hl>.

<h2>chomp on arrays</h2>

If we put the read-line operator in <a href="/scalar-and-list-context-in-perl">list context</a>,
for example by assigning it to an array, it will read all the "lines" into that array.
Each line will become an element in the array. Of course we have to put the word "lines" in quotes,
as we already know that separating the content of the file at the new-lines is "only" the default behavior.
By changing the <b>Input Record Separator</b> we can split the file at any substring.

<code lang="perl">
use strict;
use warnings;
use 5.010;

$/ = 'perl';
open my $fh, '<', 'data.txt' or die;
my @rows = <$fh>;
chomp @rows;
</code>

Calling <hl>chomp</hl> and passing the whole array to it will result in the removal of the trailing new-line (Input Record Separator)
from every element.

<h2>slurp mode</h2>

We use the <a href="/slurp">slurp</a> mode when we want to read the content of a file into a single scalar variable.
In that case we assign <a href="/undef-and-defined-in-perl">undef</a> to the Input record separator.

<h2>perldoc</h2>

<a href="https://metacpan.org/pod/perlvar">perldoc perlvar</a> (search for <hl>$INPUT_RECORD_SEPARATOR<hl>),
and <a href="http://perldoc.perl.org/functions/chomp.html">perldoc -f chomp</a> might have more to say about the topic.

<h2>Comments</h2>

This all works well when the file one is opening is an ascii file. I have a situation where the contents of my file contains RUSSIAN letters. using the normal value of $/ with perl when this file is a form of unicode - causes many of the non-ascii valued russian letters to match as 'newlines'. Can you suggest how this is handled when the files are a form of unicode? so that no 'false positive' matches occur with the Russian characters? If it helps, this is how I am opening the file... open(IN, "<:encoding(UTF-8)", $setlist )


