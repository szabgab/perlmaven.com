=title undef, the initial value and the defined function of Perl
=timestamp 2013-01-31T08:45:56
=indexes undef, defined
=status show
=books beginner
=author szabgab
=comments_disqus_enable 0

=abstract start

In some languages there is a special way to say "this field does not have a value".
In <b>SQL</b>, <b>PHP</b> and <b>Java</b> this would be <hl>NULL</hl>. In <b>Python</b> it is <hl>None</hl>.
In <b>Ruby</b> it is called <hl>Nil</hl>.

In Perl the value is called <hl>undef</hl>.

Let's see some details.

=abstract end

<h2>Where do you get undef from?</h2>

When you declare a scalar variable without assigning a value to it, the content will be the well defined <hl>undef</hl> value.

<code lang="perl">
my $x;
</code>

Some functions return <hl>undef</hl> to indicate failure.
Others might return undef if they have nothing valuable to return.

<code lang="perl">
my $x = do_something();
</code>

You can use the <hl>undef()</hl> function to reset a variable to <hl>undef</hl>:

<code lang="perl">
# some code
undef $x;
</code>

You can even use the return value of the <hl>undef()</hl> function to set a variable to <hl>undef</hl>:

<code lang="perl">
$x = undef;
</code>

The parentheses after the function name are optional and thus I left them out in the example.

As you can see there are a number of ways to get <b>undef</b> in a scalar variable.
The question is then, what happens if you use such variable?

Before that though, let's see something else:

<h2>How to check if a value or variable is undef?</h2>

The <hl>defined()</hl> function will return <a href="/boolean-values-in-perl">true</a> if
the given value is <b>not undef</b>. It will return <a href="/boolean-values-in-perl">false</a>
if the given value is <b>undef</b>.

You can use it this way:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $x;

# some code here that might set $x

if (defined $x) {
    say '$x is defined';
} else {
    say '$x is undef';
}
</code>


<h2>What is the real value of undef?</h2>

While <b>undef</b> indicates the absence of a value, it is still not unusable.
Perl provides two usable default values instead of undef.

If you use a variable that is undef in a numerical operation, it pretends to be 0.

If you use it in a string operation, it pretends to be the empty string.

See the following example:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $x;
say $x + 4, ;  # 4
say 'Foo' . $x . 'Bar' ;  # FooBar

$x++;
say $x; # 1
</code>

In the above example the variable $x - which is undef by default - acts as 0 in the addition (+).
It acts as the empty string in the concatenation (.) and again as 0 in the auto-increment (++).

It won't be flawless though. If you have asked for the warnings to be enabled with the <hl>use warnings</hl>
statement (<a href="/installing-perl-and-getting-started">which is always recommended</a>)
then you will get two <a href="/use-of-uninitialized-value">use of uninitialized value</a>
warnings for the first two operations, but not for the auto-increment:

<code>
Use of uninitialized value $x in addition (+) at ... line 6.
Use of uninitialized value $x in concatenation (.) or string at ... line 7.
</code>

I think you don't get for the auto-increment as perl is forgiving. Later we'll see that this
is quite convenient in places where you'd like to count things.

You can, of course avoid the warnings by initializing the variable to the correct initial value
(0 or the empty string, depending on what it should be), or by turning warnings off selectively.
We'll discuss that in a separate article.
