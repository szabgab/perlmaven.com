=title Can't call method ... on unblessed reference
=timestamp 2014-04-03T08:10:01
=indexes B::Deparse
=status show
=books beginner
=author szabgab
=comments_disqus_enable 0

=abstract start

A while ago I got an e-mail from one of the readers with the following question:

<blockquote>
What is the difference between <hl>subname $param</hl> and <hl>subname($param)</hl>?

One seems to work, the other throws an error <hl>Can't call method ... on unblessed reference</hl>.
</blockquote>

In other words:  <b>do you have to put parentheses around the parameters of function calls?</b>

=abstract end

Let's look at some things:

<h2>require</h2>

If we run the following script we will get an error:
<hl>Can't call method "Storable::freeze" on unblessed reference at ...</hl> pointing to the line
where we call <hl>Storable::freeze $data;</hl>.

<code lang="perl">
use strict;
use warnings;

require Storable;

my $data = { a => 42 };
my $frozen = Storable::freeze $data;
</code>


If we change the problematic line and put the parameters in parentheses:
<hl>my $frozen = Storable::freeze($data);</hl>, everything works fine.


<h2>B::Deparse</h2>

Let's try to use B::Deparse again. Once it already
<a href="/bug-in-the-for-loop-b-deparse-to-the-rescue">helped us with the for loop</a>.


Let's run the first version of the script using <hl>perl -MO=Deparse  store.pl</hl>:

<code lang="perl">
use warnings;
use strict;
require Storable;
my $data = {'a', 42};
my $frozen = $data->Storable::freeze;
</code>

and the the second version of the script:

<code lang="perl">
use warnings;
use strict;
require Storable;
my $data = {'a', 42};
my $frozen = Storable::freeze($data);
</code>

The second version returned exactly the same code as we had in our file.
(Well, except that it replaced the <a href="/perl-hashes">fat-arrow</a> by a comma, but we know they are the same.)

On the other hand, in the first script, the critical part of the code was changed to 
<hl>$data->Storable::freeze;</hl>.

Perl thought that we are calling the <hl>Storable::freeze</hl> method on the <hl>$data</hl> object.
This code would work if the <hl>$data</hl> was a <a href="/getting-started-with-classic-perl-oop">blessed reference</a>,
though it would probably do something different than what we meant.

<h2>use</h2>

If we replace the <hl>require Storable;</hl> by <hl>use Storable;</hl> then both cases work correctly,
and in both cases B::Deparse will show the code as being <hl>Storable::freeze($data);</hl>

<h2>eval "use Storable";</h2>

If instead of calling <hl>use</hl> directly we wrap it in an <hl>eval</hl> statement with a string
after it, the problem appears again.

<h2>Explanation</h2>

<hl>use</hl> happens at compile time. By the time perl sees the call to <hl>Storable::freeze</hl> it has
already loaded the <hl>Storable</hl> module.

<hl>require</hl> and <hl>eval "use ...";</hl> happens at run time, so when perl compiles the
<hl>Storable::freeze $data</hl> call, it does not know that there is going to be a <hl>Storable::freeze</hl>
functions. At this point it needs to guess what this code is, and it guesses, incorrectly,
that we are using the so-called <b>indirect object notation</b>. It thinks <hl>$data</hl> is going to be
an object and <hl>Storable::freeze</hl> is a method of this object.

That's why B::Deparse shows this code as <hl>$data->Storable::freeze;</hl>.

<h2>Solution</h2>

In order to eliminate the error you need to make sure the functions you use are already defined or loaded
before you actually use them. Putting parentheses after the function call eliminates this restriction,
and allows you put the function call wherever you want.

<h2>Comments</h2>

Hello,

I am using below code.

my $sql = "SELECT sn, givenName, employeeID, mail, sAMAccountName, department, cn, company
FROM '$table' ";

$a = $recordSearch->Fields('sn')->{Value};

I am getting "Can't call method "Fields" on unblessed reference" error. I am using Perl 5.12

It's using ADO

Please help me on this

----

Is that all your code? Where is use strict? Where did you assign value to $recordSearch?

---

Hi Gabor,

If I use strict then the page is not responding properly.
I am using ADODB connection to get the details from AD group and when I try to execute the sql query using ADODB command , I am getting below exception

code:

#!/usr/local/bin/perl
$|=1;

use CGI qw(:all);
use English;
use Sys::Hostname;
use Win32::OLE 'in';
use Net::LDAP;
use Net::LDAP::Util qw(ldap_error_name ldap_error_text);
use Mime::Lite;
use DBI;

my $connObj = Win32::OLE-> new("ADODB.Connection") || die "Connection ERROR: " . Win32::OLE->LastError() . "\n";
my $recordSearch = Win32::OLE-> new("ADODB.Recordset") || die "Recordset ERROR: " . Win32::OLE->LastError() . "\n";
my $cmdObj = Win32::OLE-> new("ADODB.Command") || die "Command ERROR: " . Win32::OLE->LastError() . "\n";

$connObj-> {Provider} = ("ADsDSOObject") || die "Provider ERROR: " . Win32::OLE->LastError() . "\n";
$connObj-> Open();
$cmdObj-> {ActiveConnection} = ($connObj);

my $table = "GC://.......";
my $sql = "SELECT name
FROM '$table'
WHERE objectCategory = 'person'
AND objectClass='user'";

$cmdObj-> {CommandText} = ("$sql");
$recordSearch = $cmdObj-> Execute;

Error: [-2147467259] Unspecified error

----

1) Read why is it important to always use strict and warnings, add them to your code and figure out what are they complaining about. They are there to help you find bugs. https://perlmaven.com/always-use-strict-and-use-warnings
2) In the code you pasted there is no such line that you had in your original question so this is not the whole code
3) In this code you assign to $recordSearch twice. That looks incorrect.
4) Figure out why is $recordSearch undef!

