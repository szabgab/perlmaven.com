=title Public and Private Methods in Moo-based Object Oriented Perl
=timestamp 2015-03-24T09:40:10
=indexes Moo, method, $self
=status show
=books moo
=author szabgab
=comments_disqus_enable 0

=abstract start

A class with only attributes can be already useful, but having methods other than getters and setters
will make the life of the objects more interesting.

=abstract end

<h2>Class with methods</h2>

When we <a href="/oop-with-moo">created a class</a>, Moo automatically added methods to get and set the values of a single attribute.
In that example we already saw that a method is called using the arrow notation: <hl>$student->name;</hl>.

Let's see now how we can create our own methods. In the first example we have class called <hl>Point</hl> representing
a single 2-dimensional point. It has two attributes: x and y. It also has an extra method called <hl>coordinates</hl>
that will return a string that looks like this: <hl>[x, y]</hl>, where x and y will be replaced by the respective numerical values.

We will use the object and its method like in the following script:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Point;
my $p = Point->new(x => 3, y => 4);
say $p->x;
say $p->y;

say $p->coordinates;
</code>

Running this script will provide the following output:

<code>
3
4
[3, 4]
</code>

Now, let's see the implementation itself in Point.pm:

<code lang="perl">
package Point;
use Moo;

has x => (is => 'rw');
has y => (is => 'rw');

sub coordinates {
   my ($self) = @_;

   return sprintf "[%s, %s]", $self->x, $self->y;
}

1;
</code>

The attributes are not new. We saw them in <a href="/oop-with-moo">the first article</a>.
However, in this example we also have a method called <hl>coordinates</hl>. If you look at it, you
won't see any obvious difference between a <hl>method</hl> and a plain <hl>subroutine</hl> in Perl.
Indeed, there is almost no difference. The main reason we call them <hl>methods</hl> is to have
a vocabulary similar to what people arriving from programming languages use.

A <hl>method</hl> in a Moo-based class is just a <hl>subroutine</hl> with a few minor differences.

We call the method with the arrow notation <hl>$p->coordinates</hl>.

Even though we did not pass any parameter to this method call, Perl will automatically take the object,
(<hl>$p</hl> in this case), and pass it as the first argument of the method.
That's what is going to be the first element of the <hl>@_</hl> array and that
will arrive in the <hl>$self</hl> variable inside the function.

While <hl>$self</hl> is not a reserved variable or a special word in Perl,
it is quite customary to use this name to hold the current object inside a class.

(In other languages this variable might be called <hl>this</hl>.
In some languages the variable springs to existence without any special code. In Perl the object always arrives
as the first parameter of the method calls.)

<h2>Another simple method</h2>

This one would return the distance of the point from the [0,0] coordinates.
I think there is nothing special in this example, but I wanted to show another simple case.

<code lang="perl">
sub distance_from_o {
   my ($self) = @_;

   my $sqr = $self->x ** 2 + $self->y ** 2;
   return $sqr ** 0.5
}
</code>

<h2>Change more than one attributes at once</h2>

In another example we would like to add a method to move the point from one coordinate
to another. This involves updating both x and y at the same time.

Running this script:

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;
use 5.010;

use Point;
my $p = Point->new(x => 2, y => 3);
say $p->x;
say $p->y;

$p->move_by(3, 4);
say $p->x;
say $p->y;
</code>

We get the following output:

<code>
2
3
5
7
</code>

This is the method we added to the Point.pm file:

<code lang="perl">
sub move_by {
    my ($self, $dx, $dy) = @_;

    my $x_temp = $self->x;
    $x_temp += $dx;
    $self->x( $x_temp );

    $self->y( $self->y + $dy );

    return;
}
</code>

In this method we get two parameters <hl>$dx</hl> and <hl>$dy</hl>, besides the object itself which is copied to <hl>$self</hl>.
These hold the distance we move x and y respectively. For x I wrote the code step-by-step. First we use <hl>$self</hl>, the current
object to get the current value of the x attribute and assign it to a temporary variable called <hl>$x_temp</hl>.
Then we increment <hl>$x_temp</hl> by the "move", and finally we put the new value from <hl>$x_temp</hl> in the x attribute.
<hl>$self->x( $x_temp );</hl>.

In the case of the y attribute, I already left out the use of the temporary variable which was only there to (hopefully) make it
clearer what we have been doing.


<h2>Public and Private methods</h2>

Just as there is no privacy for attributes, in the Object Oriented system of Perl there are no private methods either.

Nevertheless, as people want to have the feeling of privacy the tradition says that any method that starts with and underscore <hl>_</hl>
should be considered private. There is nothing in Perl that would enforce this privacy, but it is a good proximation.
After all you usually have access to the source code of any class anyway, so you could copy the class, make some changes and use your
own version.

The final call to <hl>return</hl> is not really required here, but I like to add it to make sure the caller won't receive any
accidental value till I might decide what should be returned.

This was a slightly more complex case, where we wanted to change two attributes at once, hence we created a separate method.

<h2>What is $self ?</h2>

For further explanations see what is the difference between
<a href="/methods-functions-and-subroutines-in-perl">methods, functions, subroutines and what is $self anyway?</a>


