=title Exporting and importing functions easily
=timestamp 2017-02-03T06:38:11
=indexes Exporter, @EXPORT, import
=tags screencast
=books advanced
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

Previously we talked about the <hl>use</hl> statement includes calling the <hl>import</hl> method of the module
importing the functions of the module.

How does the module author arrange for that to work? How can the module author declare which functions should
the users be able to import? After all there might be helper function or internal functions that should
not be called by an external user of the module.

=abstract end

<slidecast file="advanced-perl/libraries-and-modules/import" youtube="u42upYai4kg" />

Let's see the <b>cacla.pl</b> script:

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

use A::Calc;

print add(2, 3), "\n";
</code>

Here we have <hl>use A::Calc;</hl> without any list of functions to be imported, yet in the next line
we could use the <hl>add</hl> function imported from that module.

How could the author of the module arrange for that?

Let's see the content of <b>A/Calc.pm</b>:

<code lang="perl">
package A::Calc;
use strict;
use warnings;

use Exporter qw(import);

our @EXPORT = qw(add multiply);

my $base = 10;

sub add {
    validate_parameters(@_);

    my $total = 0;
    $total += $_ for (@_);
    return $total;
}

sub multiply {
}

sub validate_parameters {
    die 'Not all of them are numbers'
        if  grep {/\D/} @_;
    return 1;
}

1;
</code>

The author had to implement the <hl>import</hl> function that will be called by the
<hl>use</hl> statement, but because this is such a common thing to do, there is already
a module that provides this function. Perl comes with a module called
<a href="https://metacpan.org/pod/Exporter">Exporter</a> that provides this <hl>import</hl>
function. So we only had to import the <hl>import</hl> function by writing

<code lang="perl">
use Exporter qw(import);
</code>

and by that the <hl>A::Calc</hl> module now has an <hl>import</hl> method.

The way that method behaves is that it looks at the array <hl>@EXPORT</hl>
and imports the functions listed in that array.

Please note, the <hl>@EXPORT</hl> array was declared using the <hl>our</hl>
keyword.

So in our case, when the user typed in <hl>use A::Calc;</hl> all the functions
listed in <hl>@EXPORT</hl>, specifically <hl>add</hl> and <hl>multiply</hl> will
be imported into the main script.

As you can see the <hl>A::Calc</hl> module also has a function called <hl>validate_parameters</hl>,
but it is not listed in the <hl>@EXPORT</hl> array and thus it won't be imported into
the namespace of the script.

So this is how the author of a module can <b>export</b> certain function and how the user
of such module can <b>import</b> those function into another name-space.

