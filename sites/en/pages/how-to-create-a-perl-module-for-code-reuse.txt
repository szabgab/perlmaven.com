=title How to create a Perl Module for code reuse?
=timestamp 2013-02-10T09:31:56
=indexes package, use, Exporter, import, @INC, @EXPORT_OK, $0, dirname, abs_path, Cwd, File::Basename, lib, module, 1;
=status show
=books advanced
=author szabgab
=comments_disqus_enable 0

=abstract start

You may be creating more and more scripts for your systems, which need to use the same functions.

You already mastered the ancient art of copy-paste, but you are not satisfied with the result.

You probably know lots of Perl modules that allow you to use their functions and you also want to create one.

However, you don't know how to create such a module.

=abstract end

<h2>The module</h2>

<code lang="perl">
package My::Math;
use strict;
use warnings;

use Exporter qw(import);

our @EXPORT_OK = qw(add multiply);

sub add {
  my ($x, $y) = @_;
  return $x + $y;
}

sub multiply {
  my ($x, $y) = @_;
  return $x * $y;
}

1;
</code>

Save this in somedir/lib/My/Math.pm  (or somedir\lib\My\Math.pm on Windows).

<h2>The script</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

use My::Math qw(add);

print add(19, 23);
</code>

Save this in somedir/bin/app.pl (or somedir\bin\app.pl on Windows).

Now run <b>perl somedir/bin/app.pl</b>. (or <b>perl somedir\bin\app.pl</b> on Windows).

It is going to print an error like this:

<code>
Can't locate My/Math.pm in @INC (@INC contains:
...
...
...
BEGIN failed--compilation aborted at somedir/bin/app.pl line 9.
</code>

<h2>What is the problem?</h2>

In the script we loaded the module with the <hl>use</hl> keyword.
Specifically with the <hl>use My::Math qw(add);</hl> line.
This searches the directories listed in the built-in <hl>@INC</hl> variable looking for a
subdirectory called <b>My</b> and in that subdirectory for a file called <b>Math.pm</b>.

The problem is that your .pm file is not in any of the standard directories
of perl: it is not in any of the directories listed in @INC.

You could either move your module, or you could change @INC.

The former can be problematic, especially on systems where there is a strong separation
between the system administrator and the user. For example on Unix and Linux system
only the user "root" (the administrator) has write access to these directories.
So in general it is easier and more correct to change @INC.

<h2>Change @INC from the command line</h2>

Before we try to load the module, we have to make sure the directory of the module is in the @INC array.

Try this:

<b>perl -Isomedir/lib/ somedir/bin/app.pl</b>.

This will print the answer: 42.

In this case, the <hl>-I</hl> flag of perl helped us add a directory path to @INC.



<h2>Change @INC from inside the script</h2>

Because we know that the "My" directory that holds our module is in a fixed place
<b>relative</b> to the script, we have another possibility for changing the script:

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

use File::Basename qw(dirname);
use Cwd  qw(abs_path);
use lib dirname(dirname abs_path $0) . '/lib';

use My::Math qw(add);

print add(19, 23);
</code>

and run it again with this command:

<b>perl somedir/bin/app.pl</b>.

Now it works.

Let's explain the change:

<h2>How to change @INC to point to a relative directory</h2>

This line:
<hl>use lib dirname(dirname abs_path $0) . '/lib';</hl>
adds the relative lib directory to the beginning of <hl>@INC</hl>.

<hl>$0</hl> holds the name of the current script.
<hl>abs_path()</hl> of <hl>Cwd</hl> returns the absolute path to the script.

Given a path to a file or to a directory the call to
<hl>dirname()</hl> of <hl>File::Basename</hl> returns the directory part,
except of the last part.

In our case $0 contains app.pl

abs_path($0) returns  .../somedir/bin/app.pl

dirname(abs_path $0)   returns  .../somedir/bin

dirname( dirname abs_path $0)   returns  .../somedir

That's the root directory of our project.

dirname( dirname abs_path $0) . '/lib'   then points to  .../somedir/lib

So what we have there is basically

<hl>use lib '.../somedir/lib';</hl>

but without hard-coding the actual location of the whole tree.

The whole task of this call is to add the '.../somedir/lib' to be the first element of @INC.

Once that's done, the subsequent call to  <hl>use My::Math qw(add);</hl> will find the
'My' directory in '.../somedir/lib' and the Math.pm in '.../somedir/lib/My'.


The advantage of this solution is that the user of the script does not have to remember
to put the -I... on the command line.

There are other
<a href="/how-to-change-inc-to-find-perl-modules-in-non-standard-locations">ways to change @INC</a>
for you use in other situations.


<h2>Explaining use</h2>

So as I wrote earlier, the <hl>use</hl> call will look for the My directory and the Math.pm file in it.

The first one it finds will be loaded into memory and the <hl>import</hl>
function of My::Math will be called with the parameters after the name of
the module. In our case <hl>import( qw(add) )</hl> which is just the same
as calling <hl>import( 'add' )</hl>.

<h2>The explanation of the script</h2>

There is not much left to explain in the script. After the <hl>use</hl> statement
is done calling the <hl>import</hl> function, we can just call the newly imported
<b>add</b> function of the My::Math module. Just as if I declared the function in
the same script.

What is more interesting is to see the parts of the module.


<h2>The explanation of the module</h2>

A module in Perl is a namespace in the file corresponding to that namespace.
The <hl>package</hl> keyword creates the namespace. A module name My::Math
maps to the file My/Math.pm. A module name A::B::C maps to the file A/B/C.pm
somewhere in the directories listed in @INC.

As you recall, the <hl>use My::Math qw(add);</hl> statement in the script will load
the module and then call the <hl>import</hl> function. Most people don't
want to implement their own import function, so they load the <hl>Exporter</hl>
module and import the 'import' function.

Yes, it is a bit confusing. The important thing to remember is that Exporter gives you the import.

That import function will look at the <hl>@EXPORT_OK</hl> array in your module
and arrange for on-demand importing of the functions listed in this array.

OK, maybe I need to clarify:
The module "exports" functions and the script "imports" them.


The last thing I need to mention is the <hl>1;</hl> at the end of the module.
Basically the use statement is executing the module and it needs to see some kind
of a true statement there. It could be anything. Some people put there <hl>42;</hl>,
others, the really funny ones put <hl>"FALSE"</hl> there. After all every string
with letters in it is <a href="/boolean-values-in-perl">considered to be true in Perl</a>.
That confuses about everyone. There are even people who put quotes from poems there.

"Famous last words."

That's actually nice, but might still confuse some people at first.

There are also two functions in the module. We decided to export both of them,
but the user (the author of the script) wanted to import only one of the subroutines.


<h2>Conclusion</h2>

Aside from a few lines that I explained above, it is quite simple to create a Perl module.
Of course there are other things you might want to learn about modules that will
appear in other articles, but there is nothing stopping you now from moving some common
functions into a module.

Maybe one more advice on how to call your module:

<h2>Naming of modules</h2>

It is highly recommended to use capital letter as the first letter of every part in the
module name and lower case for the rest of the letters.
It is also recommended to use a namespace several levels deep.

If you work in a company called Abc, I'd recommend preceding all the modules with the Abc::
namespace. If within the company the project is called Xyz, then all its modules should be in
Abc::Xyz::.

So if you have a module dealing with configuration you might call the package
Abc::Xyz::Config which indicates the file   .../projectdir/lib/Abc/Xyz/Config.pm

Please avoid calling it just Config.pm. That will confuse both Perl (that comes with its own Config.pm)
and you.


