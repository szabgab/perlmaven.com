=title Traversing the filesystem - using a queue
=timestamp 2013-09-21T16:30:01
=indexes queue, opendir, readdir
=status show
=books beginner
=author szabgab
=comments_disqus_enable 1

=abstract start

If you really need to traverse a directory tree, please read the
article about <a href="/finding-files-in-a-directory-using-perl">Path::Iterator::Rule</a>.
This article is mostly for those who would like to see the use a simple queue,
for the generic problem of traversing a tree-like structure.

=abstract end

In another article you can find a solution using <a href="/recursive-subroutines">recursion</a>.
This time we are going to use a queue. That is a data structure where we can <hl>push</hl>
new items to the end and <hl>pull</hl> items from the beginning. In the computational world
such data structures are called <a href="http://en.wikipedia.org/wiki/FIFO">FIFO - First in First out</a>,
because the first item that gets in the queue is the first that will leave it. Even if that happens
later on. In general the sooner an item gets in the queue, the sooner it will get processed. 

In Perl, a regular <b>array</b> can be used as a <b>queue</b>. We can use the
<a href="/manipulating-perl-arrays">>push</a> function to
add more elements to the "end" of the array (the side where the index has the highest number),
and we can use the <a href="/manipulating-perl-arrays">shift</a> function to fetch a value
from the "beginning" of the array. The value at index 0.

<h2>Directory</h2>

We will use the same directory structure we used with the
<a href="/recursive-subroutines">recursive solution</a>.

<code>
root
root/a
root/a/aa.txt
root/a/foo
root/a/foo/bar.txt
root/a/foo/baz.txt
root/a/foo.txt
root/b.txt
root/c
</code>


<code lang="perl">
use strict;
use warnings;
use 5.010;

my $path = shift || '.';

traverse($path);

sub traverse {
    my @queue = @_;

    while (@queue) {
        my $thing = shift @queue;
        say $thing;
        next if not -d $thing;
        opendir my $dh, $thing or die;
        while (my $sub = readdir $dh) {
            next if $sub eq '.' or $sub eq '..';
            push @queue, "$thing/$sub";
        }
        closedir $dh;
    }
    return;
}
</code>

The beginning is simple, we allow the user to supply the directory name on
the command line, we <hl>shift</hl> it out from <a href="/argv-in-perl">@ARGV</a>,
but if no value was supplied we default to <hl>'.'</hl> which is the current directory.

Then we call the <hl>traverse()</hl> function and pass the starting path to it.
Actually we don't have to have a subroutine for this, but having subroutines usually
leads to cleaner code. Certainly more reusable code.

Inside the subroutine we declare the <hl>@queue</hl> array with the values
we just got from the caller. (In our case this will be one value.)
The <hl>while</hl> loop will as long as there are items in the queue.
(An array in scalar context will return the number of elements it contains.
If it has any elements then it is a positive number which is considered
<a href="/boolean-values-in-perl">true</a>. When the array
is empty the number of elements is 0 which is considered
<a href="/boolean-values-in-perl">false</a> in Perl.

<hl>my $thing = shift @queue;</hl> fetches the first element, and then we print it.
If this is NOT a directory, we can skip the rest of the loop and go to handle
the <hl>next</hl> item.

The next section then will read in the items from the immediate subdirectory
and add them to the end of the queue.

<code lang="perl">
   opendir my $dh, $thing or die;
   while (my $sub = readdir $dh) {
       next if $sub eq '.' or $sub eq '..';
       push @queue, "$thing/$sub";
   }
   closedir $dh;
</code>

<h2>How can we know the queue will end?</h2>

We know we are handling a tree structure that has a limited depth
and we know every directory has a limited number of entries in it.
(We also assume no loops created by symbolic links.)
So we know we have a limited number of items to print.
Once all of them were processed the <hl>@queue</hl> array will
become empty and the main <hl>while</hl> loop will end.

<h2>The result</h2>

Running <hl>perl queue.pl root</hl> gives the following output:

<code>
$ perl queue.pl root
root
root/a
root/b.txt
root/c
root/a/aa.txt
root/a/foo
root/a/foo.txt
root/a/foo/bar.txt
root/a/foo/baz.txt
</code>

The list of items is exactly the same as in the case of the
<a href="/recursive-subroutines">recursive solution</a>, though the
order of the items is different.

