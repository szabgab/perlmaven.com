=title Mojolicious::Lite with embedded templates
=timestamp 2013-08-28T17:30:01
=indexes files
=status show
=books mojolicious
=author szabgab
=comments_disqus_enable 1

=abstract start

In the <a href="/getting-started-with-mojolicious-lite">first few examples of Mojolicious::Lite</a> we had our very simple HTML
embedded in the Perl code. This was good just to get our feet wet, but soon we'd like to
have some separation between Perl and HTML. Let's see how can we do this using Mojolicious::Lite.

=abstract end

We finished the <a href="/getting-started-with-mojolicious-lite">echo example</a> with the following script:

<code lang="perl">
use Mojolicious::Lite;
get '/' => { text => 'Hello World' };

get '/echo' => { text => q{
   <form method="POST"><input name="q"><input type="submit" value="Echo"></form>
}};

post '/echo' => sub {
    my $self = shift;
    $self->render( text => $self->param('q') );
};

app->secret('My extra secret passphrase.');

app->start;
</code>

Let's save it in a file called <hl>hello_world.pl</hl> and run it
as <hl>morbo hello_world.pl</hl>

Then visit the <a href="http://localhost:3000/echo">http://localhost:3000/echo</a> to
see it is working before we make changes.

<img src="/img/mojolicious_lite_echo_form.png" alt="Mojolicious::Lite echo form" />

<h2>The echo template</h2>

We replace the <hl>get '/echo'</hl> route with the following code:

<code lang="perl">
get '/echo' => sub {
    my $self = shift;
    $self->render('echo');
};
</code>

It is a bit similar now to the <hl>post</hl> method of the <hl>/echo</hl> route
in that here too we use an anonymous subroutine, but the call to the <hl>render</hl>
method is a bit different. Instead of passing the <hl>text</hl> key with the actual
HTML we want to send back, we pass in the name of our template.

The template itself can be added to the end of the file, after the <hl>__DATA__</hl> tag.
This <hl>__DATA__</hl> tag is a general Perl syntax that allows us to include pieces
of data within our script and then access it using the <hl><DATA></hl> file-handle.
Mojolicious::Lite uses this feature of Perl to allow us to include the templates
in the script but separate from our actual Perl code.

The <hl>@@</hl> syntax is used by the templating system of Mojolicious as a way to mark
where each template starts. <hl>echo.html.ep<hl> is the mark of the template that we can
render as the <b>echo</b> template.

<code lang="perl">
__DATA__

@@ echo.html.ep

What are you looking for?
<form method="POST"><input name="q"><input type="submit" value="Echo"></form>
</code>

The actual HTML code was changed so when we reload the page we can see that it has
indeed changed and it already comes from the template:

<img src="/img/mojolicious_lite_echo_form_from_template.png" alt="Mojolicious::Lite echo form" />

The code should look like this now:

<code lang="perl">
use Mojolicious::Lite;
get '/' => { text => 'Hello World' };

get '/echo' => sub {
    my $self = shift;
    $self->render('echo');
};

post '/echo' => sub {
    my $self = shift;
    $self->render( text => $self->param('q') );
};

app->secret('My extra secret passphrase.');

app->start;

__DATA__

@@ echo.html.ep

What are you looking for?
<form method="POST"><input name="q"><input type="submit" value="Echo"></form>
</code>

<h2>Server the response from a template too</h2>

This time we change the code of the <hl>POST</hl> method handling the <hl>/echo</hl>
route. The <hl>render</hl> method receives the name of the template as the first
parameter and then we can pass key-value pairs. They values will be accessible in
the template using the keys as scalar variables.

<code lang="perl">
post '/echo' => sub {
    my $self = shift;
    $self->render( 'response', msg => $self->param('q') );
};
</code>

The template itself is added to the end of the code, after the previous template:

<code lang="perl">
@@ response.html.ep

You typed: <%= $msg %>
</code>

Mojolicious::Lite allow us to the keys we passed as if they were scalar variable
and the following syntax embeds the value in our template: <hl><%= $msg %></hl> 

We can now type something in the form, click on <b>Echo</b> and see
the new response built from the template.

<h2>Connect the POST method to the same template</h2>

There are cases when the request page and the response page are really very different,
and we need two separate templates, but in other cases we might want them to be
served from the same template. For example, we might want the form to stay on the
responses page. So we merge the two templates together to get:

<code lang="perl">
__DATA__

@@ echo.html.ep

What are you looking for?
<form method="POST"><input name="q"><input type="submit" value="Echo"></form>

You typed: <%= $msg %>
</code>

Of course we also change the <hl>POST</hl> handler to pass the template name
'echo' instead of 'response'.

As morbo has already restarted our server, we can just reload the page now.
Because this is a POST-request, the browser will ask us if we want to re-submit the form.
In Google Chrome it will look like this:

<img src="/img/google_chrome_post_reload_confirmation.png" alt="Google Chrome POST reload confirmation pop-up" />

Once we click the <b>Continue</b> button we will see the new page that includes both the form
and the text we typed earlier:

<img src="/img/mojolicious_lite_echo_form_and_response.png" alt="Mojolicious::Lite echo form and response" />

So let's try to reload the original page now by clicking on the
link to <a href="http://localhost:3000/echo">http://localhost:3000/echo</a>.

We will get a huge error message:

<img src="/img/mojolicious_lite_missing_variable_error.png" alt="Mojolicious::Lite missing variable error message" />

This is the well known Perl error
<a href="/global-symbol-requires-explicit-package-name">Global symbol requires explicit package name</a>,
but displayed in a nice way.

The problem is that in our shared template we use the <hl>$msg</hl> variable, but we pass it only in the POST-handler
as only there has it any value.

I am not sure if this is the best solution, but we can change the call to <hl>render</hl> in the GET-handler
and replace <hl>$self->render('echo');</hl> by <hl>$self->render('echo', msg => undef);</hl>.
That way we'll have the <hl>$msg</hl> variable, but it will be <a href="/undef-and-defined-in-perl">undef</a>.

The resulting page is still not perfect, but at least it is not crashing:

<img src="/img/mojolicious_lite_echo_form_with_undef.png" alt="Mojolicious::Lite echo form with undef" />

The problem is that we still display the text <b>You typed:</b> even though we have not typed anything.

<h2>Conditionals in the template</h2>

The templating system that comes with Mojolicious is very powerful and allows us to embed Perl code
using <hl>%</hl> to mark lines with Perl code:

<code lang="perl">
% if (defined $msg) {
You typed: <%= $msg %>
% }
</code>

You can even indent the HTML between the Perl snippets as in HTML white-space has no impact.

<code lang="perl">
% if (defined $msg) {
    You typed: <%= $msg %>
% }
</code>

<h2>Exercise</h2>

Now it would be a good time to do a little exercise. The current version of our code
looks like this:

<code lang="perl">
use Mojolicious::Lite;

get '/' => { text => 'Hello World' };

get '/echo' => sub {
    my $self = shift;
    $self->render('echo', msg => undef);
};

post '/echo' => sub {
    my $self = shift;
    $self->render( 'echo', msg => $self->param('q') );
};

app->secret('My extra secret passphrase.');

app->start;

__DATA__

@@ echo.html.ep

What are you looking for?
<form method="POST"><input name="q"><input type="submit" value="Echo"></form>

% if (defined $msg) {
   You typed: <%= $msg %>
% }
</code>

Two of the 3 routes are already using templates. Please, change the script so
the <hl>/</hl> route will be served using a template called 'index'.

If you'd like to show your solution, please fork this
<a href="https://github.com/PerlMaven/mojolicious-lite-add-embedded-template">GitHub repository</a>
make the changes and send a pull request. I won't merge it but we all can comment on it.

