=title Scope of variables in Perl
=timestamp 2013-04-17T17:45:59
=indexes my, scope
=status show
=books beginner
=author szabgab
=comments_disqus_enable 0

=abstract start

There are two major variable types in Perl. One of them is the package global variable declared either with the now obsolete
<hl>use vars</hl> construct or with <hl>our</hl>.

The other one is the lexical variable declared with <hl>my</hl>.

Let's see what happens when you declare a variable using <hl>my</hl>? In which parts of the code will that variable be visible?
In other words, what is the <b>scope</b> of the variable?

=abstract end

<h2>Variable scope: enclosing block</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

{
    my $email = 'foo@bar.com';
    print "$email\n";     # foo@bar.com
}
# print $email;
# $email does not exists
# Global symbol "$email" requires explicit package name at ...
</code>

Inside the anonymous block (the pair of curly braces <hl>{}</hl>), first we see the declaration of a new variable called
<hl>$email</hl>. This variable exists between the point of its declaration till the end of the block. Thus the line
after the closing curly brace <hl>}</hl> had to be commented out. If you removed the <hl>#</hl> from the
<hl># print $email;</hl> line, and tried to run the script, you'd get the following compile-time error:
<a href="/global-symbol-requires-explicit-package-name">Global symbol "$email" requires explicit package name at ...</a>.

In other words, the <b>scope of every variable declared with my is the enclosing block.</b>.

<h2>Variable scope: visible everywhere</h2>

The variable <hl>$lname</hl> is declared at the beginning of the code. It will be visible
till the end of the file everywhere. Even inside blocks. Even if those are function declarations.
If we change the variable inside the block, that will change the value for the rest of the code.
Even when you leave the block:

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $lname = "Bar";
print "$lname\n";        # Bar

{
    print "$lname\n";    # Bar
    $lname = "Other";
    print "$lname\n";    # Other
}
print "$lname\n";        # Other
</code>


<h2>Variable hidden by other declaration</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $fname = "Foo";
print "$fname\n";        # Foo

{
    print "$fname\n";    # Foo

    my $fname  = "Other";
    print "$fname\n";    # Other
}
print "$fname\n";        # Foo
</code>

In this case the variable <hl>$fname</hl> is declared at the beginning of the code. As written earlier, it will be visible
till the end of the file everywhere, <b>except in places where they are hidden by locally declared variables with the same name</b>.

Inside the block we used <hl>my</hl> to declare another variable with the same name. This will effectively hide the <hl>$fname</hl>
declared outside the block till we leave the block. At the end of the block (at the closing <hl>}</hl>), the <hl>$fname</hl>
declared inside will be destroyed and the original <hl>$fname</hl> will be accessible again.
This feature is especially important as this makes it easy to create variables inside small scopes without the need to think
about possible use of the same name outside.

<h2>Same name in multiple blocks</h2>

You can freely use the same variable name in multiple block. These variables have no connection to each other.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

{
    my $name  = "Foo";
    print "$name\n";    # Foo
}
{
    my $name  = "Other";
    print "$name\n";    # Other
}
</code>

<h2>in-file package declaration</h2>


This a bit more advanced example, but it might be important to mention it here:

Perl allows us to switch between <b>name-spaces</b> using the <hl>package</hl> keyword inside
a file. A package declaration does <b>NOT</b> provide scope. If you declare a variable in
the implicit <b>main package</b> which is just the regular body of your script, that <hl>$fname</hl>
variable will be visible even in other name-spaces in the same file.

If you declare a variable called <hl>$lname</hl> in the 'Other' name-space, it will be visible
when later you might switch back to the <hl>main</hl> name-space. If the <hl>package Other</hl>
declaration was in another file, then the variables would have separate scope created by
the file.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $fname  = "Foo";
print "$fname\n";    # Foo

package Other;
use strict;
use warnings;

print "$fname\n";    # Foo
my $lname = 'Bar';
print "$lname\n";    # Bar


package main;

print "$fname\n";    # Foo
print "$lname\n";    # Bar
</code>


