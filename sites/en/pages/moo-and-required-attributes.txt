=title Moo and required attributes
=timestamp 2015-03-12T10:13:10
=indexes Moo, required
=status show
=books moo
=author szabgab
=comments_disqus_enable 1

=abstract start

Earlier we saw how to <a href="/oop-with-moo">create a class with attributes</a> and how
to set up <a href="/type-checking-with-moo">type checking</a> in the setters.

In this article you'll see how to set an attribute to be <b>required</b>.

=abstract end

Let's start with a simple example:

<h2>required</h2>

The Person class has a single read-write attribute called 'name':

This is the <hl>Person.pm</hl> file:

<code lang="perl">
package Person;
use Moo;

has name => (is => 'rw');

1;
</code>

In the script where we use the class, we create an object without passing any value to the 'name' attribute.
If we then try to print it we get a <a href="/use-of-uninitialized-value">use of uninitialized value</a> warning.

This is the <hl>programming.pl</hl>:

<include file="examples/moo-required/programming.pl">

<code>
Use of uninitialized value in say at programming.pl line 7.

DONE
</code>

How can we make sure the attribute is always set?

We can set it to be a <b>required</b> attribute:

Let's change <hl>Person.pm</hl> to be:

<include file="examples/moo-required/Person.pm">

If we now run <hl>perl programming.pl</hl> we get the following exception:

<code>
Missing required arguments: name at (eval 13) line 30.
</code>

This is the error you get from Moo 1.002000

I raised the issue both with the author and on <a href="http://www.perlmonks.org/?node_id=1039117">Perl Monks</a>
and while my fix was not accepted, it seems there will be some improvement in a later version of Moo.

I checked it again with Moo version 2.000000 and now I got the following error:

<code>
Missing required arguments: name at (eval 9) line 49.
</code>

In any case there is a tool that we can use for some temporary improvement:

Add <hl>use Carp::Always;</hl> to either programming.pl or Person.pm and run <hl>perl programming.pl</hl> again, or
run <hl>perl -MCarp::Always programming.pl</hl>.

This time the exception will look like this:

<code>
Missing required arguments: name at (eval 13) line 30.
    Person::new('Person') called at programming.pl line 7
</code>

In the second line of the exception we can see the point in our code
where we made the problematic call.

Of course if the code using the Person class is a bit more complex, like in the next examples:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use Person;

foo();

sub foo  {
   bar();
}

sub bar {
  my $anonymous = Person->new;
  say $anonymous->name;
  say 'DONE';
}
</code>

then the error message will be bigger as we get the full stack trace from the
beginning of the script:

<code>
Missing required arguments: name at (eval 13) line 30.
    Person::new('Person') called at programming.pl line 15
    main::bar() called at programming.pl line 10
    main::foo() called at programming.pl line 7
</code>

We will have a harder time noticing the source of the problem, which is in
the second line of the output.

Calling the constructor with a value for 'name' would work:

<code lang="perl">
my $anonymous = Person->new(name => 'Foo');
</code>

generating the expected output:

<code>
Foo
DONE
</code>

<h2>Passing undef to the attribute</h2>

Of course, one might pass <a href="/undef-and-defined-in-perl">undef</a> as the value of an attribute,
either by mistake: (The get_name function might return undef in some cases and we forget to check it)

<code lang="perl">
my $name = get_name();

my $anonymous = Person->new(name => $name);

sub get_name {
   # not yet implemented
}
</code>

or even on purpose:

<code lang="perl">
my $anonymous = Person->new(name => undef);
</code>

In either cases we get the same <a href="/use-of-uninitialized-value">use of uninitialized value warning</a>
but the code keeps running:

<code>
Use of uninitialized value in say at programming.pl line 7.

DONE
</code>

We can avoid this by adding <a href="/type-checking-with-moo">type checking</a> to our class making sure
the value is always, at least <b>defined</b>.

<code lang="perl">
package Person;
use Moo;

has name => (
     is => 'rw',
     required => 1,
     isa => sub { die 'cannot be undef' if not defined $_[0] },
);
</code>

If now, someone passed <b>undef</b> to the constructor, Moo will generate the
following exception:

<code>
isa check for "name" failed: cannot be undef at Person.pm line 9.
</code>

If you included <b>use Carp::Always;</b> for better error messages you would get this output,
in which the 4th line from the top is the one revealing the real source of the error (the call to
new).

<code>
isa check for "name" failed: cannot be undef at (eval 13) line 38.
    Sub::Quote::__ANON__('cannot be undef at Person.pm line 9.\x{a}') called at Person.pm line 9
    Person::__ANON__(undef) called at (eval 13) line 40
    Person::new('Person', 'name', undef) called at programming.pl line 8
</code>

<h2>The values of required</h2>

The <hl>required</hl> field accepts a <a href="/boolean-values-in-perl">boolean value</a>.
By default it is false, but you can set it to any true value.


