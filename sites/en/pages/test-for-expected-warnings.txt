=title Test for expected warnings
=timestamp 2016-10-12T19:30:01
=indexes Test::Warn
=tags screencast
=status show
=books testing
=author szabgab
=archive 1
=comments_disqus_enable 1

=abstract start

There are many modules with function that would give a warning in certain situations. This can be a deprecation warning,
when the function is left in to provide backward compability, but you actually want to remove it in the future.

Or, it can be warning of improper input values if for some reason you do not want to throw an exception.

In either case, the warning must stay there as changes are made to the code-base. That warning is now part of API of the module
and as such we should write a test that will make sure the warning isn't removed by accident or by someone who
likes to eliminate warnings....

=abstract end

<screencast file="/media/videos/test-for-expected-warnings" />

Let's see a simple example. Let's say the <hl>fibonacci</hl> function exported by the <hl>MyTools</hl> module will warn if
the users passes a negative number:

Our test script saved as <hl>t/fibonacci_negative.t</hl> looks like this:

<code lang="perl">
use strict;
use warnings;

use Test::More tests => 1;

use MyTools;

my $result = fibonacci(-1);
is($result, 0, 'fibonacci on -1 returns 0');
</code>

If we run it using <hl>prove -l t/fibonacci_negative.t</hl> we get the following output:

<code>
1..1
Given number must be > 0 at lib/MyTools.pm line 19.
ok 1 - fibonacci on -1 returns 0
</code>

As checked by the <hl>is</hl> function, the returned values was correct, but the warning was printed on the output of the script.

It is bad because an observer will notice the warning and start complaining that the test found some problems, and it is bad
because if the code in the module changes and the warning stops appearing no one will notice.

We need to capture the warning and check if the captured value is the expected string.

We could capture the warning ourself using the <hl>SIG{__WARN__}</hl>, but it just as we build a testing module
based on <a href="http://metacpan.org/pod/Test::Builder">Test::Builder</a>, there is also a testing module called <a href="http://metacpan.org/pod/Test::Warn">Test::Warn</a>
that provides exactly this service.

<code lang="perl">
use strict;
use warnings;

use Test::More tests => 2;
use Test::Warn;

use MyTools;


my $result;
warning_is {$result = fibonacci(-1)} "Given number must be > 0",
    'warning when called with -1';
is($result, 0, 'fibonacci on -1 returns 0');
</code>

After importing the <a href="https://metacpan.org/pod/Test::Warn">Test::Warn</a> module it provides a number of testing function
with a slightly strange syntax.

The function names are <hl>warning_is</hl> to check if the given piece of code generates exactly one warning and if we would like to compare 
the text of the warning exactly.

<hl>warnings_are</hl> expects a list of warnings and succeeds if the actual warnings came the exact same order and had the exact same text.

<hl>warning_like</hl> expects one warning, but instead of comparing for equality it checks if the warning matches a given regular expression.

They are just like the <hl>is</hl> and <hl>like</hl> functions of <a href="https://metacpan.org/pod/Test::More">Test::More</a>, but of course,
they also capture a warning.

There are a few others, but I think these can give you the idea.

Now for the syntax:

Each one of these receives a block in curly braces <hl>{}</hl>, that is the code-block that will be executed, and then immediately
after it, without even a separating comma <hl>,</hl> we put the expected value(s). In case of <hl>warning_is</hl> this is a single string.
In case of <hl>warnings_are</hl>, this is a reference to an array of strings. In case of <hl>warning_like</hl> this is a regular
expression created using <hl>qr</hl>.

After that, separated by a comma, we can add the optional "name" parameter of this test unit.

As we also still would like to compare the value returned by the fibonacci function to some expected value, we need to assign it
to a variable, but because the <hl>fibonacci</hl> call is now in a block, we need to declare that variable outside of the block,
in order to have it after the <hl>warning_is</hl> function has finished. That's why we declare <hl>$result</hl> before the
<hl>warning_is</hl> call.

Running the test script now <hl>prove -l t/fibonacci_negative.t</hl> will yield the following output:

<code>
1..2
ok 1 - warning when called with -1
ok 2 - fibonacci on -1 returns 0
</code>

