=title Replace character by character (transliterate) using tr of Perl
=timestamp 2013-11-19T19:30:01
=indexes y, tr
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

I have not needed this frequently, but if you need to replace a set of characters with
another set of characters in a string, there is a better solution than using regexes.

In Perl <a href="/perldoc/tr">tr</a> is the transliterator tool that can replace characters by other characters pair-wise.

=abstract end

<h2>Simple example</h2>

<hl>tr</hl> looks very similar to the substitution operator,
but it behaves in a different way:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $text = 'abc bad acdf';
say $text;

$text =~ tr/a/z/;
say $text;
</code>

Replaces every <hl>a</hl> character by <hl>z</hl>:

<code>
abc bad acdf
zbc bzd zcdf
</code>


<h2>More than one character</h2>

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $text = 'abc bad acdf';
say $text;

$text =~ tr/ab/zx/;
say $text;
</code>

Replaces every <hl>a</hl> character by <hl>z</hl> and every <hl>b</hl> character by <hl>x</hl>:

<code>
abc bad acdf
zxc xzd zcdf
</code>

<h2>More about tr</h2>

<hl>y</hl> is a synonym of <hl>tr</hl> available for historical reasons.
There are a few modifiers and special cases with tr, that you can
read about in the <a href="/perldoc/tr">documentation of tr</a>.


<h2>When is tr better than a regex?</h2>

<hl>tr</hl> replaces character-by-character while a regex substitution will replace a match with a string.
They do different things. So <hl>tr</hl> is only better if you want to do something else.

BTW you can imitate what <hl>tr</hl> does using a regex substitution with the following code:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my %map = (
    a => 'z',
    b => 'x',
);

my $text = 'abc bad acdf';
say $text;

$text =~ s/([ab])/$map{$1}/g;
say $text;
</code>

Here we have a hash called <hl>%map</hl> that maps the original string (original character) to the new string (character)
and then uses a single global substitution to replace all the occurrences of every "original character" by the corresponding
"new character".

Note, that in this solution we actually had to write the list of "original characters" both in the <hl>%map</hl> and in the regex.
In order to avoid that we can have a more generic solution:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my %map = (
    a => 'z',
    b => 'x',
);

my $text = 'abc bad acdf';
say $text;

my $chars = join '', keys %map;
$text =~ s/([$chars])/$map{$1}/g;
say $text;
</code>

Here we generate the list of characters that we want to replace from the keys
of the <hl>%map</hl> hash.

This will work exactly as the <hl>tr</hl> works, but then again, why not use a <hl>tr</hl> if this is what we would like to do?


<h2>Comments</h2>

Gabor...I am very thankful you take the time to teach us. I do not get to use Perl much but every time I do I find myself back reading what you have written to us. Thank you sir!!!

<hr>

Can i use transliterate fom one start point to another last point.

Lets have the string "blablabla (blablah) ablaabla" and i have to transliterate only the contents of the parentheses. Can i use regex and then tr///.

I wrote this :

#!/usr/bin/perl
use strict;
use warnings;
use 5.010;


my $string = 'blablabla (blablah) ablaabla';


if ($string =~ /(\(.*?\))/g) {print $1=~ tr/abcdef/ABCDEF/;}


