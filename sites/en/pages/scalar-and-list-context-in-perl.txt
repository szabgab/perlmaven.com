=title Scalar and List context in Perl, the size of an array
=timestamp 2013-04-10T11:45:56
=indexes scalar, list, array, size, length, context, Perl
=status show
=books beginner
=author szabgab
=comments_disqus_enable 1

=abstract start

In this episode of the <a href="/perl-tutorial">Perl tutorial</a> we are going to look at
<b>context sensitivity</b> in Perl.

In English, as in most of the other spoken languages, words can have multiple meanings.
For example the word "left" has several meanings:

I left the building.

I turned left at the building.

We understand which is the correct meaning by the sentences around the word.
This is called the context.

Perl 5 is similar. Words, function calls, and other expressions can have
different meaning depending on context. It makes learning harder, but provides
more expressiveness.

=abstract end

There are two major contexts in Perl: SCALAR and LIST context.

<h2>Array in LIST context</h2>

Let's see an example:

<code lang="perl">
my @words = ('Foo', 'Bar', 'Baz');
my @names = @words;
</code>

After the above assignment <hl>@names</hl> contains a copy of the values that were in <hl>@words</hl>;

Assignment of an array to another array copies the content of the array.

<h2>Array in SCALAR context</h2>

<code lang="perl">
my @words = ('Foo', 'Bar', 'Baz');
my $people =  @words;
</code>

This time we assigned the <hl>@words</hl> array to <hl>$people</hl>, a scalar variable.

Other languages would behave differently, but in Perl this
assignment places <b>the number of elements of the array</b>
in the scalar variable.

That's arbitrary, and in the above case not very useful either, but there are a number of other
cases when this behavior can be very useful.

<h2>SCALAR and LIST context</h2>

The above two are called SCALAR and LIST context. They mean if the expectation is
to get a single value (in SCALAR context), or if multiple values are expected (LIST context).
In LIST context the number of values can be 0, 1, 2, or any other number.

<h2>The context of the if statement</h2>

Look at this example:

<code lang="perl">
my @words = ('Foo', 'Bar', 'Baz');

if (@words) {
   say "There are some words in the array";
}
</code>

Inside the the condition part of the <hl>if</hl> statement we are
expecting exactly one value. That must be SCALAR context then.

By now we know that the value of an array in SCALAR context is the number
of elements. We also know that this is 0 (that is <a href="/boolean-values-in-perl">FALSE</a>)
when the array is empty, and some other positive number (that is <a href="/boolean-values-in-perl">TRUE</a>),
when the array has 1 or more elements.

So because of that arbitrary decision above, the code <hl>if (@words)</hl>
checks if there is any content in the array and fails if the array is empty.

Turning the if-statement around  <hl>if (! @words)</hl> will be true
if the array is empty.

<h2>SCALAR and LIST context</h2>

In the <a href="/the-year-19100">previous episode</a> we saw how <hl>localtime()</hl>
behaves in SCALAR and LIST context, and now we saw how an array behaves in SCALAR and LIST context.

There is no general rule about context, and you will have to learn the specific cases,
but usually they are quite obvious. In any case, when you look up a function using
<a href="/core-perl-documentation-cpan-module-documentation">perldoc</a>,
you will see an explanation of this for each function.
At least in the cases where the SCALAR and LIST contexts yield different results.

We should now look at a few more examples for expressions in Perl,
and what kind of context they create.

<h2>Creating SCALAR context</h2>

We already saw that no matter what you assign to a scalar variable that thing will be in SCALAR context.
Let's describe it this way:

<code>
$x = SCALAR;
</code>

Because individual elements of an array are also scalars, assignment to them also creates SCALAR context:

<code>
$word[3] = SCALAR;
</code>

Concatenation expects two strings on either side so it creates SCALAR context on both sides:

<code>
"string" . SCALAR;
</code>

but also

<code>
SCALAR . "string"
</code>

So

<code lang="perl">
my @words = ('Foo', 'Bar', 'Baz');
say "Number of elements: " . @words;
say "It is now " . localtime();
</code>

Will print

<code>
Number of elements: 3
It is now Thu Feb 30 14:15:53 1998
</code>

Numerical operators usually expect two numbers - two scalars - on either side.
So numerical operators create SCALAR context on both sides:

<code>
5 + SCALAR;

SCALAR + 5;
</code>

<h2>Creating LIST context</h2>

There are constructs that create LIST context:

Assignment to an array is one of them:

<code>
@x = LIST;
</code>

Assignment to a list is another place:

<code>
($x, $y) = LIST;
</code>

Even if that list only has one element:

<code>
($x) =  LIST;
</code>

That brings us to an important issue that can easily trick people:

<h2>When are the parentheses significant?</h2>

<code lang="perl">
use strict;
use warnings;
use 5.010;

my @words = ('Foo', 'Bar', 'Baz');

my ($x) = @words;
my $y   = @words;

say $x;
say $y;
</code>

the output is:

<code>
Foo
3
</code>

This is one of the few places where the parentheses are very important.

In the first assignment <hl>my ($x) = @words;</hl> we assigned
to a <b>list</b> of scalar variable(s).
That created LIST context on the right hand side. That means the <b>values</b>
of the array were copied to the list on the left hand side. Because there was only
one scalar, the first element of the array got copied and the rest not.

In the second assignment <hl>my $y   = @words;</hl> we assigned <b>directly</b> to
a scalar variable. That created SCALAR context on the right hand side. An array in
SCALAR context returns the number of elements in it.

This will be very important when you look at
<a href="/subroutines-and-functions-in-perl">passing parameters to functions</a>.

<h2>Forcing SCALAR context</h2>

Both <hl>print()</hl> and <hl>say()</hl> create LIST context for their parameters.
So what if you would like to print the number of elements in an array?
What if you'd like to print the nicely formatted date that is returned by <hl>localtime()</hl>?

Let's try this:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my @words = ('Foo', 'Bar', 'Baz');

say @words;
say localtime();
</code>

And the output is

<code>
FooBarBaz
3542071011113100
</code>
The former is somehow understandable. These are the values in the array smashed together.

The second one is confusing. It is NOT the same as the result of the <hl>time()</hl>
function one might think. It is actually the 9 numbers returned by the <hl>localtime()</hl>
function in LIST context. If you don't remember, check it out in the episode about
<a href="/the-year-19100">the year of 19100</a>.

The solution is to use the <hl>scalar()</hl> function that  will create SCALAR context for its parameter.
Actually that's the whole job of the <hl>scalar()</hl> function. Some people might think about it as
<b>casting</b> from plural to singular, even though I think this word is not used often in Perl-land.

<code lang="perl">
say scalar @words;
say scalar localtime();
</code>

And the output will be:

<code>
3
Mon Nov  7 21:02:41 2011
</code>

<h2>Length or size of an array in Perl</h2>

In a nutshell, if you would like to get the size of an array in Perl you can use
the <hl>scalar()</hl> function to force it in SCALAR context and return the size.

<h2>The tricky way</h2>

Sometimes you might see code like this:

<code lang="perl">
0 + @words;
</code>

This is basically a tricky way to get the size of the array. The <hl> + </hl> operator
creates SCALAR context on both sides. An array will return its size in SCALAR context.
Adding 0 to it does not change the number, so the above expression returns the size of the array.

I'd recommend writing the slightly longer but clearer way using the <hl>scalar</hl> function.


