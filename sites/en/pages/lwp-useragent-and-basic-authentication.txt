=title LWP::UserAgent and Basic Authentication
=timestamp 2015-06-25T15:00:01
=indexes LWP::UserAgent
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

The so called <a href="https://en.wikipedia.org/wiki/Basic_access_authentication">Basic access authentication</a> is a very simple way
to limit access to certain web pages.

Web servers can be configured to protect a given directory, or a whole site by a few lines of configuration.
Users are required to authenticate by providing a correct username/password pair which is checked by the web server against a
given set of such pairs.

How can we write a script using <a href="https://metacpan.org/pod/LWP::UserAgent">LWP::UserAgent</a> that will authenticate
with such server and will be able to access the protected pages?

=abstract end

<h2>PAUSE</h2>

First of all, let's see an example. <a href="http://pause.perl.org/">PAUSE</a>
The Perl Authors Upload Server, which is basically the inbound gate of CPAN usess Basic Authentication.

If you click on <a href="https://pause.perl.org/pause/authenquery">Login</a>, you'll see a
"challenge", a pop-up that, depending on your browser, might look like this:

<img src="/img/pause_basic_authentication.png" alt="PAUSE Basic Authentication pop-up">

If you cancel it, you'll see and <b>Authorization Required</b> error. If you type in incorrect credentials
it will show you the pop-up again and again till you get tired of it.


<h2>Access without authentication</h2>

In the first version of our script, we just create an <a href="https://metacpan.org/pod/LWP::UserAgent">LWP::UserAgent</a>
object, and use the <hl>get</hl> method to fetch the page that requires authentication.

<code lang="perl">
use strict;
use warnings;
use 5.010;

use LWP::UserAgent;
my $ua = LWP::UserAgent->new;
my $resp = $ua->get( 'https://pause.perl.org/pause/authenquery' );
if ($resp->is_success) {
    say $resp->decoded_content;
} else {
    say $resp->status_line;
    say $resp->decoded_content;
}
</code>

We have not provided credentials (username/password) and thus the request fails. <hl>is_success</hl>
will return false.
 
The <hl>status_line</hl> method will return

<code>
401 Authorization Required
</code>

The <hl>decoded_content</hl> method will return the content of the page we saw after canceling the prompt:

<code>
<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML 2.0//EN">
<HTML><HEAD>
<TITLE>401 Authorization Required</TITLE>
</HEAD><BODY>
<H1>Authorization Required</H1>
This server could not verify that you
are authorized to access the document
requested.  Either you supplied the wrong
credentials (e.g., bad password), or your
browser doesn't understand how to supply
the credentials required.<P>
</BODY></HTML>
</code>

<h2>Providing credentials</h2>

Reading the documentation of <a href="https://metacpan.org/pod/LWP::UserAgent">LWP::UserAgent</a> there seem
to be two ways to provide the credentials.

One of them is to subclass LWP::UserAgent, implement the <hl>get_basic_credentials</hl> method that will be
called when the credential are required. At that time the method will receive 3 parameters, two of which will
identify the web address (URL or URI), and the <hl>$realm</hl>. (See realm explained later.) The method will
need to return a list of <hl>($username, $password)</hl> that will be supplied as credentials to this challenge. 

The other way is to call the <hl>credentials</hl> method with 4 parameters:
<hl>$ua->credentials( $netloc, $realm, $uname, $pass )</hl> to set the challenge/response pairs of pairs.
The credentials method needs to receive the URL (here called $netloc), the realm, and the username/password pair.

The way this works in the background is that the UserAgent will always call the <hl>get_basic_credentials</hl>
method. If we have overridden in a subclass, than it is our responsibility to recognize the current URL/realm
pair and return the correct username/password pair. On the other hand, if we have left <hl>get_basic_credentials</hl>
alone then the already existing implementation of <hl>get_basic_credentials</hl> will be called. It will receive
the URL/Realm pair and look them up in an internal two-dimensional hash. Using the <hl>credentials</hl> method
we can fill up that internal hash with URL/Real pairs mapping to username/password pairs.

<h2>The Realm</h2>

The "realm" is a string, sort of an identification string of the area protected by the basic authentication system.

The realm is usually displayed in the challenge pop-up box. In the case of the PAUSE server it has:

<code>
The site says: "PAUSE"
</code>

So the realm in this case is "PAUSE".

<h2>How to find out the URL and realm?</h2>

As calling the <hl>credentials</hl> method seems much more simple than sub-classing LWP::UserAgent and overriding
<hl>get_basic_credentials</hl>, I wanted to do the former. I tried it several times passing various values as URL,
and "PAUSE" as the realm, but it did not work. So I decided to hack the system a bit to see what are the values
received by the <hl>get_basic_credentials</hl> method.

For this I added the following 3 lines to the original script,

<code lang="perl">
sub LWP::UserAgent::get_basic_credentials {
    warn "@_\n";
}
</code>

and removed the code after the call to <hl>get</hl> as they were not needed.
The script looked like this:

<code lang="perl">
use strict;
use warnings;
use 5.010;

use LWP::UserAgent;
sub LWP::UserAgent::get_basic_credentials {
    warn "@_\n";
}
my $ua = LWP::UserAgent->new;
my $resp = $ua->get( 'https://pause.perl.org/pause/authenquery' );
</code>

Running the script resulted in the following output:

<code>
$ perl pause.pl 
Subroutine LWP::UserAgent::get_basic_credentials redefined at pause.pl line 7.
LWP::UserAgent=HASH(0x7facb1027d40) PAUSE https://pause.perl.org/pause/authenquery 
</code>

What we did here, is defined the <hl>get_basic_credentials</hl> method in the LWP::UserAgent
<a href="/packages-modules-and-namespace-in-perl">name-space</a>. Our methods does not do much,
just prints the received parameters using <hl>warn</hl>. As that method already existed,
when we run the script, we'll get a warning <hl>Subroutine LWP::UserAgent::get_basic_credentials redefined at ...</hl>.
This is, as this is only a temporary solution while we do our little research.

This technique, of reaching inside another name-space and redefining functions there is called
<a href="https://en.wikipedia.org/wiki/Monkey_patch">Monkey patching</a>. It is one of the great flexibilities of
dynamic languages. It can be very useful as quick-and-dirty solution, as in our case, and can be very harmful if
overused.

Anyway, the second line of output is what really interests us. There are 4 values in the output. The first one
is the current LWP::UserAgent instance. It is always <a href="/getting-started-with-classic-perl-oop">passed as the first parameter</a>.
The second parameter (PAUSE) is the realm, and the third parameter (https://pause.perl.org/pause/authenquery) is the URL.

<h2>Supply the credentials</h2>

The next step was to remove the newly added code (the monkey patch) and instead of that to add a
call to <hl>credentials</hl>. The resulting script looked like this (but it had my password hard-coded).

<code lang="perl">
use strict;
use warnings;
use 5.010;

use LWP::UserAgent;
my $ua = LWP::UserAgent->new;

$ua->credentials( 'https://pause.perl.org/pause/authenquery', 'PAUSE', 'szabgab', '**********');

my $resp = $ua->get( 'https://pause.perl.org/pause/authenquery' );
say $resp->status_line;
</code>

Running this script we get:

<code>
401 Authorization Required
</code>

oops.

Let's try just the hostname

<code lang="perl">
$ua->credentials( 'pause.perl.org', 'PAUSE', 'szabgab', '**********');
</code>

Still no:

<code>
401 Authorization Required
</code>

Let's try to add the port number as well (The example in the documentation
had the port number :80 in, but this is an https request and the https requests
use port 443.)

<code lang="perl">
$ua->credentials( 'pause.perl.org:443', 'PAUSE', 'szabgab', '**********');
</code>

Running the script again already yield:

<code>
200 OK
</code>

Heúrēka!

If we added a call to <hl>say $resp->decoded_content;</hl> we would get all the HTML that is on the internal, protect page.
We can now write our application to deal with the PAUSE server, or for that matter, any server protected by basic authentication.

<h2>Monkey patching again</h2>

While we already have one solution, let's see the other ways.

We can go back to the Monkey-patching and redefine the <hl>get_basic_credentials</hl> method.

<code lang="perl">
sub LWP::UserAgent::get_basic_credentials {
    my ($self, $realm, $url, $isproxy) = @_;

    return 'szabgab', '**********';
}
</code>

In this version we accept the 4 parameters passed to the method, but we have not actually used them.
(We have not seen it, but the $isproxy was in the documentation, so I added it here.)
In a real application we might use the parameters and return the correct username/password pair based on these
parameters and maybe based on some other information.

This version works, but it still gives us the <hl>Subroutine LWP::UserAgent::get_basic_credentials redefined</hl> warning.

We can use <hl>no warnings 'redefine';</hl> to turn off the redefine warning. This call will turn off
the specific warning till the end of the file or the end of the enclosing block. Hence we wrapped
the whole redefinition part in an anonymous block. It is a bit more writing, but it is much safer. This way
if we accidentally redefined another function later on, we'll still get the proper warning about those cases.
We have turned off warning only for the specific block of our code.

<code lang="perl">
{
    no warnings 'redefine';

    sub LWP::UserAgent::get_basic_credentials {
        my ($self, $realm, $url) = @_;
    
        return 'szabgab', '**********';
    }
}
</code>


So this solution works. It does not warn, but it is still just a monkey-patch.

What about proper sub-classing?


<h2>Sub-classing - in the script</h2>

In the beginning of this script we switch to a new name-space we called <hl>My::Client</hl>.
This is going to be the subclass of <hl>LWP::UserAgent</hl>.

The call <hl>use base 'LWP::UserAgent';</hl> arranges the sub-classing. It tells perl that
our current class (My::Client) inherits from the LWP::UserAgent base-class.
The we implement the <hl>get_basic_credentials</hl> method inside the My::Client package/name-space
and switch back to the default, <hl>main</hl> name-space using <hl>package main;</hl>.

In the main part of the code we create the UserAgent object using <hl>My::Client->new;</hl>.

There is no need to explicitly load <hl>LWP::UserAgent</hl> as the <hl>use base 'LWP::UserAgent';</hl>
will arrange the loading of that module.


<code lang="perl">
use strict;
use warnings;
use 5.010;

package My::Client;
use strict;
use warnings;
use base 'LWP::UserAgent';

sub get_basic_credentials {
    my ($self, $realm, $url) = @_;
    
    return 'szabgab', '**********';
}

package main;

my $ua = My::Client->new;

my $resp = $ua->get( 'https://pause.perl.org/pause/authenquery' );
say $resp->status_line;
</code>

This works: (after typing in the real password, of course)

<code>
200 OK
</code>


<h2>Sub-classing - in external file</h2>

The above sub-classing code works perfectly, but it is usually cleaner to put each package
in its own file. It also make reuse possible.

So we moved the implementation of the <hl>My::Client</hl> module to the file "lib/My/Client.pm".
We also had to add <hl>1;</hl> at the end to <a href="/getting-started-with-classic-perl-oop">make Perl happy</a>.

<code lang="perl">
package My::Client;
use strict;
use warnings;
use base 'LWP::UserAgent';

sub get_basic_credentials {
    my ($self, $realm, $url) = @_;
    
    return 'szabgab', '**********';
}

1;
</code>

The script itself also had some changes:

First of all we now have to explicitly load <hl>My::Client</hl> with a use-statement. We preceded that with a
<hl>use lib 'lib';</hl> to <a href="/how-to-add-a-relative-directory-to-inc">adjust @INC</a>,
and we could remove the extra <hl>package main;</hl> as we are in the main package by default.

<code lang="perl">
use strict;
use warnings;
use 5.010;

use lib 'lib';
use My::Client;
my $ua = My::Client->new;

my $resp = $ua->get( 'https://pause.perl.org/pause/authenquery' );
say $resp->status_line;
</code>

And the result is again:

<code>
200 OK
</code>

<h2>A simpler solution with HTTP::Request::Common</h2>

There is an even simpler solution using <a href="/basic-authentication-with-lwp-useragent-and-http-request-common">HTTP::Request::Common</a>.

<h2>Conclusion</h2>

We saw a number of ways to implement this. Using <h>credentials</hl> is the quick way, but we can also properly subclass
LWP::UserAgent which provide a lot more flexibility and a potential to reuse our code.

