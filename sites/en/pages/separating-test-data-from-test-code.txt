=title Separating test data from test code
=timestamp 2016-03-29T20:30:01
=indexes Test::Simple
=tags screencast
=status show
=books testing
=author szabgab
=comments_disqus_enable 0

=abstract start

In the earlier articles we have invested quite some energy in moving all the data from within the test code
to a <a href="/refactoring-large-test-suite-separating-data-from-code">separate array</a> and then
we dealt with the <a href="/test-without-a-plan">issue of counting</a>.

In this article we will go further and move the data into a separate file.

=abstract end

<screencast file="separating-test-data-from-test-code" youtube="DxYrs6Ur1aY" />

We can create an arbitrary format like this:

<code>
2 = 1, 1
4 = 2, 2
6 = 2, 2, 2
6 = 3, 3

# negative

-2 = -1, -1

# edge cases
0 = 1, -1
</code>

In this format we put the parameters sent to the <hl>sum()</hl> function to the right of the <hl>=</hl> sign, and put the expected value
to the left. Then we also need to update our test script to read the test cases from this file:

<code lang="perl">
use strict;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/lib";
use MyCalc;

my @cases;
BEGIN {
    my $file = "$FindBin::Bin/sum.txt";
    open my $fh, '<', $file or die "Could not open '$file': $!";

    while (my $line = <$fh>) {
        chomp $line;
        next if $line =~ /^\s*(#.*)?$/;
        my @data = split /\s*[,=]\s*/, $line;
        push @cases, \@data;
    }
}

use Test::Simple tests => scalar @cases;

for my $c (@cases) {
    my ($exp, @params) = @$c;
    my $descr = 'sum(' . join(', ', @params) . ") == $exp";
    ok sum(@params) == $exp, $descr;
}
</code>

The only part we changed here is the part inside the <hl>BEGIN</hl> block where we read in,
and parse the content of the <b>sum.txt</b> file, and then create the <hl>@cases</hl> array
in the same format as we used to have when we had the data in the script.


<h2>Fit - Framework for Integrated Test</h2>

This brings us to the idea of <a href="http://fit.c2.com/">Fit</a> (also on <a href="http://en.wikipedia.org/wiki/Framework_for_integrated_test">Wikipedia</a>).
It has various implementations, but I think the idea is more important than any of the implementations.

The idea is that there are people who are "domain experts", in our case people who know what the "sum function" should return,
and there are programmers who can interact with the application under test (AUT). Unless we are writing low level unit-tests,
these two group of people don't necessarily overlap.

If we can separate the data (input and expected output) we need for the test from the code that actually interacts with the
application, then we can improve our environment a lot. The domain experts can provide input in some format
comfortable for them. It could even be an spreadsheet using Excel or Open Office.

The programmers can then focus on writing the code to exercise the input data and compare the results to the expected output.

<h2>Exercise</h2>

Your exercise is now to enlarge the test-suite, to add more test cases to the sum.txt file.

