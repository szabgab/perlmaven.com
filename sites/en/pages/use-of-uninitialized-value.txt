=title Use of uninitialized value
=timestamp 2012-09-05T21:45:56
=indexes undef, uninitialized value, $|, warnings, buffering
=status show
=books beginner
=author szabgab
=comments_disqus_enable 1

=abstract start

This is one of the most common warning you will encounter while running Perl code.

It is a warning, it won't stop your script from running and it is only generated if
warnings were turned on. Which is recommended.

The most common way to turn on warnings is by including a <hl>use warnings;</hl> statement
at the beginning of your script or module.

=abstract end

The older way is adding a <hl>-w</hl> flag on the sh-bang line. Usually looks like this
as the first line of your script:

<hl>#!/usr/bin/perl -w</hl>

There are certain differences, but as <hl>use warnings</hl> is available for 12 years now,
there is no reason to avoid it. In other words:

Always <hl>use warnings;</hl>!


Let's go back to the actual warning I wanted to explain.

<h2>A quick explanation</h2>

<code>
Use of uninitialized value $x in say at perl_warning_1.pl line 6.
</code>

This means the variable <hl>$x</hl> has no value (its value is the special value <hl>undef</hl>).
Either it never got a value, or at some point <hl>undef</hl> was assigned to it.

You should look for the places where the variable got the last assignment,
or you should try to understand why that piece of code has never been executed.

<h2>A simple example</h2>

The following example will generate such warning.

<code lang="perl">
use warnings;
use strict;
use 5.010;

my $x;
say $x;
</code>

Perl is very nice, tells us which file generated the warning and on which line.

<h2>Only a warning</h2>

As I mentioned this is only a warning. If the script has more statements after that
<hl>say</hl> statement, they will be executed:

<code lang="perl">
use warnings;
use strict;
use 5.010;

my $x;
say $x;
$x = 42;
say $x;
</code>

This will print

<code>
Use of uninitialized value $x in say at perl_warning_1.pl line 6.

42
</code>

<h2>Confusing output order</h2>

Beware though, if your code has print statements before the line
generating the warning, like in this example:

<code lang="perl">
use warnings;
use strict;
use 5.010;

print 'OK';
my $x;
say $x;
$x = 42;
say $x;
</code>

the result might be confusing.

<code>
Use of uninitialized value $x in say at perl_warning_1.pl line 7.
OK
42
</code>

Here, 'OK', the result of the <hl>print</hl> is seen <b>after</b>
the warning, even though it was called <b>before</b> the
code that generated the warning.

This strangeness is the result of <hl>IO buffering</hl>.
By default Perl buffers STDOUT, the standard output channel,
while it does not buffer STDERR, the standard error channel.

So while the word 'OK' is waiting for the buffer to be flushed,
the warning message already arrives to the screen.

<h2>Turning off buffering</h2>

In order to avoid this you can turn off the buffering of STDOUT.

This is done by the following code: <hl>$| = 1;</hl>
at the beginning of the script.


<code lang="perl">
use warnings;
use strict;
use 5.010;

$| = 1;

print 'OK';
my $x;
say $x;
$x = 42;
say $x;
</code>

<code>
OKUse of uninitialized value $x in say at perl_warning_1.pl line 7.
42
</code>

(The warning is on the same line as the <b>OK</b> because we have not printed a newline
<hl>\n</hl> after the OK.)

<h2>The unwanted scope</h2>

<code lang="perl">
use warnings;
use strict;
use 5.010;

my $x;
my $y = 1;

if ($y) {
  my $x = 42;
}
say $x;
</code>

This code too produces <hl>Use of uninitialized value $x in say at perl_warning_1.pl line 11.</hl>

I have managed to make this mistake several times. Not paying attention I used <hl>my $x</hl>
inside the <hl>if</hl> block, which meant I have created another $x variable,
assigned 42 to it just to let it go out of the scope at the end of the block.
(The $y = 1 is just a placeholder for some real code and some real condition.
It is there only to make this example a bit more realistic.)

There are of course cases when I need to declare a variable inside an if block, but not always.
When I do that by mistake it is painful to find the bug.




