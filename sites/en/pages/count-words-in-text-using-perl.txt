=title Count the frequency of words in text using Perl
=timestamp 2013-11-21T12:07:01
=indexes hash, open, /g, split
=status show
=books beginner
=author szabgab
=comments_disqus_enable 0

=abstract start

Counting how many times a given (sub)string appears in a given text is a very common
task where Perl is a perfect fit.

It can be counting the word density on a web page,
the frequency of DNA sequences,
or the number of hits on a web site that came from various IP addresses.

=abstract end


<h2>Impatient?</h2>

Here is an example counting the frequency of strings in a text file.
The following script counts the frequency of strings separated by spaces:

<include file="examples/count_words.pl">

<h2>The task</h2>

The task basically has 3 parts:
<ol>
<li>Go over the (sub)strings one-by-one.</li>
<li>Store the number of occurrence of each string in a hash.</li>
<li>Generate the report.</li>
</ol>

We won't spend a lot of time on the report part. We assume that each string we want
to count is less than 30 characters long so they will comfortably fit in one
line of the screen together with the number of occurrence.

A hash is a perfect place to store the counters: the keys will be the string we count
and the values will be the number of occurrence.

<h2>Sort the results according to the ASCII table</h2>

Given the data in a hash called <hl>%count</hl> we can display the results like this:

<code lang="perl">
foreach my $word (sort keys %count) {
    printf "%-31s %s\n", $word, $count{$word};
}
</code>

In this case the strings are sorted according to the ASCII table.

<h2>Sort the results according to frequency</h2>

The <hl>sort { $count{$a} <=> $count{$b} }</hl> statement will sort
the strings based on the values in ascending (growing) order. The call to <hl>reverse</hl>
in front of the <hl>sort</hl> will make sure the results are in descending order.

<code lang="perl">
foreach my $word (reverse sort { $count{$a} <=> $count{$b} } keys %count) {
    printf "%-31s %s\n", $word, $count{$word};
}
</code>

<h2>Store the number of occurrence of each string</h2>

Now that we know how we are going to print the results,
let's assume the words are given in an array.

<include file="examples/store_number_of_occurances.pl">

We iterate over the elements of the array using <hl>foreach</hl>
and increment the counter of the current string. At first the
hash is going to be empty. When we first encounter the string 'hello'
<hl>$count{$str}</hl> will not yet exists. Luckily if we access a hash
element where the key does not exists yet, Perl will return 
<a href="/undef-and-defined-in-perl">undef</a>.

Then we are trying to increment that undef using <hl>++</hl>.
In numerical operations, such as <hl>++</hl> and undef will behave as if
it was a <hl>0</hl>. Though in most cases the operation on 
<a href="/undef-and-defined-in-perl">undef</a> will
generate a 
<a href="/use-of-uninitialized-value">Use of uninitialized value</a>
warning, but <hl>++</hl>, the auto-increment operator is an exception.
Perl will silently increment the undef to be 1, it will create the necessary
key in the hash and assign the new value (which is <hl>1</hl>). This is a simple
case of <a href="/autovivification">autovivification</a>.

In subsequent cases, when we encounter the same string, the counter will be just simply
incremented by one.

The result is:

<code>
Perl                            1
hello                           2
world                           1
</code>

With this we covered both collecting the data and reporting it.

Now comes the most diverse part of the whole task. How can we go over the strings
if they are not spoon-fed to us in an array?

<h2>Counting words in a string</h2>

If we have a string in the memory and we would like to count the words in it
we can get turn the string into an array of strings:

<code lang="perl">
my $text = "hello world hello Perl";
my @strings = split / /, $text;

foreach my $str (@strings) {
    $count{$str}++;
}
</code>

and we are back to our previous task.

<hl>split / /, $text</hl> will cut up the content of <hl>text</hl> every
place there was a single space character.

<h2>No need for temporary array</h2>

We don't even need that temporary array to hold the strings, we could write:

<code lang="perl">
my $text = "hello world hello Perl";

foreach my $str (split / /, $text) {
    $count{$str}++;
}
</code>

<h2>More spaces</h2>

If we want to allow for more than one space between the strings we can use
<hl>/\s+/</hl> in the split.

<h2>Strings in a file</h2>

If we need to count the strings in a file we first need to read the
content of the file into memory. We could read the whole file and create
a single array of all the words before starting to count, but that would be
extra work for us and if the file is big, like really big, then it won't fit
in the memory of our computer.

It is usually better to process the file line-by-line like in the following
example:

<include file="examples/strings_in_a_file.pl">

<h2>Extracting real words</h2>

In the above example we simply split up
each line into substrings where we saw a space.

What if we would like to count the real words?

Well, it is not easy to extract words in any arbitrary 
spoken language so we'll do something much more simple.
We count each stretch of "word-characters", where 
"word-character" means Latin letters (a-zA-Z),
Arabic numbers (0-9) and universal underscore (_). In other
words, whatever <hl>\w</hl> matches.

The only change we have to do is the way we extract the strings
from the line. Instead of a call to <hl>split</hl>:

<code lang="perl">
    foreach my $str (split /\s+/, $line) {
</code>

we use a regex matching in list context using the <hl>/g</hl>
global flag:

<code lang="perl">
    foreach my $str ($line =~ /\w+/g) {
</code>


Just to clarify, in case the above construct looks strange,
the same can be written in two lines:


<code lang="perl">
    my @strings = $line =~ /\w+/g;
    foreach my $str (@string) {
</code>

<h2>Comments</h2>

How to generate a output.txt file for the above mentioned code

use strict;
use warnings;

my %count;
my $file = shift or die "Usage: $0 FILE\n";
open my $fh, '<', $file or die "Could not open '$file' $!";
while (my $line = <$fh>) {
chomp $line;
foreach my $str (split /\s+/, $line) {
$count{$str}++;
}
}

foreach my $str (sort keys %count) {
printf "%-31s %s\n", $str, $count{$str};

----

Why not search on this site how to write to a file and then solve it yourself?

<hr>

noob to perl is there a better way for achieving this?

#!usr/bin/perl -w

#takes a sting
#returns a character that is most repeated ignoring case
# if the two character are repeated same max number of times returns in character that is ASCII lower

sub max_alphainhash{

foreach $chare (split (//,lc $_[0])){
$symbols{$chare}++ if ($chare=~ m/[a-z]/)
}

foreach $i ( reverse sort{$symbols{$a} <=> $symbols{$b}|| $b cmp $a } keys %symbols) {
return $i
}
}

print(max_alphainhash ("%,d0090900012930()sdfkkbbajj"));

