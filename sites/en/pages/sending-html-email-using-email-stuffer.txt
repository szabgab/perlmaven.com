=title Sending HTML e-mail using Email::Stuffer
=timestamp 2015-03-17T12:30:01
=indexes Email::Stuffer, Email::Sender::Transport::SMTP
=tags screencast
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

There are many ways to send e-mail using Perl. <a href="https://metacpan.org/pod/Email::Stuffer">Email::Stuffer</a>
provides a very simple API for using some of the well written e-mail sending modules.

=abstract end

<screencast file="sending-html-email-using-email-stuffer" youtube="_z_F4HPfYt8" />

<h2>Sending simple text e-mail</h2>

<code lang="perl">
use strict;
use warnings;

use Email::Stuffer;

my $text = <<"END";
Dear Perl Maven user,

thank you for subscribing to the Perl Maven Pro!
END

Email::Stuffer
    ->text_body($text)
    ->subject('Hello')
    ->from('Gabor Szabo <gabor@perlmaven.com>')
    ->to('Foo <foo@perlmaven.com>')
    ->send;
</code>

First we use a <a href="/here-documents">here document</a> to create the content of the e-mail.

Then, instead of using the <hl>new</hl> method, <a href="https://metacpan.org/pod/Email::Stuffer">Email::Stuffer</a>
allows us to create an object using a number of methods. For example, the <hl>text_body</hl> method will receive
the content of the e-mail, create an object and return it.

On this object we can call other methods. For example the <hl>subject</hl> method allows us to set the subject of the e-mail.
This too will return the object so we can stack the method-calls on each-other Finally calling the <hl>send</hl> method
will actually send the e-mail.

In order for this to work one needs to have some kind of a sendmail clone installed on the local machine.

<h2>Using SMTP</h2>

If you don't have that, no problem, you can use the SMTP server of your ISP:

With Email::Suffer we can use any of the <a href="https://metacpan.org/pod/Email::Sender">Email::Sender</a> transport layers.
We'll use <a href="https://metacpan.org/pod/Email::Sender::Transport::SMTP">Email::Sender::Transport::SMTP</a>.

After loading the module we can add a call to the <hl>transport</hl> method of Email::Stuffer, and pass an
<hl>Email::Sender::Transport::SMTP</hl> object to it that was created using the parameter <hl>host</hl> that holds
the name of the SMTP server.

<code lang="perl">
use strict;
use warnings;

use Email::Stuffer;
use Email::Sender::Transport::SMTP ();

my $text = <<"END";
Dear Perl Maven user,

thank you for subscribing to the Perl Maven Pro!
END

Email::Stuffer
    ->text_body($text)
    ->subject('Hello')
    ->from('Gabor Szabo <gabor@perlmaven.com>')
    ->transport(Email::Sender::Transport::SMTP->new({
        host => 'mail.perlmaven.com',
    }))
    ->to('Foo <foo@perlmaven.com>')
    ->send;
</code>

This will already work even if the local machine does not have a working sendmail command.


<h2>Sending HTML mail</h2>

If we would like to make our message fancier and have an HTML version as well, it is very easy.
We just create the HTML version of the message, in this example using a here-document, and then we include it in the
e-mail message using the <hl>html_body</hl> method. The e-mail will have both a text and an HTML content.

<code lang="perl">
use strict;
use warnings;

use Email::Stuffer;
use Email::Sender::Transport::SMTP ();

my $text = <<"END";
Dear Perl Maven user,

thank you for subscribing to the Perl Maven Pro!
END

my $html = <<"END";
<h1>Hi</h1>
<a href="https://perlmaven.com/">Perl Maven</a>
END

Email::Stuffer
    ->text_body($text)
    ->html_body($html)
    ->subject('Hello')
    ->from('Gabor Szabo <gabor@perlmaven.com>')
    ->transport(Email::Sender::Transport::SMTP->new({
        host => 'mail.perlmaven.com',
    }))
    ->to('Foo <foo@perlmaven.com>')

    ->send;
</code>

<h2>Sending to multiple addresses</h2>

If we would like to send the same message to multiple addresses we can add all the addresses to the
<hl>to</hl> field, but then every recipient will see the addresses of all the other people. Probably
you don't want that.

We could recreate the whole Email::Stuffer object again and again, but that is a bit of a waste.

We can also create an Email::Stuffer object from the common values (text_body, html_body, subject, from),
store it in a variable (<hl>$email</hl>) and then use that variable to send the e-mail to each recipient
separately:

<code lang="perl">
use strict;
use warnings;

use Email::Stuffer;
use Email::Sender::Transport::SMTP ();

my $text = <<"END";
Dear Perl Maven user,

thank you for subscribing to the Perl Maven Pro!
END

my $html = <<"END";
<h1>Hi</h1>
<a href="https://perlmaven.com/">Perl Maven</a>
END
my $email = Email::Stuffer
    ->text_body($text)
    ->html_body($html)
    ->subject('Hello')
    ->from('Gabor Szabo <gabor@perlmaven.com>')
    ->transport(Email::Sender::Transport::SMTP->new({
        host => 'mail.perlmaven.com',
    }));

my @addresses = ('Foo <foo@perlmaven.com>', 'Bar <bar@perlmaven.com>');
foreach my $addr (@addresses) {
    $email->to($addr)
          ->send;
}
</code>

