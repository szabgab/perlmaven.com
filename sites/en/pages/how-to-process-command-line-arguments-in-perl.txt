=title How to process command line arguments in Perl using Getopt::Long
=timestamp 2014-10-30T18:56:14
=indexes @ARGV, Getopt::Long, GetOptions
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

When a Perl script is executed the user can pass arguments on the command line in various ways.
For example <hl>perl program.pl file1.txt file2.txt</hl> or
<hl>perl program.pl from-address to-address file1.txt file2.txt</hl> or, the most common and most useful
way:

<hl>perl program.pl -vd --from from-address --to to-address file1.txt file2.txt</hl>

How can we deal with this information?

=abstract end

When the scripts starts to run, Perl will automatically create an array called <hl>@ARGV</hl> and put all the values
on the command line separated by spaces in that variable. It won't include <hl>perl</hl> and it won't include the name of our script
(<hl>program.pl</hl> in our case), that will be placed in the <hl>$0</hl> variable. <hl>@ARGV</hl> will only
include the values located after the name of the script.

In the above case <hl>@ARGV</hl> will contain:
<hl>('-vd', '--from', 'from-address', '--to', 'to-address', 'file1.txt', 'file2.txt')</hl>

We can access <hl>@ARGV</hl> manually as described in the <a href="/argv-in-perl">article about @ARGV</a>, but there are
a number of modules that will handle most of the work for you. In this article we'll see <a href="https://metacpan.org/pod/Getopt::Long">Getopt::Long</a>
a module that also comes with the standard installation of Perl.

<h2>Explain the command line</h2>

Just before doing that, let's see what is really our expectation from the command line processing.

<ul>
  <li>Long names with values: we would like to be able to accept parameters with long names followed by a value. For example <hl>--to VALUE</hl>.
    ("Long" is relative here, it just means more than 1 character.)</li>
  <li>Long names without value: We would like to accept flags that by their mere existence will turn some flag on. For example <hl>--verbose</hl>.</li>
  <li>Short names (or single-character names) with or without values. The above two just written <hl>-t VALUE</hl> and <hl>-v</hl>.</li>
  <li>Combining short names: <hl>-vd</hl> should be understood as <hl>-v -d</hl>. So we want to be able to differentiate between "long names" and "multiple short names combined".
     The difference here is that "long names" start with double-dash <hl>--</hl> while short names, even if several of them were combined together start with a single dash <hl>-</hl>.
     </li>
  <li>Non-affiliated values, values that don't have any name starting with a dash in front of them. For example <hl>file1.txt file2.txt</hl>.</li>
</ul>

There can be lots of other requirements and Getopt::Long can handle quite a few of them, but we'll focus on the basics.

<h2>Getopt::Long</h2>

<a href="https://metacpan.org/pod/Getopt::Long">Getopt::Long</a> exports a function called <hl>GetOptions</hl>, that can process
the content of <hl>@ARGV</hl> based on the configuration we give to it. It returns <a href="/boolean-values-in-perl">true or false</a>
indicating if the processing was successful or not. During processing it removes the items from <hl>@ARGV</hl> that have been successfully
recognized.  We'll take a look at possible errors later on. For now, let' see a small example we save in <hl>cli.pl</hl>:

<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);

my $source_address;
GetOptions('from=s' => \$source_address) or die "Usage: $0 --from NAME\n";
if ($source_address) {
    say $source_address;
}

</code>

After loading the module we declare a variable called <hl>$source_address</hl> where the value of the <hl>--from</hl> command line
flag will be stored. We call <hl>GetOptions</hl> with key-value pairs. The keys (in this case one key) is the description
of the flag. In this case the <hl>from=s</hl> declares that we are expecting a command line parameter called <hl>--from</hl>
with a <b>string</b> after it. Because in Perl numbers can also be seen as strings, this basically means "pass me any value".
This declaration is then mapped to the variable we declared earlier. In case the syntax is unclear <hl>=></hl> is a "fat arrow"
you might be familiar from <a href="/perl-hashes">hashes</a> and the back-slash <hl>\</hl> in-front of the variable indicates
that we are passing a reference to the variable. You don't need to understand references in order understand this code. Just remember
that the variables on the right hand side of the "fat comma" operators need to have a back-slash when calling <hl>GetOptions</hl>.

We can run this program in several ways: <hl>perl cli.pl --from Foo</hl> will print "Foo". The value passed after the <hl>-from</hl>
flag is assigned to the <hl>$source_address</hl> variable. On the other hand running <hl>perl cli.pl</hl> will not print anything as we have no
passed any value.

If we run it <hl>perl cli.pl Foo</hl>  it won't print anything either, as GetOptions only deals with options that start with a dash (<hl>-</hl>).
(This is actually configurable, but let's not get there now.)

<h2>Failures</h2>

So when will the <a href="/short-circuit">short circuit</a> <hl>or die</hl> kick-in?

<h3>Unknown option</h3>

If we run the script passing something that looks like a parameter name, but which has not been declared
when calling <hl>GetOptions</hl>. Something that starts with a dash <hl>-</hl>. For example:

<hl>perl cli.pl --to Bar</hl>

<code>
Unknown option: to
Usage: cli.pl --from NAME
</code>

The first line is a warning printed by <hl>GetOptions</hl>, the second line is the string we generated using <hl>die</hl>.

<h3>Option requires an argument</h3>

Another case is when we run the script, pass <hl>--from</hl>, but without passing any value after it:

<hl>perl cli.pl --from</hl>

In that case the output will look like this:

<code>
Option from requires an argument
Usage: cli.pl --from NAME
</code>

Here too, the first line was from GetOptions and the second line from our call to <hl>die</hl>.
When we called <hl>GetOptions</hl> we explicitly said <hl>=s</hl> that we are expecting a string after the <hl>--from</hl>.

<h2>Default values</h2>

Often we would like to give a default value to one of the options. For example in the case of the <hl>--from</hl> field we
might want it to default to the word 'Maven'. We can do it by assigning this value to the <hl>$source_address</hl> variable
before calling <hl>GetOptions</hl>. For example, at the time we declare it using <hl>my</hl>.

<code lang="perl">
my $source_address = 'Maven';
GetOptions('from=s' => \$source_address) or die "Usage: $0 --from NAME\n";
if ($source_address) {
    say $source_address;
}
</code>

If the user does not pass the <hl>--from</hl> flag then GetOptions will not modify the value in the <hl>$source_address</hl>
variable. Running <hl>perl cli.pl</hl> will result in "Maven".

<h2>Flags without value</h2>

In addition to parameters that require a value, we also would like to allow flags. Names, that by their presence make a difference.
These things are used when we want to allow the users to turn on debugging, or to set the verbosity of the script. 

<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);

my $debug;
GetOptions('debug' => \$debug) or die "Usage: $0 --debug\n";
say $debug ? 'debug' : 'no debug';
</code>

Originally the <hl>$debug</hl> variable contained <a href="/undef-and-defined-in-perl">undef</hl> which is
considered to be <a href="/boolean-values-in-perl">false</a> in Perl. If the user passes the <hl>--debug</hl> flag,
the corresponding variable will be set to some <a href="/boolean-values-in-perl">true</a> value.
(I think it is the number one, but we should only rely on the fact that it evaluates to true.)
We then use the <a href="/the-ternary-operator-in-perl">ternary operator</a> to decide what to print.

The various ways we call it and the output they produce:

<code>
$ perl cli.pl 
no debug

$ perl cli.pl --debug
debug

$ perl cli.pl --debug hello
debug
</code>

The last example shows that values placed after such name are disregarded.


<h2>Multiple flags</h2>

Obviously, in most of the scripts you will need to handle more than one flag. In those cases we still call GetOptions <b>once</b> and provide it with
all the parameters:

Combining the above two cases together we can have a larger example:

<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);

my $debug;
my $source_address = 'Maven';
GetOptions(
    'from=s' => \$source_address,
    'debug' => \$debug,
) or die "Usage: $0 --debug  --from NAME\n";

say $debug ? 'debug' : 'no debug';
if ($source_address) {
    say $source_address;
}
</code>

Running without any parameter will leave <hl>$debug</hl> as <hl>undef</hl> and the <hl>$source_address</hl> as 'Maven':
<code>
$ perl cli.pl 
no debug
Maven
</code>

Passing <hl>--debug</hl> will set <hl>$debug</hl> to true, but will leave <hl>$source_address</hl> as 'Maven':

<code>
$ perl cli.pl --debug
debug
Maven
</code>

Passing <hl>--from Foo</hl> will set the <hl>$source_address</hl>  but leave <hl>$debug</hl> as <hl>undef</hl>:

<code>
$ perl cli.pl  --from Foo
no debug
Foo
</code>

If we provide parameters, they will both set the respective variables:

<code>
$ perl cli.pl --debug --from Foo
debug
Foo
</code>

The order of the parameters on the command line does not matter:

<code>
$ perl cli.pl  --from Foo --debug
debug
Foo
</code>

<h2>Short names</h2>

<a href="http://metacpan.org/pod/Getopt::Long">Getopt::Long</a> automatically handles shortening of the option names up to ambiguity.
We can run the above script in the following manner:

<code>
$ perl cli.pl --fr Foo --deb
debug
Foo
</code>

We can even shorten the names to a single character:

<code>
$ perl cli.pl --f Foo --d
debug
Foo
</code>

and in that case we can even use single-dash <hl>-</hl> prefixes:

<code>
$ perl files/cli.pl -f Foo -d
debug
Foo
</code>

These however are not really single-character options, and as they are they cannot be combined:

<code>
$ perl cli.pl -df Foo
Unknown option: df
Usage: cli.pl --debug  --from NAME
</code>

<h2>Single-character options</h2>

In order to combine them we need two do two things. First, we need to declare the options as real single-character options.
We can do this by providing alternate, single-character names in the definition of the options:

<code lang="perl">
GetOptions(
    'from|f=s' => \$source_address,
    'debug|d' => \$debug,
) or die "Usage: $0 --debug  --from NAME\n";
</code>

The second thing is that we need to enable the <hl>gnu_getopt</hl> configuration option of Getopt::Long by calling
<hl>Getopt::Long::Configure qw(gnu_getopt);</hl>

<code lang="perl">
use Getopt::Long qw(GetOptions);
Getopt::Long::Configure qw(gnu_getopt);
</code>

After doing that we can now run

<code>
$ perl cli.pl -df Foo
debug
Foo
</code>

The full version of the script with the above changes looks like this:

<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);
Getopt::Long::Configure qw(gnu_getopt);
use Data::Dumper;

my $debug;
my $source_address = 'Maven';
GetOptions(
    'from|f=s' => \$source_address,
    'debug|d' => \$debug,
) or die "Usage: $0 --debug  --from NAME\n";

say $debug ? 'debug' : 'no debug';
if ($source_address) {
    say $source_address;
}
</code>


<h2>Non-affiliated values</h2>

The GetOptions function only handles the parameters that start with a dash and their corresponding values, when they are relevant.
Once it processed the options it will remove them from <hl>@ARGV</hl>. (Both the option name and the option value will be removed.)
Any other, non-affiliated values on the command line will
stay in <hl>@ARGV</hl>. Hence if we add <a href="https://metacpan.org/pod/Data::Dumper">Data::Dumper</a> to our script and
use that to print the content of <hl>@ARGV</hl> at the end (<hl>print Dumper \@ARGV</hl>) as in this script:

<code lang="perl">
use strict;
use warnings;
use 5.010;
use Getopt::Long qw(GetOptions);
use Data::Dumper;

my $debug;
my $source_address = 'Maven';
GetOptions(
    'from=s' => \$source_address,
    'debug' => \$debug,
) or die "Usage: $0 --debug  --from NAME\n";

say $debug ? 'debug' : 'no debug';
if ($source_address) {
    say $source_address;
}
print Dumper \@ARGV;
</code>

We get the following results:

<code>
$ perl files/cli.pl  -f Foo -d file1.txt file2.txt
debug
Foo
$VAR1 = [
          'file1.txt',
          'file2.txt'
        ];
</code>

After processing the options, <hl>file1.txt</hl> and <hl>file2.txt</hl> were left in <hl>@ARGV</hl>. We can now do whatever we want
with them, for example we can iterate over the <hl>@ARGV</hl> array using <hl>foreach</hl>.


<h2>Advanced</h2>

<a href="https://metacpan.org/pod/Getopt::Long">Getopt::Long</a> has tons of other options. You might want to check out the documentation.

There are also other solutions, for example if you are using <a href="/moo">Moo</a> for light-weight object oriented programming,
you could take a look at <a href="https://metacpan.org/pod/MooX::Options">MooX::Options</a>  explained in a number of advanced articles: for example
<a href="/switching-to-moo-adding-command-line-parameters">Switching to Moo - adding command line parameters</a>
and 
<a href="/command-line-scripts-with-moo">Writing Command line scripts and accepting command line parameters using Moo</a>.

