=title Static and state variables in Perl
=timestamp 2014-01-15T22:30:01
=indexes static, state
=status show
=books advanced
=author szabgab
=comments_disqus_enable 1

=abstract start

In most of the cases we either want a variable to be accessible only from inside a small scope,
inside a function or even inside a loop. These variables get created when we enter the
function (or the scope created by a a block) and destroyed when we leave the scope.

In some cases, especially when we don't want to pay attention to our code, we want variables
to be global, to be accessible from anywhere in our script and be destroyed only when the
script ends. In General having such global variables is not a good practice.

In some cases we want a variable to stay alive between function calls, but still to be private
to that function. We want it to retain its value between calls.

=abstract end

In the C programming language one can designate a variable to be a <a href="http://en.wikipedia.org/wiki/Static_variable">static variable</a>.
This means it gets initialized only once and it sticks around retaining its old value between function calls.

In Perl, the same can be achieved using the <a href="/what-is-new-in-perl-5.10--say-defined-or-state">state variable</a> which is available starting from version 5.10,
but there is a construct that will work in every version of Perl 5. In a way it is even more powerful.

Let's create a counter as an example:

<h2>state variable</h2>

<code lang="perl">
use strict;
use warnings;
use 5.010;

sub count {
    state $counter = 0;
    $counter++;
    return $counter;
}

say count();
say count();
say count();

#say $counter;
</code>

In this example, instead of using <a href="/variable-declaration-in-perl">my to declare the internal variable</a>, we used the
<hl>state</hl> keyword.

<hl>$counter</hl> is initialized to 0 only once, the first time we call <hl>counter()</hl>. In subsequent calls, the line <hl>state $counter = 0;</hl>
does not get executed and <hl>$counter</hl> has the same value as it had when we left the function the last time.

Thus the output will be:

<code>
1
2
3
</code>

If we removed the <hl>#</hl> from last line, it would generate a
<a href="/global-symbol-requires-explicit-package-name">Global symbol "$counter" requires explicit package name at ... line ...</a>
error when trying to compile the script. This just shows that the variable <hl>$counter</hl> is not accessible outside the function.

<h2>state is executed in the first call</h2>

Check out this strange example:

<code lang="perl">
use strict;
use warnings;
use 5.010;

sub count {
    state $counter = say "world";
    $counter++;
    return $counter;
}

say "hello";
say count();
say count();
say count();
</code>

This will print out

<code>
hello
world
2
3
4
</code>

showing that the <hl>state $counter = say "world";</hl> line only gets executed once. In the first call to <hl>count()</hl>
<hl>say</hl>, which was also <a href="/what-is-new-in-perl-5.10--say-defined-or-state">added in version 5.10</a>, will return 1 upon success.


<h2>static variables in the "traditional" way</h2>


<code lang="perl">
use strict;
use warnings;
use 5.010;

{
    my $counter = 0;
    sub count {
        $counter++;
        return $counter;
    }
}

say count();
say count();
say count();
</code>

This provides the same result as the above version using <hl>state</hl>, except that this could work
in older versions of perl as well. (Especially if I did not want to use the <hl>say</hl> keyword,
that was also introduced in 5.10.)

This version works because functions declarations are global in perl - so <hl>count()</hl> is accessible in the
main body of the script even though it was declared inside a block. On the other hand the variable <hl>$counter</hl> is 
not accessible from the outside world because it was declared inside the block.
Lastly, but probably most importantly, it does not get destroyed when we leave the <hl>count()</hl> function (or when
the execution is outside the block), because the existing <hl>count()</hl> function still references it.

Thus <hl>$count</hl> is effectively a static variable.

<h2>First assignment time</h2>

<code lang="perl">
use strict;
use warnings;
use 5.010;

say "hi";

{
    my $counter = say "world";
    sub count {
        $counter++;
        return $counter;
    }
}

say "hello";
say count();
say count();
say count();
</code>

<code>
hi
world
hello
2
3
4
</code>

This shows that in this case too, the declaration and the initial assignment <hl>my $counter = say "world";</hl>
happens only once, but we can also see that the assignment happens <b>before</b> the first call to
<hl>count()</hl> as if the <hl>my $counter = say "world";</hl> statement was part of the control flow
of the code <b>outside</b> of the block.

<h2>Shared static variable</h2>

This "traditional" or "home made" static variable has an extra feature. Because it does not belong to the the <hl>count()</hl>
subroutine, but to the block surrounding it, we can declare more than one functions in that block and we can
share this static variable between two or even more functions.

For example we could add a <hl>reset_counter()</hl> function:

<code lang="perl">
use strict;
use warnings;
use 5.010;

{
    my $counter = 0;
    sub count {
        $counter++;
        return $counter;
    }

    sub reset_counter {
        $counter = 0;
    }
}


say count();
say count();
say count();

reset_counter();

say count();
say count();
</code>

<code>
1
2
3
1
2
</code>

Now both functions can access the <hl>$counter</hl> variable, but still nothing
outside the enclosing block can access it.

<h2>Static arrays and hashes</h2>

As of now, you cannot use the <hl>state</hl> declaration in list context.
This means you cannot write <hl>state @y = (1, 1);</hl>. This limitation could be overcome by
some extra coding. For example in this implementation of the Fibonacci series, we checked if the
array is empty and set the default values:

<code lang="perl">
use strict;
use warnings;
use 5.010;

sub fib {
   state @y;
   @y = (1, 1) if not @y; # workaround initialization
   push @y, $y[0]+$y[1];
   return shift @y; 
}

say fib();
say fib();
say fib();
say fib();
say fib();
</code>

Alternatively we could use the "old-style" static variable with the enclosing block.

Here is the example generating the Fibonacci series:

<code lang="perl">
use strict;
use warnings;
use 5.010;

{
   my @y = (1, 1);
   sub fib {
      push @y, $y[0]+$y[1];
      return shift @y; 
   }
}

say fib();
say fib();
say fib();
say fib();
say fib();
</code>

