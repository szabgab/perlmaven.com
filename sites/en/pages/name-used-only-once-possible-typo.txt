=title Name "main::x" used only once: possible typo at ...
=timestamp 2013-03-05T20:31:10
=indexes warnings, strict, possible typo
=status show
=books beginner
=author szabgab
=comments_disqus_enable 0

=abstract start

If you see this warning in a Perl script you are in deep trouble.

=abstract end

<h2>Assign to a variable</h2>

Assigning to a variable, but then never using it,
or using a variable once without assigning any value to it ever,
are rarely correct in any code.

Probably the only "legitimate" case, is if you made a typo,
and that's how you ended up with a variable that is used only once.

Here is an example of code in which we <b>only assign to a variable</b>:

<code lang="perl">
use warnings;

$x = 42;
</code>

It will generate a warning like this:

<code>
Name "main::x" used only once: possible typo at ...
</code>

That "main::" part and the lack of $ might be confusing for you.
The "main::" part is there because by default
every variable in Perl is part of the "main" namespace. There are also
a number of things that could be called "main::x" and only one of them
has a $ at the beginning. If this sounds a bit confusing, don't worry.
It is confusing, but hopefully you won't need to deal with this for a long time.

<h2>Fetch value only</h2>

If you happen to <b>use a variable only once</b>

<code lang="perl">
use warnings;

print $x;
</code>

then you will probably get two warnings:

<code>
Name "main::x" used only once: possible typo at ...
Use of uninitialized value $x in print at ...
</code>

One of them we are discussing now, the other one is discussed in
<a href="/use-of-uninitialized-value">Use of uninitialized value</a>.


<h2>What is the typo there?</h2>

You might ask.

Just imagine someone using a variable called <hl>$l1</hl>. Then later,
you come and want to use the same variable but you write <hl>$ll</hl>.
Depending on your font they might look very similar.

Or maybe there was a variable called <hl>$color</hl> but you are British
and you automatically type <hl>$colour</hl> when you think about that thing.

Or there was a variable called <hl>$number_of_misstakes</hl> and you don't notice
the typo in the original variable and you write <hl>$number_of_mistakes</hl>.

You got the idea.

If you are lucky, you make this mistake only once, but if you aren't that lucky,
and you use the incorrect variable twice, then this warning won't appear.
After all if you are using the same variable twice you probably have a good reason.

So how can you avoid this?

For one, try to avoid variables with ambiguous letters in it and be very
careful when typing variable names.

If you want to solve this for real, just <b>use strict</b>!

<h2>use strict</h2>

As you can see in the above examples, I have not used strict. If I was using it,
then instead of getting a warning about possible typo, I'd get a compile time
error:
<a href="/global-symbol-requires-explicit-package-name">Global symbol requires explicit package name</a>.

That would happen even if you used the incorrect variable more than once.

Then of course there are people who would rush and paste "my" in front of the incorrect
variable, but you are not one of those. are you? You would think about the problem and search till
you find the name of the real variable.

The most common way to see this warning is if you are not using strict.

And then you are in deep trouble.

<h2>Other cases while using strict</h2>

As GlitchMr and an Anonymous commenter pointed out, there are a few other cases:

This code, can also generate it

<code lang="perl">
use strict;
use warnings;

$main::x = 23;
</code>

The warning is: <b>Name "main::x" used only once: possible typo ...</b>

Here at least it is clear where that 'main' comes from, or in
the next example, where the Mister comes from.
(hint: The 'main' and 'Mister' are both package names.
If you are interested, you can see another
<a href="/global-symbol-requires-explicit-package-name">error message, involving missing package names</a>.)
In the next example, the package name is 'Mister'.

<code lang="perl">
use strict;
use warnings;

$Mister::x = 23;
</code>

The warning is <b>Name "Mister::x" used only once: possible typo ...</b>.

The following example too generates the warning. Twice:

<code lang="perl">
use strict;
use warnings;

use List::Util qw/reduce/;
print reduce { $a * $b } 1..6;
</code>

<code>
Name "main::a" used only once: possible typo at ...
Name "main::b" used only once: possible typo at ...
</code>

This happens because <hl>$a</hl> and <hl>$b</hl> are
special variables used in the built-in sort function so
you don't need to declare them, but you are only
using them once here.
(Actually it is unclear to me why this generates the warnings,
while the same code using <b>sort</b> does not, but the
<a href="http://www.perlmonks.org/?node_id=1021888">Perl Monks</a> might know.


