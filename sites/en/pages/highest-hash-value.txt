=title The easiest way to find the highest hash value without looping through all of them (max)
=timestamp 2016-01-29T14:01:01
=indexes sort, max, List::Util, List::UtilsBy, max_by
=status show
=author szabgab
=archive 1
=comments_disqus_enable 0

=abstract start

Given a hash 

<code lang="perl">
my %height = (
    foo => 170,
    bar => 181,
    moo => 175,
);
</code>

How can I find out the highest value without looping them over using <hl>for</hl>, <hl>foreach</hl>, or <hl>each</hl>?

=abstract end

<h2>Loop</h2>

In order to find the highest values some piece of code must go over all the values and compare them, but you don't have to do it
yourself. You don't have to write a <hl>for loop</hl> yourself, but to be clear, each one of the following solutions will go over
all the hash. You can't avoid that because Perl, as other languages, don't maintain a "highest value".

<h2>max</h2>

The <a href="https://metacpan.org/pod/List::Util">List::Util</a> module has a function called <hl>max</hl> that will return
the maximum value of a given list. The <hl>values</hl> function of perl will return all the values of a hash, in our case, it will
return the numbers in the hash.

So this code, will return and then print the highest value.

<code lang="perl">
use List::Util qw(max);
my $highest = max values %height;

print "$highest\n";
</code>


<h2>Getting the key of the highest value</h2>

What if you need to return the key of the highest value? In our case, you'd expect <b>bar</b> to be returned.
In that case we cannot use the <hl>max</hl> function of List::Util, because that just looks at the values given to
so we have two choices.

Write our manual solution of use the <hl>max_by</hl> function of <a href="https://metacpan.org/pod/List::UtilsBy">List::UtilsBy</a>.

<h2>max_by of List::UtilsBy</h2>

<code lang="perl">
use List::UtilsBy qw(max_by);

my $highest = max_by { $height{$_} } keys %height;

print "$highest\n";
print "$height{ $highest }\n";
</code>

Alternatively, as poined out by jameswmoth, you could use the <hl>reduce</hl> function that comes with
<a href="https://metacpan.org/pod/List::Util">List::Util</a>:

<code lang="perl">
use List::Util qw(reduce);

my $highest = List::Util::reduce { $height{$b} > $height{$a} ? $b : $a } keys %height;

print "$highest\n";
print "$height{ $highest }\n";
</code>

The built-in <hl>keys</hl> function of perl returns the list of keys from the given hash.
The <hl>max_by</hl> function allows us to define an expression using <hl>$_</hl> as the place-holder for the "current value"
that will generate the "values to be compared" from the "values to be ordered".
In our case the "values to be ordered" are the keys of the hash (the names),
while the "values to be compared" are the numbers (the values of the hash).

<h2>Manually using sort</h2>

In you don't want to use that module, you can always sort the keys using
<a href="/sorting-arrays-in-perl">spaceship operator</a> (<hl>&lt;=&gt;</hl>) and then fetch the value of the
highest index using <hl>-1</hl> as the index. But how?

<code lang="perl">
my @heights = sort { $height{$a} <=> $height{$b} } keys %height;

my $highest = $heights[-1];

print "$highest\n";
print "$height{ $highest }\n";
</code>

Just as in the previous solution we fetch the keys of the hash - we need to sort them based on the corresponding values.
Then we use the <hl>sort</hl> function of perl that allows us to designate a "sort function" using <hl>$a</hl> and <hl>$b</hl>
as place-holders for the "current two values". In this sort function, instead of comparing the the two values directly,
we use them as the keys of the hash, fetch the respective values from the hash and compare those using the spaceship operator.

Finally we use the <hl>-1</hl> index that will fetch the last element of the already sorted array. That holds the key of the highest value.

<h2>Manually</h2>

Finally, if you really like to write a lot of code, you can implement your own max function specialized for hashes:

<code lang="perl">
sub max {
    my (%data) = @_;

    my $max;
    while (my ($key, $value) = each %data) {
        if (not defined $max) {
            $max = $key;
            next;
        }
        if ($data{$max} < $value) {
            $max = $key;
        }
    }
    return $max;
}

my $highest = max(%height);

print "$highest\n";
print "$height{ $highest }\n";
</code>

Here we iterate over the hash using the <hl>each</hl> function that on every iteration
will return key-value pair from the hash.

<hl>$max</hl> starts out as <hl>undef</hl> and thus if it is still <hl>not defined</hl> we can just assign the
<hl>$key</hl> to it and go to the <hl>next</hl> iteration.

Otherwise, if the value corresponding to the current <hl>$max</hl> key is
smaller than the <hl>$value</hl> of the current <hl>$key</hl>
then we set the new <hl>$max</hl> to be the current <hl>$key</hl>.

Finally, once we exhausted the whole hash, we can <hl>return $max</hl>.

<h2>Comments</h2>

my $highest=(sort {$b <=> $a} keys %height)[0]; # Give it to me in just one line!!

