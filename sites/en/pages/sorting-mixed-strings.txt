=title Sorting mixed strings
=timestamp 2014-04-09T10:45:56
=indexes sort, $a, $b, cmp, <=>, substr
=status show
=books beginner
=author szabgab
=comments_disqus_enable 0

=abstract start

<a href="/sorting-arrays-in-perl">Sorting an array</a> of strings or numbers is easy using the <hl>sort</hl> function,
but what if the values we would like to sort are strings, but we would like to sort based on some number
which is part of the string?

For example what if we have an array containing values such as <hl>foo_11 bar_2 moo_3</hl>. How can we sort this
based on the numbers?

=abstract end

Simply calling <hl>sort</hl> will sort the values as string.
We need to extract the numerical value

<code lang="perl">
use strict;
use warnings;
use 5.010;

my @x = qw(foo_11 bar_2 moo_3);
say join " ", sort @x;
</code>

<code>
bar_2 foo_11 moo_3
</code>

<h2>Extract numbers using substr</h2>

In order to compare the strings using the numbers in the string we need to extract those numbers.
Based on the example above we can assume that the strings consist of <b>4 characters and then the number</b>.
In that case we can use <a href="/string-functions-length-lc-uc-index-substr">substr</a> to extract the numbers:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my @x = qw(foo_11 bar_2 moo_3);

my @y = sort { substr($a, 4) <=> substr($b, 4)  } @x;

say join " ", @y;
</code>

The result is correct:

<code>
bar_2 moo_3 foo_11
</code>


What if the strings can have any number of characters before and after the number? We might need to use

<h2>Use regular expression to extract numbers</h2>

For example what if the strings look like these:

<code lang="perl">
my @x = qw(foo_11 bar_2_bar text_3);
</code>

If we run the previous solution, we'll get lots of warnings like this: <hl>Argument "2_bar" isn't numeric in numeric comparison (<=>)</hl>

We can use a regex to extract part of the string, but for this we need to create a LIST-context. Hence in the following expression we had to
put the <hl>($number)</hl> in parentheses.

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $str = 'bar_2_bar';
my ($number) = $str =~ /(\d+)/;
say $number;
</code>

printing <hl>2</hl>.

Unfortunately the <hl><=></hl>, the spaceship operator will create scalar context on both of its sides.

So how can we extract the value in scalar context?
We can do that using <a href="/perl-split">array slices</a> on-the-fly:

<code lang="perl">
my ($number) = $str =~ /(\d+)/;

my $number = ($str =~ /(\d+)/)[0];
</code>

Putting it all together we'll have:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my @x = qw(foo_11 bar_2_bar text_3);
say join " ", sort @x;

my @y = sort { ($a =~ /(\d+)/)[0] <=> ($b =~ /(\d+)/)[0] } @x;

say join " ", @y;
</code>

Resulting in

<code>
bar_2_bar text_3 foo_11
</code>

<h2>Missing numbers</h2>

As <a href="http://www.linkedin.com/in/octavianrasnita">Octavian Rasnita</a> pointed out,
if there is no number in one of the strings, we'll get a bunch of 
<hl>Use of uninitialized value in numeric comparison</hl> warnings. We can decide that when there
is no number in a string, we use 0 instead:

<code lang="perl">
my @y = sort { (($a =~ /(\d+)/)[0] || 0) <=> (($b =~ /(\d+)/)[0] || 0) } @x;
</code>


<h2>Speed</h2>

He also pointed out that the Regex engine is "slow" and thus we can improve the
speed of the expression by using the <a href="/how-to-sort-faster-in-perl">Schwartzian transform</a>.
He pointed out that if the array has at least 5 elements, using the Schwartzian transform is faster. If it has 20 elements is twice as fast as the simple sort.

The solution he gave looks like this:

<code lang="perl">
my @y = map { $_->[1] }
        sort { $a->[0] <=> $b->[0] }
        map { [ ($_ =~ /(\d+)/)[0] || 0, $_ ] } @x;
</code>

In general I am against such "premature optimization".
Before applying any such optimization I'd first recommend to finish the application,
and <b>profile</b> the code using <a href="https://metacpan.org/pod/Devel::NYTProf">Devel::NYTProf</a>. That will show if this specific
code has any impact on the overall performance of the application. If not, then I'd leave the slower, but more readable one.

In this case however I am not sure if the original version (especially after setting 0 as a default) is more
readable that the one using the Schwartzian transform. That's because the expression to get the value we can use
in the comparison is itself complex. In the original version we have two copies of the expression. In the Schwartzian transform
we only have one copy.

Maybe this version, where we move the complex expression in a subroutine, is even more readable:

<code lang="perl">
my @y = sort { getnum($a) <=> getnum($b) } @x;

sub getnum {
    my $v = shift;
   return( ($v =~ /(\d+)/)[0] || 0);
}
</code>


<h2>Hiding the sort in a sub</h2>

Another solution Octavian sent to me was using a separate subroutine to hide the implementation details of the sort:

<code lang="perl">
my @y = sort by_number @x;


sub by_number {
    my ( $anum ) = $a =~ /(\d+)/;
    my ( $bnum ) = $b =~ /(\d+)/;
    ( $anum || 0 ) <=> ( $bnum || 0 );
}
</code>

The advantage of this is that you can use the same sort in many different places,
while implementing it only once and if the name of the subroutine explains the sort,
then most people won't even need to look at the implementation.
A common trait of subroutines.

<h2>Comments</h2>

In the last part, "Hiding the sort in a sub", the by_number subroutine does not receive any parameters. Is that because $a and $b are specifically reserved by Perl for the sort function?

yes, and they are global.


