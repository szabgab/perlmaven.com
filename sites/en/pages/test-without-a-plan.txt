=title Test without a Plan
=timestamp 2016-03-26T20:30:01
=indexes Test::Simple, no_plan, BEGIN
=tags screencast
=status show
=books testing
=author szabgab
=comments_disqus_enable 1

=abstract start

We left the <a href="/refactoring-large-test-suite-separating-data-from-code">previous article</a>
with a small issue involving lack of <a href="http://en.wikipedia.org/wiki/Don't_repeat_yourself">DRY</a>-ness.

The slight problem was, that we hard-coded the number of tests in the <hl>use Test::Simple tests => 5;</hl> expression.
If we now add a new test-entry to the <hl>@cases</hl> array, we have to remember to update the test plan as well.

Not a big issue, but we as programmers are supposed to avoid any such repetition. So how can we do that?

=abstract end

<screencast file="/media/videos/test-without-a-plan" />

This was the code from the previous article.

<code lang="perl">
use strict;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/lib";
use MyCalc;

my @cases = (
    [2,  1, 1],
    [4,  2, 2],
    [6,  2, 2, 2],
# negative numbers
    [-2, -1, -1],
# edge cases:
    [0,  1, -1],
);

use Test::Simple tests => 5;

for my $c (@cases) {
    my ($exp, @params) = @$c;
    my $descr = 'sum(' . join(', ', @params) . ") == $exp";
    ok sum(@params) == $exp, $descr;
}
</code>

The obvious solution would be to write:

<code lang="perl">
use Test::Simple tests => scalar @cases;
</code>

Unfortunately it would be also incorrect.

If we ran the test script we would get the following output:

<code>
You said to run 0 tests at test.pl line 18.
BEGIN failed--compilation aborted at test.pl line 18.
</code>

The problem is that while the declaration <hl>my @cases;</hl> takes place
during the compilation phase of running the perl script, the assignment
to the array will only take place in the run-phase. On the other hand
the whole line <hl>use Test::Simple tests => scalar @cases;</hl> will be
evaluated at the compilation phase. At that time the <hl>@cases</hl> is
already declared but still empty. So we declare that we are going to run 0 tests.

That's not good. Let's look for other solutions.

<h2>no_plan</h2>

The expected number of tests is also usually referred to as <b>the plan</b>.
<a href="http://metacpan.org/pod/Test::Simple">Test::Simple</a> actually allows us to run
the tests without any <b>plan</b>. We only need to tell it that we have <hl>no_plan</hl>

Let's try this:

<code lang="perl">
use Test::Simple 'no_plan';
</code>

if we run the script we get the following output:

<code>
ok 1 - sum(1, 1) == 2
ok 2 - sum(2, 2) == 4
not ok 3 - sum(2, 2, 2) == 6
#   Failed test 'sum(2, 2, 2) == 6'
#   at test.pl line 23.
ok 4 - sum(-1, -1) == -2
ok 5 - sum(1, -1) == 0
1..5
# Looks like you failed 1 test of 5.
</code>

Please note that the line <hl>1..5</hl> has been printed at the end. Effectively Test::Simple says:
"I don't know how many tests you wanted to run, but I saw you ran 5, so that must have been your plan."

It is a bit funny if you think about it.

Please also note, that the test (row number 3) that used to fail still fails as earlier. So what happens if someone
comments out that test-case:

<code lang="perl">
#    [6,  2, 2, 2],
</code>

and runs the test again:

<code>
ok 1 - sum(1, 1) == 2
ok 2 - sum(2, 2) == 4
ok 3 - sum(-1, -1) == -2
ok 4 - sum(1, -1) == 0
1..4
</code>

Everything is fine!

On one hand it is nice that we don't need to update the test plan manually, on the other hand we won't easily notice
if for some reason only part of the test-cases ran and the test-script exited prematurely.

In a later article we'll also cover <hl>done_testing</hl>, a slightly better tool to solve the problem. Stay tuned for that article.

For now, let's try to solve it in another, robust way.

<h2>use the BEGIN block</h2>

If you put a block in your code called <hl>BEGIN</hl>, it has the special feature that the code inside it will be executed, 
immediately after it has been compiled, even before the rest of the script is compiled. So if we have the following code:

<code lang="perl">
my @cases;
BEGIN {
    @cases = (
        [2,  1, 1],
        [4,  2, 2],
        [6,  2, 2, 2],
    # negative numbers
        [-2, -1, -1],
    # edge cases:
        [0,  1, -1],
    );
}

use Test::Simple tests => scalar @cases;
</code>

and we run the script, we get this output:

<code>
1..5
ok 1 - sum(1, 1) == 2
ok 2 - sum(2, 2) == 4
not ok 3 - sum(2, 2, 2) == 6
#   Failed test 'sum(2, 2, 2) == 6'
#   at test.pl line 26.
ok 4 - sum(-1, -1) == -2
ok 5 - sum(1, -1) == 0
# Looks like you failed 1 test of 5.
</code>

The <hl>1..5</hl> moved to the front, if for some reason our test script will execute less than 5 tests, Test::Simple will complain
as explained in the article about <a href="/test-plan-and-test-descriptions">test plans and descriptions</a>.

Unfortunately, this solution will not notice the problem if we comment out the bad test case as we did in the previous example.
This will automatically adjust the plan to the new number of test-cases. Two points here:

<ol>
  <li>Avoiding repetition (going with DRY) is not always a good thing.</li>
  <li>I'll have to show you another case where the advantage of declaring a <hl>plan</hl> is clearer.</li>
</ol>

For now, let's just point out that we had to declare <hl>my @cases</hl> outside of the <hl>BEGIN</hl> block, otherwise it would have been scoped to
the block and perl would complain with the syntax error: <a href="/global-symbol-requires-explicit-package-name">Global symbol "@cases" requires explicit package name at ...</a>.
for the line where we have <hl>use Test::Simple tests => scalar @cases;</hl>.


