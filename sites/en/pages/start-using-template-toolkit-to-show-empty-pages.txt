=title Start using Template Toolkit to show the empty pages
=timestamp 2015-04-28T14:30:01
=indexes Template::Toolkit, PSGI
=tags screencast
=status show
=books search_cpan_org
=author szabgab
=comments_disqus_enable 1

=abstract start

Now that we have created the <a href="/create-skeleton-psgi-application">skeleton PSGI application</a> we should further improve it.
It is clear that we should <b>not</b> have HTML embedded in the Perl code. We need to use some kind of templating system and move
all the HTML there. There are tons of templating systems on CPAN, but <a href="http://template-toolkit.org/">Template-Toolkit</a> is
probably the most well known and most commonly used. It also has plenty of plugins. We are going to us that.

=abstract end

<screencast file="start-using-template-toolkit-to-show-empty-pages" youtube="6hNWd48HxqU" />

In <hl>lib/MetaCPAN/SCO.pm</hl> we replace the

<code>
return [ '200', [ 'Content-Type' => 'text/plain' ], ['Hello'], ];
</code>

with

<code>
return template('index');
</code>

and then implement the <hl>template</hl> function.
The value passed to the <hl>template()</hl> function is the name of the template file we need to
use to serve this page.

The templates themselves will be located in the <hl>tt/</hl> subdirectory in the root of the project. They all have <hl>.tt</hl>
extension. Thus the template of the main page of the site <hl>/</hl> will called <hl>tt/index.tt</hl>

We create the <hl>tt/</hl> directory and the <hl>tt/index.tt</hl> file with some dummy content:

<code>
MetaCPAN::SCO
</code>

This is the content of the page and it is plain text. To make the page really HTML we need to add some HTML-tags before and after.
Every web page in a web project have some common attributes. If nothing else then the html tag at the beginning of each file and the
closing html tag at the end. Usually though a lot more parts are common. In order to avoid repeating the same HTML code
we can put these common elements in separate files and then include them in the main file. Even better Template::Toolkit understand
that the beginning and the ending of each page is similar and allows us to configure templates to be processed before and
after the main template of each page. Hence we also create a two more files:

<hl>tt/incl/header.tt</hl> contains

<code>
<html>
<head>
  <title>The CPAN Search Site - search.cpan.org</title>
</head>
<body>
</code>

<hl>tt/incl/footer.tt</hl> contains

<code>
</body>
</html>
</code>
</code>

Not much, but we have to start somewhere and eliminating the repetition of these parts is already a win. Especially
when later we'll want to make some changes. These file were also created in a subdirectory of <hl>tt/</hl> called
<hl>tt/incl/</hl> to have some separation among template that are for pages and templates that are to be included.
It is not required, but it makes it easier to understand what is what.

Now let's see the <hl>template()</hl> function that ties these files together:

We'll have to supply the path to the directory of the templates. At this point I did not have better idea so
I looked at the content of the <hl>__FILE</hl> pseudo variable. It holds the name of the current file. (<hl>lib/MetaCPAN/SCO.pm</hl>
in our case.) <hl>abs_path</hl> imported from <a href="https://metacpan.org/pod/Cwd">Cwd</a> will convert the relative path of
<hl>lib/MetaCPAN/SCO.pm</hl> to a full path. Something like this: <hl>/home/gabor/work/MetaCPAN-SCO/lib/MetaCPAN/SCO.pm</hl>.
Then the <hl>dirname</hl> function of <a href="https://metacpan.org/pod/File::Basename">File::Basename</a> will return the path withut the last part.
Calling it 3 times will return <hl/home/gabor/work/MetaCPAN-SCO</hl>, the root of our project. (Not being able to pass 3 as a second parameter
of <hl>dirname</hl> always bothered me.)

The constructor of <a href="https://metacpan.org/pod/Template">Template</a>, the main module of Template::Toolkit, accepts a number
of parameters. <hl>INCLUDE_PATH</hl> is the path to the directory where it is going to look for templates. We just pass to it
a full path to the <hl>tt/</hl> directory of our project.

We set <hl>INTERPOLATE</hl>  to 0, to avoid expanding strings that look like perl scalars (e.g. <hl>$var</hl>).

<hl>POST_CHOMP</hl> will clean up whitespaces.

<hl>EVAL_PERL</hl> means TT will evaluate Perl code-blocks. I think I usually turn this off. I think I left it on by mistake.

A template is HTML with some embedded special tags defined by Template::Toolkit. By default these tags start with <hl>[%</hl>
and end with <hl>%]</hl>. The <hl>START_TAG</hl> and <hl>END_TAG</hl> directives allow us to change them. The tags I selected
will make the whole code look more like plain HTML.

Setting the <hl>PRE_PROCESS</hl> and <hl>POST_PROCESS</hl> variables tell Template Toolkit to load the respective template
before and after the main template which is going to be passed to the <hl>process</hl> method. Here we pass the path to the
header.tt and footer.tt files relative to the directory provided in the <hl>INCLUDE_PATH<hl> parameter.

<hl>$tt</hl> will hold the Template object.

We call the <hl>process</hl> method of this object with 3 parameters. The first one is the name of the template file.
It is the value our <hl>template()</hl> function received with the <hl>.tt</hl> extension. The second parameter is a hash
reference where later we'll pass the values to be inserted into the template. The 3rd parameter is a reference to an empty
scalar variable. The result of the processing will be placed in this variable. As I did not have better idea, I called <hl>die</hl>
in case the process method returned failure. In a web context this might not be a good idea. We might be better off sending back
some lame excuse, or a very cryptic error message <hl>Internal error 307</hl> to the user and log the failure.
For now, during development it will work well.

Once we have the generated HTML in the <hl>$out</hl> we can return the 3-element array expected by PSGI.
The content-Type now really should be <hl>text/html</hl> and the 3rd value is an array reference with a single element
which is the HTMl we have just generated.

<code lang="perl">
sub template {
    my ( $file ) = @_;

    my $root = dirname(dirname(dirname( abs_path(__FILE__) )));

    my $tt = Template->new(
        INCLUDE_PATH => "$root/tt",
        INTERPOLATE  => 0,
        POST_CHOMP   => 1,
        EVAL_PERL    => 1,
        START_TAG    => '<%',
        END_TAG      => '%>',
        PRE_PROCESS  => 'incl/header.tt',
        POST_PROCESS => 'incl/footer.tt',
    );
    my $out;
    $tt->process( "$file.tt", {}, \$out )
        || die $tt->error();
    return [ '200', [ 'Content-Type' => 'text/html' ], [$out], ];
}
</code>

In order this to work we also had to load a couple of modules:

<code lang="perl">
use Cwd qw(abs_path);
use File::Basename qw(dirname);
use Plack::Request;
use Template;
</code>

These module were also added as prerequisites to Makefile.PL.


Once we have all this we can launch the application again using <hl>plackup</hl> and
visit <a href="http://localhost:5000/">http://localhost:5000/</a> to check the result.
Don't forget to "view-source" in your browser to see that we really got the HTML.

<code>
$ git add .
$ git commit -m "Start using Template toolkit to show the empty pages"
</code>

<a href="https://github.com/szabgab/MetaCPAN-SCO/commit/0ce8f29b47e3f6fbebecbe904fd148bed7898904">commit</a>


