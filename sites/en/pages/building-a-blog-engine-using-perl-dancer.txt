=title Building a blog engine using Perl Dancer
=timestamp 2014-05-14T20:45:56
=indexes Dancer
=tags screencast
=books dancer
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

In this <a href="https://www.youtube.com/watch?v=NGX5pgKWVoc">screencast</a> we build a
simple blog engine using <a href="/dancer">Perl Dancer</a>.

If you are on MS Windows start by downloading <a href="http://padre.perlide.org/">Padre on Strawberry Perl</a>
that already has everything we need. On other system you'll have to install
<a href="https://metacpan.org/pod/Dancer">Dancer</a>, <a href="https://metacpan.org/pod/File::Slurp">File::Slurp</a>
and the <a href="https://metacpan.org/pod/Template">Template::Toolkit</a>. (Please note, the last one is actually identified
as Template, by the CPAN clients.)

=abstract end

<iframe width="640" height="390"
src="//www.youtube.com/embed/NGX5pgKWVoc" frameborder="0" allowfullscreen></iframe>

<screencast file="na" youtube="NGX5pgKWVoc" />

The application we build is going to be called <a href="http://dwimmer.org/">Dwimmer</a>. We need to
use the command line to build the skeleton of our application by typing in

<code>
dancer -a Dwimmer
</code>

This will create a directory called Dwimmer and all the files necessary to build a Dancer application.

The <hl>bin/app.pl</hl> file is just a simple launcher for a web server on port 3000

The real code is in <hl>lib/Dwimmer.pm</hl>

The templates are in the <hl>view/</hl>  directory.

This is the full code:

<code lang="perl">
package Dwimmer;
use Dancer ':syntax';

use File::Slurp qw(read_file write_file);

our $VERSION = '0.01';

get '/' => sub {
    my $filename = config->{dwimmer}{json};
    my $json = -e $filename ? read_file $filename : '{}';
    my $data = from_json $json;
    template 'index', {data => $data};
};


get '/page' => sub {
    template 'page';
};

post '/page' =>  sub {
    my $filename = config->{dwimmer}{json};
    my $json = -e $filename ? read_file $filename : '{}';
    my $data = from_json $json;
    my $now   = time;
    $data->{$now} = {
        title => params->{title},
        text  => params->{text},
    };

    write_file $filename, to_json($data);
    redirect '/';
};

true;
</code>

The module <a href="https://metacpan.org/pod/File::Slurp">File::Slurp</a> provides two functions for reading and writing files
in a simple way.

Dancer is route based so you for each page out there you need to define a route. 
You can even differentiate between the HTTP METHOD types such as <b>GET</b> and <b>POST</b>
though they are written in lower case in Dancer.

We changed the config.yml file in the root of the application. Commented out the 

<code>
# template: "simple"
</code>

and enabled

<code>
template: "template_toolkit"
engines:
  template_toolkit:
    encoding:  'utf8'
</code>

Later we also added the following

<code>
dwimmer:
  json: c:\work\dwimmer.json
</code>

to set where the json "database" is located. (Our home made NoSQL database.)

We edited the view/index.tt file replacing all of its content with a simple HTML
linking to the <b>page</b>.

<code>
Hi
<a href="/page">add a new entry</a>
</code>

clicking on that link would generate a 404-error so we also created a route to
serve the request:

<code lang="perl">
get '/page' => sub {
    template 'page';
};
</code>

The page template is simple form.

<code>
  <form method="POST">
  <input name="title" size="80"/><br />
  <textarea name="text" rows="20" cols="80">
  </textarea><br />
  <input type="submit" value="Post" />
  </form>
</code>

Note we set the submission method to be POST! Hence when you submit this form
you get a 404 error. We have to implement a separate route to handle this post request:

<code lang="perl">
post '/page' =>  sub {
    my $filename = config->{dwimmer}{json};
    my $json = -e $filename ? read_file $filename : '{}';
    my $data = from_json $json;
    my $now   = time;
    $data->{$now} = {
        title => params->{title},
        text  => params->{text},
    };

    write_file $filename, to_json($data);
    redirect '/';
};
</code>

In this code the <b>config</b> method returns the all the configuration options
where we already saved the name of the json file.
-e $filename checks if the file exists. If it does, we read it in to the $json
variable if not then we assign to it a json string representing an empty hash.

<code lang="perl">
my $json = -e $filename ? read_file $filename : '{}';
</code>

<b>from_json</b> a built-in Dancer function turning a json string into a Perl data structure.

We then add a new entry to our "database" with the key being the timestamp and having
a hash containing the title and text as received from the user.

<code lang="perl">
write_file $filename, to_json($data);
</code>

This will write back the whole data structure after turning it into a json file.

The last line redirects the user to the main page.

This code will save the new blog submission to the json file but won't display yet.

For that we changed the main route copying the data reading code to it.
It then passes the data to the template call.

<code lang="perl">
get '/' => sub {
    my $filename = config->{dwimmer}{json};
    my $json = -e $filename ? read_file $filename : '{}';
    my $data = from_json $json;
    template 'index', {data => $data};
};
</code>

The index.tt template itself had to be changed as well to include this:

<code>
  <% FOR e IN data.keys.sort %>
    <hr />
    <h2><% data.$e.title %></h2>
    <% data.$e.text %>
  <% END %>
</code>

Which will display all the results from the data hash.

That's it.

Well, sort of.

