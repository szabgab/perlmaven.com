=title How to get a slice of a hash?
=timestamp 2017-09-05T22:00:01
=indexes @hash{...}
=status show
=author szabgab
=archive 1
=comments_disqus_enable 0

=abstract start

Given a hash of some of the <a href="https://en.wikipedia.org/wiki/List_of_national_capitals_in_alphabetical_order">capitals of the world</a>

<code lang="perl">
my %capital_of = (
    Bangladesh => 'Dhaka',
    Tuvalu     => 'Funafuti',
    Zimbabwe   => 'Harare',
    Eritrea    => 'Asmara',
    Botswana   => 'Gaborone',
);
</code>

How can we get several values out of it in one step?

=abstract end

Earlier we saw how to get <a href="/array-slices">a slice of an array or an array reference</a>,
this time we do something similar with hashes.

<h2>Hash slice</h2>

If we have a hash like the <hl>%capital_of</hl> above we can access individual elements in it using <hl>$</hl> prefix
and curly braces after the name:

<code lang="perl">
$capital_of{'Bangladesh'}
</code>

We can also create a list of some of the values be creating a list of these values:

<code lang="perl">
($capital_of{'Zimbabwe'}, $capital_of{'Eritrea'}, $capital_of{'Botswana'})
</code>

We can assign them names of the capitals to an array:

<code lang="perl">
my @african_capitals = ($capital_of{'Zimbabwe'}, $capital_of{'Eritrea'}, $capital_of{'Botswana'});

print Dumper \@african_capitals;
</code>

<code>
$VAR1 = [
          'Harare',
          'Asmara',
          'Gaborone'
        ];
</code>

If course listing "$hash_of" 3 times is already too much for most of us, but if we had to list the
capitals of all the African countries that would be really too much repetition.

Instead of that we use a syntax called <hl>hash slice</hl>. It returns a list of values.
In this syntax we put a <hl>@</hl> in front of the name of the hash and put curly braces <hl>{}</hl>
after it. Within the curly braces we put one or more keys of the hash. This will return the values
of the appropriate keys.

<code lang="perl">
my @african_capitals = @capital_of{'Zimbabwe', 'Eritrea', 'Botswana'};
</code>

Results in the same list of capitals.

If you want to avoid the quotes and spaces you can also use <a href="/qw-quote-word">qw</a>:

<code lang="perl">
my @african_capitals = @capital_of{qw(Zimbabwe Eritrea Botswana)};
</code>


The same would work no matter how we supply the keys of the hash, (the names of African countries). It can be an expression
including a function returning names or an array holding the names like here:

<code lang="perl">
my @countries_in_africa = qw(Zimbabwe Eritrea Botswana);
my @african_capitals = @capital_of{ @countries_in_africa };
</code>

<h2>Hash slice on the left-hand side</h2>

The same syntax can also be used on the left-hand side of the equation. For example:

<code lang="perl">
@capital_of{'Belize', 'Kyrgyzstan'} = ('Belmopan', 'Bishkek');

print Dumper \%capital_of;
</code>

<code>
$VAR1 = {
          'Tuvalu' => 'Funafuti',
          'Eritrea' => 'Asmara',
          'Bangladesh' => 'Dhaka',
          'Zimbabwe' => 'Harare',
          'Kyrgyzstan' => 'Bishkek',
          'Belize' => 'Belmopan',
          'Botswana' => 'Gaborone'
        };
</code>

Here too we could use any expression in place of either of the lists:

<code lang="perl">
my @countries = ('Belize', 'Kyrgyzstan');
my @capitals = ('Belmopan', 'Bishkek');
@capital_of{ @countries } = @capitals;
</code>

<h2>Example</h2>

See the full sample script I used to generate the code snippets:

<include file="examples/hash_slices.pl">

<h2>Perl 5.20 and newer</h2>

As <a href="https://culturedperl.com/">Dave Cross</a> pointed out, starting from Perl version 5.20 you can also write:

<code>
my %africa = %capital_of{'Zimbabwe', 'Eritrea', 'Botswana'};
</code>

<h2>Comments</h2>

Terrific summary and reference, Gabor; I am going to save this post to Evernote because I know I am going to want to look it up every other month (or so). It seems like there are lots of esoteric descriptions of using hash slices in the most popular Perl Programming books (at least the ones on my shelf), but invariably I will try to use one to do something cool, and get the syntax ever so slightly wrong, and which point I try to remember where I read the most helpful description. Now it is here all in one handy place. Actually the way I used hash slices every week is described in Dave Cross' book "Data Munging with Perl" where he essentially slurps in a tab-separated ascii text file into an array of hashrefs using a nice hash slice (you have to grab the first row containing the column header/field names into an array first). Worth looking up (in my opinion) if you are not familiar with his book.

---
Dave Cross: And I should point out that the book is now available for free online. https://datamungingwithperl.com/

<h2>
And from Perl 5.20, there are the key/value hash slices that return list of key/value pairs.

my %africa = %capital_of{'Zimbabwe', 'Eritrea', 'Botswana'};

---
Thanks Dave & Gabor

%PERL_GURUS = %cool_guys{qw|gabor dave|}

<h2>
One another usage of hash slicing is also when you want to merge different hashes:

@merged{ keys %data } = values %data

Thanks for your great blog !

<h2>

Great article! Thank you. Question: how to use this syntaxt to get just the full list of African countries, or the full list of capitals by themselves.

Probably you are looking for `keys` and `values` functions.

<h2>
What's the syntax if you have a hash reference?
