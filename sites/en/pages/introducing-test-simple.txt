=title Introducing test automation with Test::Simple
=timestamp 2015-10-18T09:30:01
=indexes Test::Simple
=tags screencast
=status show
=books testing
=author szabgab
=comments_disqus_enable 1

=abstract start

Let's start by understanding the concept of automated unit tests. 

=abstract end

<screencast file="introducing-test-simple" youtube="NTQ7W1kCu74" />

Let's say we have a very simple file called <hl>lib/MyCalc.pm</hl> with the following content:

<code lang="perl">
package MyCalc;
use strict;
use warnings;

use base 'Exporter';
our @EXPORT = qw(sum);

sub sum {
    return $_[0] + $_[1];
}

1;
</code>

How can we test if the <hl>sum</hl> function works properly?

<h2>First test script</h2>

We can start by writing a script that will call the function, and pass certain input to it. We can print the return values
and observe them.

So we create a file called <hl>test.pl</hl> with the following content:

<code lang="perl">
use strict;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/lib";
use MyCalc;

print sum(1, 1), "\n";
print sum(2, 2), "\n";
print sum(2, 2, 2), "\n";
</code>

The directory layout looks like this:

<code>
root/
   lib/MyCalc.pm
   test.pl
</code>

The <hl>$Bin</hl> variable imported from the <a href="https://metacpan.org/pod/FindBin">FindBin</a> module always contains the
directory of the currently executing perl script. <hl>use lib "$Bin/lib";</hl> ensures that the next line, <hl>use MyCalc;</hl>
will find the MyCalc.pm file in the <hl>lib</hl> subdirectory.

Then we have 3 examples calling the <hl>sum</hl> function and printing the result.

We can run our test script by typing in <hl>perl test.pl</hl> and the output is:

<code>
2
4
4
</code>

It does not take a lot of thinking to understand that the result of the 3rd call is incorrect, but it is very easy to actually overlook it.
That's how our brain works. Even in such a small example, the majority of people will not notice the problem. If this was some more complex
task, some elaborate business logic, then we can't expect our programmers or testers to actually know the expected results.
We would need to bring in a domain expert (or a manager) to provide us with input values and expected output values.

To make it easier for them we can change our code to also print the expected value next to the actual value:

<h2>Print expected value</h2>

<code lang="perl">
use strict;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/lib";
use MyCalc;

print sum(1, 1),    " 2\n";
print sum(2, 2),    " 4\n";
print sum(2, 2, 2), " 6\n";
</code>

The output now looks like this:

<code>
2 2
4 4
4 6
</code>

<h2>Let the computer compare</h2>

This is an improvement as now we don't need the expert any more, but it still needs a lot of attention to manually compare
the actual result with the expected result. Let's change our tests script to do that for us, and instead of the values,
print only <hl>ok</hl> on success and <hl>not ok</hl> on failure.

<code lang="perl">
use strict;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/lib";
use MyCalc;

if (sum(1, 1) ==  2) {
    print "ok\n";
} else {
    print "not ok\n";
}

if (sum(2, 2) == 4) {
    print "ok\n";
} else {
    print "not ok\n";
}

if (sum(2, 2, 2) == 6) {
    print "ok\n";
} else {
    print "not ok\n";
}
</code>

And the output:

<code>
ok
ok
not ok
</code>

<h2>Refactoring and creating the ok() function</h2>

You might have noticed we had a lot of repetition in the above code. We could do much better.
Let's create a function called <hl>ok()</hl> that will receive a
<a href="/boolean-values-in-perl">True or False</a> value and will print <hl>ok</hl> or <hl>not ok</hl> accordingly.

<code lang="perl">
use strict;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/lib";
use MyCalc;

ok(sum(1, 1) ==  2);
ok(sum(2, 2) == 4);
ok(sum(2, 2, 2) == 6);


sub ok {
    my ($true) = @_;
    if ($true) {
        print "ok\n";
    } else {
        print "not ok\n";
    }
}
</code>

The output is still the same:

<code>
ok
ok
not ok
</code>

<h2>use Test::Simple</h2>

All that is fine, and this <hl>ok()</hl> really doesn't to much, but other people have already implemented it
and their <hl>ok()</hl> function is actually more powerful and it is part of a huge ecosystem for testing.
So let's replace our home-made <hl>ok()</hl> function by the one supplied by <a href="https://metacpan.org/pod/Test::Simple">Test::Simple</a>.

<code lang="perl">
use strict;
use warnings;

use FindBin qw($Bin);
use lib "$Bin/lib";
use MyCalc;

use Test::Simple tests => 3;

ok(sum(1, 1) ==  2);
ok(sum(2, 2) == 4);
ok(sum(2, 2, 2) == 6);
</code>

The change was simple,  and the output of running <hl>perl test.pl</hl> is a bit more
verbose than what we had earlier. It looks like this:

<code>
1..3
ok 1
ok 2
not ok 3
#   Failed test at test.pl line 12.
# Looks like you failed 1 test of 3.
</code>

Test::Simple required us to state how many times we are going to call the <hl>ok()</hl> function,
that's the <hl>tests => 3</hl> in the use-statement.
In return it provided us with a counter and at the end it even reported that one out of three tests have failed.

We'll look at these features later on.

