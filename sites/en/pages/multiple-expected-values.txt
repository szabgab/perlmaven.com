=title Multiple expected values - testing dice
=timestamp 2016-09-07T15:30:01
=indexes Test::More, cmp_ok
=tags screencast
=status show
=books testing
=author szabgab
=comments_disqus_enable 1

=abstract start

In our application there is a function called <hl>dice()</hl> that when called will return a whole number between 1 and 6.
Just as a normal die would do. How can we test this?

=abstract end

<screencast file="multiple-expected-values" youtube="gi6ICYTwkPo" />

As we already know about the <a href="/testing-timeout-with-cmp-ok">cmp_ok</a> function of Test::More we can quickly write a test script using it:

<code lang="perl">
use strict;
use warnings;

use MyTools;

use Test::More tests => 2 * 4;

for (1..4) {
    my $value = dice();
    cmp_ok $value, '>=', 1, 'bigger than 1';
    cmp_ok $value, '<=', 6, 'smaller than 6';
}
</code>

We run the script using <hl>perl -Ilib t/dice_cmp_ok.t</hl> and get the following output:

<code lang="perl">
1..8
ok 1 - bigger than 1
ok 2 - smaller than 6
ok 3 - bigger than 1
ok 4 - smaller than 6
ok 5 - bigger than 1
ok 6 - smaller than 6
ok 7 - bigger than 1
ok 8 - smaller than 6
</code>

Everything looks ok. We ship the product.

Then the bug reports start to arrive. People complaining about strange behavior. As if the die was broken.
After long hours of running after the bug we decide to amend the above test script by a call to <hl>diag $value;</hl>
to see the real values returned by the <hl>dice()</hl> function. To our surprise we see values such as 1.5, 2.5, etc.

We can now run and fix the bug in our application, but before we do that, we should also improve our test script.
We need a way to check if <hl>$value</hl> returns one of the values: <hl>1, 2, 3, 4, 5, 6<hl>.

<h2>List::MoreUtils any</h2>

We can use the built in <a href="/filtering-values-with-perl-grep">grep</a> function as follows:

<code lang="perl">
if (grep { $value == $_ } (1, 2, 3, 4, 5, 6)) {
}
</code>

Even though the code looks exactly the same, it is probably more expressive to
use the <hl>any</hl> function provided by <a href="https://metacpan.org/pod/List::MoreUtils">List::MoreUtils</a>.

<code lang="perl">
use List::MoreUtils qw(any);

if (any { $value == $_ } (1, 2, 3, 4, 5, 6)) {
}
</code>

We can use these expressions in our test script as follows:

<code lang="perl">
use strict;
use warnings;

use MyTools;

use List::MoreUtils qw(any);

use Test::More tests => 4;

for (1..4) {
    my $value = dice();
    ok( (any {$_ eq $value} (1, 2, 3, 4, 5, 6)), 'correct number');
}
</code>

Running this will result in the following output:

<code>
1..4
not ok 1 - correct number
#   Failed test 'correct number'
#   at t/dice_any.t line 14.
ok 2 - correct number
not ok 3 - correct number
#   Failed test 'correct number'
#   at t/dice_any.t line 14.
ok 4 - correct number
# Looks like you failed 2 tests of 4.
</code>

OK, so we can now see that there is some problem, but we cannot yet see the actual results.
We could add back the <hl>diag $value;</hl> as we did earlier, but there is really no point in printing
out the value in every case. Only in case of failure.

<h2>Provide failure diagnostics</h2>

All the test-functions provided by Test::More return their truth-value. That is, they will return either
<a href="/boolean-values-in-perl">true or false</a>. We can use this information to improve our test script.
We use the <a href="/short-circuit">short-circuiting</a> feature of the <hl>or</hl> operator to execute
some code only when a specific test case failed. In that case we call <hl>diag</hl> and print out the received
value.

<code lang="perl">
use strict;
use warnings;

use MyTools;

use List::MoreUtils qw(any);

use Test::More tests => 4;

for (1..4) {
    my $value = dice();
    ok( (any {$_ eq $value} (1, 2, 3, 4, 5, 6)), 'correct number')
        or diag "Received: $value\n";
}
</code>

Running this test script will generate the following output:

<code>
1..4
not ok 1 - correct number
#   Failed test 'correct number'
#   at t/dice_any_diag.t line 16.
# Received: 1.5
ok 2 - correct number
ok 3 - correct number
ok 4 - correct number
# Looks like you failed 1 test of 4.
</code>

This is much better. Now we can go and fix the <hl>dice()</hl> function and our test will work much better than earlier.
(We are still unlikely to actually encounter one of the problematic cases, so we might prefer to run the above
test 100 times and not only 4 times.)


<h2>The 8-sided dice</h2>

What if our marketing team comes to the conclusion that we need to generalize the <hl>dice()</hl> function and let it handle
dice with all kinds of sizes? As developers we'll have to provide a solution for the dice() function itself, but as a tester
we need to be able to test it.

The decision is that calling <hl>dice($N)</hl> with any number will return a random whole number between <hl>1 .. $N</hl>.
Testing this is simple for specific values of <hl>$N</hl>. We just need to copy-paste our previous solution:

<code lang="perl">
use strict;
use warnings;

use MyTools;

use List::MoreUtils qw(any);

use Test::More tests => 4*2;

for (1..4) {
    my $value = dice(6);
    ok( (any {$_ eq $value} (1, 2, 3, 4, 5, 6)), 'correct number')
        or diag "Received: $value\n";
}

for (1..4) {
    my $value = dice(8);
    ok( (any {$_ eq $value} (1, 2, 3, 4, 5, 6, 7, 8)), 'correct number')
        or diag "Received: $value\n";
}
</code>

Besides the fact that we now have a lot of duplicated code, when we receive a failed test
we'll have to rely on line numbers to know if this was a 6-sided dice or an 8-sided dice.
It would be much better to print the expectation as well. Given that each list of expected numbers is
between 1 and some $N, we could change our code to say so in words:

<code lang="perl">
    ok( (any {$_ eq $value} (1, 2, 3, 4, 5, 6)), 'correct number')
        or diag "Received: $value\nExpected number between 1-6\n";
</code>

That would work, but we would still have a lot of copies of the same code and we have a chance to solve
a much more interesting problem. What if the list of values is not numbers from 1 to some $N, but some
arbitrary list of values? In that case, the information about the expected values should list all the expected values.
In order to make it easier we put the expected values in an array and use it both with the <hl>any</hl> function
and when printing the expected values. We have also replaced the <hl>==</hl> operator with the <hl>eq</hl> operator in the
<hl>any</hl> expression. It does not have an effect on our case of numbers between 1..$N, but it provides a more generic
way to compare scalar values.

<code lang="perl">
my @expected = (1, 2, 3, 4, 5, 6);

for (1..4) {
    my $value = dice();
    ok( (any {$_ eq $value} @expected), 'correct number')
        or diag "Received: $value\nExpected:\n" .
            join "", map {"         $_\n"} @expected;
}
</code>

The full code then looks like this:

<code lang="perl">
use strict;
use warnings;

use MyTools;

use List::MoreUtils qw(any);

use Test::More tests => 4*2;

my @expected = (1, 2, 3, 4, 5, 6);

for (1..4) {
    my $value = dice(6);
    ok( (any {$_ eq $value} @expected), 'correct number')
        or diag "Received: $value\nExpected:\n" .
            join "", map {"         $_\n"} @expected;
}

my @expected = (1, 2, 3, 4, 5, 6, 7, 8);

for (1..4) {
    my $value = dice(8);
    ok( (any {$_ eq $value} @expected), 'correct number')
        or diag "Received: $value\nExpected:\n" .
            join "", map {"         $_\n"} @expected;
}
</code>

The output from this code looks like this:

<code>
1..4
not ok 1 - correct number
#   Failed test 'correct number'
#   at t/dice_any_diag.t line 16.
# Received: 1.5
# Expected:
#          1
#          2
#          3
#          4
#          5
#          6
ok 2 - correct number
ok 3 - correct number
ok 4 - correct number
ok 5 - correct number
ok 6 - correct number
not ok 7 - correct number
#   Failed test 'correct number'
#   at t/dice_any_diag.t line 24.
# Received: 3.5
# Expected:
#          1
#          2
#          3
#          4
#          5
#          6
#          7
#          8
ok 8 - correct number
# Looks like you failed 2 test of 8.
</code>

Next time we'll do some refactoring and wrap the 3 lines of code:

<code lang="perl">
    ok( (any {$_ eq $value} @expected), 'correct number')
        or diag "Received: $value\nExpected:\n" .
            join "", map {"         $_\n"} @expected;
</code>

in a generic function.

