=title Counter with Dancer Sessions
=timestamp 2015-12-11T12:50:01
=indexes Dancer, session
=status show
=books dancer2, counter
=author szabgab
=comments_disqus_enable 0

=abstract start

How to create a personal <a href="https://code-maven.com/counter">counter</a> with only a session object in the <a href="/dancer">Perl Dancer</a> framework?

=abstract end

We create a Dancer application using <hl>dancer2 -a Counter</hl> and change directory to the project: <hl>cd Counter</hl>

We launch the application using plack:

<code>
$ plackup -R . bin/app.psgi
</code>

The <hl>-R .</hl> will monitor the whole directory structure and if anything changes it will reload the application.

We enable session management in the <hl>config.yml</hl> file by adding the following line:

<code>
session: "YAML"
</code>

(According to the <a href="https://metacpan.org/pod/distribution/Dancer2/lib/Dancer2/Manual.pod">documentation</a>
one needs to enable sessions, but in fact, probably due to a <a href="https://github.com/PerlDancer/Dancer2/issues/884">bug</a>,
Simple, in-memory sessions are enabled by default. Nevertheless, in order to have sessions that survive the restart of the
application we need to enable the above.)

Then we can browse to <a href="http://127.0.0.1:5000/">http://127.0.0.1:5000/</a> and see the default page of Dancer.

A session is identified by a cookie the server sends to the browser, but it will only generate the cookie
if we actually store a value in the session. The value will be store on the server in the <hl>sessions/</hl> subdirectory
of the project in a YAML file. (Dancer could use other back-ends for session management, but using YAML files is the most simple
persistent way to store session data.)

You can check, the <hl>sessions/</hl> directory is empty now, if it even exists.

In order to set a value in the session we call the <hl>session</hl> function in our Dancer application.

<h2>Setting a value in a session</h2>

This is how we change the <hl>lib/Counter.pm</hl> file:

<code lang="perl">
get '/' => sub {
    session('cnt' => 1);
    template 'index';
};
</code>

After saving the change we can reload the web page.

Opening the developer tools of our browser we can see a new Cookie added to 127.0.0.1:

<code>
dancer.session  VTChcQABUqiiJ0PMHVAnTkm0p_F7nYnk    127.0.0.1    /   Session 50  âœ“       
</code>

The long string is the cookie identifier and it will be unique on your server.

If we look at the file system, we can see that Dancer created a new directory in the project called <hl>sessions/</hl>,
(if it did not exist earlier) and put a single file in it called <hl>VTChcQABUqiiJ0PMHVAnTkm0p_F7nYnk.yml</hl>.
Note, the name of the file is the value of the cookie. By default, the name of the cookie is <hl>dancer.session</hl> and the cookie
was set to be part of the <hl>127.0.0.1</hl> domain that served this page.

The yaml file contains the key-value pair we added to it:

<code>
$ cat sessions/VTChcQABUqiiJ0PMHVAnTkm0p_F7nYnk.yml 
---
cnt: 1
</code>

If we reload the page at <a href="http://127.0.0.1:5000/">http://127.0.0.1:5000/</a> we won't see any change in the display,
as we have not changed the page itself yet.

To cookie in the browser does not contain any information besides the value of the cookie which is the session id.
The key-value pair we set in the cookie is stored on the server in the YAML file.

If we now visited the same page using a different browse, that browser would get a different cookie and the Dancer
application would create another YAML file.

<h2>Fetching the value from the session</h2>

In order to fetch the value from the session object, we can use the <hl>session</hl> function again, this time with only one parameter:

<code lang="perl">
get '/' => sub {
    #session('cnt' => 1);
    my $counter = session('cnt');
    template 'index';
};
</code>

For the time being we have disabled setting the session so we can see that the value of 'cnt' is indeed fetched from the YAML
file created when we accessed the page earlier.

Of course the above code does not let us see what was in the session so let's print it out to the console by adding <hl>debug()</hl>
call to our code:

<code lang="perl">
get '/' => sub {
    #session('cnt' => 1);
    my $counter = session('cnt');
    debug("Counter: $counter");
    template 'index';
};
</code>

If we now reload the web page and then switch to the console window where we ran <hl>plackup</hl> we can go through the output there
and see a line that looks like this:

<code>
[Counter:86744] debug @2015-04-17 09:38:47> Counter: 1 in ...
</code>

This is the output of the <hl>debug()</hl> call in our application.

<h2>Incrementing the counter</h2>

Now we can combine the two operations, the setting of the value in the session and the fetching of the value.
We can also increment our counter in the same route:

<code lang="perl">
get '/' => sub {
    my $counter = session('cnt') || 0;
    $counter++;
    debug("Counter: $counter");
    session('cnt' => $counter);
    template 'index';
};
</code>

<ol>
  <li>We fetch the value from the session file or set it to 0 if there was no value in the session file yet.
   This is to nicely handle the case when a new browser visits the page.</li>
  <li>Increment the counter and print it to the console of the web application.</li>
  <li>Save the new value in the session object and thus in the session YAML file.</li>
</ol>

If we now reload the browser several times, we'll be able to see the counter incrementing on the console.
Unfortunately we'll also see the plackup server restarting itself after every time we load the page.

This, as I found out later, is because the <hl>-R .</hl> flag tells plackup to restart the server if <b>any</b> file changes in the
whole directory tree. As we keep updating the session file in the <hl>sessions/</hl> directory, plackup
will keep reloading the server.

A probably better way to start the application would be:

<code>
$ plackup -R config.yml,lib bin/app.psgi
</code>

This way, plackup will only monitor the <hl>config.yml</hl> file and the <hl>lib</hl> directory.

Maybe even better to also monitor the environments/ directory as well, where additional configuration files can be found:

<code>
$ plackup -R config.yml,lib,environments bin/app.psgi
</code>


<h2>Displaying the counter in the browser</h2>

We now already have a counter for the individual visitor, but the counter is only displayed on the console
of the server. To make a bit fancier, we can also include the value of the counter in the HTML sent to the client.

For this we change the call to the <hl>template</hl> and pass a key-value pair where the value is the
counter:

<code lang="perl">
get '/' => sub {
    my $counter = session('cnt') || 0;
    $counter++;
    debug("Counter: $counter");
    session('cnt' => $counter);
    template 'index', { count => $counter };
};
</code>

We also change the views/index.tt template. We can remove all the content and have only the following:

<code>
Count: <% count %>
</code>

Now, if we reload the page in the browser we can see the counter incrementing in the browser as well.

If we open another browser we can see it has a separate counter.

<h2>Per session counter</h2>

So this counter we created is not the same kind of counter we wanted to create in the big <a href="https://code-maven.com/counter">counter example</a>
project, but still a useful exercise in using sessions and cookies.


