=title What is the status of the current test script?
=timestamp 2016-03-21T19:00:01
=indexes Test::Builder, Test::More, is_passing
=tags screencast
=status show
=books testing
=author szabgab
=comments_disqus_enable 1

=abstract start

The other day I looked at the tests of the <a href="https://metacpan.org/pod/Expect">Expect</a> module, and was surprised to see that it
was still using the old-style, home-brewed testing system. Something like the one we used when we introduced
<a href="/introducing-test-simple">test automation</a>. I wanted to convert it to use <a href="http://metacpan.org/pod/Test::More">Test::More</a>,
but in addition to an <hl>ok()</hl> function, this home-made system had a unique functionality.

At the end of the script it printed a message something along the line "Don't worry if a test fails, this can happen.", but it only printed
if any of the ok() functions of the test script failed.

I wanted to preserve this functionality.

=abstract end

<screencast file="what-is-the-status-of-the-current-test-script" youtube="limsGlhl-9o" />

In addition, I know there are many people who are not very familiar with Perl yet, who are helpless when
a test fails. It might be an interesting idea to add a message that explains how to report the problem,
and how to install the module even if some of the tests fail. After all, a test failure does not necessarily
mean the module is totally useless.

Let's start with a test script called <hl>status.t</hl> with the following content:

<code lang="perl">
use strict;
use warnings;
use Test::More tests => 3;

ok 1;
ok 1;
ok 1;
</code>

It does not test anything, it just prints <hl>ok</hl> 3 times, but it can be useful to experiment with.
The output of <hl>perl status.t</hl> looks like this:

<code>
1..3
ok 1
ok 2
ok 3
</code>

Nothing special.

If we change the second call to <hl>ok</hl> and pass a false statement to it (<hl>ok 0;</hl>) the result changes to:

<code>
1..3
ok 1
not ok 2
#   Failed test at status.t line 6.
ok 3
# Looks like you failed 1 test of 3.
</code>


<h2>is_passing of Test::Builder</h2>

Behind all the Test::* infrastructure lies the <a href="https://metacpan.org/pod/Test::Builder">Test::Builder</a> module.
It does all the counting and reporting that we saw from <a href="https://metacpan.org/pod/Test::Simple">Test::Simple</a>
and friends. We can also access the Test::Builder object during the test run. We only need 3 things:

Load Test::Builder with <hl>use</hl>. Call the <hl>new</hl> method. As it is a singleton, it will return the only Test::Builder object available in a process.
Then the <hl>is_passing</hl> method will return <a href="/boolean-values-in-perl">true or false</a>. It will return <hl>true</hl>, as long
as one of the <hl>ok()</hl> assertions has not failed.

<code lang="perl">
use Test::Builder;
my $Test = Test::Builder->new;
diag $Test->is_passing;
</code>

Adding it to our real code we will get the following:

<code lang="perl">
use strict;
use warnings;
use Test::More tests => 3;

ok 1;
ok 0;
ok 1;

use Test::Builder;
my $Test = Test::Builder->new;
if (not $Test->is_passing) {
    diag 'Please report test failures to support@...';
    diag 'In the meantime you can install the module disregarding the test results using "cpanm --notest"';
}
</code>

<code>
1..3
ok 1
not ok 2
#   Failed test at status.t line 6.
ok 3
# Please report test failures to support@...
# In the meantime you can install the module disregarding the test results using "cpanm --notest"
# Looks like you failed 1 test of 3.
</code>

Of course if we now change that <hl>ok 0</hl> to <hl>ok 1</hl>, and run the script again we will get only
the success report:

<code>
1..3
ok 1
ok 2
ok 3
</code>

<h2>Caveats of is_passing</h2>

There are a few issues with <hl>is_passing</hl> though.
If no tests were executed it will report success, but Test::Harness would actually report failure.
If all the tests are successful, but the actual number of tests is smaller than the <hl>test plan</hl>, <hl>is_passing</hl>
will still return true. (Interestingly, if the actual number of tests is bigger than the planned number
then it will notice the problem.)

Let's try some other tools.

<h2>The plan: expected_tests and summary</h2>

Test::Builder provide a few more methods that we can use. For example there is the <hl>expected_tests</hl> method
that returns the number of tests planned. In our case it will return 3.

In addition there is also the <hl>summary</hl> method that returns an array true/false values for each ok() call
executed so far.

So we can add the following 3 lines to compare the expected number of tests with the
number of successes.

<code lang="perl">
my $cnt = 0;
for ($Test->summary) { $cnt++ if $_ } 
diag $cnt == $Test->expected_tests ? 'success' : 'failure';
</code>

In the full script it might look like this:


<code lang="perl">
use strict;
use warnings;
use Test::More tests => 4;

use Test::Builder;
my $Test = Test::Builder->new;

ok 1;
ok 0;
ok 1;


my $cnt = 0;
for ($Test->summary) { $cnt++ if $_ } 
diag $cnt == $Test->expected_tests ? 'success' : 'failure';
</code>

Unlike the <hl>is_passing</hl> method, this method will provide the correct answer even if the number of test executed
does not match the planned number.


<h2>Caveat</h2>

Of course none of this will help if for some reason the test script exits early,
or if it never runs because another script bails out. The former could be solved by
putting the code in the <hl>END</hl>-block, the latter, by putting the block in every
test script.

