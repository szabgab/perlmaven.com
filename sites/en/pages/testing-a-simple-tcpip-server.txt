=title Testing a simple TCP/IP server using Net::Telnet
=timestamp 2015-04-23T08:00:01
=indexes Net::Telnet
=status show
=books testing, net_server
=author szabgab
=comments_disqus_enable 0

=abstract start

Before we continue to develop our TCP/IP based server using Net::Server, let's take a look at how
we can test the server? We are going to use <a href="https://metacpan.org/pod/Test::More">Test::More</a>,
the standard testing system of Perl and <a href="https://metacpan.org/pod/Net::Telnet">Net::Telnet</a>,
that is a telnet client implementation in Perl.

=abstract end

This test will assume a Unix/Linux system, but later we are going to change it to work on Windows as well.

<h2>The test script</h2>

The test script saved in the <hl>t/01-simple-echo-server.t</hl> file looks like this:

<code lang="perl">
use strict;
use warnings;

my $pid = fork();
die 'Could not fork' if not defined $pid;

if (not $pid) {
    close STDERR;
    exec("$^X bin/simple_echo_server.pl");
}

END {
  if ($pid) {
    kill 2, $pid;
    wait;
  }
}

sleep 1;
require Test::More;
import Test::More;
require Net::Telnet;
plan(tests => 7);
my $t = Net::Telnet->new();
ok($t->open(Host => 'localhost', Port => 8000, Timeout => 10), 'connected');

{
    my ($pre, $match) = $t->waitfor('/(?<=if you want to leave)/');
    is($pre, "Welcome to the Echo server, please type in some text and press enter. Say 'bye' if you want to leave", 'banner');
    is($match, '', 'empty');
}

{
    $t->print('hello');
    my ($pre, $match) = $t->waitfor('/You said "hello"/');
    like($pre, qr/^\s*$/, 'just newline');
    is($match, 'You said "hello"', 'reply by the server');
}
{
    $t->print('bye');
    my ($pre, $match) = $t->waitfor('/You said "bye"/');
    like($pre, qr/^\s*$/, 'just newline');
    is($match, 'You said "bye"', 'reply by the server');
}
</code>

It is placed next to the <hl>lib</hl> and <hl>bin</hl> directories we created for the <a href="/getting-started-with-net-server">Simple echo server</a>.
We can run the test using <hl>prove -l t/01-simple-echo-server.t</hl> and it will print out the following:

<code>
t/01-simple-echo-server.t .. ok
All tests successful.
Files=1, Tests=7,  1 wallclock secs ( 0.03 usr  0.01 sys +  0.12 cusr  0.01 csys =  0.17 CPU)
Result: PASS
</code>

(Actually in this case we could leave the <hl>-l</hl> flag out, but I think it is better to have it in our muscle memory.)

If we want a more verbose output we can add a <hl>-v</hl> flag and run:
<hl>prove -lv t/01-simple-echo-server.t</hl> getting the following output:

<code>
t/01-simple-echo-server.t ..
1..7
ok 1 - connected
ok 2 - banner
ok 3 - empty
ok 4 - just newline
ok 5 - reply by the server
ok 6 - just newline
ok 7 - reply by the server
ok
All tests successful.
Files=1, Tests=7,  1 wallclock secs ( 0.03 usr  0.01 sys +  0.12 cusr  0.01 csys =  0.17 CPU)
Result: PASS
</code>

The first showed us a summary of the test cases collected by the
<a href="https://metacpan.org/pod/Test::Harness">Test::Harness</a>, the
second one was the raw TAP output.

<h2>Explaining the test script</h2>

We need to test a client-server application. For this we are going to launch our server from the
test script in a forked process. This is useful as then we know the process id of the server and
we can shut id down at the end of our test.

We also have to deal with a couple of other issues. If we have Test::More loaded in a script
it will expect to have a "plan" and to have test assertions in it. In our case only the client
is a real test script, the server is not. So we have to make sure we load <hl>Test::More</hl>
only in the parent process, which is going to be the client.
We also have to make sure that none of the out from the server will be mixed in the output
of our test script or it can confuse the <hl>Test::Harness</hl>.

Let's go over the test script.

<h2>Forking to launch the server</h2>

This code <a href="/fork">forks</a>, and the child process runs the server. Because the server will print various
things on the Standard Error channel of its console, first <hl>close STDERR</hl> in the child
process. Then we call <hl>exec</hl> to tun the external script. This will make sure the external
script has the same process id as the child process and when the external script exits, the
child process itself exits. For that reason there is no need to call <hl>exit</hl> in the
child process.

<code lang="perl">
my $pid = fork();
die 'Could not fork' if not defined $pid;

if (not $pid) {
    close STDERR;
    exec("$^X bin/simple_echo_server.pl");
}
</code>

<h2>Closing the server when the test ends</h2>

We want to make sure the server stops working after the test script
finishes, even if the test script died an unnatural death. (e.g. by throwing an exception.)
So we send <hl>kill 2</hl> to the process id of the child process. We do it only if <hl>$pid</hl>
had a value so this block will only run in the parent process.
We wrap our code in and <hl>END</hl> block. This will ensure the code is executed even if
something throws an exception in the test script:

<code lang="perl">
END {
  if ($pid) {
    kill 2, $pid;
    wait;
  }
}
</code>

<h2>Loading Test::More late and planning late</h2>

Then we sleep a bit, just to let the server launch - this is a bit problematic.
On very slow system 1 sec might not be enough for the server to launch, on fast systems this
is a waste of time. Even if it is only one second. The good part is that it is a simple solution.

After sleeping we load <hl>Test::More</hl>, using the <hl>require</hl> function. We don't call <hl>use</hl>
as that would load <hl>Test::More</hl> at compile-time and thus it would be loaded even before the forking.
Regardless of the physical location of the <hl>use</hl>-statement. <hl>require</hl> on the other hand will
load the module during run-time. <hl>import</hl> will, well import the various testing function such as <hl>ok()</hl>
and <hl>is()</hl> and <hl>plan()</hl>.

We also load <hl>Net::Telnet</hl> in the client. We don't need to import anything from there as it is an object
oriented module.
Then we declare the expected number of test cases using <hl>plan(tests => 7);</hl>.
In this case we need to put parentheses after the parameters of <hl>plan()</hl> just as after <hl>ok()</hl> and all the
other testing functions, because they are not declared yet when perl compiles this part of the code.
We would not need the parentheses if we loaded <hl>Test::More</hl> with <hl>use</hl> during compile time, but we do need
the parentheses now.

<code lang="perl">
sleep 1;
require Test::More;
import Test::More;
require Net::Telnet;
plan(tests => 7);
</code>

<h2>Connect to the server</h2>

This is just creating a <hl>Net::Telnet</hl> object and the opening a connection to the server.
The result is going to be <a href="/boolean-values-in-perl">true</a> if the connection succeeded.

Nothing fancy here:

<code lang="perl">
my $t = Net::Telnet->new();
ok($t->open(Host => 'localhost', Port => 8000, Timeout => 10), 'connected');
</code>

<h2>Waiting for the banner</h2>

Once our connection is established, the server is supposed to send a welcome message to the client.
It is sometimes called a <b>banner</b>. So we tell the telnet client to wait for a sign that should be the end of the
banner. As our application does not have a <hl>prompt</hl> we have to rely on the expected content. So we call
the <hl>waitfor</hl> method of the <hl>Net::Telnet</hl> object with a regular expression. For historical reasons this
needs to be regex inside a string. That's why we have both single-quote <hl>'</hl> and a slash <hl>/</hl>.

The telnet client collects everything the server sends to the client in a buffer. The <hl>waitfor</hl> method watches that
buffer and tries to match the regex. Once the regex is matched the <hl>waitfor</hl> method returns two values.
(At least when it is called in list context as we called it.)
The second value is the actual match. (What would be in <hl>$&</hl> normally. The first value is the content of the
buffer up to the beginning of the match.
In this case we used <hl>(?<= ... )</hl> around the regex. This is a fixed-width look-behind. It means we are looking
for something that fits the regex between the parentheses, but when it matches it won't be part of <hl>$&</hl>.
This means we are expecting <hl>$&</hl> and thus the <hl>$match</hl> variable to be empty and the <hl>$pre</hl> variable
to contain the whole banner. Even the part that was in our regex.

Once we have <hl>$pre</hl> and <hl>$pre</hl> we can use the regular testing functions <hl>is()</hl> to compare
them to the expected values.

<code lang="perl">
{
    my ($pre, $match) = $t->waitfor('/(?<=if you want to leave)/');
    is($pre, "Welcome to the Echo server, please type in some text and press enter. Say 'bye' if you want to leave", 'banner');
    is($match, '', 'empty');
}
</code>

One more thing about this code. We put the 3 lines in a pair of curly braces because in the next few test we will want to
use the same variables <hl>$pre</hl> and <hl>$match</hl> and we want to avoid accidentally checking an older
version of these values in a later test case. Putting them in a block restricts their scope.

<h2>Testing the echo</h2>

The rest of test script is the main part of the testing, but we have covered almost all the aspects of
the test script already. One thing remains. In both of the cases below, first we call the <hl>print</hl>
method of <hl>Net::Telnet</hl>. This simply sends its parameter to the server.

Then we go back our previous ritual, waiting for specific responses from the server.

<code lang="perl">
{
    $t->print('hello');
    my ($pre, $match) = $t->waitfor('/You said "hello"/');
    like($pre, qr/^\s*$/, 'just newline');
    is($match, 'You said "hello"', 'reply by the server');
}
{
    $t->print('bye');
    my ($pre, $match) = $t->waitfor('/You said "bye"/');
    like($pre, qr/^\s*$/, 'just newline');
    is($match, 'You said "bye"', 'reply by the server');
}
</code>


