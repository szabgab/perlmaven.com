=title Add some acceptance tests using search.cpan.org
=timestamp 2015-04-02T11:30:01
=indexes Test::WWW::Mechanize, SCO
=tags screencast
=status show
=books testing, search_cpan_org
=author szabgab
=comments_disqus_enable 1

=abstract start

The plan is, that I am going to write a lot of tests accessing the live <a href="http://search.cpan.org/">search.cpan.org</a> web site and use those
tests as a suite of <b>acceptance tests</b> for the <a href="/search-cpan-org">search.cpan.org clone</a> project.

=abstract end

<screencast file="/media/videos/add-some-acceptance-tests" />

<a href="https://www.youtube.com/watch?v=Puzc9QkV_Vg">YouTube</a>

While working on the next step, I noticed that the swap-files used by vim show up as untracked file by git. In order to avoid adding
them to Git, I added <hl>*.swp</hl> to the <hl>.gitignore file. <a href="https://github.com/szabgab/MetaCPAN-SCO/commit/a87232f428308f93b45ab70009d3254cde77f9cb">commit</a>.

<h2>Testing search.cpan.org</h2>

There are a couple of ways to test web site, and using <a href="https://metacpan.org/pod/WWW::Mechanize">WWW::Mechanize</a>
is a great way to do that. WWW::Mechanize is basically a web client (it is a subclass of <a href="https://metacpan.org/pod/LWP::UserAgent">LWP::UserAgent</a>
that understand HTML. So you can tell it to click on a link or fill a form.

In itself WWW::Mechanize is great to automate interaction with web sites, but if we want to write <b>unit-test</b> (even if in this case we call them <b>acceptance tests</b>)
it is much better to use <a href="https://metacpan.org/pod/Test::WWW::Mechanize">Test::WWW::Mechanize</a> which is a wrapper around Mechanize providing test-functions
using <a href="https://metacpan.org/pod/Test::Builder">Test::Builder</a> backend. Exactly the same way as it was explained in the testing mini-series that ended
when we <a href="/is-any-create-test-module">created a test module</a>.


Using <a href="https://metacpan.org/pod/Test::WWW::Mechanize">Test::WWW::Mechanize</a> is a bit different than the test module we created. This module provides an object
oriented interface. First we create an instance of this class and then use its methods to interact with a web site. As a start we call <hl>get_ok</hl> with a URL.
This will fetch the page on that URL and check if the fetching was successful. (If it returned <hl>200 ok</hl>.) Then we can use all kinds of methods
to check the content of this page. In this case I went for a very minimalistic test, I used the <hl>title_is</hl> method to check if the page has the expected title.

At this point I already started to have concerns: In the clone, am I going to have the same title or will it explicitly say it is a clone? Am I going to
be able to reuse these tests with the clone? I did not have an answer to these, but I kept going and added another
<a href="/subtest">subtest</a>. The <hl>follow_link_ok</hl> method clicks on the link that says "Authors", checks if there was such a link and
if the new page was loaded successfully.

<code lang="perl">
use strict;
use warnings;

use Test::More;
use Test::WWW::Mechanize;

plan tests => 2;

my $w = Test::WWW::Mechanize->new;

my $url = 'http://search.cpan.org/';

subtest home => sub {
    $w->get_ok($url);
    $w->title_is('The CPAN Search Site - search.cpan.org');
};

subtest authors => sub {
    $w->follow_link_ok( {text => 'Authors'}, 'Authors link' );
};
</code>

<code>
$ git add .
$ git commit -m "first tests of live search.cpan.org site"
</code>

<a href="https://github.com/szabgab/MetaCPAN-SCO/commit/9f6dba883c3263ae744d546a9b8485595386719e">commit</a>


<h2>Add some more tests</h2>


Once I had the skeleton of the tests  I could add some more checks.

On the home-page there is a form. The parameters of <hl>submit_form_ok</hl> method gets help identify one specific form.
In our case it is quite easy, as there is only one form on this page. (I checked it by looking at the HTML source of
the page and looking for the word "form".) So we identify the form by being the first (and only) form on this page:
<hl>form_number => 1</hl>. Then we supply the values to the fields. In order to know the names of the fields I had
to look at the HTML source of the page again. The <hl>query</hl> is some free-text field, while the <hl>mode</hl> is a
selector. The <hl>submit_form_ok</hl> method will fill the form, click on the submit button, and check if the
resulting page returned <hl>200 ok</hl>.

Then we have two methods checking the content of the resulting page. <hl>content_contains</hl> check if the given string
can be found in the source of the HTML file and <hl>content_lacks</hl> checks if the given string is <b>not</b> in the HTML.

This test actually is even more problematic than the one checking the title. When searching for "sz" among the authors,
I was expecting to see all the authors that have "sz" in their name. It turns out search.cpan.org does not show all of them.
Specifically I did not see my own name. This is disturbing on many levels, besides hurting my ago. Does this mean there is a
bug in search.cpan.org or is there something more subtle in this search, I am not aware of. Should the clone try to
show the exact same list, or shall we consider this a bug and fix it?

Can we even understand why specific people are listed here and others not?

How can I write a test that will pass on both search.cpan.org and on the clone, if they will show different results?

<code lang="perl">
use strict;
use warnings;

use Test::More;
use Test::WWW::Mechanize;

plan tests => 2;

my $w = Test::WWW::Mechanize->new;

my $url = 'http://search.cpan.org/';

subtest home => sub {
    $w->get_ok($url);
    $w->title_is('The CPAN Search Site - search.cpan.org');
    $w->submit_form_ok( {
            form_number => 1,
            fields      => {
                query => 'sz',
                mode  => 'author', 
            },
        }, 'search "sz" in author'
    );
    $w->content_contains('Arpad Szasz');
    $w->content_lacks('Szabo');
};

subtest authors => sub {
    $w->follow_link_ok( {text => 'Authors'}, 'Authors link' );
    $w->title_is('The CPAN Search Site - search.cpan.org');
    $w->follow_link_ok( {text => 'A'}, 'A link' );
    $w->title_is('The CPAN Search Site - search.cpan.org');
    $w->content_contains('Andy Adler');
    $w->content_contains('Sasha Kovar');

    $w->follow_link_ok( {text => 'AADLER'}, 'Link to author AADLER');
};
</code>

The second subtest was also extended with a couple of additional checks.
I checked the title here too, and then followed the link that has <b>A</b> on it. Checked the title again
and used <hl>content_contains</hl> to make sure two names appear in the list. Then followed one of the links.

These tests are also slightly problematic. What if one of the people changes her name. Will the test fail then?
Is this ok to have test that are dependent on the environment?

<code>
$ git add .
$ git commit -m "add some more tests"
</code>

<a href="https://github.com/szabgab/MetaCPAN-SCO/commit/0a6b97396145f8a109ee3647f2aa5b3e9200892e">commit</a>


