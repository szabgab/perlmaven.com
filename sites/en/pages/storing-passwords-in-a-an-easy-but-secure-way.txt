=title Storing Password in an easy and secure way using Perl
=timestamp 2013-11-09T13:30:01
=indexes Crypt::PBKDF2, Digest::SHA, Digest::MD5, crypt, salt
=status show
=author szabgab
=comments_disqus_enable 0

=abstract start

In a system where users need to authenticate with a password, we need some way to
validate if the user has typed in the correct password. For this we need to keep
a copy of the password on our system. Keeping it in clear-text form would be a security
issue as anyone who might get a copy of this storage would know what is the password
for each user. Even if the production machine is well protected, someone might break in
to a testing machine, or might find a copy of the database backup.

So we need to encrypt the clear-text password and save the encrypted version.

=abstract end

Before going on with the explanation, let's see the recommended solution:

<code lang="perl">
use Crypt::PBKDF2;

my $pbkdf2 = Crypt::PBKDF2->new(
    hash_class => 'HMACSHA2',
    hash_args => {
        sha_size => 512,
    },
    iterations => 10000,
    salt_len => 10,
);

my $password = "s3kr1t_password";
my $hash = $pbkdf2->generate($password);
</code>



<h2>Encrypt the password</h2>

<code lang="perl">
use Crypt::PBKDF2;

my $pbkdf2 = Crypt::PBKDF2->new;
my $password = "s3kr1t_password";

my $hash = $pbkdf2->generate($password);
</code>

The <hl>$password</hl> is received from the user
and after computing it, we store the <hl>$hash</hl> in the database,
along with the user identifier (e.g. username).

The hash will look like this string:
<hl>{X-PBKDF2}HMACSHA1:AAAD6A:SEvDOw==:1rmVDmR6OgwPEYV5CiwUeYnd+OE=</hl>

<h2>Check the password</h2>

<code lang="perl">
use Crypt::PBKDF2;

my $password = "s3kr1t_password";
my $hash = '{X-PBKDF2}HMACSHA1:AAAD6A:SEvDOw==:1rmVDmR6OgwPEYV5CiwUeYnd+OE=';

my $pbkdf2 = Crypt::PBKDF2->new;
if ($pbkdf2->validate($hash, $password)) {
    print "valid password\n";
    ...
}
</code>

The user will supply the <hl>$password</hl> and the user identifier (probably a username).
The <hl>$hash</hl> will be fetched from the database using the username.
<hl>validate</hl> will compute the hash again from the <hl>$password</hl> and
compare it with the <hl>$hash</hl>. You might ask why do we need a separate method
for this and you'll find the explanation below.

<h2>Encrypt the password with improvements</h2>

Later in the article this will be explained, but let's have an improved
version of the code here, that provides a lot stronger encryption than the
default:

<code lang="perl">
use Crypt::PBKDF2;

my $pbkdf2 = Crypt::PBKDF2->new(
    hash_class => 'HMACSHA2',
    hash_args => {
        sha_size => 512,
    },
    salt_len => 10,
);

my $password = "s3kr1t_password";
my $hash = $pbkdf2->generate($password);
</code>



<h2>Theory and Explanation</h2>

The encryption employed should be some kind of an algorithm that makes it easy to
convert the clear-text password to an encoded version, but makes it impossible,
or at least unreasonably difficult to convert the encoded version back to clear-text,
or to find another string that converts to the same encrypted version.

In the above sentence, the <b>impossible</b> refers to the assumption that
mathematicians so far could not find a fault in the algorithm.

The <b>unreasonably difficult</b> means that a potential attacker
would need many years and many very powerful computer to compute all encrypted
version of all the possible clear-text strings. So unless the attackers are lucky
they won't find the real password for some time.

Having such protection for a few centuries sounds like reasonable protection.

<h2>Hashing or encryption</h2>

While the word <hl>encryption</hl> is often used in this context and it is
also used in this article, an <hl>encryption</hl> usually means that there is
also a reverse method called <hl>decryption</hl>. In our case there is no
decryption so the proper name is either <hl>one-way-encryption</hl> or <hl>hashing</hl>

<h2>Hashing algorithms</h2>

Perl provides the <hl>crypt</hl> function that can be used as such hashing function,
but the algorithm behind it is relatively simple and there are newer better such
hashing functions.

<a href="https://metacpan.org/pod/Digest::SHA">Digest::MD5</a> implements the
well know MD5 algorithm.

<a href="https://metacpan.org/pod/Digest::SHA">Digest::SHA</a> implements
the SHA algorithm with various complexity. The higher the number the more secure the
algorithm.

There are other algorithms as well, but instead of changing the algorithm there
are other techniques as well that help improving the security.
<a href="http://en.wikipedia.org/wiki/Key_stretching">Key stretching</a> (or key strengthening)
usually refers to two techniques:

One of them is to make the clear-text password longer by adding some random characters.
This is usually referred to as <a href="http://en.wikipedia.org/wiki/Salt_(cryptography)">salting</a>.

The other technique is to call the encryption function repeatedly.

Both strategies make it more computational intensive to generate hashed strings from possible passwords
reducing the possibility of a
<a href="http://en.wikipedia.org/wiki/Brute_force_attack">brute force attack</a> or the creation
of a <a href="http://en.wikipedia.org/wiki/Rainbow_table">rainbow table</a>.

One can implement these techniques with either of the above hashing algorithms (the built-in
<hl>crypt</hl> function even accepts a parameter called salt, but using
<a  href="https://metacpan.org/pod/Crypt::PBKDF2">Crypt::PBKDF2</a> make it unnecessary.

By default the <hl>generate</hl> method of the module will use a random 4-bytes long salt
and will encode the salt and the password 1000 times.
By default it will use the HMAC-SHA1 algorithm implemented in
<a href="https://metacpan.org/pod/Digest::SHA">Digest::SHA</a>.

That means, using Crypt::PBKDF2 is a lot less vulnerable than plain SHA1.

While the above defaults work well, you can set your own values in the constructor
as shown in the synopsis of the module:

<code lang="perl">
use Crypt::PBKDF2;

my $pbkdf2 = Crypt::PBKDF2->new(
    hash_class => 'HMACSHA1', # this is the default
    iterations => 1000,       # so is this
    output_len => 20,         # and this
    salt_len => 4,            # and this.
);
</code>


The module even limits the length of the output to make it easy to insert the data
in a database with max-width fields.


<h2>Improve security</h2>

Actually, would be more secure to use SHA256 instead of SHA1 as a default there.
Luckily Crypt::PBKDF2 already comes with back-end that can use other than SHA1. The
<a href="https://metacpan.org/pod/Crypt::PBKDF2::Hash::HMACSHA2">Crypt::PBKDF2::Hash::HMACSHA2</a>
hash class can handle SHA 224, 256, 384, and 512. It defaults to 256.
All you need to do is tell the constructor to use <hl>HMACSHA2</hl> as the hash_class:

<code lang="perl">
use Crypt::PBKDF2;

my $pbkdf2 = Crypt::PBKDF2->new(
    hash_class => 'HMACSHA2',
);
</code>

If you want to go all the way to SHA 512 you need to pass a parameter to the constructor
of HMACSHA2 using the hash_args key:


<code lang="perl">
use Crypt::PBKDF2;

my $pbkdf2 = Crypt::PBKDF2->new(
    hash_class => 'HMACSHA2',
    hash_args => {
        sha_size => 512,
    },
);
</code>

Finally you might want to add more salt:

<code lang="perl">
use Crypt::PBKDF2;

my $pbkdf2 = Crypt::PBKDF2->new(
    hash_class => 'HMACSHA2',
    hash_args => {
        sha_size => 512,
    },
    iterations => 10000,
    salt_len => 10,
);
</code>


<h2>The basic implementation</h2>

Let's see how, more or less, the <hl>generate</hl> method of Crypt::PBKDF2 is implemented:

Simple hashing would look like this:

<code lang="perl">
sub generate {
    my ($self, $password) = @_;

    return hashing($password);
}
</code>

Salted hashing first, and incorrect attempt:

<code lang="perl">
sub generate {
    my ($self, $password) = @_;

    my $salt = $self->generate_salt(); # returns a 4 character long string
    return hashing( "$salt$password" );
}
</code>

but it has a big problem. How can we validate the password? If we call
generate again on the same password it will generate a different random salt
and the result will be different.
So the code looks more like this:

<code lang="perl">
sub generate {
    my ($self, $password) = @_;

    my $salt = $self->generate_salt(); # returns a 4 character long string
    return $salt . hashing( "$salt$password" );
}
</code>

Then we know that the first 4 characters of the string returned by generate
is the salt so we can write a <hl>validate</hl> method that will look like this:

<code lang="perl">
sub validate {
    my ($self, $password, $hash) = @_;

    my $salt = substr($hash, 0, 4);
    my $generated = $self->generate($password, $salt);
    return $hash eq $generated;
}
</code>

For this to work we will of course need to alter the <hl>generate</hl>
method a bit, to accept an optional salt:

<code lang="perl">
sub generate {
    my ($self, $password, $salt) = @_;

    $salt //= $self->generate_salt(); # returns a 4 character long string
    return $salt . hashing( "$salt$password" );
}
</code>

Now we already have a pair of working generate and validate methods.


<h3>Salted hashing with iterations would work like this:</h3>

<code lang="perl">
sub generate {
    my ($self, $password, $salt, $iteration) = @_;

    $salt //= $self->generate_salt();
    $iterations //= $self->iterations;
    for (1 .. $iterations) {
        $password = hashing( "$salt$password" );
    }
    return $salt . ':' .  $iterations . ':' . $password;
}
</code>

In this case the <hl>generate</hl> method needs to return both the salt and the number
of iterations, but as the number of iterations can be any number, it is better to
use a character to separate these field than to assume a fixed length.
This also allows us to use longer strings for salt.

The <hl>validate</hl> function in this case looks like this:

<code lang="perl">
sub validate {
    my ($self, $password, $hash) = @_;

    my ($salt, $iterations) = split /:/, $hash;
    my $generated = $self->generate($password, $salt, $iterations);
    return $hash eq $generated;
}
</code>

Finally we might want to be able to change the actual hashing algorithm
and then we have to ensure that the string returned from <hl>generate</hl>
will contain this information so that the <hl>validate</hl> method will
know what to use during validation.

The name of the method can be attached to the beginning of the string returned
by <hl>generate</hl>.

As a really, really last step, we might want to ensure that we can later change
the format of the string returned by the <hl>generate</hl> method so we would like
to add a field that identifies this method.

In case of <a  href="https://metacpan.org/pod/Crypt::PBKDF2">Crypt::PBKDF2</a> the format looks
like this:

<hl>{X-PBKDF2}HMACSHA1:AAAD6A:SEvDOw==:1rmVDmR6OgwPEYV5CiwUeYnd+OE=</hl>

which is called the ldap-like format.

In the curly braces we have the identifier of the string format. (X-PBKDF2)

Immediately after that is the identifier of the hashing algorithm. (HMACSHA1)

The next field, separated by a <hl>:</hl>, is the number of iterations encoded
with MIME::Base64.

Then the salt and finally the actual hash.

<h2>Longer is better</h2>

While we have been educated to use short passwords with all kinds of strange characters,
actually having long passwords is more important than having cryptic passwords.

So in general it is a good idea to encourage the users to use long passwords.

<h2>Denial of Service attack</h2>

One thing to note. If the application allows arbitrary length passwords
it can create a denial of service attack by overloading the server.
This problem was recently
<a href="https://www.djangoproject.com/weblog/2013/sep/15/security/">reported and fixed</a> in
<a href="https://djangoproject.com/">Django</a>.

So while users should be encouraged to use long password, the accepted password length
should be also limited.


<h2>Some reference</h2>

For extra flexibility it might be worth to look at the
<a href="https://docs.djangoproject.com/en/1.5/topics/auth/passwords/">password management in Django</a>.

In addition, Sebastian Willing has a good explanation of why
<a href="http://www.pal-blog.de/entwicklung/perl/when-slower-is-better-secure-your-passwords.html">slower is better</a>.


<h2>Update</h2>

You might also want to read <a href="https://hynek.me/articles/storing-passwords/">this post</a>, take a look at <a href="https://metacpan.org/pod/Crypt::Argon2">Crypt::Argon2</a>. See also <a href="https://en.wikipedia.org/wiki/Argon2">Argon2</a>. I have not checked it myself.

<h2>Comments</h2>

but if you get the string from a server, can't one just run it through their own perl encrypt program, reverse engineer it and get the password?

no, they can't

<hr>

Any idea what to do with:
Can't locate object method "get_or_add_package_symbol" via package "Package::Stash" at /usr/lib64/perl5/vendor_perl/Class/MOP/Package.pm line 128.

I have all the perl mods installed and up to date.


I don't see that method being called in the latest version https://metacpan.org/release/ETHER/Moose-2.2009/source/lib/Class/MOP/Package.pm I think you need to print the version numbers of the loaded modules using Dumper \%INC , the version of perl and the code snippet you are trying to use. Post all that in Stack overflow.


