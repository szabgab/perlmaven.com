=title Transforming a Perl array using map
=timestamp 2013-04-25T10:45:55
=indexes map, transform, list, array
=status show
=books advanced
=author szabgab
=comments_disqus_enable 0

=abstract start

The <hl>map</hl> function of Perl provides a simple way to transform a
list of values to another list of values. Usually this is a one-to-one
transformation but in general the resulting list can be also shorter or
longer than the original list.

=abstract end

We saw that <a href="/filtering-values-with-perl-grep">grep of Perl</a> is a generalization of the
UNIX grep command. It selects some or all of the elements from the
original list, and returns them unchanged.

<hl>map</hl>, on the other hand, is used when you would like to make changes
to the values of the original list.

The syntax is similar. You provide a block of code and a list of values: an array
or some other expression that returns a list of values.
For every value in the original list, the value is placed in <hl>$_</hl>,
<a href="/the-default-variable-of-perl">the default variable of Perl</a>, and the block
is executed. The resulting values are passed on, to the left-hand side of the assignment.

<h2>map for simple transformation</h2>

<code lang="perl">
my @numbers = (1..5);
print "@numbers\n";       # 1 2 3 4 5
my @doubles = map {$_ * 2} @numbers;
print "@doubles\n";       # 2 4 6 8 10
</code>

<h2>Building fast look-up table</h2>

Sometimes we have a list of values and during the execution of the code
we need to check if a given value is within this list. We can use
<a href="/filtering-values-with-perl-grep">grep</a> every time, to check if the current
value is in the list. We can even use the <a href="/filtering-values-with-perl-grep">any</a>
function from <a href="http://metacpan.org/modules/List::MoreUtils">List::MoreUtils</a>,
but it can be much more readable and faster if we used a hash for lookup.

We need to create a hash once, where the keys are the elements of the array,
and the values of the hash are all 1s. Then, a simple hash lookup
can replace the <hl>grep</hl>.

<code lang="perl">
use Data::Dumper qw(Dumper);

my @names = qw(Foo Bar Baz);
my %is_invited = map {$_ => 1} @names;

my $visitor = <STDIN>;
chomp $visitor;

if ($is_invited{$visitor}) {
   print "The visitor $visitor was invited\n";
}

print Dumper \%is_invited;
</code>

This is the output of the <hl>Dumper</hl> call:

<code lang="perl">
$VAR1 = {
          'Bar' => 1,
          'Baz' => 1,
          'Foo' => 1
        };
</code>

In this code we don't really care about the values of the hash elements,
other than the fact that they should evaluate to true in Perl.

This solution is only interesting if you do repeated look-ups on a large
set of values. (The exact meaning of "large" might depend on the system.)
Otherwise <hl>any</hl> or even <hl>grep</hl> will do it.

As you can see, in this example, for every element in the original array,
<hl>map</hl> returned two values. The original value and 1.

<code lang="perl">
my @names = qw(Foo Bar Baz);
my @invited = map {$_ => 1} @names;
print "@invited\n"
</code>

will print:

<code>
Foo 1 Bar 1 Baz 1
</code>


<h2>Fat arrow</h2>

In case you are wondering <hl>=></hl> is called the <b>fat arrow</b> or <b>fat comma</b>. It basically acts
as a regular comma <hl>,</hl> with an exception that is not relevant in our case. (There is a description
of it in the article about <a href="/perl-hashes">Perl hashes</a>.)


<h2>Complex expressions in map</h2>

You can put more complex expressions with map:

<code lang="perl">
my @names = qw(Foo Bar Baz);
my @invited = map { $_ =~ /^F/ ? ($_ => 1) : () } @names;
print "@invited\n"
</code>

Will print

<code>
Foo 1
</code>

Inside the block we have a ternary operator that returns either
a pair like earlier or an empty list. Apparently we only want
to let people in if their name starts with an F.

<code lang="perl">
$_ =~ /^F/ ? ($_ => 1) : ()
</code>

<h2>Schwartzian transform</h2>

One of the many uses of <hl>map</hl> is in the 
<a href="/how-to-sort-faster-in-perl">Schwartzian transform</a>
improving the speed of <hl>sort</hl> in certain cases.


<h2>perldoc</h2>

For further explanation with a couple of strange cases,
check out <a href="http://perldoc.perl.org/functions/map.html">perldoc -f map</a>.

