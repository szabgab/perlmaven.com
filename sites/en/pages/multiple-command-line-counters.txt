=title Multiple command line counters with plain TSV text file back-end
=timestamp 2015-09-30T15:30:01
=indexes split, \t, CSV, TSV
=status show
=books counter
=author szabgab
=archive 1
=comments_disqus_enable 0

=abstract start

As part of the <a href="https://code-maven.com/counter">big counter example</a> project, this example runs on the command line and uses a plain text file as back-end 'database'.
It is the second most basic version of all the <a href="https://code-maven.com/counter">counter examples</a>, that provides multiple counters.
(The most simple one provided a <a href="/command-line-counter">single counter</a>.
It accepts a value (a word) on the command line and uses a separate counter for each such value.

=abstract end

<h2>Front-end</h2>

The front end is the command line. We just run the script as <hl>perl counters.pl foo</hl> providing the name of the counter on the command line.

<h2>Back-end</h2>

In this example the "database" is going to be a simple file called 'counters.txt' with several lines in it. Every line represents one counter.
In every line we save the "name of the counter" and the most recent number separated by a <hl>TAB</hl> character.

This can of file is called a TSV which stands for Tab Separated Values. It is a special case of what people usually refer to as CSV file
(though CSV really stands for Comma Separated Values). The idea behind these files is that we have many rows of data and in each row
we have multiple fields. Just like in a spreadsheet (e.g. Microsoft Excel). The lines are separated by newlines, the fields are separted
by some character. In a CSV file this is a comman. In a TSV file this separator character is a TAB, but in the general case any character
can be used a separator as long as both who writes the file and who reads the file agree on the separator character.

In the generic case a CSV file can contain even newslines inside its lines which makes parsing it quite complex. That's
why for th generic case there are specilalized module that can <a href="/search/Text::CSV">read and write CSV files</a>,
but in our case we can assume that the name received on the command line does not contain a TAB and this we can get-by
with the use of <a href="/perl-split">split</a>.

<h2>Code</h2>


The multiple counter example is a lot more complex.

In the memory we need to keep a mapping from "counter name" to "counter value. We can use a hash where each key is a "counter name" and
each value is the respective "counter value", but we will need to map this to the 'database' file and then we will have to be able to read
the data back from the file and fill the hash.

For the database file we are going to use the rather simple TSV format explained above.
We just need to assume that the character which separates the fields (TAB in our case), cannot be part of any of the values.

<include file="examples/counters.pl">

Let's then see the implementation. After declaring the <hl>$file</hl> variable that contains the name of the storage file, we retreive the value
from the command line and complain (throw an exception using <hl>die</hl>) if the user has forgotten to pass a value.
<a href="/argv-in-perl">@ARGV</a> contains the list of values on the command line (and it does not include the script itself).


<code lang="perl">
my ($name) = @ARGV;
</code>

copies the first value from <hl>@ARGV</hl> to <hl>$name</hl>. Please make sure you include the parentheses around the <hl>$name</hl> variable.
Without those the assignment will be in <a href="/scalar-and-list-context-in-perl">scalar context</a> and Perl will assign the length of the <hl>@ARGV</hl> array to
the <hl>$name</hl> variable.

The next expression:

<code lang="perl">
die "Usage: $0 NAME\n" if not $name;
</code>

will throw an excpetion if the user has not supplied any value on the command line.

Then we declare the hash <hl>%counters</hl> where we are going to store our counters in memory.

Unlike in the <a href="/command-line-counter">single command line counter</a> case, here we cannot assign an initial 0 to all the keys
as any string can be a key. We will have to rely on the <a href="/autovivification">autovivification</a> feature of Perl.
When we increment the value of one of the keys that did not exists before (e.g. <hl>$counters{foo}++;</hl>),
that key will just spring into existence pretending its value is 0 which is then incremented to 1.


The next step here too is checking if the 'counters.txt' file exists, but as it does not exist during the first run, we will skip that code for now
and look at the code after the closing curly brace.

There we will just increment the value of the appropriate key in the hash using the <hl>$counter{$name}++;</hl> expression.
If there already was a key <hl>$name</hl> then it is quite clear that this code will just increment that number, but what will happen
the first time we encounter the given value? And especially the first time we run the script when the hash is still empty?

That's where <a href="/autovivification">autovivification</a> playes its nice role. The key will just appear from nowhere with a value
of <hl>undef</hl>. Because in a numerical operation, such as <hl>++</hl>  <hl>undef</hl>, acts as if it was actuall 0, the autoincrement
will increase it to 1, and that will be the new value of the <hl>$name</hl> key in the <hl>%counter</hl> hash.
Then we just print the new content of <hl>$counter{$name}</hl>.

In the next few lines:

<code lang="perl">
open my $fh, '>', $file or die "Could not open '$file' for writing. $!";
foreach my $str (keys %counter) {
    print $fh "$str\t$counter{$str}\n";
}
close $fh;
</code>

we open the 'counters.txt' file for writing, then we go over all the keys of the <hl>%counter</hl> hash (that were fetches by the <hl>keys</hl> keyword)
and for each key we print the key (in the <hl>$str</hl> variable), a TAB (represented in Perl by <hl>\t</hl>) and the corresponding value of in <hl>%counter</hl>.
Finally we close the file.

After incrementing the current counter, this will save the new values back in the file.

Now that we have seen how do we save the counters.txt file, lets' go back to the <hl>if</hl> statement and see how can we read the file.

<code lang="perl">
if (-e $file) {
    open my $fh, '<', $file or die "Could not open '$file' for reading. $!";
    while (my $line = <$fh>) {
        chomp $line;
        my ($str, $count) = split /\t/, $line;
        $counter{$str} = $count;
    }
    close $fh;
}
</code>

After making sure that the file exists using the <hl>-e</hl> operator we try to <a href="/open-and-read-from-files">open the file for reading</a> and throw
an exception using <hl>die</hl> we we don't succeed.
Then we need to go over all the rows of the file which we do using the <hl>while</hl> loop. As the condition of the <hl>while</hl>
statement we can see a simple call to the readline operator of Perl. Just we have seen in the <a href="/command-line-counter">simple example</a>.
Then we do something different though. We call the <a href="/chomp">chomp</a> function to remove the trailing newline.
The <hl>$line</hl> variable holds a string followed by a TAB, followed by a number. We use <a href="/perl-split">split</a> along the TAB to cut the string into
two pieces and we assign those two pieces to <hl>$str</hl> and <hl>$count</hl> respsectively.

Then we insert the key-value pair into the <hl>%counter</hl> hash.

Finally we close the file.

Try the script:

<code>
$ perl counters.pl 
Usage: counters.pl NAME

$ perl counters.pl foo
1

$ perl counters.pl bar
1

$ perl counters.pl bar
2

$ perl counters.pl bar
3

$ perl counters.pl foo
2
</code>

<h2>sort</h2>

If you ran the script several times with several counters, you'll notice that the order of the lines in the database file always changes.
That's because we save the rows in the same order the <hl>keys</hl> function returns the key and it returns the keys in random order.
If you'd like the order to be stable then you can <hl>sort</hl> the keys and iterate over the sorted keys:

<code lang="perl">
foreach my $str (sort keys %counter) {
</code>


<h2>Memory size/processing time</h2>

This is not really an issue, but let me just mention it.
In this solution on every run of the script we load the whole 'counters.txt' into memory. If there are many counters (in the millions)
then this can be both time-consuming and it can take a lot of memory of the script. There will be other solutions
in the <a href="https://code-maven.com/counter">counter examples</a> with different back-ends that will make it easy to fecth and store a single entry.


