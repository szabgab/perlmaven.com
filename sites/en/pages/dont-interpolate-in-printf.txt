=title Don't interpolate in printf (Missing argument in sprintf at ...)
=timestamp 2014-08-10T15:30:01
=indexes printf, sprintf
=tags screencast
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

Just the other day, when I ran one of my scripts, I encountered an exception <hl>Missing argument in sprintf at ...</hl>.
The nasty thing was the line number showed one of my logging calls. I think this is one of the nastiest thing when
you have a bug in your logging or debugging statement...

Anyway, I created a smaller version of the problem. Look at the example:

=abstract end

<screencast file="dont-interpolate-in-printf" youtube="Plv9nT9RzdE" />

In the root directory of the project I had two files, a script and a module representing the current file
to be processed:

<code>
dir/
   printf_interpolate.pl
   lib/App/File.pm
</code>


The <hl>printf_interpolate.pl</hl> script looks like this:

<code lang="perl">
use strict;
use warnings FATAL => 'all';

use App::File;

my $LIMIT = 80;

my $file = read_params(@ARGV);
_log("START");
check_file($file);
_log("DONE");


sub read_params {
    my $filename = shift or die "Usage: $0 FILENAME\n";
    my $file = App::File->new($filename);
    return $file;
}

sub check_file {
    my ($file) = @_;

    open my $fh, '<', $file->filename or die;
    while (my $line = <$fh>) {
        my $actual = length $line;
        if ($actual > $LIMIT) {
            _log(sprintf "Line is too long ($actual > $LIMIT) ($line) (file %s)", $file->filename);
        }
    }
}


sub _log {
    my $str = shift;
    say $str;
}
</code>

All this example does is reading in a file line-by-line and checking if each line is shorter than
the expected line. If it the line is too long, call the <hl>_log</hl> function and report the issue.

The <hl>/lib/App/File.pm</hl> looks like this:

<code lang="perl">
package App::File;
use strict;
use warnings;

sub new {
    my ($class, $filename) = @_;
    return bless {filename => $filename}, $class;
}
sub filename {
    my ($self) = @_;
    return $self->{filename};
}

1;
</code>

It is a simple module representing the file. (In the real code it was of course much more complex, but
this is enough to show the problem.

So what if I run the script passing itself as a parameter? That is, what if we check that none of the lines
of this script is longer than 80 characters?

<code>
cd dir/
perl printf_interpolate.pl printf_interpolate.pl
</code>

After seeing the word "START" printed, we get the following exception:
<hl>Missing argument in sprintf at printf_interpolate.pl line 27, <$fh> line 27.</hl>
pointing us to the line where we call <hl>sprintf</hl>.

It took me a while to understand two things:

First of all, this was actually a <b>warning</b>, but because of the <hl>FATAL => 'all'</hl>, all the warnings were turned into fatal exceptions.

The actual problem was that the line that was too long actually had some parts that looked like sprintf place-holders. You know the <hl>%s</hl> things.
As the <hl>sprintf</hl> statement had some embedded (interpolated) variables in it (specifically the <hl>$line</hl> variable), after the interpolation
the string contains more than one <hl>%s</hl> place-holders.

Changing the line to
<code lang="perl">
  _log(sprintf "Line is too long ($actual > $LIMIT) (%s) (file %s)", $line, $file->filename);
</code>

solved the problem, as now we are inserting the <hl>%s</hl> snippets during the <hl>sprintf</hl> call and not
before it.

If we run the code now we'll see the following output:

<code>
START
Line is too long (104 > 80) (            _log(sprintf "Line is too long ($actual > $LIMIT) (%s) (file %s)", $line, $file->filename);
) (file printf_interpolate.pl)
DONE
</code>

Much better.

Of course if we have already moved one variable outside the string we should do with the other two as well, even though we can
assume they will be always numbers:

<code lang="perl">
  _log(sprintf "Line is too long (%s > %s) (%s) (file %s)", $actual, $LIMIT, $line, $file->filename);
</code>

<h2>Short example</h2>

The next snippet can already reproduce the warning:

<code lang="perl">
use strict;
use warnings;

my $name = 'Foo';
#my $smalltalk = 'how are you?';
my $smalltalk = '%s hi?';

printf "Hello %s, $smalltalk\n", $name; 
</code>

<code>
Missing argument in printf at printf_interpolate_short line 8.
Hello Foo,  hi?
</code>

<h2>Bottom line</h2>

If you use <hl>printf</hl>, or <hl>sprintf</hl> don't embed variables that might contain characters that have special meaning
for <hl>sprintf</hl>.





