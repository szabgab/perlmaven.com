=title Import on demand
=timestamp 2017-02-21T20:00:11
=indexes @EXPORT, @EXPORT_OK
=tags screencast
=books advanced
=status show
=author szabgab
=comments_disqus_enable 1

=abstract start

<a href="/import">We saw</a> that if we have a module and we would like to export some of the functions
we can use the <a href="https://metacpan.org/pod/Exporter">Exporter</a> module with
the <hl>import</hl> function and we put the list of functions to be exported in the
<hl>@EXPORT</hl> array.

=abstract end

<slidecast file="/media/videos/advanced-perl/libraries-and-modules/on-demand-import" />

As the number of functions we export grows, this soon can get out of hand. Especially
for people who do not 
<a href="/restrict-the-import">restrict the import by listing the functions to be imported</a>.
People who just write <hl>use Module::Name;</hl> are going to receive a growing number
of function in their namespace. Many of those functions they will probably never use and they
just pose a risk of colliding with functions from other sources.

For the module author at first it might have seemed to be a good idea to export everything.
After all that makes it much easier to import everything, but later on it turned out that it
would be probably better to export by default only the most commonly used functions and
have a separate list of functions that the users can opt-in to import.

For that the <hl>import</hl> function of the <a href="https://metacpan.org/pod/Exporter">Exporter</a> module
will look at two arrays. The <hl>@EXPORT</hl> array holds the list of functions that are imported
by default and the <hl>@EXPORT_OK</hl> array holds the list of functions that can also be imported optionally.

So when the use write <hl>use Module::Name;</hl> without providing a list of functions, then everything listed
in <hl>@EXPORT</hl> will be imported but nothing else.

Alternatively, the user can specify which functions to import by listing them after the module name:
<hl>use Module::Name qw(this that);</hl> and then exactly those functions will be imported. Each one of
the requested function names can come from either the <hl>@EXPORT</hl> array or the <hl>@EXPORT_OK</hl> array.

So given the B::Calc module in the <b>B/Calc.pm</b> file:

<code lang="perl">
package B::Calc;
use strict;
use warnings;

use Exporter qw(import);

our @EXPORT = qw(add);
our @EXPORT_OK = qw(multiply);

my $base = 10;

sub add {
    validate_parameters(@_);

    my $total = 0;
    $total += $_ for (@_);
    return $total;
}

sub multiply {
}

sub validate_parameters {
    die 'Not all of them are numbers'
        if  grep {/\D/} @_;
    return 1;
}


1;
</code>

The user can write

<code lang="perl">
use B::Calc;
</code>

and then only the <hl>add</hl> function will be imported.

The user can write:

<code lang="perl">
use B::Calc qw(multiply);
</code>

and then only the <hl>multiply</hl> function will be imported but not the <hl>add</hl> function.

So once you start to list the function you'd like to import, you'll have to write down every function
and you'll have to specify the exact list of functions to be imported.


<h2>Conclusion</h2>

<hl>@EXPORT</hl> is the list of functions to be imported by default.

<hl>@EXPORT_OK</hl> is the list of function to be imported optionally.

