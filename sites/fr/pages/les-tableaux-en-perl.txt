=title Les tableaux en Perl
=timestamp 2013-06-15T15:45:02
=indexes tableau, tableaux, longueur, taille, @, foreach, Data::Dumper, scalar, push, pop, shift
=status show
=original perl-arrays
=books beginner
=author szabgab
=translator oval
=comments_disqus_enable 0

=abstract start

Dans cet article du <a href="/perl-tutorial">tutoriel Perl</a>, nous allons en apprendre davantage sur les tableaux en Perl.
Ceci est un aperçu de la façon dont fonctionnent les tableaux en Perl. Nous donnerons des explications plus détaillées dans de futurs articles.

Les noms de variables des tableaux en Perl sont préfixés par le symbole <hl>@</hl> appelé dans le jargon Perl un <i>sigil</i>.
(mnémonique : <hl>@</hl> représente le premier a de <i>array</i>).

Comme nous encourageons systématiquement l'utilisation de <hl>strict</hl>,
vous devez déclarer ces variables en utilisant le mot-clé <hl>my</hl> avant la première assignation.

=abstract end

Rappelez-vous ! Tous les exemples ci-dessous supposent que votre fichier commence par :

<code lang="perl">
use strict;
use warnings;
use 5.010;
</code>

<h2>Initialisation d'un tableau Perl</h2>

Déclarer un tableau tout simplement :

<code lang="perl">
my @noms;
</code>

Déclarer un tableau tout en lui assignant des valeurs :

<code lang="perl">
my @noms = ("Foo", "Bar", "Baz");
</code>

<h2>Débogage d'un tableau Perl</h2>

<code lang="perl">
use Data::Dumper qw(Dumper);

my @noms = ("Foo", "Bar", "Baz");

say Dumper \@noms;
</code>

Le résultat est le suivant :

<code>
$VAR1 = [
    'Foo',
    'Bar',
    'Baz'
];
</code>

<h2>Le mot-clé foreach pour parcourir un tableau Perl</h2>

<code lang="perl">
my @noms = ("Foo", "Bar", "Baz");
foreach my $n (@noms) {
  say $n;
}
</code>

Le résultat est le suivant :

<code>
Foo
Bar
Baz
</code>

<h2>Accéder à un des éléments d'un tableau Perl</h2>

<code lang="perl">
my @noms = ("Foo", "Bar", "Baz");
say $noms[0];
</code>

Attention ! Lorsque vous accédez à un seul élément d'un tableau, le <i>sigil</i> change de <hl>@</hl> à <hl>$</hl>.
Cela pourrait causer de la confusion pour certaines personnes, mais si vous y réfléchissez deux minutes, il est assez facile de comprendre pourquoi.

<hl>@</hl> marque le pluriel et <hl>$</hl> le singulier.
Lorsque vous accédez à un seul élément d'un tableau, celui-ci se comporte comme une variable scalaire normale.

<h2>Les indices dans un tableau Perl</h2>

Les indices d'un tableau commencent à partir de 0.
Le plus grand indice est toujours dans la variable notée <hl>&lt;$#nom_du_tableau></hl>.

<code lang="perl">
my @noms = ("Foo", "Bar", "Baz");
say $#noms;
</code>

Le résultat est le suivant :

<code>
2
</code>

car les indices sont 0, 1 et 2.

<h2>Longueur ou taille d'un tableau Perl</h2>

En Perl il n'ya pas de fonction spéciale pour aller chercher la taille d'un tableau,
mais il y a plusieurs façons d'obtenir cette valeur.

D'une part, la taille d'un tableau est le plus grand indice augmenté de 1.
Dans l'exemple ci-dessus, <hl>$#noms+1</hl> est la <b>taille</b> ou la <b>longueur</b> du tableau.

D'autre part, la fonction prédéfinie <hl>scalar</hl> peut être utilisée pour obtenir la taille d'un tableau :

<code lang="perl">
my @noms = ("Foo", "Bar", "Baz");
say scalar @noms;
</code>

Le résultat est le suivant :

<code>
3
</code>

La fonction fonction prédéfinie <hl>scalar</hl> est une sorte de fonction de conversion qui
- entre autres choses - convertit un tableau en un scalaire.
En raison d'une décision arbitraire, mais intelligente, cette conversion donne la taille du tableau.

<h2>Une boucle basée les indices d'un tableau</h2>

Il y a des cas où une boucle sur les valeurs d'un tableau n'est pas suffisant.
Nous pourrions avoir besoin à la fois de la valeur et de l'indice de cette valeur.
Dans ce cas, nous avons besoin de faire une boucle sur les indices, et nous obtenons les valeurs en utilisant les indices.

<code lang="perl">
my @noms = ("Foo", "Bar", "Baz");
foreach my $i (0 .. $#noms) {
  say "$i - $noms[$i]";
}
</code>

Le résultat est le suivant :

<code>
0 - Foo
1 - Bar
2 - Baz
</code>

<h2>La fonction prédéfinie push appliquée sur un tableau Perl</h2>

La fonction prédéfinie <hl>push</hl> ajoute une nouvelle valeur à la fin du tableau qui donc grandit.

<code lang="perl">
my @noms = ("Foo", "Bar", "Baz");

push @noms, 'Moo';
say Dumper \@names;
</code>

Le résultat est le suivant :

<code>
$VAR1 = [
    'Foo',
    'Bar',
    'Baz',
    'Moo'
];
</code>

<h2>La fonction prédéfinie pop appliquée sur un tableau Perl</h2>

La fonction prédéfinie <hl>pop</hl> récupère le dernier élément du tableau.

<code lang="perl">
my @noms = ("Foo", "Bar", "Baz");
my $dernier_element = pop @noms;

say "Dernier élément : $dernier_element";
say Dumper \@noms;
</code>

Le résultat est le suivant :

<code>
Last: Baz
$VAR1 = [
    'Foo',
    'Bar',
];
</code>

<h2>La fonction prédéfinie shift appliquée sur un tableau Perl</h2>

La fonction prédéfinie <hl>shift</hl> :
<ul>
<li>retourne l'élément le plus à gauche d'un tableau (càd le premier élément)</li>
<li>et déplace tous les autres éléments vers la gauche</li>
</ul>

<code lang="perl">
my @noms = ("Foo", "Bar", "Baz");
my $element_le_plus_a_gauche = shift @noms;

say "Premier élément : $element_le_plus_a_gauche";
say Dumper \@noms;
</code>

Le résultat est le suivant :

<code>
Premier élément : Foo
$VAR1 = [
    'Bar',
    'Baz',
];
</code>
