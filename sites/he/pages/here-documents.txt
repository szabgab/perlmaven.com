=title Here documents, או כיצד ליצור מחרוזות מרובות שורות בפרל
=timestamp 2013-07-07T07:01:01
=indexes <<, /m, /g, q, qq
=status show
=original here-documents
=books beginner
=author szabgab
=translator bruck
=comments_disqus_enable 0

=abstract start

לעתים יש צורך ליצור מחרזות שמתפרסת על מספר שורות.
כפי שמקובל בפרל, יש לכך מספר פתרונות.
אחד הפתרונות הנפוצים הוא שימוש במבנה שנקרא here-document.

=abstract end

 <b>here-document</b> מאפשר לך ליצור מחרוזת שמתפרסת  על <b>מספר שורות</b> ושומרת על הרווחים ועל סופי השורות. אם תריץ את הקוד הבא הוא ידפיס בדיוק את מה שאתה רואה החל מהמילה Dear עד השורה לפני ההופעה השניה של END_MESSAGE.

<h2> here document ללא אינטרפולציה</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $name = 'Foo';

my $message = <<'END_MESSAGE';
Dear $name,

this is a message I plan to send to you.

regards
  the Perl Maven
END_MESSAGE

print $message;
</code>

פלט:

<code>
Dear $name,

this is a message I plan to send to you.

regards
  the Perl Maven
</code>

מחרוזת  here document עם שני סימני 'קטן מ-' <hl>&lt;&lt;</hl> ומיד אחריהם מחרוזת שרירותית שהופכת לסימן הסיום של ה-here-document,אחריה יש נקודה פסיק <hl>;</hl> שמסמן את סוף הפקודה.
זה קצת מוזר כיוון שלמעשה היא אינה מסתיימת כאן.  התוכן של מחרוזת ה-here-document מתחיל רק שורה אחרי הנקודה-פסיק, (במקרה שלנו עם המילה "Dear"), וממשיך עד שפרל מוצאת את סימן סיום הפקודה השרירותי שנבחר. במקרה שלנו המחרוזת <b>END_MESSAGE</b>.

אם כבר ראית שימוש של here-document בקוד אז ייתכן שאתה מופתע מהשימוש בגרש בודד מסביב ל-<b>END_MESSAGE</b> הראשון. אני סבור שאם תמצא דוגמאות של here-document באינטרנט, או מאחורי חומות האש של מקום העבודה שלך, קרוב לוודאי שתמצא את החלק הפותח ללא גרשיים כלשהם. כך:

<code lang="perl">
my $message = <<END_MESSAGE;
...
END_MESSAGE
</code>

כשכותבים כך זה אותו דבר כמו לכתוב את END_MESSAGE בגרשיים כפולים כמו בדוגמה הבאה,
אך צורת כתיבה זו נחשבת <b>מיושנת</b> והיא תוצא משימוש בגרסה 5.20. אין להשתמש בצורה זו!
אין להשתמש במחרוזות here document ללא גרשיים מסביב להגדרת מחרזות הסיום.:

<code lang="perl">
my $message = <<"END_MESSAGE";
...
END_MESSAGE
</code>

אם אתה כבר מכיר את ההבדל בין
<a href="/quoted-interpolated-and-escaped-strings-in-perl">גרשיים בודדים וגרשיים כפולים quotes</a>
בפרל, אז לא תהיה זו הפתעה לגלות שב-here-document הם מתנהגים באותה צורה.
ההבדל היחיד הוא שהגרשיים הם מסביב סימן הסיום ולא סביב המחרוזת עצמה. אם אין גרשיים, פרל מניחה כברירת מחדל שהתתנהגות היא של גרשיים כפולות.

אם תחזור ותסתכל בדוגמה הראשונה, ודאי תשים לב שהיה לנו את <hl>$name</hl> כחלק מה- here-document
והוא גם נותר כחלק מהפלט. הסיבה לכך היא שפרל לא ניסתה מלא את המקום עם תוכנו של המשתנה
 <hl>$name</hl> . אפילו לא היינו צריכים להגדיר את המשתנה בקוד. אפשר לנסות להריץ את הקוד אפילו בלי השורה<hl>my $name = 'Foo';</hl> . 

<h2>אינטרפולציה של here document</h2>

בדוגמה הבאה  נשים את סימן הסיום בגרשיים כפולות ולכן תתבצע אינטפולציה של המשתנה
 <hl>$name</hl> :

<code lang="perl">
use strict;
use warnings;

my $name = 'Foo';
my $message = <<"END_MSG";
Hello $name,

how are you?
END_MSG

print $message;
</code>

התוצאת של הרצת הקוד היא:

<code>
Hello Foo,

how are you?
</code>

<h2>אזהרה: יש לדייק במחרוזת הסיום</h2>

רק עוד הערה. יש לוודא שמחרוזת הסיום מועתקת 
<b>במדויק</b> כפי שהיא נכתבה בהתחלה. ללא רווחים לפניה או אחריה.
אחרת פרל לא תזהה אותה ותחשוב שה-here-document עוד לא הסתיים.
המשמעות של זה היא שאי אפשר לכתוב את מחרוזת הסיום באינדנטציה שמתאימה לשאר הקוד.
או אולי בכל זאת אפשר?

<h2>מחרוזות here document ואינדנציה של קוד</h2>

אם ה-here-document צריך להיות במקום שבו בדרך כלל נוסיף רווחי אינדנטציה לקוד, אז יש לנו שתי בעיות:


<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $name = 'Foo';
my $send = 1;

if ($send) {
    my $message = <<"END_MESSAGE";
        Dear $name,
    
        this is a message I plan to send to you.
    
        regards
          the Perl Maven
END_MESSAGE
    print $message;
}
</code>

בעיה אחת היא הבעיה שהזכרתי קודם, מחרוזת הסיום חייבת להיות זהה למחרוזת בתחילת ה-here-document, ולכן אי אפשר להוסיף רווחים לפניה. 

הבעיה השניה היא שבפלט יופיעו כל הרווחים הנוספים בתחילת כל שורה.

<code>
        Dear Foo,
    
        this is a message I plan to send to you.
    
        regards
          the Perl Maven
</code>

ניתן לפתור את הבעיה הראשונה, רווחים במחרוזת הסיום, על ידי שימוש במחרוזת שכבר כוללת מספיק רווחים בתחילתה: (אני משתמש כאן ב-4 רווחים אמיתיים, כיוון שטאבים לא נראים טוב במאמר, אך הם עובדים בקוד אמיתי): אם אתה מחסידי האינדטציה על ידי טאבים).

<code lang="perl">
    my $message = <<"    END_MESSAGE";
       ...
    END_MESSAGE
</code>

ניתן להסיר את הרווחים הנוספים בטקסט עצמו על ידי שימוש בחילוף בעת ההצבה.

<code lang="perl">
    (my $message = <<"    END_MESSAGE") =~ s/^ {8}//gm; 
        ...
    END_MESSAGE
</code>

בחילוף אנחנו מחליפים 8 רווחים מקדימים במחרוזת הריקה. אנחנו משתמשים בשני מגדירים:
<hl>/m</hl> משנה את ההתנהגות של <hl>^</hl> כך שבמקום למצוא התאמה <b>בתחילת המחרוזת</b>, תימצא התאמה</b>
<b>בתחילת השורה</b>.  <hl>/g</hl> אומר להחלפה לפעול <b>גלובאלית</b>,
כלומר לחזור על ההחלפה כמה פעמים שאפשר.

ביחד הם גורמים חילוף למחוק 8 רווחים מקדימים מכל שורה שמשתנה בצד שמאל של 
 <hl>=~</hl>.
בצד שמאל שמנו את ההצבה בסוגריים כיוון שהקדימות של סימן ההצבה
 (<hl>=</hl>) היא גבוהה יותר מהקדימות של הביטוי הרגולרי <hl>=~</hl>. בלי הסוגריים, פרל קודם תציב את התוכן של ה-here-document במשתנה <hl>$message</hl>
ואז היא תנסה להריץ את החילוף על ה-here-document עצמו, מה שיגרום לשגיאת קומפילציה:

Can't modify scalar in substitution (s///) at programming.pl line 9, near "s/^ {8}//gm;"

<h2>שימוש ב- q או ב- qq </h2>

לאחר כל ההסבר הזה, אני לא בטוח שאני באמת רוצה להמליץ על שימוש ב-here-documents.
במקרים רבים אני מעדיף להשתמש ב- <hl>qq</hl> או ב- <hl>q</hl> operator.
תלוי אם אני רוצה מחרוזת עם אינטרפולציה או בלי אינטרפולציה:

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $name = 'Foo';
my $send = 1;

if ($send) {
    (my $message = qq{
        Dear $name,
    
        this is a message I plan to send to you.
    
        regards
          the Perl Maven
        }) =~ s/^ {8}//mg;
    print $message;
}
</code>

