=title Scalar found where operator expected
=timestamp 2013-07-25T13:00:03
=indexes syntax error, scalar found, operator expected
=status show
=original scalar-found-where-operator-expected
=books beginner
=author szabgab
=translator spidamoo
=comments_disqus_enable 1

=abstract start

Это сообщение об ошибке встречается действительно часто. Однако понять его может быть довольно
сложно.

Дело в том, что люди знают о <b>числовых операторах</b> и <b>строковых операторах</b>, но не
рассматривают запятую (<hl>,</hl>) как оператор. Терминология сообщения об ошибке немного вводит их
в заблуждение.

Давайте рассмотрим пару примеров:

=abstract end

<h2>Пропущенная запятая</h2>

Код выглядит так:

<code lang="perl">
use strict;
use warnings;

print 42 "\n";
my $name = "Foo";
</code>

а сообщение об ошибке

<code>
String found where operator expected at ex.pl line 4, near "42 "\n""
      (Missing operator before  "\n"?)
syntax error at ex.pl line 4, near "42 "\n""
Execution of ex.pl aborted due to compilation errors.
</code>

Оно четко указывает местонахождение проблемы, но, как показывает опыт, многие люди торопятся открыть
свой редактор и исправить ошибку, толком не прочитав сообщение. Они вносят изменения в надежде, что
этим решат проблему, а в итоге получают еще одно сообщение об ошибке.

В данном случае проблема была в том, что мы забыли запятую (<hl>,</hl>) после числа 42. Эта строчка
должна выглядеть так: <hl>print 42, "\n";</hl>.


<h2>String found where operator expected</h2>

В этом коде мы пропустили оператор конкатенации <hl>.</hl> и получили такое же сообщение об ошибке:

<code lang="perl">
use strict;
use warnings;

my $name = "Foo"  "Bar";
</code>

<code>
String found where operator expected at ex.pl line 4, near ""Foo"  "Bar""
      (Missing operator before   "Bar"?)
syntax error at ex.pl line 54, near ""Foo"  "Bar""
Execution of ex.pl aborted due to compilation errors.
</code>

На самом деле код должен был выглядеть так: <hl>my $name = "Foo" . "Bar";</hl>.

<h2>Number found where operator expected</h2>

Этот код
<code lang="perl">
use strict;
use warnings;

my $x = 23;
my $z =  $x 19;
</code>

выдает такую ошибку:

<code>
Number found where operator expected at ex.pl line 5, near "$x 19"
  (Missing operator before 19?)
syntax error at ex.pl line 5, near "$x 19"
Execution of ex.pl aborted due to compilation errors.
</code>

Здесь, видимо, не хватает оператора сложения <hl>+</hl> или умножения <hl>*</hl>, ну или оператора
повтора <hl>x</hl>.

<h2>Ошибка синтаксиса, когда не хватает запятой</h2>

Отсутствующая запятая не всегда понимается интерпретатором, как пропущенный оператор. Например, этот
код:

<code lang="perl">
use strict;
use warnings;

my %h = (
  foo => 23
  bar => 19
);
</code>

Генерирует такую ошибку: <b>syntax error at ... line ..., near "bar"</b>, без дополнительных деталей.

Добавление запятой после числа 23 исправит код:

<code lang="perl">
my %h = (
  foo => 23,
  bar => 19
);
</code>

Я предпочитаю добавлять запятую после каждой пары в хэше (то есть, в данном случае, после 19):

<code lang="perl">
my %h = (
  foo => 23,
  bar => 19,
);
</code>

Эта привычка в большинстве случаев помогает избежать таких ошибок.


<h2>Scalar found where operator expected at</h2>

<code lang="perl">
use strict;
use warnings;

my $x = 23;
my $y = 19;

my $z =  $x $y;
</code>

<code>
Scalar found where operator expected at ... line 7, near "$x $y"
    (Missing operator before $y?)
syntax error at ... line 7, near "$x $y"
Execution of ... aborted due to compilation errors.
</code>

Опять же, между $x и $y может быть числовой или строковый оператор.

<h2>Array found where operator expected</h2>

<code lang="perl">
use strict;
use warnings;

my @x = (23);
my $z =  3 @x;
</code>

<h2>С какими еще случаями вы часто сталкиваетесь?</h2>

Знаете ли вы другие интересные случаи, в которых мы получаем такую синтаксическую ошибку?


