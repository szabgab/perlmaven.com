=title Barewords - "голые" слова в Perl
=timestamp 2013-07-25T13:00:01
=indexes bareword, strict
=status show
=original barewords-in-perl
=books beginner
=author szabgab
=translator spidamoo
=comments_disqus_enable 0

=abstract start

<hl>use strict</hl> состоит из трех частей. Одна из них ( <hl>use strict "subs"</hl>) запрещает
неправильное использование <b>barewords</b>.

Что это значит?

=abstract end

Без этого ограничения такой код бы сработал и выдал "hello".

<code lang="perl">
my $x = hello;
print "$x\n";    # hello
</code>

Это само по себе странно, потому что мы привыкли помещать строки в кавычки, но Perl по умолчанию
позволяет использовать "голые слова" (<b>barewords</b>) - слова без кавычек - в качестве строк.

Поэтому код из примера выше выдаст "hello".

Ну, по крайней мере пока кто-нибудь не добавит подпрограмму "hello" в начало вашего скрипта:

<code lang="perl">
sub hello {
  return "zzz";
}

my $x = hello;
print "$x\n";    # zzz
</code>

Да. В этой версии perl видит подпрограмму hello(), вызывает ее и присваивает возвращаемое ей 
значение переменной $x.

Однако, если кто-то переместит эту подпрограмму в конец файла, после присвоения, perl внезапно
перестанет видеть ее во время присвоения, и мы снова получаем "hello" в $x.

Нет, вы не хотите попасть в такую неразбериху в какой-то момент. А возможно, никогда. Добавив в свой
код <hl>use strict</hl>, вы укажете perl'у, что bareword <b>"hello"</b> в вашем коде недопустим, и
избежите этой путаницы.

<code lang="perl">
use strict;

my $x = hello;
print "$x\n";
</code>

Получим следуюшую ошибку:

<code>
Bareword "hello" not allowed while "strict subs" in use at script.pl line 3.
Execution of script.pl aborted due to compilation errors.
</code>

<h2>Правильное использование bareword'ов</h2>

Существуют другие места, где bareword'ы могут использоваться, даже когда применено 
<hl>use strict "subs"</hl>.

В первую очередь, имена подпрограмм, которые мы создаем, по сути являются bareword'ами.
Допустим.

Кроме того, когда мы указываем элемент хэша, мы можем поставить bareword в фигурных скобках, и слова
по левую сторону толстой стрелки (=>) тоже могут обходиться без кавычек:

<code lang="perl">
use strict;
use warnings;

my %h = ( name => 'Foo' );

print $h{name}, "\n";
</code>

В обоих случаях "name" - это bareword, но такое использование допустимо даже при включенном strict.


