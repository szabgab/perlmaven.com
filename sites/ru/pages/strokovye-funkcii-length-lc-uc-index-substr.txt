=title Строковые функции: length, lc, uc, index, substr
=timestamp 2013-10-15T12:00:01
=indexes length, lc, uc, index, substr, scalar
=status show
=original string-functions-length-lc-uc-index-substr
=books beginner
=author szabgab
=translator spidamoo
=comments_disqus_enable 0

=abstract start

В этой части <a href="/perl-tutorial">Учебника Perl</a> мы узнаем о некоторых функциях Perl,
предназначенных для работы со строками.

=abstract end

<h2>lc, uc, length</h2>

Существует несколько простых функций, таких как <b>lc</b> и <b>uc</b>, которые возвращают строку
соответственно в нижнем и в верхнем регистре. Еще есть функция <b>length</b>, возвращающая 
количество символов в данной строке.

Взгляните на пример:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $str = 'HeLlo';

say lc $str;      # hello
say uc $str;      # HELLO
say length $str;  # 5
</code>


<h2>index</h2>

Еще одна функция - <b>index</b>. Она принимает две строки и возвращает положение второй строки 
внутри первой.

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $str = "The black cat jumped from the green tree";

say index $str, 'cat';             # 10
say index $str, 'dog';             # -1
say index $str, "The";             # 0
say index $str, "the";             # 26
</code>

Первый вызов <hl>index</hl> вернул 10, потому что строка "cat" начинается на индекс 10. Второй 
вызов <hl>index</hl> вернул -1, указывая на то, что в этом предложении нет "dog".

Третий вызов показывает, что <hl>index</hl> возвращает 0, когда вторая строка стоит в начале первой.

Четвертый пример показывает, что <hl>index</hl> ищет точное совпадение, так что регистр влияет на ее
работу. Поэтому "the" и "The" - не одно и то же.

<hl>index()</hl> ищет строки, а не слова, так что строку "e " тоже можно найти:

<code lang="perl">
say index $str, "e ";              # 2
</code>

<hl>index()</hl> также может принимать третий параметр, указывающий на место, откуда начать поиск.
Так что, раз мы нашли "e " сначала на 2-м символе первой строки, можно попытаться поискать, начиная
с 3-его, чтобы узнать, не встречается ли в этой строке еще "e ":

<code lang="perl">
say index $str, "e ";              # 2
say index $str, "e ", 3;           # 28
say index $str, "e", 3;            # 18
</code>

Поиск "e" без пробела даст другой результат.

Наконец, существует еще одна функция под названием <b>rindex</b> (right index, правый индекс), 
которая начинает поиск с правого конца строки:

<code lang="perl">
say rindex $str, "e";              # 39
say rindex $str, "e", 38;          # 38
say rindex $str, "e", 37;          # 33
</code>

<h2>substr</h2>

Наверное, самая интересная функция в этой статье это <hl>substr</hl>. Это, по сути, 
противоположность index(). Если index() говорит нам, <b>где расположена данная строка</b>, substr 
возвращает <b>подстроку, расположенную в данном месте</b>. Как правило, <hl>substr</hl> принимает 3
параметра. Первый - это строка. Второй - это положение (начиная с нуля), также называемое 
<b>смещение</b>, и третий - <b>длина</b> строки, которую мы хотим получить.

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $str = "The black cat climbed the green tree";

say substr $str, 4, 5;                      # black
</code>

substr начинает отсчет с нуля, так что символ со смещением 4 это буква b.

<code lang="perl">
say substr $str, 4, -11;                    # black cat climbed the
</code>

Третий параметр (длина) может быть и отрицательным числом. В этом случае он определяет количество
символов с правого конца строки, которые НЕ будут включены. Таким образом, предыдущий код значит:
отсчитать 4 символа от начала, 11 от конца, и вернуть то, что между ними.

<code lang="perl">
say substr $str, 14;                        # climbed the green tree
</code>

Также можно опустить 3-й параметр (длину), что будет значить: вернуть все символы начиная от 14 и до
конца строки.

<code lang="perl">
say substr $str, -4;                        # tree
say substr $str, -4, 2;                     # tr
</code>

Мы можем использовать отрицательное число в качестве смещения, что будет значить: отсчитать 4 
символа с конца и начать оттуда. То есть то же самое, что установить смещение 
<hl>length($str)-4</hl>.

<h2>Замена части строки.</h2>

Последний пример немного стремный.
Пока что <hl>substr</hl> всегда возвращала подстроку и оставляла исходную строку нетронутой. В этом
примере возвращаемое значение будет определяться так же, но вдобавок будет изменено содержимое 
исходной строки!

Возвращаемое значение <hl>substr()</hl> всегда определяется первыми тремя параметрами, но в данном
случае мы передаем еще и четвертый. Это строка, которая заменит подстроку в исходной строке.

<code lang="perl">
my $z = substr $str, 14, 7, "jumped from";
say $z;                                                     # climbed
say $str;                  # The black cat jumped from the green tree
</code>

Итак, <hl>substr $str, 14, 7, "jumped from"</hl> возвращает слово<b>climbed</b>, но, поскольку мы
передали четвертый параметр, исходная строка была изменена.

