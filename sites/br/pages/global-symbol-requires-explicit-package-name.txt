=title Global symbol requires explicit package name
=timestamp 2014-09-15T08:53:00
=indexes strict, my, package, global symbol
=status show
=original global-symbol-requires-explicit-package-name
=books beginner
=author szabgab
=translator leprevost
=comments_disqus_enable 1

=abstract start

<b>Global symbol requires explicit package name</b>  é uma mensagem bastante comum, e na minha opinião, bastante enganadora. Pelo menos para os iniciantes.

Uma rápida tradução seria algo do tipo “Você precisa declarar a variável utilizando <b>my</b>.

=abstract end

<h2>O exemplo mais simples</h2>

<code lang="perl">
use strict;
use warnings;

$x = 42;
</code>

E o erro é

<code>
Global symbol "$x" requires explicit package name at ...
</code>

Enquanto que a mensagem de erro está correta, é de pouca utilidade para o programador iniciante em Perl.
Muito provavelmente ele ainda não aprendeu sobre o uso de pacotes.
Ou então não saiba ainda o que pode ser mais explícito do que $x.

Este erro é gerado pelo uso do pragma <b>use strict</b>

A explicação encontrada na documentação é a seguinte:

<i>
Isso gera um erro em tempo de compilação se você tentar acessar uma variável que não fora declarada através do comando “our” ou “use vars”, localizado via “my()”, ou então, não qualificada inteiramente.
</i>

É de se esperar que um iniciante comece todos os seus scripts com <b>use strict</b>, e dessa forma irá provavelmente aprender sobre <b>my</b> muito antes de qualquer outra possibilidade.

Eu não sei se o texto atual pode, ou então deve, ser modificado na documentação, este não é o ponto deste artigo. O objetivo é auxiliar iniciantes a entender em sua linguagem, o que a mensagem significa.

Para eliminar o erro acima você precisa escrever :

<code lang="perl">
use strict;
use warnings;

my $x = 42;
</code>

Ou seja, você precisa <b>declarar a variável antes de utilizá-la pela primeira vez</b>.

<h2>A má solução</h2>

A outra “solução” seria remover o <b>strict</b>:

<code lang="perl">
#use strict;
use warnings;

$x = 23;
</code>

isso funcionaria também, porém o código acima geraria a seguinte mensagem de aviso:
<a href="/name-used-only-once-possible-typo">Name "main::x" used only once: possible typo at ...</a>.

De qualquer forma, normalmente você não dirigiria um carro sem cintos de segurança, certo?

<h2>Exemplo 2: escopo</h2>

Outra situação que normalmente vejo iniciantes fazendo se parece com o seguinte:

<code lang="perl">
use strict;
use warnings;

my $x = 1;

if ($x) {
my $y = 2;
}

print $y;
</code>

O erro que obtemos é o mesmo que o acima:

<code>
Global symbol "$y" requires explicit package name at ...
</code>

o que gera surpresa em muitas pessoas, especialmente quando elas estão começando a programar.
Afinal de contas, a variável <hl>$y</hl> foi declarada utilizando <hl>my</hl>.

Primeiro, há um pequeno problema visual.  A indentação da linha <hl>my $y = 2;</hl> está faltando.
Se fosse indentado alguns espaços à direita, como no próximo exemplo, a fonte do problema ficaria mais óbvia.

<code lang="perl">
use strict;
use warnings;

my $x = 1;

if ($x) {
    my $y = 2;
}

print $y;
</code>

O problema é que a variável <hl>$y</hl> está declarada dentro de um bloco (aquele par de chaves) o que significa que a variável não existe fora desse bloco. Isso é chamado de <a href="/escopo-das-variaveis-em-perl"><b>escopo</b> de uma variável</a>.

A ideia toda do <b>escopo</b> difere um pouco entre diferentes linguagens de programação.
No Perl, um bloco cercado por chaves gera um escopo novo.
O que está declarado dentro desse bloco utilizando <hl>my</hl> não será visível fora do bloco.

A solução seria chamar a função <hl>print</hl> dentro do bloco:

<code lang="perl">
use strict;
use warnings;

my $x = 1;

if ($x) {
    my $y = 2;
    print $y;
}
</code>

ou então declarar a variável fora do bloco ( e não do lado de dentro!):

<code lang="perl">
use strict;
use warnings;

my $x = 1;
my $y;

if ($x) {
    $y = 2;
}

print $y;
</code>

Qual forma é a melhor depende da sua tarefa, estas são apenas exemplos de soluções corretas.

Obviamente, se nós esquecermos de remover o <hl>my</hl> de dentro do bloco, ou se <hl>$x</hl> for falsa, nós obteremos uma viso do tipo <a href="uso-de-valor-nao-inicializado">Use of uninitialized value</a> .

<h2>Outras formas</h2>

A explicação das funcionalidades dos comandos <hl>our</hl> e <hl>use vars</hl> foi deixado para outro tutorial.
