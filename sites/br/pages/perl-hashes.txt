=title Hashes em Perl
=timestamp 2013-05-31T01:35:06
=indexes hash, keys, value, associative, %, =>, fat arrow, fat comma
=status show
=original perl-hashes
=books beginner
=author szabgab
=translator aramisf
=comments_disqus_enable 0

=abstract start

Neste artigo do  <a href="/perl-tutorial">Perl Tutorial</a> vamos aprender
sobre <b>hashes</b>, uma das mais poderosas partes do Perl.

Por vezes chamadas de arrays associativos, dicionários ou mapeamentos; as
hashes são uma das estruturas de dados disponíveis no Perl.

=abstract end

Uma hash é um grupo não ordenado de pares chave-valor. As chaves são strings
únicas. Os valores são valores escalares. Cada valor pode ser um número, uma
string, ou uma referência. Vamos aprender sobre referências mais tarde.

Hashes, como outras variáveis do Perl, são declaradas usando a palavra
reservada <hl>my</hl>. O nome da variável é precedido pelo sinal de
porcentagem (<hl>%</hl>).

É um pequeno truque mnemônico para lhe ajudar a lembrar da estrutura
chave-valor.

Alguns pensam que as hashes são como arrays (o nome antigo 'array associativo'
também indica isso, e em algumas outras linguagens, tais como PHP, não há
diferença entre arrays e hashes.), mas existem duas principais diferenças
entre arrays e hashes. Arrays são ordenados, e você pode acessar um elemento
de um array usando seu índice numérico. Hashes não são ordenadas e o acesso a
um valor se dá por uma string, que é a chave.

Cada chave de uma hash está associada a um único <b>valor</b> e as chaves são
todas únicas dentro de uma mesma hash. Isso significa que não são permitidas
chaves repetidas. (se você quer muito ter mais de um valor por chave, você
terá que esperar um pouco, até chegarmos às referências.)

Vejamos um pouco de código:

<h2>Criando uma hash vazia</h2>

<code lang="perl">
my %color_of;
</code>

<h2>Inserir um par chave-valor em uma hash</h2>

Neste caso 'maca' é a chave e 'vermelha' é o valor associado.

<code lang="perl">
$color_of{'maca'} = 'vermelha';
</code>

Você pode usar também uma variável ao invés da chave, e então você não precisa
colocar a variável entre aspas:

<code lang="perl">
my $fruit = 'maca';
$color_of{$fruit} = 'vermelha';
</code>

Na verdade, se a chave for uma string simples, você pode deixar de colocar
aspas mesmo quando usar a string diretamente:

<code lang="perl">
$color_of{maca} = 'vermelha';
</code>

Como você pode ver acima, ao acessar um par chave-valor específico, nós usamos
o sinal <hl>$</hl> (e não o sinal <hl>%</hl>) porque estamos acessando um
valor único que é um <b>escalar</b>. A chave é colocada entre chaves ({ }).

<h2>Buscar um elemento de uma hash</h2>

Do mesmo modo como inserimos um elemento, também podemos obter o valor de um
elemento.

<code lang="perl">
print $color_of{maca};
</code>

Se a chave não existir, a hash retornará um <a
href="/undef-e-definido-em-perl">undef</a>, e se <hl>warnings</hl> estiver
habilitado, como deveria estar, então nós receberemos um aviso sobre um <a
href="/uso-de-valor-nao-inicializado">valor não iniciado</a>.

<code lang="perl">
print $color_of{laranja};
</code>

Vejamos mais alguns pares chave-valor para a hash:

<code lang="perl">
$color_of{laranja} = "laranja";
$color_of{uva} = "lilas";
</code>

<h2>Inicie a hash com valores</h2>

Nós poderíamos ter instanciado a variável com pares chave-valor passando
simultaneamente para a hash uma lista de pares chave-valor:

<code lang="perl">
my %color_of = (
    "maca"  => "vermelha",
    "laranja" => "laranja",
    "uva"  => "lilas",
);
</code>

<hl>=></hl> é chamado <b>flecha espessa</b> ou <b>vírgula espessa</b>, e é
usada para indicar pares de elementos. O primeiro nome, flecha espessa, fica
claro uma vez que olhamos a outra, flecha fina (->) usada no Perl. O nome
vírgula espessa vem do fato de que essas flechas são basicamente o mesmo que
as vírgulas. Então poderíamos ter escrito assim também:

<code lang="perl">
my %color_of = (
    "maca",  "vermelha",
    "laranja", "laranja",
    "uva",  "lilas",
);
</code>

Na realidade, a flecha espessa lhe permite abandonar as aspas do lado
esquerdo, fazendo com que o código fique mais limpo e legível.

<code lang="perl">
my %color_of = (
    maca  => "vermelha",
    laranja => "laranja",
    uva  => "lilas",
);
</code>

<h2>Atribuição em um elemento de uma hash</h2>

Vejamos o que acontece quando atribuímos outro valor para uma chave existente:

<code lang="perl">
$color_of{maca} = "green";
print $color_of{maca};     # green
</code>

A atribuição modificou o valor associado com a chave <b>maca</b>. Lembre-se,
chaves são únicas e cada chave tem um único valor.

<h2>Iterando sobre hashes</h2>

Para acessar um valor em uma hash você precisa conhecer a chave.
Quando as chaves de uma hash não são valores pré-definidos você pode usar a
função <hl>keys</hl> para obter uma lista de chaves. Então você pode iterar
sobre estas chaves:

<code lang="perl">
my @fruits = keys %color_of;
for my $fruit (@fruits) {
    print "The color of '$fruit' is $color_of{$fruit}\n";
}
</code>

Você nem precisa utilizar a variável temporária <hl>@fruits</hl>, você pode
iterar diretamente sobre os valores retornados pela função <hl>keys</hl>:

<code lang="perl">
for my $fruit (keys %color_of) {
    print "The color of '$fruit' is $color_of{$fruit}\n";
}
</code>


<h2>O tamanho de uma hash</h2>

Quando dizemos o tamanho de uma has, geralmente nos referimos ao número de
pares chave-valor. Você pode obter este valor colocando a função <hl>keys</hl>
no contexto escalar.

<code lang="perl">
print scalar keys %hash;
</code>

<h2>Obrigado</h2>

A primeira edição deste artigo foi escrita por <a
href="http://www.leprevost.com.br/">Felipe da Veiga Leprevost</a> que também
faz <a href="https://br.perlmaven.com/">traduções em Português</a> dos artigos
Perl Maven.

