=title Variável "my" mascara declaração anterior no mesmo escopo
=timestamp 2014-11-12T22:01:56
=indexes my, scope
=status show
=original my-variable-masks-earlier-declaration-in-same-scope
=books beginner
=author szabgab
=translator aramisf
=comments_disqus_enable 0

=abstract start

Este erro de compilação aparecerá se, por engano, você tentou declarar a mesma
variável mais de uma vez dentro do mesmo escopo.

<code>
"my" variable ... masks earlier declaration in same scope at ... line ...
</code>

Como isto pode acontecer, e como funciona o processo de declarar novamente as
variáveis a cada iteração de um laço?

Se eu não posso escrever <hl>my $x</hl> duas vezes em um escopo, então como
posso esvaziar esta variável?

=abstract end

Vejamos as diferenças entre os breves casos a seguir:

<h2>Script simples</h2>

<code lang="perl">
use strict;
use warnings;

my $x = 'this';
my $z = rand();
my $x = 'that';
print "OK\n";
</code>

Neste caso recebo o seguinte alerta em tempo de compilação:

<code>
"my" variable $x masks earlier declaration in same scope at ... line 7. )
</code>

Você sabe que isso é apenas um alerta, porque executar o script também
imprimirá "OK".


<h2>Bloco dentro de uma condicional</h2>

<code lang="perl">
use strict;
use warnings;

my $z = 1;
if (1) {
    my $x = 'this';
    my $z = rand();
    my $x = 'that';
}
</code>

Isso gera o seguinte alerta:

<code>
"my" variable $x masks earlier declaration in same scope at ... line 7.
</code>

Em ambos os casos, declaramos <hl>$x</hl> duas vezes no mesmo escopo, o que
vai gerar um alerta em tempo de compilação.

No segundo exemplo, declaramos <hl>$z</hl> duas vezes também, mas isto não
gerou nenhum alerta. Isto ocorre porque o <hl>$z</hl> dentro do bloco é um
<a href="/escopo-das-variaveis-em-perl">escopo à parte</a>.

<h2>O escopo de uma função</h2>

Mesmo código, mas em uma função:

<code lang="perl">
use strict;
use warnings;

sub f {
    my $x = 'this';
    my $z = rand();
    my $x = 'that';
}
f(1);
f(2);
</code>

Aqui também, você é alertado (uma vez) em tempo de compilação para a variável
<hl>$x</hl>, mesmo que ela seja 'trazida à existência' repetidamente, uma vez
para cada chamada da função.
Tudo bem quanto a isso. A variável <hl>$z</hl> não gera o alerta:
o Perl pode criar a mesma variável duas vezes, é você quem não deveria
fazê-lo. Ao menos não dentro de um mesmo escopo.

<h2>O escopo de um laço for</h2>

Mesmo código, mas em um laço:

<code lang="perl">
use strict;
use warnings;

for (1 .. 10) {
    my $x = 'this';
    my $z = rand();
    my $x = 'that';
}
</code>

Isto também vai gerar (apenas uma vez!) o alerta citado anteriormente para a
variável <hl>$x</hl>, mas nenhum para <hl>$z</hl>.

Neste código o mesmo acontece para <b>cada</b> iteração:
o Perl vai alocar memória para a variável <hl>$z</hl> a cada iteração.

<h2>O que o "my" realmente significa?</h2>

O significado do <hl>my $x</hl> é que você diz ao Perl, e mais especificamente ao
<hl>strict</hl>, que você gostaria de usar uma variável privada chamada
<b>$x</b> no <a href="/escopo-das-variaveis-em-perl">escopo atual</a>.
Sem isso, o Perl vai procurar uma declaração nos escopos superiores e se ele
não conseguir encontrar uma declaração em lugar algum, ele vai gerar um erro
em tempo de compilação <a
href="/simbolo-global-requer-nome-de-pacote-explicito">Símbolo Global requer
nome explícito de pacote</a>.
Cada início de bloco, cada chamada para função, cada iteração de um laço é um
novo mundo.

Por outro lado, escrever <hl>my $x</hl> duas vezes no mesmo escopo significa
apenas que você quer dizer a mesma coisa duas vezes ao Perl. Isto não é
necessário e costuma significar que há um erro em algum lugar.

Em outras palavras, o alerta que recebemos está relacionado à
<b>compilação</b> do código e não à execução. Está relacionado à declaração da
variável pelo desenvolvedor, e não à alocação de memória feita pelo Perl
durante a execução.

<h2>Como esvaziar uma variável existente?</h2>

Se não podemos escrever <hl>my $x</hl> duas vezes no mesmo escopo, então
como podemos definir a variável como "vazia"?

Antes de tudo, se uma variável é declarada dentro de um escopo, ou seja, entre
duas chaves quaisquer, então ela automaticamente desaparece quando a execução
deixa o <a href="/escopo-das-variaveis-em-perl">escopo</a>.

Se você só quer "esvaziar" a variável escalar no escopo atual, atribua a ela
um <hl>undef</hl>, e se a variável for um <a
href="https://perlmaven.com/undef-on-perl-arrays-and-hashes">array ou hash</a>, então esvazie-a
atribuindo uma lista vazia:

<code lang="perl">
$x = undef;
@a = ();
%h = ();
</code>

Enfim, para fins de esclarecimento. "my" diz ao Perl que você gostaria de usar
uma variável. Quando o Perl alcança o código onde você tem "my variável" ele
aloca memória para a variável e seu conteúdo.
Quando o Perl alcança o código <hl>my $x = undef;</hl> ou <hl>@x = ();</hl> ou
<hl>undef @x;</hl> ele remove o conteúdo que a variável possuía.
