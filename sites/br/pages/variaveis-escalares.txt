=title Variáveis Escalares
=timestamp 2013-05-27T01:27:00
=indexes strict, my, undef, say, +, x, ., sigil, $, @, %, FATAL warnings
=status show
=original scalar-variables
=books beginner
=author szabgab
=translator aramisf
=comments_disqus_enable 0

=abstract start

Nesta seção do <a href="/perl-tutorial">Tutorial Perl</a>, vamos aprender
sobre as estruturas de dados disponíveis no Perl e como podemos utiliza-las.

No Perl 5 existem basicamente 3 estruturas. <b>Escalares, arrays e hashes</b>.
A última também é conhecida em outras linguagens como dicionário, tabela de
consulta ou array associativo.

=abstract end

Variáveis em Perl são sempre precedidas com um sinal chamado <b>selo</b>.
Esse sinal é <hl>$</hl> para escalares, <hl>@</hl> para arrays, e <hl>%</hl> para hashes.

Um escalar pode conter um único valor tal como um número ou uma string. Ele
pode também conter uma referência para outra estrutura de dados que vamos
aprender posteriormente.

O nome do escalar sempre começa com um <hl>$</hl> (cifrão) seguido por letras
números e sublinhados. Um nome de variável pode ser <hl>$nome</hl> ou
<hl>$um_nome_longo_e_descritivo</hl>. Pode ser ainda
<hl>$NomeLongoEDescritivo</hl>, formato este que costuma ser chamado
"CamelCase", mas a comunidade Perl geralmente prefere nomes com letras
minúsculas separadas por sublinhados.

Como sempre usamos <b>strict</b>, devemos sempre declarar nossas variáveis
antes de usá-las, usando o <b>my</b>. (Posteriormente você vai aprender sobre
<b>our</b> e algumas outras formas, mas por enquanto vamos focar somente na declaração
<b>my</b>). Podemos inclusive definir um valor imediatamente, como neste
exemplo:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $name = "Foo";
say $name;
</code>

ou podemos declarar a variável antes e só atribuir um valor depois:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $name;

$name = "Foo";
say $name;
</code>

Preferimos a primeira forma se a lógica do código permitir.

Se declaramos uma variável, mas ainda não lhe atribuímos nada, então ela tem
um valor chamado <a href="/undef-e-definido-em-perl">undef</a> que é
semelhante ao <b>NULL</b> dos bancos de dados, mas que tem um valor
ligeiramente distinto.

Podemos verificar se uma variável é <hl>undef</hl> ou não usando a função
<hl>defined</hl>:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $name;

if (defined $name) {
  say 'defined';
} else {
  say 'NOT defined';
}

$name = "Foo";

if (defined $name) {
  say 'defined';
} else {
  say 'NOT defined';
}

say $name;
</code>

Podemos definir uma variável escalar como <hl>undef</hl> atribuindo
<hl>undef</hl> a ela:

<code lang="perl">
$name = undef;
</code>

As variáveis escalares podem armazenar tanto números quanto strings. Então posso escrever:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $x = "hi";
say $x;

$x = 42;
say $x;
</code>

e simplesmente vai funcionar.

Como isto funciona juntamente com operadores e sobrecarga de operadores em
Perl?

Geralmente o Perl funciona de forma oposta a outras linguagens. Ao invés dos
operandos determinarem como o operador irá se comportar, o operador diz aos
operandos como eles devem se comportar.

Por exemplo, se eu tenho duas variáveis que contêm números, então o operador
decide se elas realmente se comportam como números, ou se elas se comportam
como strings:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $z = 2;
say $z;             # 2
my $y = 4;
say $y;             # 4

say $z + $y;        # 6
say $z . $y;        # 24
say $z x $y;        # 2222
</code>

<hl>+</hl>, o operador de adição numérica, soma os dois números, então ambos
<hl>$y</hl> e <hl>$z</hl> agem como números.

<hl>+</hl>, concatena duas strings, então ambos <hl>$y</hl> e <hl>$z</hl> agem
como strings. (Em outras linguagens você pode chamar isto de adição de
strings.)

<hl>x</hl>, o operador de repetição, repete a string do lado esquerdo o número
de vezes indicado pelo número ao lado direito, então neste caso, <hl>$z</hl>
se comporta como uma string e <hl>$y</hl> como um número.

Os resultados seriam os mesmos se eles tivessem sido criados como strings:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $z = "2";
say $z;             # 2
my $y = "4";
say $y;             # 4

say $z + $y;        # 6
say $z . $y;        # 24
say $z x $y;        # 2222
</code>

Mesmo se um deles for criado como número e o outro como string:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $z = 7;
say $z;             # 7
my $y = "4";
say $y;             # 4

say $z + $y;        # 11
say $z . $y;        # 74
say $z x $y;        # 7777
</code>

O Perl converte automaticamente números para strings e strings para números
conforme requerido pelo operador.

Chamamos isso de <b>contextos</b> numéricos e <b>contextos</b> de string.

Os casos acima são fáceis. Quando convertemos um número em uma string é como
se colocássemos aspas ao seu redor. Ao converter uma string em um número em
uma string, existem casos simples como vimos, quando toda a string consiste
apenas de dígitos. O mesmo aconteceria se houvesse um ponto decimal na string,
tal como em <hl>"3.14"</hl>.
A questão é: E se a string tiver caracteres que não são parte de nenhum
número? ex. <hl>"3.14 is pi"</hl>.
Como isto se comportaria em uma operação numérica (também conhecido como
contexto numérico)?

Até isso é simples, mas pode exigir alguma explicação.

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $z = 2;
say $z;             # 2
my $y = "3.14 is pi";
say $y;             # 3.14 is pi

say $z + $y;        # 5.14
say $z . $y;        # 23.14 is pi
say $z x $y;        # 222
</code>

Quando uma string está em um contexto numérico o Perl olha para o lado
esquerdo da string, e tenta converte-la em um número. Enquanto esta conversão
fizer sentido, a parte convertida se torna o valor numérico da variável.
Dentro do contexto numérico (<hl>+</hl>) a string <hl>"3.14 is pi"</hl> é
considerada como o número <hl>3.14</hl>.

De certa forma isto é completamente arbitrário, mas este é seu comportamento e
convivemos com isso.

O código acima também vai gerar um alerta na saída de erro padrão
(<hl>STDERR</hl>):

<code>
Argument "3.14 is pi" isn't numeric in addition(+) at example.pl line 10.
</code>

considerando que você usou <b>use warnings</b> o que é altamente recomendável.
Usar os alertas vai te ajudar a perceber quando algo não está ocorrendo
exatamente como o esperado.
Espero que o resultado de <hl>$x + $y</hl> agora esteja claro.

<h2>Fundamento</h2>

Para deixar claro, o Perl não converteu <hl>$y</hl> para 3.14. Ele apenas utilizou
seu valor numérico para a adição.
Isto provavelmente também explica o resultado de <hl>$z . $y</hl>.
Neste caso o Perl está usando o valor original da string.

Você pode estar se perguntando por que <hl>$z x $y</hl> mostra 222 ao passo que
tínhamos 3.14 no lado direito do operador, mas o Perl parece poder apenas
repetir uma string um número inteiro de vezes... O Perl silenciosamente
arredonda o valor do número à direita na operação. (Se você quer mesmo
compreender a fundo, você pode reconhecer que o contexto "numérico" mencionado
anteriormente tem na verdade diversos sub-contextos, um deles sendo o contexto
"inteiro". Na maioria dos casos, o Perl faz o que parece ser "a coisa certa"
para a maioria das pessoas que não são programadores. para a maioria das
pessoas que não são programadores)

Mais que isso, nós nem mesmo vemos o alerta de "conversão parcial de string
para número" que vimos no caso do <hl>+</hl>.

Isto não ocorre por causa da diferença no operador. Se comentarmos a adição
veremos o alerta nesta operação. A razão para a falta de um segundo alerta é
que quando o Perl gerou o valor numérico da string <hl>"3.14 is pi"</hl> ele
também armazenou secretamente um espaço para a variável <hl>$y</hl>. Então
efetivamente <hl>$y</hl> agora armazena ambos, uma string e um valor numérico,
e o Perl usará o valor adequado em qualquer nova operação, evitando a conversão.

Existem mais três coisas que eu gostaria de apontar. Uma é o comportamento de
uma variável contendo <hl>undef</hl>, a outra é o <hl>alerta de
fatalidade</hl> e a terceira é evitar a "conversão automática de string para
número".

<h2>undef</h2>

Se dentro de uma variável eu tenho um <hl>undef</hl> que muitos diriam que é o
mesmo que "nada", isto ainda assim pode ser usado.
Dentro do contexto numérico ele vai agir como 0, e em um contexto de string,
como a string vazia.

<code lang="perl">
use strict;
use warnings;
use 5.010;

my $z = 3;
say $z;        # 3
my $y;

say $z + $y;   # 3
say $z . $y;   # 3

if (defined $y) {
  say "defined";
} else {
  say "NOT";          # NOT
}
</code>

Com dois alertas:

<code>
Use of uninitialized value $y in addition (+) at example.pl line 9.

Use of uninitialized value $y in concatenation (.) or string at example.pl line 10.
</code>

Como você pode ver, a variável ainda é <hl>undef</hl> no final, e a
condicional vai imprimir "NOT".


<h2>Alertas de Fatalidade</h2>

O outro ponto é que algumas pessoas preferem que a aplicação lance uma exceção
forte ao invés de um alerta suave. Se esta é a sua maneira, você pode
modificar o início do script e escrever

<code lang="perl">
use warnings FATAL => "all";
</code>

Tendo isso escrito no código, o script vai imprimir o número 3, e então lançar
a seguinte exceção:

<code>
Use of uninitialized value $y in addition (+) at example.pl line 9.
</code>

Esta é a mesma mensagem do primeiro alerta, mas dessa vez o script interrompe
sua execução. (Claro, a menos que a exceção seja tratada, mas vamos tratar
disso em outro momento.)

<h2>Evitando a conversão automática de string para número</h2>

Se você quiser evitar a conversão automática de strings quando não houver uma
conversão exata, você pode verificar se a string parece um número quando você
recebe-la do mundo exterior.

Para isso vamos carregar o módulo <a
href="https://metacpan.org/pod/Scalar::Util">Scalar::Util</a>, e usar a
subrotina <hl>looks_like_number</hl> que ela provê.

<code lang="perl">
use strict;
use warnings FATAL => "all";
use 5.010;

use Scalar::Util qw(looks_like_number);

my $z = 3;
say $z;
my $y = "3.14";

if (looks_like_number($z) and looks_like_number($y)) {
  say $z + $y;
}

say $z . $y;

if (defined $y) {
  say "defined";
} else {
  say "NOT";
}
</code>


<h2>Sobrecarga de Operador</h2>

Por fim, é possível ter sobrecarga de operadores no caso onde os operandos
diriam ao operador o que ele deve fazer, mas vamos deixar isto como um tópico
avançado.


