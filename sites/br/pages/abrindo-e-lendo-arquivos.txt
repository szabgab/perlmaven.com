=title Abrindo e Lendo Arquivos de Texto
=timestamp 2013-04-25T10:40:56
=indexes open, <$fh>, read, <, encoding, UTF-8, die, open or die
=status show
=original open-and-read-from-files
=books beginner
=author szabgab
=translator leprevost
=comments_disqus_enable 0

=abstract start

Nesta parte do <a href="/perl-tutorial">tutorial Perl</a> nós iremos ver <b>como ler arquivos de texto em Perl</b>.

Por enquanto iremos focar apenas nos arquivos de texto.

=abstract end
Existem duas formas tradicionais de abrir um arquivo dependendo da forma como você quer
lifar com o tratamento de erros.

<h2>Exceções</h2>

Caso 1: Lance uma exceção caso não consiga abrir o arquivo:

<code lang="perl">
use strict;
use warnings;

my $filename = 'data.txt';
open(my $fh, '<:encoding(UTF-8)', $filename)
  or die "Não foi possível abrir o arquivo '$filename' $!";

while (my $row = <$fh>) {
  chomp $row;
  print "$row\n";
}
</code>

<h2>Alerta ou nenhum aviso</h2>

Caso 2: Receber apenas um aviso caso o arquivo não possa ser aberto, mas mantenha o código executando:

<code lang="perl">
use strict;
use warnings;

my $filename = 'data.txt';
if (open(my $fh, '<:encoding(UTF-8)', $filename)) {
  while (my $row = <$fh>) {
    chomp $row;
    print "$row\n";
  }
} else {
  warn "Não foi possível abrir o arquivo '$filename' $!";
}
</code>

<h2>Explicações</h2>

Vamos ver as explicações dos casos:

Primeiramente, utilizando um editor de textos, crie um arquivo chamado 'data.txt' e adicione algumas linhas a ele:

<code>
Primeira linha
Segunda linha
Terceira linha
</code>

Abrir um arquivo para leitura é muito parecido com o que fizemos quando 
<a href="/escrevendo-em-arquivos-com-perl">vimos como abrir um arquivo para escrita</a>,
porém ao invés de utilizar o sinal "maior do que" (<hl>></hl>), nós iremos utilizar o
o sinal "menor do que" (<hl><</hl>).

Desta vez também estamos definindo a codificação para UTF-9. Masn na maioria dos casos,
você irá apenas encontrar o sinal "menor do que".

<code lang="perl">
use strict;
use warnings;

my $filename = 'data.txt';
open(my $fh, '<:encoding(UTF-8)', $filename)
  or die "Não foi possível abrir o arquivo '$filename' $!";

my $row = <$fh>;
print "$row\n";
print "pronto\n";
</code>

Após ter o <i>filehandle</i> nós podemos então ler a partir dele, utilizando o mesmo
operador de leitura de linhas, o <i>readline</i>,  
<a href="/instalando-o-perl">para leitura do teclado (STDIN)</a>.
Assim conseguiremos ler a primeira linha do arquivo.
Em seguida nós imprimimos o conteúdo da variável <hl>$row</hl> e depois "pronto" apenas
para que fique evidente que chegamos até o final do nosso exemplo.

Se você executar o script acima verá o seguinte ser impresso:

<code>
Primeira linha

pronto
</code>
Por que existe uma linha vazia antes do "pronto", você pode se perguntar.

Isso ocorre porque o operador <i>readline</i> lê a linha inteira, incluíndo o caractere
especial de nova linha. Quando nós utilizamos a função <hl>print</hl>, acabamos incluíndo 
um segundo caractere de nova linha.

Da mesma forma que ocorre quando lemos algo a partir do STDIN, nós normalmente não precisamos
o caractere final indicando uma nova linha, então podemos usar a função <hl>chomp()</hl> para removê-lo.

<h2>Lendo mais de uma linha</h2>

Agora que já sabemos como ler uma linha, podemos segui a diante e colocar o <i>readline</i>
dentro de um laço <hl>while</hl>.

<code lang="perl">
use strict;
use warnings;

my $filename = $0;
open(my $fh, '<:encoding(UTF-8)', $filename)
  or die "Não foi possível abrir o arquivo '$filename' $!";

while (my $row = <$fh>) {
  chomp $row;
  print "$row\n";
}
print "pronto\n";
</code>

Toda vez que atingimos a condicional do laço  <hl>while</hl>, primeiro será executado 
<hl>my $row = <$fh></hl>, a parte que irá ler a próxima linha do arquivo.
Se existir alguma coisa nessa linha, a condicional será avaliada como VERDADEIRA.
Até mesmo linhas vazias possuem caracteres especiais de nova linha, isso significa que 
ao ler essas linhas a variável <hl>$row</hl> irá conter o <hl>\n</hl> que irá ser avaliado
como VERDADEIRO em contexto booleano.

Após ler a última, na próxima iteração do laço o operador <i>readline</i> (<hl><$fh></hl>) irá
retornar undef que então é avaliado como FALSO. Sendo assim o laço while termina.

<h3>Casos extremos</h3>

Em determinados casos, existe a possibilidade do arquivo possuir um 0 em sua última linha.
O código acima, iria neste caso, avaliar a última linha como FALSO e o laço não seria mais executado.
Felizmente, o Perl acaba trapaceando nessa situação. Neste caso específico (ler uma linha de um arquivo
dentro de um laço while), o perl atua como se você tivesse escrito <hl>while (defined my $row = <$fh>) {</hl> 
fazendo com que até mesmo essas linhas sejam executadas corretamente.

<h2>Abrindo o arquivo sem matar o script</h2>

A forma descrita acima de abrir arquivos é utilizada em scripts Perl onde você 
deve obrigatoriamente ter um arquivo para ser lido.
Por exemplo quando o objetivo do código é parsear aquele arquivo.

Mas e se for um arquivo de configuração opcional? Se você conseguir ler o arquivo então
você altera seu conteúdo, caso não consiga utilizará os valores padrões.

Neste caso a segunda solução apresentada acima pode ser a melhor forma de escrever o seu código.

<code lang="perl">
if (open(my $fh, '<:encoding(UTF-8)', $filename)) {
  while (my $row = <$fh>) {
    chomp $row;
    print "$row\n";
  }
} else {
  warn "Não foi possível abrir o arquivo '$filename' $!";
}
</code>

Neste caso avaliamos o valor de retorno da função <hl>open</hl>.
Se for VERDADEIRO, nós lemos o seu conteúdo.

Se falhar nós apenas recebemos um aviso utilizando a função <hl>warn</hl>
mas sem lançar uma exceção, nós não precisamos nem mesmo incluir
a parte do <hl>else</hl>:

<code lang="perl">
if (open(my $fh, '<:encoding(UTF-8)', $filename)) {
  while (my $row = <$fh>) {
    chomp $row;
    print "$row\n";
  }
}
</code>
