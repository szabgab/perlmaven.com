=title Escopo das Variáveis em Perl
=timestamp 2013-11-28T23:14:25
=indexes my, scope
=status show
=original scope-of-variables-in-perl
=books beginner
=author szabgab
=translator leprevost
=comments_disqus_enable 0

=abstract start

Existem dois principais tipos de variáveis em Perl. A primeira delas é a variável global do pacote, declarada tanto com o já obsoleto
<hl>use vars</hl> ou com o comando <hl>our</hl>.

O outro tipo é a variável léxica declarada com <hl>my</hl>.

Vamos ver o que acontece quando você declara uma variável utilizando <hl>my</hl>? Em quais partes do código essa variável será visível?
Em outras palavras, qual é o <b>escopo</b> da variável?

=abstract end

<h2>Escopo da Variável: Blocos de código</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

{
    my $email = 'foo@bar.com';
    print "$email\n";     # foo@bar.com
}
# print $email;
# $email does not exists
# Global symbol "$email" requires explicit package name at ...
</code>

No interior do bloco anônimo (formado pelo par de chaves <hl>{}</hl>), primeiro nós vemos a declaração de uma nova variável
chamada <hl>$email</hl>. Esta variável existe a partir da linha de sua declaração até o final do bloco. Então isso significa
que a linha logo após o fechamento do bloco <hl>}</hl> precisa ser comentada. Se você remover o <hl>#</hl> da linha contendo
<hl># print $email;</hl> e tentar rodar o script, você terá o seguinte erro:
<a href="/simbolo-global-requer-nome-de-pacote-explicito">Global symbol "$email" requires explicit package name at ...</a>.

Em outras palavras, o <b>escopo de toda variável declarada com o comando my é o bloco em que se encontra cercado por chaves.</b>

<h2>Escopo da Variável: Visível em qualquer lugar</h2>

A variável <hl>$name</hl> é declarada no início do código, isso significa que ela será visível até o final do arquivo,
em qualquer lugar. Até mesmo dentro dos blocos, mesmo havendo declarações de funções.
Se nós alterarmos o valor da variável no interior do bloco, isso irá alterar o valor da variável no resto do código.
Mesmo quando você sai do bloco.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $lname = "Bar";
print "$lname\n";        # Bar

{
    print "$lname\n";    # Bar
    $lname = "Other";
    print "$lname\n";    # Other
}
print "$lname\n";        # Other
</code>


<h2>Variáveis escondidas por outras declarações</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $fname = "Foo";
print "$fname\n";        # Foo

{
    print "$fname\n";    # Foo

    my $fname  = "Other";
    print "$fname\n";    # Other
}
print "$fname\n";        # Foo
</code>

Neste caso a variável <hl>$fname</hl> é declarada no início do código. Como foi dito anteriormente, ela será visível até o final
do arquivo, em qualquer parte do código, <b>exceto em lugares em que a varável fique oculta por conta de uma declaração de uma
nova variável com o mesmo nome</b>.

Dentro do bloco nós utilizamos <hl>my</hl> para delcarar uma nova variável com o mesmo nome. Isso irá efetivamente esconder a variável
<hl>$fname</hl> declarada fora do bloco, até o momento da saída do mesmo. Ao chegar ao final do bloco (no sinal de fechamento <hl>}</hl>,
a variável <hl>$fname</hl> declarada dentro do bloco será destruída e a original <hl>$fname</hl> será acessível novamente.
Esta característica é especialmente importante pois faz com que seja fácil criar variáveis dentro de pequenos escopos sem que haja necessidade
de pensar nas implicações de se usar variáveis com o mesmo nome de outras declaradas por fora.

<h2>Mesmo nome em múltiplos blocos</h2>

Você pode usar livremente o mesmo nome das variáveis em múltiplos blocos. Estas variáveis não possuirão conexão alguma entre elas. 

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

{
    my $name  = "Foo";
    print "$name\n";    # Foo
}
{
    my $name  = "Other";
    print "$name\n";    # Other
}
</code>

<h2>Declarações dentro de arquivos pacotes</h2>

Este é um exemplo um pouco mais avançado, mas é importante mencioná-lo aqui:

O Perl nos permite alterar entre <b>name-spaces</b> usando o comando <hl>package</hl> dentro de um arquivo.
A declaração de um pacote <b>NÃO</b> provê um novo escopo. Se você declarar uma variável no 
<b>pacote main</b>, que acaba sendo apenas o corpo do seu script, a variável <hl>$fname</hl>
será visível até mesmo em outros <b>name-spaces</b>, no mesmo arquivo.

Se você declarar uma variável chamada <h>$lname</hl> no 'Outro' name-space, a mesma será visível 
quando depois, você alterar para o name-space <hl>main</hl>. Se a declaração do 'Outro' pacote estiver 
em uma arquivo diferente, então as variáveis estarão separadas em diferentes escopos, criados pelos arquivos.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $fname  = "Foo";
print "$fname\n";    # Foo

package Other;
use strict;
use warnings;

print "$fname\n";    # Foo
my $lname = 'Bar';
print "$lname\n";    # Bar


package main;

print "$fname\n";    # Foo
print "$lname\n";    # Bar
</code>
