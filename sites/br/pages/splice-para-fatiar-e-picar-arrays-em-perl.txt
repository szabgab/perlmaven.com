=title Splice para fatiar e picar arrays em Perl
=timestamp 2013-04-30T17:05:56
=indexes splice, array
=status show
=original splice-to-slice-and-dice-arrays-in-perl
=books advanced
=author szabgab
=translator aramisf
=comments_disqus_enable 1

=abstract start

Depois de aprender sobre <a href="http://perlmaven.com/manipulating-perl-arrays">pop, push, shift,
and unshift</a>, estudantes às vezes me perguntam como remover um elemento do
meio de um array.

Eu geralmente não tenho tempo para explicar isto. Existem outras coisas a
ensina-los, que parecem ser mais importantes que <hl>splice()</hl> no tempo
limitado que temos, mas geralmente eu ao menos aponto-lhes a direção correta.

Desta vez isto será muito mais fácil dado que você, o(a) leitor(a), pode decidir se
gostaria de investir seu tempo extra.

=abstract end

<h2>Como remover um elemento do meio de um array em Perl?</h2>

<code lang="perl">
my @dwarfs = qw(Doc Grumpy Happy Sleepy Sneezy Dopey Bashful);
splice @dwarfs, 3, 2;
print "@dwarfs";    # Doc Grumpy Happy Dopey Bashful
</code>

Como você pode ver o 4o e 5o elementos do array foram removidos.
Isto ocorre porque o segundo parâmetro do <b>splice</b> é o offset do primeiro
elemento a ser removido, e o terceiro parâmetro é o número de elementos a ser
removido.

<h2>Como inserir um elemento no meio de  um array em Perl?</h2>

<code lang="perl">
my @dwarfs = qw(Doc Grumpy Happy Sleepy Sneezy Dopey Bashful);
splice @dwarfs, 3, 0, 'SnowWhite';
print "@dwarfs";
# Doc Grumpy Happy SnowWhite Sleepy Sneezy Dopey Bashful
</code>

Nesse caso nós usamos <hl>splice</hl> para inserir um elemento.
Normalmente o segundo parâmetro (o offset) define onde inicia a remoção de
elementos, mas neste caso o terceiro parâmetro - o número de elementos - foi 0
então <hl>splice</hl> não removeu nenhum elemento. Em vez disso, o offset é
usado como a posição para inserir algo novo: o valor passado como 4o parâmetro
para <hl>splice</hl>.

Assim é como SnowWhite terminou entre os sete anões.

<h2>Como inserir uma lista de valores em um array em Perl?</h2>

Inserir um elemento é na verdade apenas um caso especial de inserção de vários
elementos.

<code lang="perl">
my @dwarfs = qw(Doc Grumpy Happy Sleepy Sneezy Dopey Bashful);
splice @dwarfs, 3, 0, 'SnowWhite', 'Humbert';
print "@dwarfs";

# Doc Grumpy Happy SnowWhite Humbert Sleepy Sneezy Dopey Bashful
</code>

Neste caso depois do 3o parâmetro temos vários valores (2 neste caso).
Eles são todos inseridos no array.

<h2>Como inserir um array no meio de outro array em Perl?</h2>

O mesmo aconteceria se passássemos um array como o 4o parâmetro:

<code lang="perl">
my @others = qw(SnowWhite Humbert);
my @dwarfs = qw(Doc Grumpy Happy Sleepy Sneezy Dopey Bashful);
splice @dwarfs, 3, 0, @others;
print "@dwarfs";
</code>


<h2>Substituir parte de um array com alguns outros valores</h2>

Você pode também adicionar e remover elementos em um único comando:

<code lang="perl">
my @others = qw(SnowWhite Humbert);
my @dwarfs = qw(Doc Grumpy Happy Sleepy Sneezy Dopey Bashful);
splice @dwarfs, 2, 4, @others;
print "@dwarfs\n";

# Doc Grumpy SnowWhite Humbert Bashful
</code>

Neste caso nós removemos quatro dos anões e os substituimos por duas pessoas
de tamanho natural: SnowWhite e Humbert o Caçador.

<h2>splice</h2>

Splice é a função definitiva para modificar arrays em Perl.
Você pode remover qualquer seção de um array e substitui-la por qualquer outra
lista de valores.
O número de elementos removidos pode ser diferente, e qualquer um deles pode
ser 0 também.

A sintaxe padrão da função tem os seguintes parâmetros, apesar de todas as
partes (bem, exceto pelo array propriamente dito) são opcionais:

<code lang="perl">
splice ARRAY, OFFSET, LENGTH, LIST
</code>

OFFSET e LENGTH definem a seção no ARRAY que será removida.
Ambos são (números) inteiros. LIST é uma lista de valores que será inserida no lugar da
seção que foi removida. Se LIST não é fornecida, ou é vazia, então splice vai
apenas remover itens mas não vai inserir nenhum.

<h2>Valores de Retorno</h2>

Dentro do <b>contexto LIST</b> splice retorna os elementos removidos do
array.

<code lang="perl">
my @others = qw(SnowWhite Humbert);
my @dwarfs = qw(Doc Grumpy Happy Sleepy Sneezy Dopey Bashful);
my @who = splice @dwarfs, 3, 2, @others;
print "@who\n";

# Sleepy Sneezy
</code>

Dentro do <b>contexto SCALAR</b>, splice retorna o último elemento removido,
ou undef caso nenhum elemento tenha sido removido.

<code lang="perl">
my @others = qw(SnowWhite Humbert);
my @dwarfs = qw(Doc Grumpy Happy Sleepy Sneezy Dopey Bashful);
my $who = splice @dwarfs, 3, 2, @others;
print "$who\n";

# Sneezy
</code>

<h2>Parâmetros Negativos</h2>

Ambos offset e length podem ser números negativos. Em cada caso, isso
significa "conte a partir do final do array".

<code lang="perl">
my @dwarfs = qw(Doc Grumpy Happy Sleepy Sneezy Dopey Bashful);
my @who = splice @dwarfs, 3, -1;
print "@who";

# Sleepy Sneezy Dopey
</code>

Isso significa, deixe 3 intactos e então remova (e retorne) todos os elementos
até 1 antes do fim.

<code lang="perl">
my @dwarfs = qw(Doc Grumpy Happy Sleepy Sneezy Dopey Bashful);
my @who = splice @dwarfs, -3, 1;
print "@who";

# Sneezy
</code>

Isso significa: "Conte 3 a partir do final e remova (e retorne) 1 elemento
iniciando deste ponto.

<h2>Conclusão</h2>

Espero que ao menos parte disto lhe ajude a entender melhor como
<hl>splice</hl> opera arrays em Perl.
