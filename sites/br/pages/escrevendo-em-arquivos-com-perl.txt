=title Escrevendo em Arquivos com Perl
=timestamp 2013-04-19T16:45:56
=indexes open, close, write, die, open or die, >, encoding, UTF-8
=status show
=original writing-to-files-with-perl
=books beginner
=author szabgab
=translator leprevost
=comments_disqus_enable 0

=abstract start

Vários programas escritos em Perl lidam com arquvios de texto, como por exemplo arquivos
de configuração ou arquivos de histórico, então para que o nosso conhecimento seja útil
é importante que possmos aprender desde o início a manipular arquivos.

Primeiramente vamos ver como fazemos para escrever em um arquivo.

=abstract end

Antes que você escrever algo em um arquivo você precisa <b>abrir</b> o arquivo, pedindo
ao sistema operacional (Windows, Linux, OSX, etc) abrir um canal de comunicação
entre o seu programa e o arquivo em si. Para isso o Perl fornece a função <hl>open</hl>,
possuindo uma sintaxe um pouco incomun.

<code lang="perl">
use strict;
use warnings;

my $arquivo = 'report.txt';
open(my $fh, '>', $arquivo) or die "Não foi possível abrir o arquivo '$arquivo' $!";
print $fh "Meu primeiro relatório gerado pelo Perl\n";
close $fh;
print "pronto\n";
</code>

Este é um bom exemplo funcional e nós logo iremos retornar a ele, mas antes vamos iniciar com um exemplo
um pouco mais simples:

<h2>Exemplo Simples</h2>

<code lang="perl">
use strict;
use warnings;

open(my $fh, '>', 'report.txt');
print $fh "Meu primeiro relatório gerado pelo Perl\n";
close $fh;
print "pronto\n";
</code>

Esse exemplo também precisa de uma explicação. A função <b>open</b> necessita de 3 parâmetros.

O primeiro, <hl>$fh</hl> é uma variável escalar que nós declaramos dentro da função <hl>open</hl>.
Nós pederíamso também ter declarado a variável antes, mas geralmente dessa forma o código fica um
pouco mais limpo, mesmo que parece um pouco estranho de início. O segundo parâmetro define a forma como
iremos abrir o arquivo.
Neste caso, utilizamos o sinal de <i>maior que</i> (<hl>&gt;</hl>) significando que iremos abrir o arquivo
para escrita.
O terceiro parâmetro é o caminho do arquivo que gostaríamos de abrir. 

Quando esta função é invocada, é colocado dentro da variável <hl>$fh</hl> um símbolo especial.
Este símbolo é chamado de <i>file-handle</i> (podemos traduzir como 'manipulador de arquivos).
No momento não nos importamos muito com o conteúdo desta variável; iremos somente utilizá-la
no futuro. Apens lembre-se que no momento o conteúdo do arquivo está apenas no disco e <b>NÃO</b>
na variável <hl>$fh</hl>.

Uma vez estando aberto, podemos utilizar o <i>file-handle</i> <hl>$fh</hl> junto da função <hl>print()</hl>.
O seu uso se assemelha bastante com o uso do <hl>print</hl> visto anteriormente em outros tutoriais,
porém agora o primeiro parâmetro é o próprio <i>file-handle</i> <b>não</b> sendo seguido por vírgula.

A chamada ao print() irá imprimir no arquivo o texto definido.

Então, na linha seguinte nós fechamos o <i>file-handle</i>. Mesmo não sendo
algo estritamente necessário em Perl. O Perl irá automaticamente fechar todos os
<i>file-handles</i> quando a variável sair do seu escopo, ou então quando o scritp terminar.
Em todos os casos, consideramos uma oa prática realizar o fechamento de forma explícita.

A últina linha <hl>print "pronto\n"</hl> está lá somente para que o nosso exemplo fique mais didático.

<h2>Lidando com Erros</h2>

Vamos utilizar o exemplo acima novamente e substituir o nome do arquivo por um caminho que não existe.
Por exemplo:

<code lang="perl">
open(my $fh, '>', 'algum_caminho_qualquer/report.txt');
</code>

Agora quando executar o programa verá a seguinte menssagem de erro:

<code>
print() on closed file-handle $fh at ... # impressão realizada em arquivo fechado
done
</code>

Na verdade isto é apenas um aviso; o script continuará executando e por isso que precisamos de
algo como o "pronto" impresso na tela.

Além disso, nós somente pudemos ver a mensagem de erro porque nós pedimos para receber avisos
ao utilizar o pragma <hl>use warnings</hl>.
Experimente comentar essa linha e perceba que agora o script é silencioso quando falha em tentar abrir o arquivo.
Dessa forma você não perceberia até o seu cliente, ou até mesmo, seu chefe reclamar.

De qualquer forma ocorreu um erro. Nós tentamos abrir um arquivo, nós falhamos e ainda assim tentamos
escrever algo no arquivo.

Nós deveríamos ter avaliado se a função <hl>open()</hl> funcionou corretamente antes de proceder.

Por sorte, a função retorna valor <a href="/valores-booleanos-em-perl">VERDADEIRO caso tenha sucesso e
FALSO se falhar</a>, então podemos escrever o seguinte:

<h2>Open or die</h2>

<code lang="perl">
open(my $fh, '>', 'algum_caminho_qualquer/report.txt') or die;
</code>

Esta é a forma "padrão" do idioma <b>open or die</b> (abra ou morra). Muito commum em Perl..

<hl>die</hl> é a função que irá lançar uma exceção e então terminar o script.

"open or die" (abra ou morra) é uma expressão lógica. Como você deve ter visto em uma edição
anterior do tutorial, o "or" (ou) em Perl é avaliado por curto-circuíto, assim como
em outras linguagens. Isto significa que se a parte da esquerda for VERDADE, nós já sabemos que
o restante da declaração também será VERDADE, fazendo com que o lado direito não seja executado.
Por outro lado, se o lado esquerdo for FALSO então o lado direito é executado também e o resultado
será proveniente da expressão como um todo.

Neste caso nós tiramos proveito desse curto-circuito para escrever expressões.

Caso o <hl>open()</hl> funcione corretamente então retornará VERDADEIRO e a parte da direita
nunca será executada, levando o script a executar a próxima linha.

Caso o <hl>open()</hl> falhe, então retornará FALSO. Neste caso o lado direito será executado,
lançando uma exceção e em seguida matando a execução do código.

No exemplo acima nós não realizamos uma avaliação do valor de retorno propriamente dito.
Nós não nos importamos. Apenas tiramos proveito do retorno para o "efeito colateral".

Se você experimentar executar o código acima com as alterações, irá receber uma mensagem de erro:

<code>
Died at ...
</code>

e NÃO irá imprimir "pronto".

<h2>Uma Forma Melhor de Reportar Erros</h2>

Ao invés de apenas chamar a função <hl>die</hl> sem parâmetros, nos podemos adicionar uma pequena explicação do
que ocorreu.

<code lang="perl">
open(my $fh, '>', 'algum_caminho_qualquer/report.txt')
  or die "Não foi possível abrir o arquivo 'algum_caminho_qualquer/report.txt'";
</code>

irá imprimir:

<code>
  Não foi possível abrir o arquivo 'algum_caminho_qualquer/report.txt' ...
</code>

Dessa forma é melhor mas em algum momento alguém irá tentar alterar para o caminho correto do arquivo ...

<code lang="perl">
open(my $fh, '>', 'caminho_correto_com_erro_de_digitação/report.txt')
  or die "Não foi possível abrir o arquivo 'algum_caminho_qualquer/report.txt'";
</code>

...mas você ainda assim receberá a mensagem antiga porque foi alterado apenas
o conteúdo da função <hl>open()</hl>, e não a mensagem de erro. 

É melhor usarmos uma variável para os nomes de arquivos:

<code lang="perl">
my $arquivo = 'caminho_correto_com_erro_de_digitação/report.txt';
open(my $fh, '>', $arquivo) or die "Não foi possível abrir o arquivo '$arquivo'";
</code>

Agora nós temos a mensagem de erro correta, mas ainda não sabemos porquê houve uma falha.
Indo um passo a diante, nós podemos utilizar <hl>$!</hl> - uma variável interna do Perl - para
imprimir a mensagem de erro proveniente do sistema operacional sobre a falha:

<code lang="perl">
my $arquivo = 'caminho_correto_com_erro_de_digitação/report.txt'';
open(my $fh, '>', $arquivo) or die "Não foi possível abrir o '$arquivo' $!";
</code>

Isso irá imprimir:

<code>
Não foi possível abrir o arquivo 'caminho_correto_com_erro_de_digitação/report.txt' No such file or directory ... 
</code>

Agora sim, muito melhor!

Com isso nós retornamos ao nosso exemplo original.

<h2>Maior do que?</h2>

O sinal <i>maior do que</i> no início da função pode ser um pouco obscuro,
mas se você está familiarizado com o sinal de direcionamento utilizado la linha de comandos então este
caso é familiar a você. Caso contrário pense apenas que o sinal atua como uma flecha indicando a direção do
fluxo de dados:
para dentro do arquivo, no lado direito da expressão.

<h2>Caracteres não latinos?</h2>
Em casos em que precise lidar com caracteres que não fazem parte da table ASCII, você provavelmente precisará salvá-los
como UTF-8. Para fazer isso você precisa avisar ao Perl que irá abrir um arquivo com codificação UTF-8.

<code lang="perl">
open(my $fh, '>:encoding(UTF-8)', $arquivo)
  or die "Não foi possível abrir o arquivo '$arquivo'";
</code>
