=title Folosirea unei valori neinițializate
=timestamp 2013-09-29T21:45:56
=indexes undef, uninitialized value, $|, warnings, buffering
=status show
=original use-of-uninitialized-value
=books beginner
=author szabgab
=translator stefansbv
=comments_disqus_enable 0

=abstract start

Acesta este una dintre cele mai comune avertizări (în lb. en:
warning) pe care le întâlnești când rulezi cod Perl.

Este doar o avertizare, nu va opri execuția programului și este
generată numai dacă avertizările sunt activate.  Ceea ce este
recomandat.

Cea mai uzuală cale de a activa avertizările este prin includerea unei
comenzi <hl>use warnings;</hl> la începutul scriptului sau al
modulului.

=abstract end

Calea mai veche metodă este prin adăugarea fanionului <hl>-w</hl> pe
linia "sh-bang".  În mod uzual acesta arată astfel:

<hl>#!/usr/bin/perl -w</hl>

Sunt şi câteva diferențe, dar cum <hl>use warnings</hl> este
disponibilă de 12 ani, nu sunt motive pentru a o evita. Cu alte
cuvinte:

Întotdeauna <hl>use warnings;</hl>!

Să ne întoarcem la mesajul de avertizare pe care am dorit să-l
explicăm.

<h2>O explicație rapidă</h2>

<code>
Use of uninitialized value $x in say at perl_warning_1.pl line 6.
</code>

Acesta înseamnă că variabile <hl>$x</hl> nu are valoare (valoarea ei
este de fapt valoarea specială <hl>undef</hl>).

Adică nu a avut niciodată o valoare (alta decât undef), sau la un
moment dat a primit valoarea <hl>undef</hl>.

Ar trebui să examinezi liniile de cod unde variabila a primit ultima
dată o nouă valoare sau ar trebui să încerci să înțelegi de ce acea
bucățică de cod nu a mai ajuns să fie executată.

<h2>Un exemplu simplu</h2>

Exemplul următor va genera o astfel de avertizare.

<code lang="perl">
use warnings;
use strict;
use 5.010;

my $x;
say $x;
</code>

Perl este foarte amabil, îți spune care fișier a generat avertizarea
și la ce linie.

<h2>Doar o avertizare</h2>


Așa cum am mai menționat, acesta este doar o avertizare. Dacă scriptul
are mai multe comenzi după cea cu <hl>say</hl>, acestea vor fi
executate:

<code lang="perl">
use warnings;
use strict;
use 5.010;

my $x;
say $x;
$x = 42;
say $x;
</code>

Acesta va printa

<code>
Use of uninitialized value $x in say at perl_warning_1.pl line 6.

42
</code>

<h2>Ordine derutantă</h2>

Atenție însă, dacă codul are comenzi de printare înainte de linia care
generează avertizarea, așa ca în exemplul următor:

<code lang="perl">
use warnings;
use strict;
use 5.010;

print 'OK';
my $x;
say $x;
$x = 42;
say $x;
</code>

rezultatul poate fi derutant.

<code>
Use of uninitialized value $x in say at perl_warning_1.pl line 7.
OK
42
</code>

Aici, 'OK', rezultatul comenzii <hl>print</hl> este văzut <b>după</b>
mesajul de avertizare, chiar dacă comanda a fost
executată <b>înainte</b> de codul care a generat avertizarea.

Acestă ciudățenie este rezultatul a <hl>IO buffering</hl>. În mod
implicit Perl folosește o zonă de memorie tampon pentru STDOUT, ieșirea
standard, dar nu și pentru STDERR, ieșirea de eroare standard.

Adică, în timp ce cuvântul 'OK' așteaptă ca memoria tampon să fie
golită, mesajul de eroare ajunge deja pe ecran.

<h2>Dezactivare memorie tampon</h2>

Pentru a evita acesta poți să dezactivezi memoria tampon pentru STDOUT.

Aceasta se face prin folosirea comenzii: <hl>$| = 1;</hl> la începutul
scriptului;


<code lang="perl">
use warnings;
use strict;
use 5.010;

$| = 1;

print 'OK';
my $x;
say $x;
$x = 42;
say $x;
</code>

<code>
OKUse of uninitialized value $x in say at perl_warning_1.pl line 7.
42
</code>

(Avertizare este pe aceeași linie cu <b>OK</b> pentru că nu am printat
un caracter de linie nouă <hl>\n</hl> după OK.)

<h2>Vizibilitate nedorită</h2>

<code lang="perl">
use warnings;
use strict;
use 5.010;

my $x;
my $y = 1;

if ($y) {
  my $x = 42;
}
say $x;
</code>

Și acest cod produce <hl>Use of uninitialized value $x in say at
perl_warning_1.pl line 11.</hl>

Am reușit să fac acestă greșeală de mai multe ori.  Nu am fost atent
și am folosit <hl>my $x</hl> în interiorul blocului <hl>if</hl>, ceea
ce a însemnat că am creat o altă variabilă $x, i-am dat valoarea 42
doar pentru a lăsa-o apoi să iasă din zona de vizibilitate (en: out of
the scope) la sfârșitul blocului.

(Comanda $y = 1 este doar un substituent pentru cod real și pentru
condiții reale.  Este folosit numai pentru a face ca acest exemplu să
arate un pic mai realist.)

Sunt, bineînțeles, cazuri în care este nevoie să declar variabile în
interiorul unui bloc, dar nu întotdeauna.  Când se întâmplă din
greșeală, este greu de depistat eroarea.
