=title Simbolul global necesită un nume de pachet explicit
=timestamp 2013-09-12T07:45:56
=indexes strict, my, package, global symbol
=status show
=original global-symbol-requires-explicit-package-name
=books beginner
=author szabgab
=translator stefansbv
=comments_disqus_enable 0

=abstract start

<b>Global symbol requires explicit package name</b> adică în limba
română: "simbolul global necesită un nume de pachet explicit", este un
mesaj de eroare comun, și după opinia mea unul foarte derutant al
interpretorului Perl.  Cel puțin pentru începători.

Sensul mesajului ar fi "Trebuie să declari o variabilă cu ajutorul
operatorului <b>my</b>."

=abstract end

<h2>Cel mai simplu exemplu</h2>

<code lang="perl">
use strict;
use warnings;

$x = 42;
</code>

Iar eroarea este

<code>
Global symbol "$x" requires explicit package name at ...
</code>

deși mesajul este corect, este puțin folositor programatorului Perl
începător.  Probabil ei nu au învățat încă ce sunt pachetele (n.t. en:
packages).  Și nici nu știu ce poate fi mai explicit decât $x ?

Această eroare este generată de <b>use strict</b>.

Explicația din documentație este:

<i>
Aceasta generează o eroare la compilare dacă accesezi o variabilă
care nu a fost declarată via "our" sau "use vars", localizată via
"my()", sau nu a fost calificată variabile (en: fully qualified).
</i>

Un începător, este de dorit să înceapă toate scripturile cu <b>use
strict</b>, și probabil va învăța despre <b>my</b> mult înainte de
celelalte variante.

Nu știu dacă acest mesaj de eroare poate fi, sau ar trebui să fie
schimbat în Perl.  Nu acesta este scopul acestui articol.  Scopul este
să ajutăm începătorii să înțeleagă, la nivelul lor, ce înseamnă de fapt
acest mesaj de eroare.

Pentru a elimina mesajul de eroare de mai sus trebuie să scriem:

<code lang="perl">
use strict;
use warnings;

my $x = 42;
</code>

Adică, trebuie să <b>declarăm variabila folosind "my" înainte de prima utilizare.</b>.

<h2>Soluția nerecomandată</h2>

Cealaltă "soluție" este este să ștergem <b>use strict</b>:

<code lang="perl">
#use strict;
use warnings;

$x = 23;
</code>

Aceasta va funcționa, dar acest cod va genera o atenționare:
<a href="https://perlmaven.com/name-used-only-once-possible-typo">Name "main::x" used only
once: possible typo at ...</a>.

În orice caz, în mod normal nu v-ați conduce mașina fără centura de
siguranță, nu-i așa?

<h2>Exemplul 2: scopul</h2>

Un alt caz des întâlnit arată cam așa:

<code lang="perl">
use strict;
use warnings;

my $x = 1;

if ($x) {
my $y = 2;
}

print $y;
</code>

Mesajul de eroare pe care-l primim este același ca mai sus:

<code>
Global symbol "$y" requires explicit package name at ...
</code>

Ceea ce este surpinzător pentru multă lume.  Mai ales când încep să scrie programe.  La urma urmelor au declarat <hl>$y</hl> folosind <hl>my</hl>.

Întâi de toate îți sare în ochi o problemă.  Indentarea lui <hl>my $y
= 2;</hl> lipsește.  Dacă ar fi fost indentat cu câteva spații sau cu
un Tab la dreapta, ca în exemplul următor, sursa problemei ar fi fost
evidentă:

<code lang="perl">
use strict;
use warnings;

my $x = 1;

if ($x) {
    my $y = 2;
}

print $y;
</code>

Problema este că variabila <hl>$y</hl> este declarată în cadrul
blocului, (între perechea de acolade) ceea ce înseamnă că nu există în
afara acelui bloc.  Aceasta se
numește <a href="https://perlmaven.com/scope-of-variables-in-perl"><b>vizibilitatea</b>
variabilei</a> (n.t. en: <b>scope</b>).

Conceptul de <b>vizibilitate</b> diferă în funcție de limbajul de
programare.  În Perl, un bloc închis între acolade creează o
vizibilitate limitată.  Ceea ce este declarat înăuntru
folosind <hl>my</hl> nu va fi accesibil în afara blocului.

(Apropo <hl>$x = 1</hl> este folosit numai pentru a da o aparență de
legitimitate care creează o vizibilitate.  Cu alte cuvinte,
condiționarea <hl>if ($x) {</hl> este menită pentru a face ca exemplul
să arate realist.)

Soluția este, ori să punem <hl>print</hl> în interiorul blocului:

<code lang="perl">
use strict;
use warnings;

my $x = 1;

if ($x) {
    my $y = 2;
    print $y;
}
</code>

ori declarăm variabila în afara blocului (și nu în interior!):

<code lang="perl">
use strict;
use warnings;

my $x = 1;
my $y;

if ($x) {
    $y = 2;
}

print $y;
</code>

Care variantă o alegeți depinde de sarcina reală.  Acestea sunt doar
soluții posibile și corecte d.p.d.v. sintactic.

Bine înțeles, dacă uităm să ștergem <hl>my</hl> din interiorul
blocului, sau dacă <hl>$x</hl> este fals, atunci primim un mesaj de avertizare
<a href="https://perlmaven.com/use-of-uninitialized-value">Use of uninitialized value</a>
(folosirea unei valori neinițializate).

<h2>Celelalte căi</h2>

Explicațiile privind <hl>our</hl> și <hl>use vars</hl>, sau cum se pot
califica numele de variabile este subiectul altor articole.
