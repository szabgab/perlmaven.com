=title Konversi otomatis dari string ke angka di Perl
=timestamp 2014-03-15T11:00:00
=indexes is_number, looks_like_number, Scalar::Util, casting, type conversion
=status show
=original automatic-value-conversion-or-casting-in-perl
=books beginner
=author szabgab
=translator khadis
=comments_disqus_enable 0

=abstract start

Bayangkan Anda sedang menyiapkan daftar belanja, tertulis di dalamnya

<code>
"2 potong roti"
</code>

dan memberikannya pada pasangan Anda yang langsung
melemparkan kesalahan penulisan itu ke wajah Anda.
Selanjutnya "2" di sini adalah string, bukan angka.

Hal ini tentu akan sangat membingunkan, bukan?

=abstract end

<h2>Tipe konversi di Perl</h2>

Pada sebagian besar bahasa pemrograman jenis-jenis operand menentukan bagaimana sebuah operator bekerja.
Yaitu, menambahkan dua angka menghasilkan penjumlahan, sementara menambahkan dua string akan menggabungkan keduanya.
Fungsi ini disebut 'operator overloading'.

Perl, bekerja sebaliknya.

Di Perl, operator adalah sesuatu yang mendefinisikan bagaimana sebuah operand digunakan.

Artinya, jika Anda menggunakan operasi bilangan (contohnya penjumlahan) maka kedua nilainya
secara otomatis dikonversikan ke dalam angka. Jika Anda melakukan operasi string
(contohnya penggabungan) maka kedua nilainya dikonversikan ke dalam string.

Programmer bahasa C mungkin menyebutnya <b>casting</b> namun istilah ini
tidak digunakan dalam Perl. Mungkin karena segalanya otomatis.

Perl tidak peduli apakah Anda menulis sesuatu sebagai angka atau string.
Perl mengkonversikan keduanya secara otomatis berdasarkan konteksnya.

Konversi <hl>angka => string</hl> itu mudah.
Hanya seperti membayangkan ada "" mengapit sebuah angka.

Konversi <hl>string => angka</hl> mungkin membuat Anda berpikir sejenak.
Jika sebuah string tampak seperti angka bagi Perl, maka mudah.
Nilai yang berupa angka adalah hal yang sama. Tanpa tanda petik.

Jika ada suatu karakter yang menahan Perl dari mengkonversi string ke
angka, maka Perl akan menggunakannya sebanyak yang ia mampu pada sisi kiri string untuk
angka dan mengabaikan sisanya.

Berikut saya tunjukkan beberapa contohnya:

<code>
Original   As string   As number

  42         "42"        42
  0.3        "0.3"       0.3
 "42"        "42"        42
 "0.3"       "0.3"       0.3

 "4z"        "4z"        4        (*)
 "4z3"       "4z3"       4        (*)
 "0.3y9"     "0.3y9"     0.3      (*)
 "xyz"       "xyz"       0        (*)
 ""          ""          0        (*)
 "23\n"      "23\n"      23
</code>

Pada kasus-kasus di mana konversi string ke angka berjalan tidak sempurna,
kecuali yang terakhir, Perl akan mengeluarkan peringatan. Tentu, diasumsikan Anda menggunakan
<hl>use warnings</hl> seperti yang direkomendasikan.

<h2>Contoh</h2>

Sekarang Anda lihat tabel tersebut, mari lihat kodenya:

<code lang="perl">
use strict;
use warnings;

my $x = "4T";
my $y = 3;

</code>

Penggabungan mengkonversikan kedua nilai tersebut ke dalam string:

<code lang="perl">
print $x . $y;    # 4T3
</code>

Penjumlahan angka mengkonversikan kedua nilai ke dalam angka:

<code lang="perl">
print $x + $y;  # 7
                # Argument "4T" isn't numeric in addition (+) at ...
</code>

<h2>Argument bukan angka</h2>

Itu adalah peringatan yang akan Anda dapat ketika Perl mencoba mengkonversikan
sebuah string ke dalam angka dan konversinya tak sempurna.

Ada banyak peringatan dan pesan kesalahan lainnya di Perl.
Contohnya <a href="https://id.perlmaven.com/simbol-global">Simbol global</a>
dan <a href="https://id.perlmaven.com/penggunaan-nilai-tak-terinisiasi">Penggunaan nilai tak terinisiasi</a>.

<h2>Bagaimana menghindari munculnya pesan peringatan?</h2>

Ini sangat menyenangkan karena Perl akan memperingatkan Anda (jika diminta demikian) ketika tipe konversinya tak sempurna, tapi tidak adakah fungsi
seperti <b>is_number</b> yang akan memeriksa jika string yang diberikan benar-benar sebuah angka?

Ya dan tidak.

Perl tidak memiliki fungsi <b>is_number</b> seolah ini semacam komitmen bahwa pengembang Perl
tahu angka itu seperti apa. Sayangnya banyak orang yang tidak sepenuhnya setuju terhadap hal ini. Ada beberapa sistem
yang menerima ".2" sebagai angka, namun sistem-sistem lain tidak menerimanya.
Bahkan tidak jarang jika "2" tidak diterima, namun ada sistem di mana ia diterima sepenuhnya sebagai angka.

Bahkan ada tempat di mana 0xAB dianggap sebagai angka. Angka heksadesimal.

Jadi tidak ada fungsi <b>is_number</b>, tapi ada fungsi yang disebut <b>looks_like_number</b>.

Itulah yang Anda pikirkan. Ia akan memeriksa apakah string yang ditulis tampak seperti angka bagi Perl.

Ini sudah tersedia dalam modul <a href="http://perldoc.perl.org/Scalar/Util.html">Scalar::Util</a>
dan Anda dapat menggunakannya seperti berikut:

<code lang="perl">
use strict;
use warnings;

use Scalar::Util qw(looks_like_number);

print "Berapa potong roti yang akan saya beli? ";
my $potong = <STDIN>;
chomp $potong;

if (looks_like_number($potong)) {
    print "Saya mengerti...\n";
} else {
    print "Maaf, saya tidak mengerti\n";
}
</code>


Jangan lupa susunya!


