=title Global symbol requires explicit package name
=timestamp 2013-08-05T08:40:56
=indexes strict, my, package, global symbol
=status show
=original global-symbol-requires-explicit-package-name
=books beginner
=author szabgab
=translator mca
=comments_disqus_enable 0

=abstract start

 <b>Global symbol requires explicit package name</b> ist eine gängige,
und m.E.n. sehr irreführende Fehlermeldung in Perl. Zumindest für Anfänger.

Die schnelle Übersetzung wäre: "Du musst die Variable mit <b>my</b> deklarieren."

=abstract end

<h2>Das einfachste Beispiel</h2>

<code lang="perl">
use strict;
use warnings;

$x = 42;
</code>

Und die Fehlermeldung ist:

<code>
Global symbol "$x" requires explicit package name at ...
</code>

Obwohl die Fehlermledung richtig ist, ist sie für
Perl-Anfänger wenig hilfreich. Sie haben vielleicht noch
nicht gelernt, was "Packages" sind. Noch weniger wissen
sie, was expliziter sein sollte, als ein $x.

Diese Fehlermeldung wird von  <b>use strict</b> ausgelöst.

Die Erklärung in der Dokumentation ist diese:

<i>
Dies löst einen Compile-Time-Fehler aus, wenn auf eine Variable zugegriffen wird,
die zuvor nicht mit "our" oder "use vars" deklariert, oder via "my()" lokalisiert
oder vollständig qulifiziert wurde.
</i>

Ein Anfänger wird hoffentlich jedes Skript mit <b>use strict</b> beginnen und
über <b>my</b> vor allen anderen bestehenden Möglichkeiten lernen.

Ich weiß nicht, ob die aktuelle Fehlerbeschreibung geändert werden kann oder geändert werden sollte.
Das ist nicht der Inhalt dieses Artikels. Der Sinn besteht darin, Anfängern in ihrer eigenen
Sprache verstehen zu lassen, was diese Fehlermeldung bedeutet.

Um die obige Fehlermeldung zu unterdrücken, musst Du folgendes schreiben:

<code lang="perl">
use strict;
use warnings;

my $x = 42;
</code>

Das bedeutet: Man muss eine <b>Variable deklarieren bevor sie verwendet wird.</b>

<h2>Die schlechte Lösung</h2>

Die andere "Lösung" wäre, <b>strict</b> zu löschen:

<code lang="perl">
#use strict;
use warnings;

$x = 23;
</code>

Das würde funktionieren, aber der Code würde die Warnung <a href="/name-used-only-once-possible-typo">Name "main::x" used only once: possible typo at ...</a>
ausgeben.

Unabhängig davon, normalerweise würdest Du ein Auto nicht ohne Sicherheitsgurt fahren. Oder doch?

<h2>Beispiel 2: scope</h2>

Ein anderer Fall, den ich oft von Anfängern sehe, ist dieser:

<code lang="perl">
use strict;
use warnings;

my $x = 1;

if ($x) {
my $y = 2;
}

print $y;
</code>

Die Fehlermeldung, die wir bekommen, ist die gleiche wie oben:

<code>
Global symbol "$y" requires explicit package name at ...
</code>

Das ist für viele Leute überrachend. Vor allem, wenn sie gerade zu programmieren begonnen haben.
Und das, obwohl sie <hl>$y</hl> mit <hl>my</hl> deklariert haben.

Zuerst einmal gibt es hier ein visuelles Problem. Die Einrückung der Zeile <hl>my $y = 2;</hl> ist falsch.
Wenn die Zeile mit ein paar Leerzeichen oder einem Tabulatorzeichen - wie im nächsten Beispiel gezeigt - eingerückt worden wäre,
wäre die Ursache des Fehlers deutlicher geworden.

<code lang="perl">
use strict;
use warnings;

my $x = 1;

if ($x) {
    my $y = 2;
}

print $y;
</code>

Das Problem ist, dass die Variable <hl>$y</hl> innerhalb eines Blocks (innerhalb zweier
geschwungener Klammern) deklariert wurde, was dazu führt, dass sie außerhalb des Blocks
nicht existiert. Das wird als der <a href="https://perlmaven.com/scope-of-variables-in-perl"><b>Scope</b> einer Variable</a> bezeichnet.

Das Grundprinzip des <b>Scopes</b> ist von Programmiersprache zu Programmiersprache verschieden.
In Perl wird ein Scope durch einen Block innerhalb geschweifter Klammern gebildet.
Was mit <hl>my</hl> innerhalb eines Blocks deklariert wird, ist außerhalb nicht erreichbar.

(Übrigens: Die Zeile <hl>$x = 1</hl> ist hier nur, um eine legitim aussehende Bedingung zu schaffen.
Mit anderen Worten: <hl>if ($x) {</hl> ist nur da, um das Beispiel echt aussehen zu lassen.)

Die Lösung ist entweder die <hl>print</hl>-Anweisung innerhalb des Blocks zu plazieren:

<code lang="perl">
use strict;
use warnings;

my $x = 1;

if ($x) {
    my $y = 2;
    print $y;
}
</code>

oder die Variable außerhalb des Blocks zu deklarieren.

<code lang="perl">
use strict;
use warnings;

my $x = 1;
my $y;

if ($x) {
    $y = 2;
}

print $y;
</code>

Welchen Weg Du einschlagen wirst, ist von der tatsächlichen Aufgabe abhängig. Das hier sind nur die
syntaktisch richtigen Lösungen.

Desweiteren, wenn wir vergessen <hl>my</hl> innerhalb des Blocks zu löschen oder wenn <hl>$x</hl>
'false' ist, dann erhalten wir eine <a href="/use-of-uninitialized-value">Use of uninitialized value</a>-Warnung.

<h2>Die anderen Möglichkeiten</h2>

Die Erklärung für <hl>our</hl> und <hl>use vars</hl>, oder auch wie
eine Varaible vollständig qulifiziert werden kann, ist für einen anderen Artikel vorgesehen.
