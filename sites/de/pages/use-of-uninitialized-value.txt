=title Use of uninitialized value
=timestamp 2013-08-04T21:45:56
=indexes undef, uninitialized value, $|, warnings, buffering
=status show
=original use-of-uninitialized-value
=books beginner
=author szabgab
=translator mca
=comments_disqus_enable 0

=abstract start

Das ist eine der meistverbreiteten Warnungen, die Dir beim Ausführen von Perl-Code begegenen wird.

Es ist eine Warnung, die die Programmausführung nicht beeinträchtigt und nur dann generiert
wird, wenn Warnungen eingeschaltet wurden. Was auch empfohlen ist.

Der gängiste Weg Warnungen einzuschalten ist die Anweisung <hl>use warnings;</hl> am
Anfang des Skripts oder Moduls einzufügen.

=abstract end

Der ältere Weg ist das Hinzufügen der <hl>-w</hl>-Option an die She-Bang-Zeile. Das
sieht normalerweise als erste Zeile im Skript so aus:

<hl>#!/usr/bin/perl -w</hl>

Es gibt bestimmte Unterschiede. Aber nachdem es nun <hl>use warnings</hl> rund 12 Jahre
gibt, gibt es keinen Grund, dies nicht zu verwenden. Anders ausgedrückt:

Benutze immer: <hl>use warnings;</hl>!

Lass uns zurückkehren zu der eigentlichen Warnung, die ich erklären wollte.

<h2>Eine schnelle Erklärung</h2>

<code>
Use of uninitialized value $x in say at perl_warning_1.pl line 6.
</code>

Das bedeutet, die Variable <hl>$x</hl> hat keinen Wert (der Wert ist der spezielle Wert <hl>undef</hl>).
Entweder sie hatte nie einen Wert oder zu irgendeinem Punkt wurde  <hl>undef</hl> zugewiesen.

Du solltest die Stellen identifizieren, an denen die Variable zuletzt Werte zugewiesen
bekommen hat. Oder Du solltest herausfinden, warum der Codestrang nie ausgeführt wurde.

<h2>Ein einfaches Beispiel</h2>

Das nachfolgende Beispiel wird eine solche Warnung generieren:

<code lang="perl">
use warnings;
use strict;
use 5.010;

my $x;
say $x;
</code>

Perl ist sehr nett. Es sagt uns, welche Datei auf welcher Zeile die Warnung verursacht.

<h2>Nur eine Warnung</h2>

Wie schon gesagt, es nur eine Warnung. Sollte das Skript nach der <hl>say</hl>-Anweisung mehr
Anweisungen haben, würden diese ausgeführt.

<code lang="perl">
use warnings;
use strict;
use 5.010;

my $x;
say $x;
$x = 42;
say $x;
</code>

Das würde folgendes ausgeben:

<code>
Use of uninitialized value $x in say at perl_warning_1.pl line 6.

42
</code>

<h2>Verwirrende Ausgabereihenfolge</h2>

Gib jedoch acht: Wenn Dein Code print-Anweisungen vor der Stelle hat,
die die Warnung auslöst, wie im nachfolgenden Beispiel:

<code lang="perl">
use warnings;
use strict;
use 5.010;

print 'OK';
my $x;
say $x;
$x = 42;
say $x;
</code>

ist die Ausgabe möglicherweise verwirrend.

<code>
Use of uninitialized value $x in say at perl_warning_1.pl line 7.
OK
42
</code>

Das Ergebnis 'OK' der <hl>print</hl>-Anweisung erscheint <b>nach</b>
der Warnung, obwohl sie <b>vor</b> der Codestelle liegt, die die
Warnung verursacht.

Diese Eigenart ist das Ergebnis des <hl>IO-Bufferings</hl>.
Im Standard puffert Perl STDOUT, den Standard-Ausgabe-Kanal,
während es STDERR, den Standard-Fehler-Kanal, nicht puffert.

Also, während das Wort 'OK' darauf wartet, dass der Ausgabekanal
geleert (geflusht) wird, hat die Warnmeldung den Bildschirm
schon erreicht.

<h2>Puffern abschalten</h2>

Um genau das zu vermeiden, kannst Du das Puffern von STDOUT abschalten.

Das kann durch folgenen Code zu Beginn des Skripts erfolgen:
<hl>$| = 1;</hl>


<code lang="perl">
use warnings;
use strict;
use 5.010;

$| = 1;

print 'OK';
my $x;
say $x;
$x = 42;
say $x;
</code>

<code>
OKUse of uninitialized value $x in say at perl_warning_1.pl line 7.
42
</code>

(Die Warnung ist auf der gleichen Zeile wie das <b>OK</b>, weil wir keine neue Zeile <h1>\n</h1> nach dem OK ausgegeben haben.)

<h2>Der ungewollte Scope</h2>

<code lang="perl">
use warnings;
use strict;
use 5.010;

my $x;
my $y = 1;

if ($y) {
  my $x = 42;
}
say $x;
</code>

Dieser Code produziert ebenfalls <hl>Use of uninitialized value $x in say at perl_warning_1.pl line 11.</hl>

Ich habe es fertiggebracht, diesen Fehler merfach zu machen. Unbedacht habe ich <hl>my $x</hl>
innerhalb eine Blocks verwendet, so dass ich eine zusätzliche Variable $x erzeugt habe,
dieser 42 zuwies und dann die Variable am Ende des Blocks aus dem Scope gehen ließ.
(Das  $y = 1 ist nur ein Platzhalter für ein wenig realen Codes und einer realen Bedingung.
Es ist nur dazu da, den Code ein wenig realistischer erscheinen zu lassen.)

Es gibt natürlich Fälle, in denen es sinnvoll ist, eine Variable innerhalb eines Blocks zu deklarieren.
Wenn ich das aus versehen mache, dann ist es ein Fehler, der nur sehr schmerzlich zu finden ist.
